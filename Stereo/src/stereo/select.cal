/*
 * Author: Jinglin
 * Date: 16/01/13 
 * actor of selecting the best disparity with WTA
 */
package stereo;
 
actor select()
float Cost
 ==> float Disp: 
 
 	 //const
	 int max_rd = 60;
	 int height = 375;
	 int width = 450;
	 int minDisp = 0;
	
	 //variable 
	 int d := 0;
	 int id := 0;
	 int cnt := 0;
	 float error := 0.0;
	 float temp_err := 0.0;
	 int cj := 0;
	 float disperr[height*width*max_rd]; 
	 float disp[height*width];
 
 
 	 load:action  Cost:[error] ==>
	 guard
		 cnt < height*width*max_rd 
	 do
		 disperr[cnt] := error;
		 cnt := cnt + 1;
	 end
	 
 	 load_done:action  ==>
	 guard
		 cnt = height*width*max_rd 
	 do
		 cnt := 0;
	 end	
	 
	 start: action  ==> 
	 do
	 	foreach int j in 0 .. height-1
	 	do 
	 		foreach int i in 0 .. width-1
	 		do
	 			d := 0;
	 			error := disperr[j*width+i];
	 			cj := j;
	 			foreach int rd in 0 .. max_rd-1
	 			do
	 				temp_err := disperr[cj*width+i];
	 				cj := cj + height;
	 				if temp_err < error then
	 					d := rd;
	 					error := temp_err;
	 				end
	 			end
	 			disp[j*width+i] := d + minDisp;
	 		end
	 	end
	 end
 
 	send: action ==> Disp:[disparity]
	guard
		id < height*width
	var
	//set the variable of output
	float disparity
	do
		disparity := disp[id];
		id := id + 1;
	end
	
	send_done: action ==>
	guard
		id = height*width
	do
		id := 0;
	end
 
 	schedule fsm s_load :
		s_load ( load ) --> s_load;
		s_load ( load_done ) --> s_star;
		s_star ( start ) --> s_send;
		s_send ( send ) --> s_send;
		s_send ( send_done ) --> s_load;
	end
	
	priority
		load_done > load;
		send_done > send;
	end
 end