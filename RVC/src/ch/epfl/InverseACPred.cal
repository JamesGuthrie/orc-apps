package ch.epfl;

import std.util.BitOps.* ;

actor InverseACPred () int(size=13) PQF,
int POS,
int(size=2) AC_PRED_DIR,
bool SKIPPED
==>
int(size=13) QF
:

int MAXW_IN_MB = 45;
int MAXH_IN_MB = 30;

List(type:List(type:List(type:List(type:int(size=13), size=14), size=6), size=MAXW_IN_MB), size=MAXH_IN_MB) ac_buf;

action AC_PRED_DIR:[ s ], POS:[ mby, mbx, comp ], PQF:[ pqf ] repeat 64, SKIPPED:[skipped] ==> QF:[ qf ] repeat 64//, POS_OUT:[mby, mbx, comp]
var
	List(type:int(size=13), size=64) qf := [pqf[i] : for int i in 0 .. 63],
 	bool acpred_flag = s != 0,
 	bool top = s = 2
do
	if acpred_flag and not skipped then
		if comp = 0 then
			if top then 
				if mby != 0 then
					foreach int i in 1 .. 7 do
						qf[i] := qf[i] + ac_buf[mby-1][mbx][2][i - 1];
					end
				end
			else
				if mbx != 0 then
					foreach int i in 1 .. 7 do
						qf[8 * i] := qf[8 * i] + ac_buf[mby][mbx-1][1][7 + i - 1];
					end
				end
			end
		else
			if comp = 1 then
				if top then
					if mby != 0 then
						foreach int i in 1 .. 7 do
							qf[i] := qf[i] + ac_buf[mby-1][mbx][3][i - 1];
						end
					end
				else
					foreach int i in 1 .. 7 do
						qf[8 * i] := qf[8 * i] + ac_buf[mby][mbx][0][7 + i - 1];
					end
				end
			else
				if comp = 2 then
					if top then
						foreach int i in 1 .. 7 do
							qf[i] := qf[i] + ac_buf[mby][mbx][0][i - 1];
						end
					else
						if mbx != 0 then
							foreach int i in 1 .. 7 do
								qf[8 * i] := qf[8 * i] + ac_buf[mby][mbx-1][3][7 + i - 1];
							end
						end
					end
				else
					if comp = 3 then
						if top then
							foreach int i in 1 .. 7 do
								qf[i] := qf[i] + ac_buf[mby][mbx][1][i - 1];
							end
						else
							foreach int i in 1 .. 7 do
								qf[8 * i] := qf[8 * i] + ac_buf[mby][mbx][2][7 + i - 1];
							end
						end
					else // comp = 4, 5 
						if top then
							if mby != 0 then
								foreach int i in 1 .. 7 do
									qf[i] := qf[i] + ac_buf[mby-1][mbx][comp][i - 1];
								end
							end
						else
							if mbx != 0 then
								foreach int i in 1 .. 7 do
									qf[8 * i] := qf[8 * i] + ac_buf[mby][mbx-1][comp][7 + i - 1];
								end
							end
						end
					end
				end
			end
		end
	end
	
	foreach int i in 1 .. 7 do
		ac_buf[mby][mbx][comp][i - 1] := qf[i];
		ac_buf[mby][mbx][comp][7 + i - 1] := qf[8 * i];
	end
end

end
