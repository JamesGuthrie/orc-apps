/*************************************************************************
* This software module was originally developed by David B. Parlour (Xilinx Inc.), 
* in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
* for reference purposes and its performance may not have been optimized. 
* This software module is an implementation of one or more tools as 
* specified by the ISO/IEC 23002-4.
* 
* ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
* license to copy, distribute, and make derivative works of this software 
* module or modifications thereof for use in implementations of the 
* ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 
* 
* Those intending to use this software module in products are advised that 
* its use may infringe existing patents. ISO/IEC have no liability for use 
* of this software module or modifications thereof.
* 
* Copyright is not released for products that do not conform to audiovisual 
* and image-coding related ITU Recommendations and/or ISO/IEC International 
* Standards. 
* 
* Assurance that the originally developed software module can be used 
* (1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
* (2) to develop the ISO/IEC 23002-4: 
* 
* Xilinx Inc.  grants ISO/IEC all rights necessary to 
* include the originally developed software module or modifications thereof 
* in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
* worldwide, non-exclusive, copyright license to copy, distribute, and make 
* derivative works for use in implementations of the ISO/IEC 23002-4 in 
* products that satisfy conformance criteria (if any), and to the extent 
* that such originally developed software module or portions of it are 
* included in the ISO/IEC 23002-4.  To the extent that Xilinx Inc. 
* owns patent rights that would be required to make, use, or 
* sell the originally developed software module or portions thereof 
* included in the ISO/IEC 23002-4 in a conforming product, Xilinx Inc.
* will assure the ISO/IEC that it is willing to 
* negotiate licenses under reasonable and non-discriminatory terms and 
* conditions with applicants throughout the world.
* 
* ISO/IEC gives You a free license to this software module or modifications 
* thereof for the sole  purpose of developing theISO/IEC 23002-4
* 
* Xilinx Inc. retains full right to modify and use the 
* code for its own purpose, assign or donate the code to a third party and 
* to inhibit third parties from using the code for products that do not 
* conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
* Standards. 
* 
* This copyright notice must be included in all copies or derivative works. 
* Copyright (c) ISO/IEC 2007-2011.
*************************************************************************/

/********************************************
*  Algo_DCRAddr_ThreeLeftTop_8x8
* Created By:  David B. Parlour (dave.parlour@xilinx.com)
* Modified By: Andrew Kinane (kinanea@eeng.dcu.ie)
*
* Functionality:
* This module calculates the addresses of the three neighbouring blocks for the current
* 8x8 block used for DC prediction. If any of the neighbours is not coded for some reason
* (either outside frame boundaries or skipped by encoder), the address is set as zero.
* Otherwise the lowest two bits specify the 8x8 component in the macroblock and the other
* higher order bits specify the macroblock index using circular buffer addressing. 
*
* Input Ports:
* ============
* int(size=BTYPE_SZ) BTYPE
* This input token specifies the coding mode of the current 8x8 block. When executing, 
* the first 3 BTYPE tokens of a VOP relate to global parameters of the VOP. The first BTYPE 
* token indicates a new VOP, and the subsequent two BTYPE tokens indicate the width and the 
* height of the VOP in macroblocks respectively. All subsequent BTYPE tokens for the VOP 
* specify the coding mode of the current 8x8 block. For details on the structure of the
* BTYPE token, refer to ParseHeaders.cal
*
* Output Ports:
* =============
* int(size=MB_COORD_SZ) A
* The address of the 8x8 block neighbour to the left of the current 8x8 block.
*
* int(size=MB_COORD_SZ) B
* The address of the 8x8 block neighbour to the above left of the current 8x8 block.
*
* int(size=MB_COORD_SZ) C
* The address of the 8x8 block neighbour to the above of the current 8x8 block.
*
* Modifications:
* * In advance action, indices adjust after every component
*   dealing with a single 8x8 block rather than 6 8x8 blocks.
* * The actions predict.b1 predict.b2 predict.b3 predict.b45 are removed since not needed.
* * In action predict.b0 all neighbouring blocks are now component 0.
* * Block pointer bitwidth reduced from MB_COORD_SZ+3 to MB_COORD_SZ since only
*   1 component per block.
* * Variable 'comp' removed since only one component.
**********************************************************/
package org.sc29.wg11.mpeg4.part2.sp.texture.dc_reconstruction;

import org.sc29.wg11.mpeg4.part2.Constants.*;
import org.sc29.wg11.mpeg4.part2.ConstantsBtype.*;

actor Algo_DCRAddr_ThreeLeftTop_8x8 () 
	int(size=16) WIDTH, 
	uint (size=4) BTYPE
		==> 
		int(size=MB_COORD_SZ) A, 
		int(size=MB_COORD_SZ) B, 
		int(size=MB_COORD_SZ) C :

	// Generate indices into the stream history to locate
	//   A  neighbor to the left
	//   B  neighbor above left
	//   C  neighbor above
	// Use 3 address lines for component index to save address calculation effort
	// Also, location 0 is reserved for all uncoded blocks. This protocol must
	// be identical to the buffer addressing in Reconstruct.cal, since pointer
	// values are passed on ports A, B, C.
	int(size=MB_COORD_SZ) mbx := 0;

	// Current macroblock x position
	bool top_edge := true;

	// Top edge macroblock flag
	bool left_edge;

	// Left edge macroblock flag
	int(size=MB_COORD_SZ) mbwidth := 0;

	// Macroblock width of current vop
	int BUF_SIZE = (MAXW_IN_MB + 2);

	List(type: bool, size=BUF_SIZE) coded := [ false : for int i in 1 .. BUF_SIZE ];

	int(size=MB_COORD_SZ) ptr;

	// Pointer to current macroblock                   
	int(size=MB_COORD_SZ) ptr_left;

	// Pointer to left neighbour macroblock ('A')      
	int(size=MB_COORD_SZ) ptr_above;

	// Pointer to above neighbour macroblock ('C')     
	int(size=MB_COORD_SZ) ptr_above_left;

	// Pointer to above left neighbour macroblock ('B')
	// Buffer pointer wrap, skipping over address 0
	function decrement(int(size=MB_COORD_SZ) p) --> int(size=MB_COORD_SZ) :
		if p = 1 then
			BUF_SIZE - 1
		else
			p - 1
		end
	end

	// New VOP
	start: action BTYPE:[newvop] ==>
	guard
		newvop = NEWVOP
	do
		mbx := 0;
		top_edge := true;
		left_edge := true;
	end

	// Decode width of VOP and initialise flags
	getw: action WIDTH:[w] ==>
	do
		mbwidth := w;
		ptr := 1;
		ptr_left := 2;
		ptr_above := 1 + w;
		ptr_above_left := 2 + w;
	end

	// Is this an INTRA 8x8 block?
	read.intra: action BTYPE:[blocktype] ==>
	guard
		blocktype=2
	do
		coded[ptr] := true;
	end

	// Is this not an INTRA 8x8 block?
	read.other: action BTYPE:[blocktype] ==>
	do
		coded[ptr] := false;
	end

	// Move to the next 8x8 block and set flags appropriately
	advance: action ==>
	do
		mbx := mbx + 1;
		left_edge := false;
		if mbx = mbwidth then
			mbx := 0;
			top_edge := false;
			left_edge := true;
		end
		ptr := decrement(ptr);
		ptr_left := decrement(ptr_left);
		ptr_above := decrement(ptr_above);
		ptr_above_left := decrement(ptr_above_left);
	end

	// Locate prediction indices for block 0
	predict.b0: action ==> A:[ a ], B:[ b ], C:[ c ] //guard comp = 0

	var
		int(size=MB_COORD_SZ + 3) a := 0 , int(size=MB_COORD_SZ + 3) b := 0 , int(size=MB_COORD_SZ + 3) c
		:= 0
	do
		if not left_edge then
		// left-neighbor is block 0 from previous macroblock
			a := ptr_left;
			if not coded[a] then
				a := 0;
			end
			if not top_edge then
			// above-left-neighbor is block 0 from previous row, col macroblock
				b := ptr_above_left;
				if not coded[b] then
					b := 0;
				end
			end
		end
		// above-neighbor is block 0 from previous row macroblock
		if not top_edge then
			c := ptr_above;
			if not coded[c] then
				c := 0;
			end
		end
	end

	schedule fsm read :
		read ( start ) --> getw;
		getw ( getw ) --> read;
		read ( read.intra ) --> predict;
		read ( read.other ) --> advance;
		predict ( predict ) --> advance;
		advance ( advance ) --> read;
	end

	priority
		start > read;
		read.intra > read.other;
	end

end