/********************************************************************************

This software module was originally developed by 
Damien de Saint Jorre (IETR/INSA of Rennes), 
in the course of development of the ISO/IEC 23002-4 (Video Tool Library) 
for reference purposes and its performance may not have been optimized. 
This software module is an implementation of one or more tools as 
specified by the ISO/IEC 23002-4.

ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright 
license to copy, distribute, and make derivative works of this software 
module or modifications thereof for use in implementations of the 
ISO/IEC 23002-4 in products that satisfy conformance criteria (if any). 

Those intending to use this software module in products are advised that 
its use may infringe existing patents. ISO/IEC have no liability for use 
of this software module or modifications thereof.

Copyright is not released for products that do not conform to audiovisual 
and image-coding related ITU Recommendations and/or ISO/IEC International 
Standards. 

Assurance that the originally developed software module can be used 
(1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and 
(2) to develop the ISO/IEC 23002-4: 

IETR/INSA of Rennes grants ISO/IEC all rights necessary to 
include the originally developed software module or modifications thereof 
in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, 
worldwide, non-exclusive, copyright license to copy, distribute, and make 
derivative works for use in implementations of the ISO/IEC 23002-4 in 
products that satisfy conformance criteria (if any), and to the extent 
that such originally developed software module or portions of it are 
included in the ISO/IEC 23002-4.  To the extent that IETR/INSA of Rennes
owns patent rights that would be required to make, use, or 
sell the originally developed software module or portions thereof 
included in the ISO/IEC 23002-4 in a conforming product, IETR/INSA of
Rennes will assure the ISO/IEC that it is willing to 
negotiate licenses under reasonable and non-discriminatory terms and 
conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications 
thereof for the sole  purpose of developing theISO/IEC 23002-4

IETR/INSA of Rennes retains full right to modify and use the 
code for its own purpose, assign or donate the code to a third party and 
to inhibit third parties from using the code for products that do not 
conform to MPEG-related ITU Recommendations and/or ISO/IEC International 
Standards. 

This copyright notice must be included in all copies or derivative works. 
Copyright (c) ISO/IEC 2008.
*****************************************************************************/
package org.sc29.wg11.mpeg4.part10.php.interPred;

import org.sc29.wg11.mpeg4.part10.MacroBlockInfo.*;

actor SelectMvpLX()
	uint(size=6)  MbType,
	uint(size=4)  SubMbType,
	int (size=16) MvL0,
	uint(size=11) LocationL0,
	uint(size=32) FrameL0,
	int (size=16) MvL1,
	uint(size=32) FrameL1,
	uint(size=11) LocationL1,
	bool          DirectPredL0,
	bool          DirectPredL1,
	uint(size=5)  RefIdxL0,
	uint(size=5)  RefIdxL1
		==>
	int (size=16) MvOut,
	uint(size=32) FrameToRead,
	uint(size=11) Location,
	uint(size=2)  SelectList,
	uint(size=5)  RefIdx
		:

	uint(size=3) MbPartIdxMax;
	uint(size=3) MbPartIdx;
	uint(size=1) Idx_Sub_Tab;
	bool         Is8x8Mb;
	uint(size=3) SubMbPartIdxMax;
	uint(size=3) SubMbPartIdx;
	bool         IsSubPredL0;
	bool         IsSubPredL1;
	uint(size=6) mb_type;

	List(type: bool, size=2) IsPredL0;
	List(type: bool, size=2) IsPredL1;
	
	bool DEBUG=false; 

	ReadNewSlice: action
			MbType :[MacroBlkType]
				==>
		guard
			MacroBlkType >= NEW_SLICE
		end
			
	SkipMbType: action
			MbType :[MacroBlkType]
				==>
		guard
			Mb_IsIntra(MacroBlkType)
		end


	ReadMbType: action MbType:[MacroBlkType]
		==>
	do
		mb_type := MacroBlkType;
		Idx_Sub_Tab   :=
			if mbIsBiPred(MacroBlkType) then
				0
			else
				1
			end;
		MbPartIdx     := 0;
		MbPartIdxMax  := numMbPart(MacroBlkType);
		Is8x8Mb :=    (MacroBlkType = BLOCK_TYPE_P_8x8ref0)
		           or (MacroBlkType = BLOCK_TYPE_P_8x8)
		           or (MacroBlkType = BLOCK_TYPE_B_8x8);
		IsPredL0[0] := Mb_Is_PredL0(MacroBlkType,0);
		IsPredL0[1] := Mb_Is_PredL0(MacroBlkType,1);
		IsPredL1[0] := Mb_Is_PredL1(MacroBlkType,0);
		IsPredL1[1] := Mb_Is_PredL1(MacroBlkType,1);
if DEBUG then
		println("ReadMbType MacroBlkType "+ MacroBlkType);
end
	end

	MacroBlkType.IsPredL0: action
			MvL0       :[Mvx, Mvy],
			FrameL0    :[frameL0],
			RefIdxL0   :[ref0],
			LocationL0 :[Mbx_in_pix,Mby_in_pix]
				==>
			MvOut       :[Mvx, Mvy],
			FrameToRead :[frameL0],
			RefIdx      :[ref0],
			Location    :[Mbx_in_pix,Mby_in_pix],
			SelectList  :[PRED_L0]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			IsPredL0[MbPartIdx&1],
			not IsPredL1[MbPartIdx&1]
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsPredL0 MvL0 "+ Mvx+"; " + Mvy);
		println("MacroBlkType.IsPredL0 frameL0 "+ frameL0);
		println("MacroBlkType.IsPredL0 MvL0 "+ Mbx_in_pix+"; " + Mby_in_pix);
end
		end

	MacroBlkType.IsPredL1: action
			MvL1       :[Mvx,Mvy],
			FrameL1    :[frameL1],
			LocationL1 :[Mbx_in_pix,Mby_in_pix],
			RefIdxL1   :[ref1]
				==>
			MvOut       :[Mvx,Mvy],
			FrameToRead :[frameL1],
			RefIdx      :[ref1],
			Location    :[Mbx_in_pix,Mby_in_pix],
			SelectList  :[PRED_L1]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			not IsPredL0[MbPartIdx&1],
			IsPredL1[MbPartIdx&1]
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsPredL1 MvL1 "+ Mvx+"; " + Mvy);
		println("MacroBlkType.IsPredL1 frameL1 "+ frameL1);
		println("MacroBlkType.IsPredL1 MvL1 "+ Mbx_in_pix+"; " + Mby_in_pix);
end
		end

	MacroBlkType.IsBiPred: action
			MvL0       :[MvxL0, MvyL0],
			FrameL0    :[frameL0],
			LocationL0 :[Mbx_in_pixL0,Mby_in_pixL0],
			RefIdxL0   :[ref0],

			MvL1       :[MvxL1,MvyL1],
			FrameL1    :[frameL1],
			LocationL1 :[Mbx_in_pixL1,Mby_in_pixL1],
			RefIdxL1   :[ref1]
				==>
			MvOut       :[MvxL0,MvyL0 , MvxL1,MvyL1],
			FrameToRead :[frameL0 , frameL1],
			RefIdx      :[ref0, ref1],
			Location    :[Mbx_in_pixL0,Mby_in_pixL0 , Mbx_in_pixL1,Mby_in_pixL1],
			SelectList  :[BIPRED]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			IsPredL0[MbPartIdx&1] and IsPredL1[MbPartIdx&1]
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsBiPred MvL0 "+ MvxL0+"; " + MvyL0);
		println("MacroBlkType.IsBiPred frameL0 "+ frameL0);
		println("MacroBlkType.IsBiPred MvL0 "+ Mbx_in_pixL0+"; " + Mby_in_pixL0);
		println("MacroBlkType.IsBiPred ref0 "+ ref0);
		println("MacroBlkType.IsBiPred MvL1 "+ MvxL1+"; " + MvyL1);
		println("MacroBlkType.IsBiPred frameL1 "+ frameL1);
		println("MacroBlkType.IsBiPred MvL1 "+ Mbx_in_pixL1+"; " + Mby_in_pixL1);
		println("MacroBlkType.IsBiPred ref1 "+ ref1);
end
		end

	MacroBlkType.IsDirect_PredL0: action
			MvL0         :[Mvxl0, Mvyl0],
			FrameL0      :[frameL0],
			RefIdxL0     :[ref0],
			LocationL0   :[Mbx_in_pixl0,Mby_in_pixl0],
			DirectPredL0 :[Is_direct_l0],
			DirectPredL1 :[Is_direct_l1]
				==>
			MvOut       :[Mvxl0,Mvyl0],
			FrameToRead :[frameL0],
			RefIdx      :[ref0],
			Location    :[Mbx_in_pixl0,Mby_in_pixl0],
			SelectList  :[PRED_L0]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			(not IsPredL0[MbPartIdx&1]) and (not IsPredL1[MbPartIdx&1]),
	        Is_direct_l0  and (not Is_direct_l1)
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsDirect_PredL0 MvL0 "+ Mvxl0+"; " + Mvyl0);
		println("MacroBlkType.IsDirect_PredL0 frameL0 "+ frameL0);
		println("MacroBlkType.IsDirect_PredL0 MvL0 "+ Mbx_in_pixl0+"; " + Mby_in_pixl0);
		println("MacroBlkType.IsDirect_PredL0 ref0 "+ ref0);
		println("MacroBlkType.IsDirect_PredL0 Is_direct_l0 "+ Is_direct_l0);
		println("MacroBlkType.IsDirect_PredL0 Is_direct_l1 "+ Is_direct_l1);
end
		end

	MacroBlkType.IsDirect_PredL1: action
			MvL1         :[Mvxl1, Mvyl1],
			FrameL1      :[frameL1],
			RefIdxL1     :[ref1],
			LocationL1   :[Mbx_in_pixl1,Mby_in_pixl1],
			DirectPredL1 :[Is_direct_l1],

			DirectPredL0 :[Is_direct_l0]
				==>
			MvOut       :[Mvxl1,Mvyl1],
			FrameToRead :[frameL1],
			RefIdx      :[ref1],
			Location    :[Mbx_in_pixl1,Mby_in_pixl1],
			SelectList  :[PRED_L1]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			(not IsPredL0[MbPartIdx&1]) and (not IsPredL1[MbPartIdx&1]),
			(not Is_direct_l0) and Is_direct_l1
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsDirect_PredL1 MvL1 "+ Mvxl1+"; " + Mvyl1);
		println("MacroBlkType.IsDirect_PredL1 frameL0 "+ frameL1);
		println("MacroBlkType.IsDirect_PredL1 MvL1 "+ Mbx_in_pixl1+"; " + Mby_in_pixl1);
		println("MacroBlkType.IsDirect_PredL1 ref1 "+ ref1);
		println("MacroBlkType.IsDirect_PredL1 Is_direct_l0 "+ Is_direct_l0);
		println("MacroBlkType.IsDirect_PredL1 Is_direct_l1 "+ Is_direct_l1);
end
		end

	MacroBlkType.IsDirect_BiPred: action
			MvL0         :[Mvxl0, Mvyl0],
			FrameL0      :[frameL0],
			RefIdxL0     :[ref0],
			LocationL0   :[Mbx_in_pixl0,Mby_in_pixl0],
			DirectPredL0 :[Is_direct_l0],

			MvL1         :[Mvxl1, Mvyl1],
			FrameL1      :[frameL1],
			RefIdxL1     :[ref1],
			LocationL1   :[Mbx_in_pixl1,Mby_in_pixl1],
			DirectPredL1 :[Is_direct_l1]
				==>
			MvOut       :[Mvxl0,Mvyl0 , Mvxl1,Mvyl1],
			FrameToRead :[frameL0 , frameL1],
			RefIdx      :[ref0, ref1],
			Location    :[Mbx_in_pixl0,Mby_in_pixl0 , Mbx_in_pixl1,Mby_in_pixl1],
			SelectList  :[BIPRED]
		guard
			MbPartIdx < MbPartIdxMax,
			not Is8x8Mb,
			(not IsPredL0[MbPartIdx&1]) and (not IsPredL1[MbPartIdx&1]),
			        Is_direct_l0  and      Is_direct_l1
			or (not Is_direct_l0) and (not Is_direct_l1)
		do
			MbPartIdx := MbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsDirect_BiPred MvL0 "+ Mvxl0+"; " + Mvyl0);
		println("MacroBlkType.IsDirect_BiPred frameL0 "+ frameL0);
		println("MacroBlkType.IsDirect_BiPred MvL0 "+ Mbx_in_pixl0+"; " + Mby_in_pixl0);
		println("MacroBlkType.IsDirect_BiPred ref0 "+ ref0);
		println("MacroBlkType.IsDirect_BiPred Is_direct_l0 "+ Is_direct_l0);

		println("MacroBlkType.IsDirect_BiPred MvL1 "+ Mvxl1+"; " + Mvyl1);
		println("MacroBlkType.IsDirect_BiPred frameL0 "+ frameL1);
		println("MacroBlkType.IsDirect_BiPred MvL1 "+ Mbx_in_pixl1+"; " + Mby_in_pixl1);
		println("MacroBlkType.IsDirect_BiPred ref1 "+ ref1);
		println("MacroBlkType.IsDirect_BiPred Is_direct_l1 "+ Is_direct_l1);
end
		end

	MacroBlkType_Is8x8: action
			SubMbType :[ValSubMbType]
				==>
		guard
			MbPartIdx < MbPartIdxMax,
			Is8x8Mb
		do
			SubMbPartIdx    := 0;
			SubMbPartIdxMax := numSubMbPart(mb_type,ValSubMbType);
			IsSubPredL0     := (SubMbPartPredMode[Idx_Sub_Tab][ValSubMbType & 15] & PRED_L0) != 0;
			IsSubPredL1     := (SubMbPartPredMode[Idx_Sub_Tab][ValSubMbType & 15] & PRED_L1) != 0;
if DEBUG then
		println("ReadMbType SubMbType "+ ValSubMbType);
end
		end

	GetParameters_Done: action
				==>
		guard
			MbPartIdx = MbPartIdxMax
		end

	SubMacroBlkType.IsPredL0: action
			MvL0       :[Mvx, Mvy],
			FrameL0    :[frameL0],
			RefIdxL0   :[ref0],
			LocationL0 :[Mbx_in_pix,Mby_in_pix]
				==>
			MvOut       :[Mvx, Mvy],
			FrameToRead :[frameL0],
			RefIdx      :[ref0],
			Location    :[Mbx_in_pix,Mby_in_pix],
			SelectList  :[PRED_L0]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			IsSubPredL0,
			not IsSubPredL1
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("SubMacroBlkType.IsPredL0 MvL0 "+ Mvx+"; " + Mvy);
		println("SubMacroBlkType.IsPredL0 frameL0 "+ frameL0);
		println("SubMacroBlkType.IsPredL0 MvL0 "+ Mbx_in_pix+"; " + Mby_in_pix);
end
		end

	SubMacroBlkType.IsPredL1: action
			MvL1       :[Mvx, Mvy],
			FrameL1    :[frameL1],
			RefIdxL1   :[ref1],
			LocationL1 :[Mbx_in_pix,Mby_in_pix]
				==>
			MvOut       :[Mvx, Mvy],
			FrameToRead :[frameL1],
			RefIdx      :[ref1],
			Location    :[Mbx_in_pix,Mby_in_pix],
			SelectList  :[PRED_L1]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			not IsSubPredL0,
			IsSubPredL1
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("SubMacroBlkType.IsPredL1 MvL1 "+ Mvx+"; " + Mvy);
		println("SubMacroBlkType.IsPredL1 frameL1 "+ frameL1);
		println("SubMacroBlkType.IsPredL1 MvL1 "+ Mbx_in_pix+"; " + Mby_in_pix);
end
		end

	SubMacroBlkType.IsBiPred: action
			MvL0       :[Mvxl0, Mvyl0],
			FrameL0    :[frameL0],
			RefIdxL0   :[ref0],
			LocationL0 :[Mbx_in_pixl0,Mby_in_pixl0],

			MvL1       :[Mvxl1,Mvyl1],
			FrameL1    :[frameL1],
			RefIdxL1   :[ref1],
			LocationL1 :[Mbx_in_pixl1,Mby_in_pixl1]
				==>
			MvOut       :[Mvxl0, Mvyl0 , Mvxl1, Mvyl1],
			FrameToRead :[frameL0 , frameL1],
			RefIdx      :[ref0, ref1],
			Location    :[Mbx_in_pixl0,Mby_in_pixl0 , Mbx_in_pixl1,Mby_in_pixl1],
			SelectList  :[BIPRED]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			IsSubPredL0 and IsSubPredL1
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsBiPred MvL0 "+ Mvxl0+"; " + Mvyl0);
		println("MacroBlkType.IsBiPred frameL0 "+ frameL0);
		println("MacroBlkType.IsBiPred MvL0 "+ Mbx_in_pixl0+"; " + Mby_in_pixl0);
		println("MacroBlkType.IsBiPred ref0 "+ ref0);
		println("MacroBlkType.IsBiPred MvL1 "+ Mvxl1+"; " + Mvyl1);
		println("MacroBlkType.IsBiPred frameL1 "+ frameL1);
		println("MacroBlkType.IsBiPred MvL1 "+ Mbx_in_pixl1+"; " + Mby_in_pixl1);
		println("MacroBlkType.IsBiPred ref1 "+ ref1);
end
		end

	SubMacroBlkType.IsDirect_PredL0: action
			MvL0         :[Mvxl0, Mvyl0],
			FrameL0      :[frameL0],
			RefIdxL0     :[ref0],
			LocationL0   :[Mbx_in_pixl0,Mby_in_pixl0],
			DirectPredL0 :[Is_direct_l0],
			DirectPredL1 :[Is_direct_l1]
				==>
			MvOut       :[Mvxl0,Mvyl0],
			FrameToRead :[frameL0],
			RefIdx      :[ref0],
			Location    :[Mbx_in_pixl0,Mby_in_pixl0],
			SelectList  :[PRED_L0]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			(not IsSubPredL0) and (not IsSubPredL1),
	        Is_direct_l0  and (not Is_direct_l1)
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsDirect_PredL0 MvL0 "+ Mvxl0+"; " + Mvyl0);
		println("MacroBlkType.IsDirect_PredL0 frameL0 "+ frameL0);
		println("MacroBlkType.IsDirect_PredL0 MvL0 "+ Mbx_in_pixl0+"; " + Mby_in_pixl0);
		println("MacroBlkType.IsDirect_PredL0 ref0 "+ ref0);
		println("MacroBlkType.IsDirect_PredL0 Is_direct_l0 "+ Is_direct_l0);
		println("MacroBlkType.IsDirect_PredL0 Is_direct_l1 "+ Is_direct_l1);
end
		end

	SubMacroBlkType.IsDirect_PredL1: action
			MvL1         :[Mvxl1, Mvyl1],
			FrameL1      :[frameL1],
			RefIdxL1     :[ref1],
			LocationL1   :[Mbx_in_pixl1,Mby_in_pixl1],
			DirectPredL1 :[Is_direct_l1],

			DirectPredL0 :[Is_direct_l0]
				==>
			MvOut       :[Mvxl1,Mvyl1],
			FrameToRead :[frameL1],
			RefIdx      :[ref1],
			Location    :[Mbx_in_pixl1,Mby_in_pixl1],
			SelectList  :[PRED_L1]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			(not IsSubPredL0) and (not IsSubPredL1),
			(not Is_direct_l0) and Is_direct_l1
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("SubMacroBlkType.IsDirect_PredL1 MvL1 "+ Mvxl1+"; " + Mvyl1);
		println("SubMacroBlkType.IsDirect_PredL1 frameL0 "+ frameL1);
		println("SubMacroBlkType.IsDirect_PredL1 MvL1 "+ Mbx_in_pixl1+"; " + Mby_in_pixl1);
		println("SubMacroBlkType.IsDirect_PredL1 ref1 "+ ref1);
		println("SubMacroBlkType.IsDirect_PredL1 Is_direct_l0 "+ Is_direct_l0);
		println("SubMacroBlkType.IsDirect_PredL1 Is_direct_l1 "+ Is_direct_l1);
end
		end

	SubMacroBlkType.IsDirect_BiPred: action
			MvL0         :[Mvxl0, Mvyl0],
			FrameL0      :[frameL0],
			RefIdxL0     :[ref0],
			LocationL0   :[Mbx_in_pixl0,Mby_in_pixl0],
			DirectPredL0 :[Is_direct_l0],

			MvL1         :[Mvxl1, Mvyl1],
			FrameL1      :[frameL1],
			RefIdxL1     :[ref1],
			LocationL1   :[Mbx_in_pixl1,Mby_in_pixl1],
			DirectPredL1 :[Is_direct_l1]
				==>
			MvOut       :[Mvxl0,Mvyl0 , Mvxl1,Mvyl1],
			FrameToRead :[frameL0 , frameL1],
			RefIdx      :[ref0, ref1],
			Location    :[Mbx_in_pixl0,Mby_in_pixl0 , Mbx_in_pixl1,Mby_in_pixl1],
			SelectList  :[BIPRED]
		guard
			SubMbPartIdx < SubMbPartIdxMax,
			(not IsSubPredL0) and (not IsSubPredL1),
			        Is_direct_l0  and      Is_direct_l1
			or (not Is_direct_l0) and (not Is_direct_l1)
		do
			SubMbPartIdx := SubMbPartIdx + 1;
if DEBUG then
		println("MacroBlkType.IsDirect_BiPred MvL0 "+ Mvxl0+"; " + Mvyl0);
		println("MacroBlkType.IsDirect_BiPred frameL0 "+ frameL0);
		println("MacroBlkType.IsDirect_BiPred MvL0 "+ Mbx_in_pixl0+"; " + Mby_in_pixl0);
		println("MacroBlkType.IsDirect_BiPred ref0 "+ ref0);
		println("MacroBlkType.IsDirect_BiPred Is_direct_l0 "+ Is_direct_l0);

		println("MacroBlkType.IsDirect_BiPred MvL1 "+ Mvxl1+"; " + Mvyl1);
		println("MacroBlkType.IsDirect_BiPred frameL0 "+ frameL1);
		println("MacroBlkType.IsDirect_BiPred MvL1 "+ Mbx_in_pixl1+"; " + Mby_in_pixl1);
		println("MacroBlkType.IsDirect_BiPred ref1 "+ ref1);
		println("MacroBlkType.IsDirect_BiPred Is_direct_l1 "+ Is_direct_l1);
end
		end

	GetParamSubMb_Done: action ==>
		guard
			SubMbPartIdx = SubMbPartIdxMax
		do
			MbPartIdx := MbPartIdx + 1;
		end

	schedule fsm ReadMbType:
		ReadMbType         (ReadNewSlice        )--> ReadMbType;
		ReadMbType         (SkipMbType          )--> ReadMbType;
		ReadMbType         (ReadMbType          )--> GetParameters;

		GetParameters      (MacroBlkType_Is8x8  )--> GetParametersSubMb;
		GetParameters      (GetParameters_Done  )--> ReadMbType;
		GetParameters      (MacroBlkType        )--> GetParameters;

		GetParametersSubMb (SubMacroBlkType     )--> GetParametersSubMb;
		GetParametersSubMb (GetParamSubMb_Done  )--> GetParameters;
	end
	
	priority
		ReadNewSlice > SkipMbType > ReadMbType;
	end
	
end