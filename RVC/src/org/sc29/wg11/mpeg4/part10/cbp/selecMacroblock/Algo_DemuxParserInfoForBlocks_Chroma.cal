/********************************************************************************

This software module was originally developed by Damien de Saint Jorre IETR/INSA of Rennes (France), in the course of development of the <standard> for reference purposes and its performance may not have been optimized. This software module is an implementation of one or more tools as specified by the MPEG-C part 4.
ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works of this software module or modifications thereof for use in implementations of the <standard>  in products that satisfy conformance criteria (if any).
Those intending to use this software module in products are advised that its use may infringe existing patents. ISO/IEC have no liability for use of this software module or modifications thereof.
Copyright is not released for products that do not conform to audiovisual and image-coding related ITU Recommendations and/or ISO/IEC International Standards.
Assurance that the originally developed software module can be used (1) in the <standard> once the <standard> has been adopted; and (2) to develop the MPEG-C part 4:

IETR/INSA of Rennes (France) grants ISO/IEC all rights necessary to include the originally developed software module or modifications thereof in the <standard> and to permit ISO/IEC to offer You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works for use in implementations of the MPEG-C part 4 in products that satisfy conformance criteria (if any), and to the extent that such originally developed software module or portions of it are included in the MPEG-C part 4.  To the extent that IETR/INSA of Rennes (France) owns patent rights that would be required to make, use, or sell the originally developed software module or portions thereof included in the MPEG-C part 4 in a conforming product,  IETR/INSA of Rennes (France) will assure the ISO/IEC that it is willing to negotiate licenses under reasonable and non-discriminatory terms and conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications thereof for the sole  purpose of developing the MPEG-C part 4

IETR/INSA of Rennes (France) retains full right to modify and use the code for its own purpose, assign or donate the code to a third party and to inhibit third parties from using the code for products that do not conform to MPEG-related ITU Recommendations and/or ISO/IEC International Standards.
This copyright notice must be included in all copies or derivative works. Copyright (c) ISO/IEC 2008.

*****************************************************************************/
package org.sc29.wg11.mpeg4.part10.cbp.selecMacroblock;

import org.mpeg4.part10.cbp.MacroBlockInfo.*;
import org.mpeg4.part10.Imp_PictureInfo.*;

actor Algo_DemuxParserInfoForBlocks_Chroma()
	int (size=9)  Coef_ACR,
	uint(size=6)  MBType,
	bool          ConstrainedIFlag,
	uint(size=7)  MbLocation,
	bool          NeighbourAvail
	
		==>
	int (size=9)  Coef_ACR_Intra,
	bool          NeighbourAvail_Intra,

	int (size=9)  Coef_ACR_Inter
		:

	List( type:bool, size=MAX_PICWIDTH_BLK) is_intra := [false : for int i in 0 .. MAX_PICWIDTH_BLK-1];
	bool          intra_top_left := false;	
	uint(size=13) curr_pos       := 0;
	bool          CurrValIsIntra := false;

/*
*
* Actions
*
*/

	CheckSlices: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType >= NEW_FRAME
		end

	bool constrainedIFlag;
	
	Get_IFlag: action ConstrainedIFlag :[ Constrained_I_Flag ] ==>
	do
		constrainedIFlag:= Constrained_I_Flag;
	end

	MBTypeIsPCM: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType = BLOCK_TYPE_I_PCM
		do
			CurrValIsIntra := true;
		end


	MBTypeIsIntra: action
			MBType             :[ValMBType]
				==>
		guard
			ValMBType <= BLOCK_TYPE_INTRA_MAX,
			ValMBType != BLOCK_TYPE_I_PCM
		do
			CurrValIsIntra := true;	
			
		end

	SendAcrIntra: action
			Coef_ACR           :[ValCoef] repeat 64
				==>
			Coef_ACR_Intra     :[ValCoef] repeat 64
		end

	SendNeighbourIntra: action
			MbLocation   :[MbLocxInPic , MbLocyInPic],
			NeighbourAvail :[NeighbourA , NeighbourB , NeighbourC , NeighbourD]
				==>
			NeighbourAvail_Intra  :[NeighA, NeighB, NeighC, NeighD]
		var
			bool NeighA,
			bool NeighB,
			bool NeighC,
			bool NeighD
		do
			curr_pos    := MbLocxInPic;
			NeighA := NeighbourA && (is_intra[(curr_pos-1) & 0x3F] or not (constrainedIFlag));
			NeighB := NeighbourB && (is_intra[curr_pos & 0x3F] or not(constrainedIFlag));
			NeighC := NeighbourC && (is_intra[(curr_pos+1) & 0x3F] or not(constrainedIFlag)) && NeighB;
			NeighD := NeighbourD && (intra_top_left or not (constrainedIFlag));
		end

	SendInfosDone: action ==>
		do
			intra_top_left     := is_intra[curr_pos];
			is_intra[curr_pos] := CurrValIsIntra;
		end

	MBTypeIsInter: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType > BLOCK_TYPE_INTRA_MAX
		do
			CurrValIsIntra := false;
		end

	MBTypeIsInterPSkip: action
			MBType           :[ValMBType]
				==>
				Coef_ACR_Inter :[[0: for int i in 0 .. 63]] repeat 64
		guard
			ValMBType = BLOCK_TYPE_P_SKIP or ValMBType = BLOCK_TYPE_B_SKIP
		do
			CurrValIsIntra := false;
		end

	SendAcrInter: action
			Coef_ACR       :[ValCoef] repeat 64
				==>
			Coef_ACR_Inter :[ValCoef] repeat 64
		end

	GetConstrIAndFstMbInter: action
				==>
		end	

	UpdateIsIntra: action
			MbLocation     :[MbLocxInPic , MbLocyInPic],
			NeighbourAvail :[NeighbourA , NeighbourB , NeighbourC , NeighbourD]
				==>
		do
			curr_pos    := MbLocxInPic;
		end	

	schedule fsm CheckMbType:
		CheckMbType    (MBTypeIsInterPSkip      )--> SendInterInfos2;
		CheckMbType    (MBTypeIsInter           )--> SendInterInfos1;
		CheckMbType    (MBTypeIsPCM             )--> SendPCMInfos1;
		CheckMbType    (MBTypeIsIntra           )--> SendIntraInfos1;
		CheckMbType    (CheckSlices             )--> GetIFlag;
		GetIFlag       (Get_IFlag               )--> CheckMbType;

		SendIntraInfos1 (SendAcrIntra            )--> SendIntraInfos2;
		SendIntraInfos2 (SendNeighbourIntra      )--> SendIntraInfos3;
		SendIntraInfos3 (SendInfosDone           )--> CheckMbType;

		SendInterInfos1 (SendAcrInter            )--> SendInterInfos2;
		SendInterInfos2 (GetConstrIAndFstMbInter )--> SendInterInfos3;
		SendInterInfos3 (UpdateIsIntra           )--> SendInterInfos4;
		SendInterInfos4 (SendInfosDone           )--> CheckMbType;

		SendPCMInfos1   (UpdateIsIntra           )--> SendPCMInfos2;
		SendPCMInfos2   (SendInfosDone           )--> CheckMbType;
	end

	priority
		MBTypeIsInterPSkip > MBTypeIsInter > MBTypeIsIntra > MBTypeIsPCM;
	end

end