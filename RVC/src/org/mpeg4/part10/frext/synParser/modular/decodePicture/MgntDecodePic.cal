/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>
package org.mpeg4.part10.frext.synParser.modular.decodePicture;

import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;
//Clean me !!
actor MgntDecodePic()
	int (size=32) ValVLD,

	int (size=6)  Header1TypeOfVLD,
	int (size=32) Header1VLDParam,
	int (size=6)  Header2TypeOfVLD,
	int (size=32) Header2VLDParam,
	uint(size=5)  DecRefPicMarkingTypeOfVLD,
	int (size=32) DecRefPicMarkingNbBitsToRead,
	uint(size=5)  RefPicListTypeOfVLD,
	int (size=32) RefPicListNbBitsToRead,
	int (size=6)  DataTypeOfVLD,
	int (size=32) DataVLDParam,
	int (size=6)  MbLayerTypeOfVLD,
	int (size=32) MbLayerVLDParam,
	uint(size=5)  MbPredTypeOfVLD,
	int (size=32) MbPredNbBitsToRead,
	uint(size=5)  SubMbPredTypeOfVLD,
	int (size=32) SubMbPredNbBitsToRead,
	int (size=6)  ResTypeOfVLD,
	int (size=32) ResActorLaunched,
	uint(size=5)  ResBlkCavlcTypeOfVLD,
	int (size=32) ResBlkCavlcVLD_Param,
	uint(size=5)  ResBlkCabacTypeOfVLD,
	int (size=32) ResBlkCabacVLD_Param
		==>
	uint(size=5)  TypeOfVLD,
	int (size=32) VLD_Param,
	int (size=32) Header1ValVLD,
	int (size=32) Header2ValVLD,
	int (size=32) DecRefPicMarkingValVLD,
	int (size=32) RefPicListValVLD,
	int (size=32) DataValVLD,
	int (size=32) MbLayerValVLD,
	int (size=32) MbPredValVLD,
	int (size=32) SubMbPredValVLD,
	int (size=32) ResBlkCavlcValVLD,
	int (size=32) ResBlkCabacValVLD
		:

	uint(size=8)  callStack[16];
	uint(size=32) nbParamToDecod := 0;
	uint(size=3)  stackIdx;

	initialize ==>
	do
		stackIdx     := 0;
		callStack[0] := SLICE_HEADER1;
	end

	findOutNextActor.isSliceHeader1: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER1
	end

	findOutNextActor.isSliceHeader2Avc: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER2
	end

	findOutNextActor.isRefPicMark: action ==>
	guard
		callStack[stackIdx] = REF_PIC_MARKING
	end

	findOutNextActor.isRefPicList: action ==>
	guard
		callStack[stackIdx] = REF_PIC_LIST
	end

	findOutNextActor.isSliceData: action ==>
	guard
		callStack[stackIdx] = SLICE_DATA
	end

	findOutNextActor.isMbLayer: action ==>
	guard
		callStack[stackIdx] = MB_LAYER
	end

	findOutNextActor.isMbPred: action ==>
	guard
		callStack[stackIdx] = MB_PRED
	end

	findOutNextActor.isSubMbPred: action ==>
	guard
		callStack[stackIdx] = SUB_MB_PRED
	end

	findOutNextActor.isResidual: action ==>
	guard
		callStack[stackIdx] = RESIDUAL
	end

	findOutNextActor.isResidualCavlc: action ==>
	guard
		callStack[stackIdx] = RESIDUAL_BLOCK_CAVLC
	end

	findOutNextActor.isResidualCabac: action ==>
	guard
		callStack[stackIdx] = RESIDUAL_BLOCK_CABAC
	end

	getVldVal.done: action ==>
	guard
		nbParamToDecod = 0
	end

	waitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	SliceHeader1.GetVldVal.Launch: action ValVLD :[VLDecoding] ==>
		Header1ValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceHeader1.vld.withoutParam: action Header1TypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceHeader1.vld.with1Param: action
		Header1TypeOfVLD :[ValTypeOfVLD],
		Header1VLDParam  :[Param]
			==>
		TypeOfVLD       :[ValTypeOfVLD],
		VLD_Param       :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceHeader1.LaunchActor: action
		Header1TypeOfVLD :[Function],
		Header1VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx       := (stackIdx + 1) &0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceHeader1.Done: action Header1TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	SliceHeader2.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		Header2ValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceHeader2.vld.withoutParam: action Header2TypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceHeader2.vld.with1Param: action
		Header2TypeOfVLD :[ValTypeOfVLD],
		Header2VLDParam  :[Param]
			==>
		TypeOfVLD       :[ValTypeOfVLD],
		VLD_Param       :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceHeader2.LaunchActor: action
		Header2TypeOfVLD :[Function],
		Header2VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceHeader2.Done: action Header2TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	RefPicMark.GetVLDVal.Launch: action
		ValVLD                 :[VLDecoding]
			==>
		DecRefPicMarkingValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	RefPicMark.vld.withoutParam: action
		DecRefPicMarkingTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD                 :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	RefPicMark.vld.with1Param: action
		DecRefPicMarkingTypeOfVLD    :[ValTypeOfVLD],
		DecRefPicMarkingNbBitsToRead :[Param]
			==>
		TypeOfVLD :[ValTypeOfVLD],
		VLD_Param :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	RefPicMark.Done: action DecRefPicMarkingTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	RefPicList.GetVLDVal.Launch: action
		ValVLD           :[VLDecoding]
			==>
		RefPicListValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	RefPicList.vld.withoutParam: action
		RefPicListTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD           :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	RefPicList.vld.with1Param: action
		RefPicListTypeOfVLD    :[ValTypeOfVLD],
		RefPicListNbBitsToRead :[Param]
			==>
		TypeOfVLD              :[ValTypeOfVLD],
		VLD_Param              :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	RefPicList.Done: action RefPicListTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	SliceData.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		DataValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceData.vld.withoutParam: action
		DataTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD     :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceData.vld.with1Param: action
		DataTypeOfVLD :[ValTypeOfVLD],
		DataVLDParam  :[Param]
			==>
		TypeOfVLD     :[ValTypeOfVLD],
		VLD_Param     :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceData.vld.with2Param: action
		DataTypeOfVLD :[typeOfVLD],
		DataVLDParam  :[Param] repeat 2
			==>
		TypeOfVLD     :[typeOfVLD],
		VLD_Param     :[Param] repeat 2
	guard
		NbParamForFunction(typeOfVLD) = 2
	do
		if(typeOfVLD != CABAC_INIT)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceData.vld.with5Param: action
		DataTypeOfVLD :[typeOfVLD],
		DataVLDParam  :[Param] repeat 5
			==>
		TypeOfVLD     :[typeOfVLD],
		VLD_Param     :[Param] repeat 5
	guard
		NbParamForFunction(typeOfVLD) = 5
	end

	SliceData.LaunchActor: action
		DataTypeOfVLD :[Function],
		DataVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceData.Done: action DataTypeOfVLD :[Function] ==>
		TypeOfVLD :[SKIP_NAL]
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	MbLayer.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==> MbLayerValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	MbLayer.vld.withoutParam: action
		MbLayerTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD        :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	MbLayer.vld.with1Param: action
		MbLayerTypeOfVLD :[ValTypeOfVLD],
		MbLayerVLDParam  :[Param]
			==>
		TypeOfVLD        :[ValTypeOfVLD],
		VLD_Param        :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbLayer.vld.with2Param: action
		MbLayerTypeOfVLD :[ValTypeOfVLD],
		MbLayerVLDParam  :[Param] repeat 2
			==>
		TypeOfVLD        :[ValTypeOfVLD],
		VLD_Param        :[Param] repeat 2
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbLayer.LaunchActor : action
		MbLayerTypeOfVLD :[Function],
		MbLayerVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	MbLayer.Done: action
		MbLayerTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	MbPred.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		MbPredValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	MbPred.vld.withoutParam: action MbPredTypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD       :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	MbPred.vld.with1Param: action
		MbPredTypeOfVLD    :[ValTypeOfVLD],
		MbPredNbBitsToRead :[VldParam]
			==>
		TypeOfVLD          :[ValTypeOfVLD],
		VLD_Param          :[VldParam]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbPred.vld.with2Param: action
		MbPredTypeOfVLD    :[ValTypeOfVLD],
		MbPredNbBitsToRead :[VldParam] repeat 2
			==>
		TypeOfVLD          :[ValTypeOfVLD],
		VLD_Param          :[VldParam] repeat 2
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbPred.Done: action MbPredTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	SubMbPred.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
			SubMbPredValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SubMbPred.vld.withoutParam: action SubMbPredTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD          :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SubMbPred.vld.with1Param: action
		SubMbPredTypeOfVLD    :[ValTypeOfVLD],
		SubMbPredNbBitsToRead :[VldParam]
			==>
		TypeOfVLD             :[ValTypeOfVLD],
		VLD_Param             :[VldParam]
	guard
			NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SubMbPred.Done: action
		SubMbPredTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end


	Residual.LaunchActor: action
		ResTypeOfVLD     :[Function],
		ResActorLaunched :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	Residual.Done: action
		ResTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	ResBlkCavlc.GetVLDVal.Launch: action
		ValVLD            :[VLDecoding]
			==>
		ResBlkCavlcValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	ResBlkCavlc.vld.withoutParam: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD            :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	ResBlkCavlc.vld.with1Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCavlc.FunctionWith2Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param1 , Param2]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param1 , Param2]
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCavlc.Done: action
		ResBlkCavlcTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	ResBlkCabac.GetVLDVal.Launch: action
		ValVLD            :[VLDecoding]
			==>
		ResBlkCabacValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	ResBlkCabac.vld.withoutParam: action
		ResBlkCabacTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD            :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	ResBlkCabac.vld.with1Param: action
		ResBlkCabacTypeOfVLD :[ValTypeOfVLD],
		ResBlkCabacVLD_Param :[Param]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCabac.vld.with2Param: action
		ResBlkCabacTypeOfVLD :[ValTypeOfVLD],
		ResBlkCabacVLD_Param :[Param1 , Param2]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param1 , Param2]
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCabac.Done: action
		ResBlkCabacTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	schedule fsm FindOutNextActor:
		FindOutNextActor (findOutNextActor.isSliceHeader1    )--> LaunchSlcHead;
		FindOutNextActor (findOutNextActor.isSliceHeader2Avc )--> LaunchSlcHead2Avc;
		FindOutNextActor (findOutNextActor.isRefPicMark      )--> LaunchRefPicMark;
		FindOutNextActor (findOutNextActor.isRefPicList      )--> LaunchRefPicList;
		FindOutNextActor (findOutNextActor.isSliceData       )--> LaunchSliceData;
		FindOutNextActor (findOutNextActor.isMbLayer         )--> LaunchMbLayer;
		FindOutNextActor (findOutNextActor.isMbPred          )--> LaunchMbPred;
		FindOutNextActor (findOutNextActor.isSubMbPred       )--> LaunchSubMbPred;
		FindOutNextActor (findOutNextActor.isResidual        )--> LaunchResidual;
		FindOutNextActor (findOutNextActor.isResidualCavlc   )--> LaunchResBlkCavlc;
		FindOutNextActor (findOutNextActor.isResidualCabac   )--> LaunchResBlkCabac;

		LaunchSlcHead (SliceHeader1.vld                  )--> LaunchSlcHead;
		LaunchSlcHead (waitVLDVal                        )--> LaunchSliceHeaderGetVLDVal;
		LaunchSlcHead (SliceHeader1.LaunchActor          )--> FindOutNextActor;
		LaunchSlcHead (SliceHeader1.Done                 )--> LaunchSlcHead;
			LaunchSliceHeaderGetVLDVal (SliceHeader1.GetVldVal.Launch )--> LaunchSliceHeaderGetVLDVal;
			LaunchSliceHeaderGetVLDVal (getVldVal.done   )--> LaunchSlcHead;

		LaunchSlcHead2Avc (SliceHeader2.vld )--> LaunchSlcHead2Avc;
		LaunchSlcHead2Avc (waitVLDVal           )--> LaunchSlcHead2AvcGetVLDVal;
		LaunchSlcHead2Avc (SliceHeader2.LaunchActor          )--> FindOutNextActor;
		LaunchSlcHead2Avc (SliceHeader2.Done                 )--> FindOutNextActor;
			LaunchSlcHead2AvcGetVLDVal (SliceHeader2.GetVLDVal.Launch )--> LaunchSlcHead2AvcGetVLDVal;
			LaunchSlcHead2AvcGetVLDVal (getVldVal.done   )--> LaunchSlcHead2Avc;
	
		LaunchRefPicMark (RefPicMark.vld       )--> LaunchRefPicMark;
		LaunchRefPicMark (waitVLDVal           )--> LaunchRefPicMarkGetVLDVal;
		LaunchRefPicMark (RefPicMark.Done                 )--> LaunchSlcHead2Avc;
			LaunchRefPicMarkGetVLDVal (RefPicMark.GetVLDVal.Launch )--> LaunchRefPicMarkGetVLDVal;
			LaunchRefPicMarkGetVLDVal (getVldVal.done   )--> LaunchRefPicMark;
		
		LaunchRefPicList (RefPicList.vld       )--> LaunchRefPicList;
		LaunchRefPicList (waitVLDVal           )--> LaunchRefPicListGetVLDVal;
		LaunchRefPicList (RefPicList.Done                 )--> LaunchSlcHead2Avc;
			LaunchRefPicListGetVLDVal (RefPicList.GetVLDVal.Launch )--> LaunchRefPicListGetVLDVal;
			LaunchRefPicListGetVLDVal (getVldVal.done   )--> LaunchRefPicList;
	
		LaunchSliceData   (SliceData.vld          )--> LaunchSliceData;
		LaunchSliceData   (waitVLDVal             )--> LaunchSliceDataGetVLDVal;
		LaunchSliceData   (SliceData.LaunchActor            )--> FindOutNextActor;
		LaunchSliceData   (SliceData.Done                   )--> FindOutNextActor;
			LaunchSliceDataGetVLDVal   (SliceData.GetVLDVal.Launch )--> LaunchSliceDataGetVLDVal;
			LaunchSliceDataGetVLDVal   (getVldVal.done  )--> LaunchSliceData;
		
		LaunchMbLayer    (MbLayer.vld    )--> LaunchMbLayer;
		LaunchMbLayer    (waitVLDVal              )--> LaunchMbLayerGetVLDVal;
		LaunchMbLayer    (MbLayer.Done                    )--> FindOutNextActor;
		LaunchMbLayer    (MbLayer.LaunchActor             )--> FindOutNextActor;
			LaunchMbLayerGetVLDVal    (MbLayer.GetVLDVal.Launch    )--> LaunchMbLayerGetVLDVal;
			LaunchMbLayerGetVLDVal    (getVldVal.done      )--> LaunchMbLayer;
		
		LaunchMbPred    (MbPred.vld    )--> LaunchMbPred;
		LaunchMbPred    (waitVLDVal    )--> LaunchMbPredGetVLDVal;
		LaunchMbPred    (MbPred.Done   )--> FindOutNextActor;
			LaunchMbPredGetVLDVal    (MbPred.GetVLDVal.Launch )--> LaunchMbPredGetVLDVal;
			LaunchMbPredGetVLDVal    (getVldVal.done   )--> LaunchMbPred;
		
		LaunchSubMbPred (SubMbPred.vld )--> LaunchSubMbPred;
		LaunchSubMbPred (waitVLDVal           )--> LaunchSubMbPredGetVLDVal;
		LaunchSubMbPred (SubMbPred.Done                 )--> FindOutNextActor;
			LaunchSubMbPredGetVLDVal (SubMbPred.GetVLDVal.Launch )--> LaunchSubMbPredGetVLDVal;
			LaunchSubMbPredGetVLDVal (getVldVal.done   )--> LaunchSubMbPred;
	
		LaunchResidual  (Residual.Done                  )--> FindOutNextActor;
		LaunchResidual  (Residual.LaunchActor           )--> FindOutNextActor;
	
		LaunchResBlkCavlc (ResBlkCavlc.vld )--> LaunchResBlkCavlc;
		LaunchResBlkCavlc (ResBlkCavlc.FunctionWith2Param   )--> LaunchResBlkCavlc;
		LaunchResBlkCavlc (waitVLDVal          )--> LaunchResBlkCavlcGetVLDVal;
		LaunchResBlkCavlc (ResBlkCavlc.Done                 )--> FindOutNextActor;
			LaunchResBlkCavlcGetVLDVal (ResBlkCavlc.GetVLDVal.Launch      )--> LaunchResBlkCavlcGetVLDVal;
			LaunchResBlkCavlcGetVLDVal (getVldVal.done        )--> LaunchResBlkCavlc;

		LaunchResBlkCabac (ResBlkCabac.vld  )--> LaunchResBlkCabac;
		LaunchResBlkCabac (waitVLDVal       )--> LaunchResBlkCabacGetVLDVal;
		LaunchResBlkCabac (ResBlkCabac.Done )--> FindOutNextActor;
			LaunchResBlkCabacGetVLDVal (ResBlkCabac.GetVLDVal.Launch      )--> LaunchResBlkCabacGetVLDVal;
			LaunchResBlkCabacGetVLDVal (getVldVal.done        )--> LaunchResBlkCabac;
	end
	priority
		SliceHeader1.vld > waitVLDVal > SliceHeader1.LaunchActor;
		SliceHeader1.LaunchActor      > SliceHeader1.Done;
		SliceHeader1.GetVldVal.Launch > getVldVal.done;

		SliceHeader2.vld > waitVLDVal > SliceHeader2.LaunchActor;
		SliceHeader2.LaunchActor > SliceHeader2.Done;
		SliceHeader2.GetVLDVal.Launch > getVldVal.done;

		RefPicMark.vld  > waitVLDVal > RefPicMark.Done;
		RefPicMark.GetVLDVal.Launch > getVldVal.done;
		RefPicList.vld  > waitVLDVal > RefPicList.Done;
		RefPicList.GetVLDVal.Launch > getVldVal.done;

		SliceData.vld                    > waitVLDVal                     > SliceData.LaunchActor        > SliceData.Done;
		SliceData.GetVLDVal.Launch       > getVldVal.done;
		MbLayer.vld   > waitVLDVal             > MbLayer.LaunchActor         > MbLayer.Done;
		MbLayer.GetVLDVal.Launch         > getVldVal.done;
		MbPred.vld      > waitVLDVal              > MbPred.Done;
		MbPred.GetVLDVal.Launch          > getVldVal.done;
		SubMbPred.vld   > waitVLDVal           > SubMbPred.Done;
		SubMbPred.GetVLDVal.Launch       > getVldVal.done;
		ResBlkCavlc.vld > ResBlkCavlc.FunctionWith2Param > waitVLDVal > ResBlkCavlc.Done;
		ResBlkCavlc.GetVLDVal.Launch     > getVldVal.done;
		ResBlkCabac.vld > waitVLDVal > ResBlkCabac.Done;
		ResBlkCabac.GetVLDVal.Launch     > getVldVal.done;
	end
end
