/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>

package org.mpeg4.part10.frext.synParser.modular.decodePicture;

import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;
import org.mpeg4.part10.cbp.synParser.modular.cavlcLookUpTables.*;
import org.mpeg4.part10.frext.synParser.modular.decodePicture.cabacDecoding.*;
import org.mpeg4.part10.cbp.MacroBlockInfo.*;
import std.util.BitOps.*;
import std.util.Math.*;

actor Algo_VariableLengthDecoding()
	uint(size=8)  RbspByte,
	bool          NewNal,

	uint(size=5)   TypeOfVLD,
	int (size=32)  VLD_Param
		==>
	int (size=32)  ValDecoded
		:

	bool DEBUG_FLAG_DECODED = false;
	bool DEBUG_CABAC = false;

	uint(size=4) SLICE_TYPE_P  = 1;
	uint(size=4) SLICE_TYPE_B  = 1;

	uint(size=2) RENORM_D       = 0;
	uint(size=2) DECODE_BYPASS  = 1;
	uint(size=2) SKIP           = 2;

	//Contains the Byte which is currently read.
	uint(size=16) WorkingByte      := 0;
	//Contains the number of bit which are not read yet in WorkingByte
	uint(size=4)  CountBufferRbsp  := 0;
	//Contains up to 4 next byte in the flow. It helps for more_rbsp_data()
	//and Cabac initialisation
	uint(size=3)  SizeBufferRbsp   := 0;
	List(type: uint(size=8), size=4) BufferRbsp := [0: for int s in 0 .. 3];
	/* ue(v) and Cavlc coeff_token()*/
	bool          Bit1Found       := false;
	uint(size=5)  leadingZeroBits := 0;
	/* ue(v) only */
	int (size=31) CodeNum         := 0;
	/* u(n) */
	uint(size=5)  BitsCount       := 0;
	uint(size=31) ReadValue       := 0;
	uint(size=1)  TeVal           := 0;
	/* more_rbsp_data() */
	bool          NewNalAlreadyDetected  := false;

	/* Cavlc */
	uint(size=5) NbBitsRead;
	/* Cavlc coeff_token */
	uint(size=3) Idx_nC;
	uint(size=6) coeff_token;
	/* Cavlc total_zeros and run_before */
	uint(size=3) ValRead         := 0;
	/* Cavlc total_zeros */
	uint(size=2) IdxmaxNumCoeff  := 0;
	uint(size=4) TotalCoeffParam := 0;
	/* Cavlc run_before */
	uint(size=8) IdxzerosLeft    := 0;

	/* Cabac inittialisation */
	int (size=3) cabacInitIdc;
	int (size=6) pStateIdx[640];
	int (size=8) sliceQpY;
	uint(size=1) valMPS[640];
	/* Cabac Arithmetic Decoding */
	int (size=16) codIRange;
	int (size=16) codIOffset;
	/* Cabac Mb type parsing */
	uint(size=5)  syntaxElement;
	int (size=6)  binIdx;
	uint(size=1)  binVal;
	uint(size=32) binString;
	uint(size=32) binStringPrefix;
	uint(size=3)  cabacFLRemainBits;
	uint(size=8)  cMax;
	bool          hasSuffix     := false;
	bool          prefixDecoded;
	uint(size=2)  cabacEndDecodeBin;
	uint(size=11) ctxIdx;
	bool          byPass;
	bool          signedVal;

	bool          askSignedFlag;
	uint(size=4)  ctxBlockCat;
	uint(size=6)  currMbType;
	uint(size=3)  currMbPartIdx;
	uint(size=6)  currSubMbType;
	uint(size=3)  currSubMbPartIdx;
	uint(size=4)  levelListIdx;
	uint(size=4)  luma4x4Idx;
	bool          first0Found := true;
	uint(size=6)  mbTypeNeighbours[2][16];
	bool          neighboursAvail[2][16] :=
		[[true : for int s in 0 .. 15]: for int t in 0 .. 1];
	uint(size=1)  neighboursCbfChroma[2][17];
	uint(size=1)  neighboursCbfLumaAc[2][16];
	uint(size=1)  neighboursCbfLumaDc[2];
	uint(size=2)  neighboursCbpLuma[2];
	uint(size=2)  neighboursCbpChroma[2];
	uint(size=4)  neighboursIntraPredC[2];
	uint(size=3)  neighbourMbPartIdx[16][2];
	bool          neighbourMbSkip[2];
	int(size=16)  neighbourMvdLX[16][2][2];
	uint(size=4)  neighbourSubMbType[16][2];
	uint(size=5)  numDecodAbsLvlEq1;
	uint(size=5)  numDecodAbsLvlGt1;
	uint(size=6)  prevCbp;
	int(size=8)   prevMbQpDelta;
	uint(size=1)  priorDecBinVal[4];
	bool          signedFlag;
	bool          signedFlagDecoded;
	uint(size=4)  sliceType;


	/* Cabac procedures */

	// Table 9-39
	procedure getCtxIdx(uint(size=11) ctxIdxOffset)
	begin
		uint(size=2) invNeighbour8x8A[4] = [1, 0, 3, 2];
		uint(size=2) invNeighbour8x8B[4] = [2, 3, 0, 1];
		uint(size=2) ctxIdxInc;

		//Remove these variable when the bug #49 will be fixed
		uint(size=1) priorTab[2];
		bool         isCurrMbTab[2];
		uint(size=1) neighbourCbf[2];
		bool         neighbAvail[2];
		uint(size=6) neighbMbType[2];
		uint(size=4) neighboursBlk8x8Idx[2];
		uint(size=3) locNeighbMbPtIdx[2];
		uint(size=4) locNeighbSubMbType[2];
		int(size=16) locNeighbMvdLX[2][2];


		if(syntaxElement != CABAC_CODED_BLOCK_FLAG and
		   syntaxElement != CABAC_SIGNIFICANT_COEFF_FLAG and
		   syntaxElement != CABAC_LAST_SIGNIFICANT_COEFF_FLAG and
		   syntaxElement != CABAC_COEFF_ABS_LEVEL_MINUS1) then
			if(binIdx = 0) then
				if(ctxIdxOffset = 0 or ctxIdxOffset = 3 or ctxIdxOffset = 27) then
					neighbAvail := [neighboursAvail[0][0], neighboursAvail[1][0]];
					neighbMbType := [mbTypeNeighbours[0][0], mbTypeNeighbours[1][0]];
					ctxIdxInc := getCtxIdxIncMbType(neighbAvail,
						neighbMbType, ctxIdxOffset);
				else if(ctxIdxOffset = 11 or ctxIdxOffset = 24) then
					neighbAvail := [neighboursAvail[0][0], neighboursAvail[1][0]];
					ctxIdxInc := getCtxIdxIncMbSkipFlag(neighbAvail,
						neighbourMbSkip);
				else if(ctxIdxOffset = 40 or ctxIdxOffset = 47) then
					neighbAvail := [neighboursAvail[0][luma4x4Idx],
						neighboursAvail[1][luma4x4Idx]];
					neighbMbType := [mbTypeNeighbours[0][luma4x4Idx],
						mbTypeNeighbours[1][luma4x4Idx]];
					locNeighbMbPtIdx := [neighbourMbPartIdx[luma4x4Idx][0],
						neighbourMbPartIdx[luma4x4Idx][1]];
					locNeighbSubMbType := [neighbourSubMbType[luma4x4Idx][0],
						neighbourSubMbType[luma4x4Idx][1]];
					foreach int i in 0 .. 1
					do
						foreach int j in 0 .. 1
						do
							locNeighbMvdLX[i][j] :=
								neighbourMvdLX[luma4x4Idx][i][j];
						end
					end
					ctxIdxInc := getCtxIdxIncMvdLX(ctxIdxOffset, neighbAvail,
						neighbMbType, locNeighbMbPtIdx, true,
						locNeighbSubMbType,
						locNeighbMvdLX);
				else if(ctxIdxOffset = 54) then
					/*ctxIdxInc := getCtxIdxIncRefIdx();*/
				else if(ctxIdxOffset = 60) then
					// /!\ should be the previous Mb and not the neighbour for avail
					// and mbtype
					ctxIdxInc := getCtxIdxIncMbQpDelta(neighboursAvail[0][0],
						mbTypeNeighbours[0][0], prevCbp,  prevMbQpDelta);
				else if(ctxIdxOffset = 64) then
					neighbAvail := [neighboursAvail[0][0], neighboursAvail[1][0]];
					neighbMbType := [mbTypeNeighbours[0][0], mbTypeNeighbours[1][0]];
					ctxIdxInc := getCtxIdxIncIntraChromPred(neighbAvail,
						neighbMbType, neighboursIntraPredC);
				else if(ctxIdxOffset = 70) then
					/* We don't manage field */
	//				ctxIdxInc := getCtxIdxIncMbFieldDec([neighbourAAvail,
	//					neighbourBAvail], [true, true]);
				else if(ctxIdxOffset = 73 or ctxIdxOffset = 77) then
					priorTab    := [0,0];
					isCurrMbTab := [false,false];
					neighbAvail := [neighboursAvail[0][binIdx<<2], neighboursAvail[1][binIdx<<2]];
					neighbMbType := [mbTypeNeighbours[0][binIdx<<2], mbTypeNeighbours[1][binIdx<<2]];
					neighboursBlk8x8Idx := [invNeighbour8x8A[binIdx], invNeighbour8x8B[binIdx]];
					ctxIdxInc := getCtxIdxIncCodBlkPattern(binIdx, ctxIdxOffset,
						neighbAvail, neighbMbType, isCurrMbTab/*[false, false]*/,
						neighboursCbpLuma, neighboursBlk8x8Idx, priorTab/*[0,0]*/,
						neighboursCbpChroma);
				else if(ctxIdxOffset = 399) then
					/*ctxIdxInc := getCtxIdxIncTrsfSz8x8(bool neighbourAvail[2],
						bool neighbourTrsfSz8x8[2]);*/
				else
					ctxIdxInc := 0;
				end end end end end end end end end
			else if(binIdx = 1)then
				if(ctxIdxOffset = 3 or ctxIdxOffset = 17 or ctxIdxOffset = 32) then
					ctxIdxInc    := 0;
					ctxIdxOffset := 276;
				else if(ctxIdxOffset = 73 or ctxIdxOffset = 77) then
					priorTab    := [priorDecBinVal[0],0];
					isCurrMbTab := [true,false];
					neighbAvail :=
						if(ctxIdxOffset = 73) then
							[neighboursAvail[0][binIdx<<2], neighboursAvail[1][binIdx<<2]]
						else
							[neighboursAvail[0][0], neighboursAvail[1][0]]
						end;
					neighbMbType :=
						if(ctxIdxOffset = 73) then
							[mbTypeNeighbours[0][binIdx<<2], mbTypeNeighbours[1][binIdx<<2]]
						else
							[mbTypeNeighbours[0][0], mbTypeNeighbours[1][0]]
						end;
					neighboursBlk8x8Idx := [invNeighbour8x8A[binIdx], invNeighbour8x8B[binIdx]];
					ctxIdxInc := getCtxIdxIncCodBlkPattern(binIdx, ctxIdxOffset, 
						neighbAvail, neighbMbType, isCurrMbTab/*[true, false]*/,
						neighboursCbpLuma, neighboursBlk8x8Idx, priorTab/*[priorDecBinVal[0],0]*/,
						neighboursCbpChroma);
				else if(ctxIdxOffset = 14 or ctxIdxOffset = 21 or ctxIdxOffset = 36)
					then
					ctxIdxInc := 1;
				else if(ctxIdxOffset = 27 or ctxIdxOffset = 40 or ctxIdxOffset = 47
					or ctxIdxOffset = 64) then
					ctxIdxInc := 3;
				else if(ctxIdxOffset = 54) then
					ctxIdxInc := 4;
				else if(ctxIdxOffset = 60) then
					ctxIdxInc := 2;
				else
					ctxIdxInc := 0;
				end end end end end end
			else if(binIdx = 2) then
				if(ctxIdxOffset = 14 or ctxIdxOffset = 27 or ctxIdxOffset = 36)
					then
					ctxIdxInc := getCtxIdxIncPriorDec(ctxIdxOffset, binIdx,
						priorDecBinVal);
				else if(ctxIdxOffset = 73) then
					priorTab    := [0,priorDecBinVal[0]];
					isCurrMbTab := [false,true];
					neighbAvail := [neighboursAvail[0][binIdx<<2], neighboursAvail[1][binIdx<<2]];
					neighbMbType := [mbTypeNeighbours[0][binIdx<<2], mbTypeNeighbours[1][binIdx<<2]];
					neighboursBlk8x8Idx := [invNeighbour8x8A[binIdx], invNeighbour8x8B[binIdx]];
					ctxIdxInc := getCtxIdxIncCodBlkPattern(binIdx, ctxIdxOffset, 
						neighbAvail, neighbMbType, isCurrMbTab/*[false,true]*/,
						neighboursCbpLuma, neighboursBlk8x8Idx, priorTab/*[0,priorDecBinVal[0]]*/,
						neighboursCbpChroma);
				else if(ctxIdxOffset = 3 or ctxIdxOffset = 60 or ctxIdxOffset = 64)
					then
					ctxIdxInc := 3;
				else if(ctxIdxOffset = 17 or ctxIdxOffset = 32) then
					ctxIdxInc := 1;
				else if(ctxIdxOffset = 21) then
					ctxIdxInc := 2;
				else if(ctxIdxOffset = 40 or ctxIdxOffset = 47) then
					ctxIdxInc := 4;
				else if(ctxIdxOffset = 54) then
					ctxIdxInc := 5;
				else
					ctxIdxInc := 0;
				end end end end end end end
			else if(binIdx = 3) then
				if(ctxIdxOffset = 3) then
					ctxIdxInc := 4;
				else if(ctxIdxOffset = 17 or ctxIdxOffset = 32) then
					ctxIdxInc := 2;
				else if(ctxIdxOffset = 27 or ctxIdxOffset = 40 or ctxIdxOffset = 47
					or ctxIdxOffset = 54) then
					ctxIdxInc := 5;
				else if(ctxIdxOffset = 36 or ctxIdxOffset = 60) then
					ctxIdxInc := 3;
				else
					priorTab    := [priorDecBinVal[2],priorDecBinVal[1]];
					isCurrMbTab := [true,true];
					neighbAvail := [neighboursAvail[0][binIdx<<2], neighboursAvail[1][binIdx<<2]];
					neighbMbType := [mbTypeNeighbours[0][binIdx<<2], mbTypeNeighbours[1][binIdx<<2]];
					neighboursBlk8x8Idx := [invNeighbour8x8A[binIdx], invNeighbour8x8B[binIdx]];
					ctxIdxInc := getCtxIdxIncCodBlkPattern(binIdx, ctxIdxOffset, 
						neighbAvail, neighbMbType, isCurrMbTab/*[true,true]*/,
						neighboursCbpLuma, neighboursBlk8x8Idx, priorTab/*[priorDecBinVal[2],priorDecBinVal[1]]*/,
						neighboursCbpChroma);
				end end end end
			else if(binIdx = 4) then
				if(ctxIdxOffset = 3 or ctxIdxOffset = 17 or ctxIdxOffset = 32)
					then
					ctxIdxInc := getCtxIdxIncPriorDec(ctxIdxOffset, binIdx,
						priorDecBinVal);
				else if(ctxIdxOffset = 27 or ctxIdxOffset = 54) then
					ctxIdxInc := 5;
				else if(ctxIdxOffset = 36 or ctxIdxOffset = 60) then
					ctxIdxInc := 3;
				else if(ctxIdxOffset = 40 or ctxIdxOffset = 47) then
					ctxIdxInc := 6;
				end end end end
			else if(binIdx = 5) then
				if(ctxIdxOffset = 3) then
					ctxIdxInc := getCtxIdxIncPriorDec(ctxIdxOffset, binIdx,
						priorDecBinVal);
				else if(ctxIdxOffset = 17 or ctxIdxOffset = 32 or ctxIdxOffset = 36
					or ctxIdxOffset = 60) then
					ctxIdxInc := 3;
				else if(ctxIdxOffset = 27 or ctxIdxOffset = 54) then
					ctxIdxInc := 5;
				else if(ctxIdxOffset = 40 or ctxIdxOffset = 47) then
					ctxIdxInc := 6;
				end end end end
			else
				if(ctxIdxOffset = 3) then
					ctxIdxInc := 7;
				else if(ctxIdxOffset = 17 or ctxIdxOffset = 32 or ctxIdxOffset = 60)
					then
					ctxIdxInc := 3;
				else if(ctxIdxOffset = 27 or ctxIdxOffset = 54) then
					ctxIdxInc := 5;
				else if(ctxIdxOffset = 40 or ctxIdxOffset = 47) then
					ctxIdxInc := 6;
				end end end end
			end end end end end end
		else if(syntaxElement = CABAC_CODED_BLOCK_FLAG) then
			neighbAvail := [neighboursAvail[0][luma4x4Idx], neighboursAvail[1][luma4x4Idx]];
			neighbMbType := [mbTypeNeighbours[0][luma4x4Idx], mbTypeNeighbours[1][luma4x4Idx]];
			neighbourCbf :=
				if(ctxBlockCat = 0) then
					[neighboursCbfLumaDc[0], neighboursCbfLumaDc[1]]
				else if(ctxBlockCat <= 2) then
					[neighboursCbfLumaAc[0][luma4x4Idx], neighboursCbfLumaAc[1]
						[luma4x4Idx]]
				else if(ctxBlockCat = 3) then
					[neighboursCbfChroma[0][0], neighboursCbfChroma[1][0]]
				else
					[neighboursCbfChroma[0][1+luma4x4Idx],
						neighboursCbfChroma[1][1+luma4x4Idx]]
				end end end;
			ctxIdxInc := getCtxIdxIncCodedBlockFlag(neighbAvail, currMbType,
				neighbMbType, neighbourCbf);
			ctxIdxInc := ctxIdxInc + ctxIdxBlockCatOffset[0][ctxBlockCat];
		else if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1) then
			ctxIdxInc := getCtxIdxIncCoeffAbsLvlMin1(ctxBlockCat, binIdx,
				numDecodAbsLvlGt1, numDecodAbsLvlEq1);
			ctxIdxInc := ctxIdxInc + ctxIdxBlockCatOffset[2][ctxBlockCat];
		else
			ctxIdxInc := getCtxIdxIncSignCoeff(ctxBlockCat, levelListIdx);
			ctxIdxInc := ctxIdxInc + ctxIdxBlockCatOffset[1][ctxBlockCat];
		end end end

		ctxIdx := ctxIdxOffset + ctxIdxInc;
	end

	//9.3.3.2.1
	procedure decodeDecision()
	begin
		uint(size=2) qCodIRangeIdx;
		uint(size=8) codIRangeLPS;

		qCodIRangeIdx := (codIRange >> 6) & 0x03;
		codIRangeLPS  := rangeTabLPS[pStateIdx[ctxIdx]][qCodIRangeIdx];
		codIRange     := codIRange - codIRangeLPS;

		if(DEBUG_CABAC) then
	  		println("codIRange := "+codIRange+ " codIOffset := " + codIOffset+"  codIRangeLPS : "+ codIRangeLPS);
	  	end
		if(codIOffset >= codIRange) then
			binVal     := 1 - valMPS[ctxIdx];
			codIOffset := codIOffset - codIRange;
			codIRange  := codIRangeLPS;
			
			if pStateIdx[ctxIdx] = 0 then
				valMPS[ctxIdx] := 1 - valMPS[ctxIdx];
			end
			pStateIdx[ctxIdx] := transIdxLPS[pStateIdx[ctxIdx]];
		else
			binVal := valMPS[ctxIdx]; 
			pStateIdx[ctxIdx] := transIdxMPS[pStateIdx[ctxIdx]];
		end
	end

	procedure updateMbPartIdx(uint(size=3) sizeMbPart)
	begin
		uint(size=4) neighbourAIdx[12] = [1, 3, 4, 5, 6, 7,  9, 11, 12, 13, 14, 15];
		uint(size=4) neighbourBIdx[12] = [2, 3, 6, 7, 8, 9, 10, 11, 12, 13, 14 ,15];
		uint(size=4) columnLumaIdx[4]  = [0, 2, 8, 10];
		uint(size=4) lineLumaIdx[4]    = [0, 1, 4, 5];
		uint(size=4) neighbA8x8[4]     = [1, 3, 4, 6];
		uint(size=4) neighbB8x8[4]     = [2, 3, 8, 9];

		if(sizeMbPart = SZ_16x16) then
			foreach int i in 0 .. 11
			do
				neighbourMbPartIdx[neighbourAIdx[i]][0] := 0;
				neighbourMbPartIdx[neighbourBIdx[i]][1] := 0;
			end
		else if(sizeMbPart = SZ_16x8) then
			foreach uint x in 1 .. 3
			do
				foreach uint y in 0 .. 1
				do
					neighbourMbPartIdx[lineLumaIdx[x] + 2*y][1] := 0;
					neighbourMbPartIdx[lineLumaIdx[x] + 2*y+8][1] := 1;
				end
			end
			foreach uint i in 0 .. 3
			do
				neighbourMbPartIdx[lineLumaIdx[i]+2][1] := 0;
				neighbourMbPartIdx[lineLumaIdx[i]+8][1] := 0;
				neighbourMbPartIdx[lineLumaIdx[i]+10][1] := 1;
			end
		else if(sizeMbPart = SZ_8x16) then
			foreach uint i in 0 .. 3
			do
				neighbourMbPartIdx[columnLumaIdx[i]+1][0] := 0;
				neighbourMbPartIdx[columnLumaIdx[i]+4][0] := 0;
				neighbourMbPartIdx[columnLumaIdx[i]+5][0] := 1;
			end
			foreach uint x in 0 .. 1
			do
				foreach uint y in 1 .. 3
				do
					neighbourMbPartIdx[x + columnLumaIdx[y]][1] := 0;
					neighbourMbPartIdx[x+4 + columnLumaIdx[y]][1] := 1;
				end
			end
		else
			neighbourMbPartIdx[5][0] := 1;
			neighbourMbPartIdx[7][0] := 1;
			neighbourMbPartIdx[13][0] := 3;
			neighbourMbPartIdx[15][0] := 3;
			foreach int i in 0 .. 3
			do
				neighbourMbPartIdx[neighbA8x8[i]][0] := 0;
				neighbourMbPartIdx[neighbA8x8[i]+8][0] := 2;
			end
			neighbourMbPartIdx[10][1] := 2;
			neighbourMbPartIdx[11][1] := 2;
			neighbourMbPartIdx[14][1] := 3;
			neighbourMbPartIdx[15][1] := 3;
			foreach int i in 0 .. 3
			do
				neighbourMbPartIdx[neighbB8x8[i]][1] := 0;
				neighbourMbPartIdx[neighbB8x8[i]+4][1] := 1;
			end
		end end end
	end

	procedure updateSubMbPartIdx(uint(size=4) sizeSubMbPart, uint(size=2) mbPartIdx)
	begin
		uint(size=4) invNeighbourAIdx[16] = [1, 4, 3, 6, 5, 0, 7, 0, 9, 12, 11,
			14, 13, 0, 15, 0];
		uint(size=4) invNeighbourBIdx[16] = [2, 3, 8, 9, 6, 7, 12, 13, 10, 11,
			0, 0, 14, 15, 0, 0];
		uint(size=4) idx;

		if(sizeSubMbPart = SZ_8x8) then
			foreach uint i in 0 .. 3
			do
				idx := 4*mbPartIdx + i;
				neighbourMbPartIdx[invNeighbourAIdx[idx]][0] := 0;
				neighbourMbPartIdx[invNeighbourBIdx[idx]][1] := 0;
			end
		else if(sizeSubMbPart = SZ_8x4) then
			foreach uint i in 0 .. 1
			do
				idx := 4*mbPartIdx + i;
				neighbourMbPartIdx[invNeighbourAIdx[idx]][0] := 0;
				neighbourMbPartIdx[invNeighbourBIdx[idx]][1] := 0;
				neighbourMbPartIdx[invNeighbourAIdx[idx]+2][0] := 1;
				neighbourMbPartIdx[invNeighbourBIdx[idx]+2][1] := 1;
			end
		else if(sizeSubMbPart = SZ_4x8) then
			foreach uint i in 0 .. 1
			do
				idx := 4*mbPartIdx + 2*i;
				neighbourMbPartIdx[invNeighbourAIdx[idx]][0] := 0;
				neighbourMbPartIdx[invNeighbourBIdx[idx]][1] := 0;
				neighbourMbPartIdx[invNeighbourAIdx[idx]+1][0] := 1;
				neighbourMbPartIdx[invNeighbourBIdx[idx]+1][1] := 1;
			end
		else
			foreach uint i in 0 .. 3
			do
				idx := 4*mbPartIdx + i;
				neighbourMbPartIdx[invNeighbourAIdx[idx]][0] := i;
				neighbourMbPartIdx[invNeighbourBIdx[idx]][1] := i;
			end
		end end end
	end

	procedure updateNeighbourSubMvdLX(uint(size=4) sizeSubMbPart, uint(size=2) mbPartIdx, uint(size=2) subMbPartIdx, int(size=16) mvd)
	begin
		uint(size=4) invNeighbourAIdx[16] = [1, 4, 3, 6, 5, 0, 7, 0, 9, 12, 11,
			14, 13, 0, 15, 0];
		uint(size=4) invNeighbourBIdx[16] = [2, 3, 8, 9, 6, 7, 12, 13, 10, 11,
			0, 0, 14, 15, 0, 0];
		uint(size=4) idx;
		uint(size=2) idxLX =
			if(syntaxElement = CABAC_MVD_LX_0) then
				0
			else
				1
			end;

		if(sizeSubMbPart = SZ_8x8) then
			foreach uint i in 0 .. 3
			do
				idx := 4*mbPartIdx + i;
				neighbourMvdLX[invNeighbourAIdx[idx]][0][idxLX] := mvd;
				neighbourMvdLX[invNeighbourBIdx[idx]][1][idxLX] := mvd;
			end
		else if(sizeSubMbPart = SZ_8x4) then
			foreach uint i in 0 .. 1
			do
				idx := 4*mbPartIdx + i;
				neighbourMvdLX[invNeighbourAIdx[idx]+2*subMbPartIdx][0][idxLX]
					:= mvd;
				neighbourMvdLX[invNeighbourBIdx[idx]+2*subMbPartIdx][1][idxLX]
					:= mvd;
			end
		else if(sizeSubMbPart = SZ_4x8) then
			foreach uint i in 0 .. 1
			do
				idx := 4*mbPartIdx + 2*i;
				neighbourMbPartIdx[invNeighbourAIdx[idx]+subMbPartIdx][0]
					:= mvd;
				neighbourMbPartIdx[invNeighbourBIdx[idx]+subMbPartIdx][1]
					:= mvd;
			end
		else
			idx := 4*mbPartIdx + subMbPartIdx;
			neighbourMvdLX[invNeighbourAIdx[idx]][0][idxLX] := mvd;
			neighbourMvdLX[invNeighbourBIdx[idx]][1][idxLX] := mvd;
		end end end
	end

	/* Read bits procedure */
	uint(size=32) Out_ReadNBits;
	procedure ReadNBitsInWorkingByte(uint(size=32) InitialValue, uint(size=4) NbBitsToRead)
	begin
		Out_ReadNBits    := InitialValue << NbBitsToRead;
		Out_ReadNBits    := Out_ReadNBits + selectBits(WorkingByte >> (8-NbBitsToRead), 0 , NbBitsToRead);
		WorkingByte     := WorkingByte << NbBitsToRead;
		CountBufferRbsp := CountBufferRbsp - NbBitsToRead;
	end


	//Syntaxe decoding have finished the decoding of this Nal
	IsEndOfNal: action TypeOfVLD :[Func] ==>
	guard
		Func    = SKIP_NAL
	do
		CountBufferRbsp := 0;
		SizeBufferRbsp  := 0;
	end

	//We skip all bytes in the current nal
	WaitForNewNal: action NewNal :[IsNewNal], RbspByte :[ValRbsp] ==>
	guard
		IsNewNal = false
	end

	NewNalDetected: action NewNal :[IsNewNal] ==>
	guard
		IsNewNal = true
	do
		SizeBufferRbsp := 0;
	end

	NewNalHasBeenSent: action ==>
	guard
		NewNalAlreadyDetected = true
	end

	ResetNewNalAlreadyDetected: action ==>
	do
		NewNalAlreadyDetected := false;
	end

	IsMoreRbspData: action TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		ValTypeOfVLD = MORE_RBSP_DATA
	end

	CheckSizeBufferRbsp.IsEqualOrSupTo2: action ==> ValDecoded :[1]
	guard
		SizeBufferRbsp >= 2
	end

	CheckSizeBufferRbsp.IsEqualTo1: action ==> ValDecoded :[IsMoreDataInRbsp]
	guard
		SizeBufferRbsp = 1
	do
		uint(size=1) IsMoreDataInRbsp = 
			if(((WorkingByte = 0 and (BufferRbsp[0] & 0xFF) = 0x03) or 
			   ((BufferRbsp[0] & 0xFF) = 0x80)) and (CountBufferRbsp = 0) )then
				0
			else
				1
			end;
	end

	CheckSizeBufferRbsp.IsEqualTo0: action ==> ValDecoded :[IsMoreDataInRbsp]
	guard
		SizeBufferRbsp = 0
	var
		uint(size=1) IsMoreDataInRbsp
	do
		IsMoreDataInRbsp :=
			if( (WorkingByte & 0xFF) = 0x80 )then
				 0
			else
				1
			end;
	end

	FillBufferRbsp: action RbspByte :[ValRbspByte], NewNal :[ValNewNal] ==>
	guard
		SizeBufferRbsp        < 4,
		ValNewNal             = false,
		NewNalAlreadyDetected = false
	do
		BufferRbsp[3]  := BufferRbsp[2];
		BufferRbsp[2]  := BufferRbsp[1];
		BufferRbsp[1]  := BufferRbsp[0];
		BufferRbsp[0]  := ValRbspByte;
		SizeBufferRbsp := SizeBufferRbsp + 1;
	end

	FutureNewNalDetected: action NewNal :[IsNewNal] ==>
	guard
		IsNewNal              = true,
		NewNalAlreadyDetected = false
	do
		NewNalAlreadyDetected := true;
	end

	IsVldUe: action TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		ValTypeOfVLD = VLD_UE
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end

	IsVldSe: action TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		ValTypeOfVLD = VLD_SE
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end

	IsReadBits: action TypeOfVLD :[ValTypeOfVLD], VLD_Param :[ValNbBits] ==>
	guard
		ValTypeOfVLD = READ_BITS
	do
		BitsCount := ValNbBits;
		ReadValue := 0;
		TeVal     := 0;
	end

	IsVldTe.u: action TypeOfVLD :[ValTypeOfVLD], VLD_Param :[TypeOfVld] ==>
	guard
		ValTypeOfVLD = VLD_TE,
		TypeOfVld    = 1
	do
		BitsCount := 1;
		ReadValue := 0;
		TeVal     := 1;
	end

	IsVldTe.ue: action TypeOfVLD :[ValTypeOfVLD], VLD_Param :[TypeOfVld] ==>
	guard
		ValTypeOfVLD = VLD_TE,
		TypeOfVld    > 1
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end

	IsCavlcCoeffToken: action TypeOfVLD :[ValTypeOfVLD], VLD_Param :[nC] ==>
	guard
		ValTypeOfVLD = CAVLC_COEFF_TOKEN
	do
		Idx_nC :=
			if(nC < 0)then
				if(nC = -1)then
					 4
				else
					5
				end
			else
				if(nC >=8)then
					3
				else
					if(nC >=4)then
						2
					else
						nC/2
					end
				end
			end;
		coeff_token := 0;
		NbBitsRead := 0;
	end

	IsCavlcTotalZeros: action TypeOfVLD :[ValTypeOfVLD], 
		VLD_Param :[maxNumCoeff , TotalCoeff] ==>
	guard
		ValTypeOfVLD = CAVLC_TOTAL_ZEROS
	do
		IdxmaxNumCoeff :=
			if(maxNumCoeff = 4) then
				0
			else
				if(maxNumCoeff=8) then
					1
				else
					2
				end
			end;
		NbBitsRead      := 0;
		ValRead         := 0;
		TotalCoeffParam := TotalCoeff & 0x0F;
	end

	IsCavlcRunBefore: action TypeOfVLD :[ValTypOfVLD], VLD_Param :[zerosLeft] ==>
	guard
		ValTypOfVLD = CAVLC_RUN_BEFORE
	do
		IdxzerosLeft :=
			if(zerosLeft > 0 and zerosLeft <= 6) then
				zerosLeft - 1
			else
				6
			end;
		NbBitsRead      := 0;
		ValRead         := 0;
	end

	isCabacInit: action TypeOfVLD:[typeOfVld], VLD_Param:[cabacInit, qpValue, slcType] ==>
	guard
		typeOfVld = CABAC_INIT
	do
		cabacInitIdc := cabacInit;
		sliceQpY     := qpValue;
		sliceType    := slcType;
	end

	isCabacGetMbType: action TypeOfVLD:[typeOfVld], VLD_Param:[mbType] ==>
	guard
		typeOfVld = CABAC_SEND_MB_TYPE
	do
		uint(size=4) neighbourAIdx[12] = [1, 3, 4, 5, 6, 7,  9, 11, 12, 13, 14, 15];
		uint(size=4) neighbourBIdx[12] = [2, 3, 6, 7, 8, 9, 10, 11, 12, 13, 14 ,15];
		uint(size=3) sizeMbPart;

		currMbType := mbType;
		foreach int i in 0 .. 11
		do
			mbTypeNeighbours[0][neighbourAIdx[i]] := mbType;
			mbTypeNeighbours[1][neighbourBIdx[i]] := mbType;
		end
		sizeMbPart := SizeMbPart(mbType);
		updateMbPartIdx(sizeMbPart);
	end

	isCabacDecod.withoutParam: action TypeOfVLD:[typeOfVld] ==>
	guard
		NbParamForFunction(typeOfVld) = 0 && typeOfVld >= CABAC_DECOD_MINI
	do
		binIdx        := -1;
		binString     :=  0;
		syntaxElement := typeOfVld;
		byPass        := (syntaxElement = CABAC_COEFF_SIGN_FLAG);

		//Needed for FL decoding.
		cabacFLRemainBits :=
			if(syntaxElement != CABAC_REM_INTRA4x4_8x8_PRED_MODE) then
				1
			else
				3
			end;
		if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1) then
			cMax := 14;
			prefixDecoded := false;
			hasSuffix := true;
			signedVal := false;
			CodeNum   := 0;
		end
	end

	isCabacDecod.with1Param: action TypeOfVLD:[typeOfVld], VLD_Param:[param]
		==>
	guard
		NbParamForFunction(typeOfVld) = 1 && typeOfVld >= CABAC_DECOD_MINI
	do
		uint(size=4) invNeighbourAIdx[16] = [1, 4, 3, 6, 5, 0, 7, 0, 9, 12, 11,
			14, 13, 0, 15, 0];
		uint(size=4) invNeighbourBIdx[16] = [2, 3, 8, 9, 6, 7, 12, 13, 10, 11,
			0, 0, 14, 15, 0, 0];
		int (size=5)  lastLuma4x4Idx := luma4x4Idx;
		uint(size=10) neighbChrReceiv;


		binIdx        := -1;
		binString     :=  0;
		syntaxElement := typeOfVld;
		byPass        := false;

		if(syntaxElement = CABAC_CODED_BLOCK_FLAG)then
			luma4x4Idx  := (param >> 4) & 0x0F;
			if((param & 0x0F) != ctxBlockCat) then
				lastLuma4x4Idx := 0;
			end
			ctxBlockCat := param & 0x0F;
			neighbChrReceiv := (param >> 14) & 0x3FF;
			if(luma4x4Idx < lastLuma4x4Idx) then
				lastLuma4x4Idx := -1;
			end
			foreach int i in lastLuma4x4Idx + 1 .. luma4x4Idx - 1
			do
				neighboursCbfLumaAc[0][invNeighbourAIdx[i]] := 0;
				neighboursCbfLumaAc[1][invNeighbourBIdx[i]] := 0;
			end
			neighboursCbfChroma[0][0] := (neighbChrReceiv >> 9)&1;
			neighboursCbfChroma[1][0] := (neighbChrReceiv >> 4)&1;
			neighboursCbfChroma[0][0+1] := (neighbChrReceiv >> 6)&1;
			neighboursCbfChroma[0][2+1] := (neighbChrReceiv >> 8)&1;
			neighboursCbfChroma[1][0+1] := (neighbChrReceiv >> 2)&1;
			neighboursCbfChroma[1][1+1] := (neighbChrReceiv >> 3)&1;
		else if(syntaxElement = CABAC_MB_SKIP_FLAG) then
			//Used by mb_skip_flag decoding
			neighbourMbSkip[0] := getFlag(param, 1);
			neighbourMbSkip[1] := getFlag(param, 0);
			neighboursAvail[0][0] := getFlag(param, 2);
			neighboursAvail[1][0] := getFlag(param, 3);
		else if(syntaxElement = CABAC_MB_TYPE)then
			sliceType := param;
		else if(syntaxElement = CABAC_MVD_LX_0 or syntaxElement = CABAC_MVD_LX_1) then
			cMax := 14;
			prefixDecoded := false;
			hasSuffix := true;
			signedVal := true;
			CodeNum   := 0;
			currMbPartIdx := param >> 3;
			//We took a neighbour of the first partition of the sub_macroblock
			currSubMbType := neighbourSubMbType[4*currMbPartIdx+1][0];
			currSubMbPartIdx := param & 0x07;
			luma4x4Idx := partIdxToLuma4x4(currMbType, currMbPartIdx,
				currSubMbType, currSubMbPartIdx);
		else
			//ctxBlockCat did not change.
			levelListIdx := param;
		end end end end
		numDecodAbsLvlEq1 := 0;
		numDecodAbsLvlGt1 := 0;
		cabacFLRemainBits := 1;
	end

	isCabacDecod.with2Param: action TypeOfVLD:[typeOfVld],
		VLD_Param:[param] repeat 2 ==>
	guard
		NbParamForFunction(typeOfVld) = 2 && typeOfVld >= CABAC_DECOD_MINI
	do
		binIdx        := -1;
		binString     :=  0;
		syntaxElement := typeOfVld;
		byPass        := false;

		if(typeOfVld = CABAC_INTRA_CHROMA_PRED_MODE) then
			neighboursIntraPredC[0] := param[0];
			neighboursIntraPredC[1] := param[1];
			cMax := 3;
		else if (typeOfVld = CABAC_CODED_BLOCK_PATERN) then
			neighboursCbpLuma[0]   := param[0] mod 16;
			neighboursCbpLuma[1]   := param[1] mod 16;
			neighboursCbpChroma[0] := param[0]  /  16;
			neighboursCbpChroma[1] := param[1]  /  16;
			cabacFLRemainBits := 4;
			prefixDecoded := false;
			hasSuffix     := true;
			cMax := 2;
		else
			prevCbp := param[0];
			prevMbQpDelta := param[1];
		end end
	end

	IsCavlcLevelPrefix: action TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		ValTypeOfVLD = CAVLC_LEVEL_PREFIX
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
	end

	IsByteAligned: action TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		ValTypeOfVLD = BYTE_ALIGNED_REQUIRED
	do
		if(CountBufferRbsp != 8)then
			CountBufferRbsp := 0;
		end
	end

	isCabacInitMbVal: action TypeOfVLD:[typeOfVld], VLD_Param:[param] repeat 6
		==>
	guard
		typeOfVld = CABAC_INIT_MB_VAL
	do
		uint(size=4) neighbourAIdx[4] = [0, 2, 8, 10];
		uint(size=4) neighbourBIdx[4] = [0, 1, 4,  5];

		foreach int i in 0 .. 3
		do
			neighboursAvail[0][neighbourAIdx[i]] := getFlag(param[0], 0);
			neighboursAvail[1][neighbourBIdx[i]] := getFlag(param[0], 1);
			mbTypeNeighbours[0][neighbourAIdx[i]] := param[1];
			mbTypeNeighbours[1][neighbourBIdx[i]] := param[2];
			neighboursCbfLumaAc[0][neighbourAIdx[i]]    := (param[3] >> (neighbourAIdx[i] + 5)) & 1;
			neighboursCbfLumaAc[1][neighbourBIdx[i]]    := (param[4] >> (neighbourBIdx[i] + 10)) & 1;
		end
		neighboursCbfLumaDc[0] := (param[5]>>1) & 1;
		neighboursCbfLumaDc[1] := param[5] & 1;
		luma4x4Idx := 0;
		currMbPartIdx := 0;
	end

	Buffer.IsEmpty: action ==>
	guard
		CountBufferRbsp = 0,
		SizeBufferRbsp  > 0
	do
		CountBufferRbsp := 8;
		SizeBufferRbsp  := SizeBufferRbsp - 1;
		WorkingByte     := BufferRbsp[SizeBufferRbsp];
	end

	Buffer.IsNotEmpty: action ==>
	guard
		CountBufferRbsp > 0
	end

	GetCodeNum.Launch: action ==>
	var
		uint(size=5) NbBitsToAdd
	do
		if(not Bit1Found)then
			while( ((WorkingByte & 0x80) = 0) and (CountBufferRbsp > 0))
			do
				CountBufferRbsp := CountBufferRbsp - 1;
				WorkingByte     := WorkingByte << 1;
				leadingZeroBits := leadingZeroBits + 1;
			end
			if(CountBufferRbsp > 0)then
				leadingZeroBits := leadingZeroBits + 1;
				Bit1Found       := true;
			end
		end
		if(Bit1Found)then
			NbBitsToAdd :=
				if(CountBufferRbsp < leadingZeroBits)then
					CountBufferRbsp
				else
					leadingZeroBits
				end;
			ReadNBitsInWorkingByte(CodeNum,NbBitsToAdd);
			CodeNum         := Out_ReadNBits;
			leadingZeroBits := leadingZeroBits - NbBitsToAdd;
		end
	end

	GetCodeNum.Finished: action ==>
	guard
		leadingZeroBits = 0
	end

	GetCodeNum.NotFinished: action ==>
	guard
		leadingZeroBits != 0
	end

	SendUeValue: action ==> ValDecoded :[CodeNum]
	do
		CodeNum := CodeNum - 1;
	end

	SendSeValue: action ==> ValDecoded :[CodeNum]
	do
		//CodeNum computed is equal to CodeNum + 1
		if( (CodeNum mod 2) = 0 )then
			CodeNum := CodeNum / 2;
		else
			CodeNum := -CodeNum / 2;
		end
	end

	Get_u.Launch: action ==>
	var
		uint(size=5) NbBitsToAdd
	do
		NbBitsToAdd :=
			if(CountBufferRbsp < BitsCount)then
				CountBufferRbsp
			else
				BitsCount
			end;
		ReadNBitsInWorkingByte(ReadValue,NbBitsToAdd);
		ReadValue       := Out_ReadNBits;
		BitsCount       := BitsCount - NbBitsToAdd;
	end

	Get_u.Finished: action ==>
	guard
		BitsCount = 0
	end

	Get_u.NotFinished: action ==>
	guard
		BitsCount != 0
	end

	SendUValue: action ==> ValDecoded :[ValueDec]
	var
		uint(size=31) ValueDec
	do
		ValueDec := ReadValue ^ TeVal;
	end

	GetCavlcCoeffToken.Launch: action ==>
	do
		ReadNBitsInWorkingByte(coeff_token,1);
		coeff_token := Out_ReadNBits;
		NbBitsRead  := NbBitsRead + 1;
		while((not TotalCoeffValid[Idx_nC][(NbBitsRead - 1) & 0x0F]
			[coeff_token & 0x3F]) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(coeff_token,1);
			coeff_token := Out_ReadNBits;
			NbBitsRead  := NbBitsRead + 1;
		end
	end

	GetCavlcCoeffToken.NotFinished: action ==>
	guard
		TotalCoeffValid[Idx_nC][(NbBitsRead - 1) & 0x0F][coeff_token & 0x3F] = false
	end

	GetCavlcCoeffToken.Finished: action ==>
		ValDecoded:[ TotalCoeffValue[Idx_nC][(NbBitsRead-1) & 0x0F][coeff_token & 0x3F] ]
	do
		if(DEBUG_FLAG_DECODED)then
			println("Cavlc CoeffToken; Idx_nc :" + Idx_nC + " ; NbBitsRead : "+NbBitsRead+ " ; coeff_token : "+coeff_token);
		end
	end

	GetCavlcLevelPrefix.Launch: action ==>
	do
		while( ((WorkingByte & 0x80) = 0) and (CountBufferRbsp > 0))
		do
			CountBufferRbsp := CountBufferRbsp - 1;
			WorkingByte     := WorkingByte << 1;
			leadingZeroBits := leadingZeroBits + 1;
		end
		if(CountBufferRbsp > 0)then
			Bit1Found       := true;
			/* We read the one bit value */
			CountBufferRbsp := CountBufferRbsp - 1;
			WorkingByte     := WorkingByte << 1;
		end
	end

	GetCavlcLevelPrefix.NotFinished: action ==>
	guard
		not Bit1Found
	end

	GetCavlcLevelPrefix.Finished: action ==> ValDecoded :[leadingZeroBits]
	guard
		Bit1Found
	end

	GetCavlcTotalZeros.Launch: action ==>
	do
		ReadNBitsInWorkingByte(ValRead,1);
		ValRead := Out_ReadNBits;
		NbBitsRead      := NbBitsRead + 1;
		while((not TotalZerosValid[IdxmaxNumCoeff][TotalCoeffParam]
			[NbBitsRead & 0x0F][ValRead & 0x07]) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(ValRead,1);
			ValRead    := Out_ReadNBits;
			NbBitsRead := NbBitsRead + 1;
		end
	end

	GetCavlcTotalZeros.NotFinished: action ==>
	guard
		not TotalZerosValid[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07]
	end

	GetCavlcTotalZeros.Finished: action ==>
		ValDecoded :[TotalZerosValue[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07]]
	do
		if(DEBUG_FLAG_DECODED)then
			println("Cavlc TotalZeros; Idx :" + IdxmaxNumCoeff +" ; TotalCoeff :"+TotalCoeffParam+ " ; NbBitsRead : "+NbBitsRead+ " ; Codeword : "+ValRead);
		end
	end

	GetCavlcRunBefore.Launch: action ==>
	do
		ReadNBitsInWorkingByte(ValRead,1);
		ValRead    := Out_ReadNBits;
		NbBitsRead := NbBitsRead + 1;
		while((not RunBeforeValid[IdxzerosLeft][NbBitsRead & 0x0F]
			[ValRead & 0x07]) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(ValRead,1);
			ValRead    := Out_ReadNBits;
			NbBitsRead := NbBitsRead + 1;
		end
	end

	GetCavlcRunBefore.NotFinished: action ==>
	guard
		not RunBeforeValid[IdxzerosLeft][NbBitsRead & 0x0F][ValRead & 0x07]
	end

	GetCavlcRunBefore.Finished: action ==>
		ValDecoded :[RunBeforeValue[IdxzerosLeft][NbBitsRead & 0x0F]
			[ValRead & 0x07]]
	do
		if(DEBUG_FLAG_DECODED)then
			print("Run Before; Idx :" + IdxzerosLeft +" ; Run Before :"+
				RunBeforeValue[IdxzerosLeft][NbBitsRead & 0x0F]
				[ValRead & 0x07]);
			println(" ; NbBitsRead : "+NbBitsRead+ " ; Codeword : "+ValRead);
		end
	end

/******* Cabac Decoding *******/

	/* Initialization process */
	cabacInitContextVar: action ==>
	do
		int preCtxState;

		foreach int i in 0 .. 459 do
			//Should be modify : CabacContextInit I and PB should be merged and
			//we should use the index cabacInitIdc + 1
			preCtxState :=
				if(cabacInitIdc = -1) then
					computePreCtxState(sliceQpY, Cabac_Context_Init_I[i][0],
						Cabac_Context_Init_I[i][1])
				else
					computePreCtxState(sliceQpY, Cabac_Context_Init_PB
						[cabacInitIdc][i][0], Cabac_Context_Init_PB
						[cabacInitIdc][i][1])
				end;				
			if(preCtxState  <=  63)  then
				pStateIdx[i] := 63 - preCtxState;
				valMPS[i]    := 0 ;
			else 
				pStateIdx[i] := preCtxState - 64;
				valMPS[i]    := 1;
			end
		end
	end

	cabacInitArithDecod: action ==>
	guard
		SizeBufferRbsp >= 2
	do
		uint(size=4) nbBitInFirstByte = CountBufferRbsp;

		codIRange	:= 0x01FE;
		ReadNBitsInWorkingByte(0, CountBufferRbsp);

		CountBufferRbsp := 8;
		SizeBufferRbsp  := SizeBufferRbsp - 1;
		WorkingByte     := BufferRbsp[SizeBufferRbsp];

		ReadNBitsInWorkingByte(Out_ReadNBits, 9- nbBitInFirstByte);
		codIOffset := Out_ReadNBits;
	end

	cabacGetCtxIdx: action ==>
	do
		uint(size=11) ctxIdxOffset;
		bool          ctxIdxOffParam1;
		int(size=32)  ctxIdxOffParam2;


		//Only needed for Fixed Lenght decoding
		cabacFLRemainBits := cabacFLRemainBits - 1;

		//Begining of DecodeBin(ctxIdx)
		if(byPass) then
			cabacEndDecodeBin := DECODE_BYPASS;
		else
			binIdx := binIdx + 1;
			ctxIdxOffParam1 :=
				syntaxElement != CABAC_MB_SKIP_FLAG && prefixDecoded ||
				syntaxElement = CABAC_MB_SKIP_FLAG &&
					(sliceType != SLICE_TYPE_B);
			ctxIdxOffParam2 :=
				if(syntaxElement = CABAC_MB_TYPE or
					syntaxElement = CABAC_SUB_MB_TYPE) then
					sliceType
				else
					ctxBlockCat
				end;
			ctxIdxOffset := getCtxIdxOff(syntaxElement, ctxIdxOffParam1,
				ctxIdxOffParam2);
			getCtxIdx(ctxIdxOffset);
//			println("ctxIdx : "+ctxIdx);
			if(ctxIdx = 276) then
				//9.3.3.2.4 : decodeTerminate()
				codIRange := codIRange - 2;
				if(codIOffset >= codIRange) then
					binVal := 1;
					cabacEndDecodeBin := SKIP;
				else
					binVal := 0;
					cabacEndDecodeBin := RENORM_D;
				end
			else
				decodeDecision();
				cabacEndDecodeBin := RENORM_D;
			end
			if(not isCabacFL(syntaxElement, not prefixDecoded)) then
				binString := binString << 1;
				binString := binString + binVal;
				if(syntaxElement = CABAC_MB_TYPE && not hasSuffix) then
					if(sliceType = SLICE_TYPE_P && binIdx = 0 &&
						binString = 1) then
						hasSuffix := true;
					else if(sliceType = SLICE_TYPE_B && binIdx = 5 &&
						binString = 0x3D) then
						hasSuffix := true;
					end end
				end
			end
		end
	end

	cabacRenormD.launch: action ==>
	guard
		codIRange < 0x0100 and cabacEndDecodeBin = RENORM_D
	do
		codIRange := codIRange << 1;
		ReadNBitsInWorkingByte(codIOffset, 1);
		codIOffset := Out_ReadNBits;
	end

	cabacEndDecode.skip: action ==>
	guard
		//RenormD is finished
		codIRange >= 0x0100 and cabacEndDecodeBin = RENORM_D or
		//no Extra Renorm or Decode Asked
		cabacEndDecodeBin = SKIP
	end

	decodeByPass: action ==>
	guard
		cabacEndDecodeBin = DECODE_BYPASS
	do
		ReadNBitsInWorkingByte(codIOffset, 1);
		codIOffset := Out_ReadNBits;
		if(codIOffset >= codIRange) then
			binVal := 1;
			codIOffset := codIOffset - codIRange;
		else
			binVal := 0;
			if(not first0Found) then
				first0Found := true;
				binVal := 1;
			end
		end

		if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1) then
			//Needed for UEGk
			if(not first0Found) then
				binIdx := binIdx + 1;
			else
				binIdx := binIdx - 1;
				CodeNum := (CodeNum << 1) + binVal;
			end
		else if(askSignedFlag) then
			signedFlag := (binVal != 0);
			signedFlagDecoded := true;
		end end
	end

	cabacCheckBinarization.done: action ==> ValDecoded:[decodedVal]
	guard
		binarizationDone(binIdx, binString, syntaxElement, cabacFLRemainBits,
			binVal, not prefixDecoded, cMax, byPass, sliceType, signedFlagDecoded),
		not hasSuffix || prefixDecoded || signedFlagDecoded ||
		skipSuffixDecoding(syntaxElement, signedVal, binVal, binIdx)
	do
		int(size=32) decodedVal;
		uint(size=4) sizeSubMbPart;

		uint(size=4) invNeighbourAIdx[16] = [1, 4, 3, 6, 5, 0, 7, 0, 9, 12, 11,
			14, 13, 0, 15, 0];
		uint(size=4) invNeighbourBIdx[16] = [2, 3, 8, 9, 6, 7, 12, 13, 10, 11,
			0, 0, 14, 15, 0, 0];

		decodedVal :=
			if(isCabacFL(syntaxElement, not prefixDecoded)) then
				binString + (binVal << binIdx)
			else if(syntaxElement = CABAC_CODED_BLOCK_PATERN) then
				if(binVal = 1)then
					(2 << 4) + binStringPrefix
				else
					(binIdx << 4) + binStringPrefix
				end
			else if(isCabacTU(syntaxElement, not prefixDecoded or
				not hasSuffix)) then
				if(binVal = 1 && (not prefixDecoded or not (syntaxElement =
					CABAC_MVD_LX_0 or syntaxElement = CABAC_MVD_LX_1)))then
					cMax
				else
					binIdx
				end
			else if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1 ||
				syntaxElement = CABAC_MVD_LX_0 || syntaxElement =
				CABAC_MVD_LX_1) then
				cMax + CodeNum - 1
			else if(byPass) then
				binVal
			else if(syntaxElement = CABAC_MB_QP_DELTA) then
				if( ((binIdx + 1) & 1) = 0 )then
					(binIdx + 1) / 2
				else
					-binIdx / 2
				end
			else
				getBinarizationValue(binIdx, binString, sliceType, not prefixDecoded, syntaxElement)
			end end end end end end;

		if(signedFlag) then
			signedFlag := false;
			decodedVal := - decodedVal;
		end

		if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1) then
			if(abs(decodedVal + 1) > 1) then
				numDecodAbsLvlGt1 := numDecodAbsLvlGt1 + 1;
			else if(abs(decodedVal + 1) = 1) then
				numDecodAbsLvlEq1 := numDecodAbsLvlEq1 + 1;
			end end
		else if(syntaxElement = CABAC_CODED_BLOCK_FLAG) then
			if(ctxBlockCat <= 2) then
				neighboursCbfLumaAc[0][invNeighbourAIdx[luma4x4Idx]] := decodedVal;
				neighboursCbfLumaAc[1][invNeighbourBIdx[luma4x4Idx]] := decodedVal;
			else if(ctxBlockCat = 4) then
				neighboursCbfChroma[0][1+invNeighbourAIdx[luma4x4Idx]] := decodedVal;
				neighboursCbfChroma[1][1+invNeighbourBIdx[luma4x4Idx]] := decodedVal;
			end end
		else if(syntaxElement = CABAC_SUB_MB_TYPE) then
			foreach int i in 4*currMbPartIdx .. 4*currMbPartIdx+3
			do
				neighbourSubMbType[invNeighbourAIdx[i]][0] := decodedVal;
				neighbourSubMbType[invNeighbourBIdx[i]][1] := decodedVal;
			end
			sizeSubMbPart := SizeSubMbPart(currMbType, decodedVal);
			updateSubMbPartIdx(sizeSubMbPart, currMbPartIdx);
			currMbPartIdx := currMbPartIdx + 1;
		else if(syntaxElement = CABAC_MVD_LX_0 or
			    syntaxElement = CABAC_MVD_LX_1) then
			sizeSubMbPart := SizeSubMbPart(currMbType, decodedVal);
			updateNeighbourSubMvdLX(sizeSubMbPart, 0/*currMbPartIdx*/, 0/*currSubMbPartIdx*/, decodedVal);
		end end end end

		hasSuffix := false;
		signedFlagDecoded := false;
	end

	cabacCheckBinarization.notFinished: action ==>
	do
		if(isCabacFL(syntaxElement, not prefixDecoded)) then
			binString := binString + (binVal << binIdx);
		end
		//Only needed by coded_block_pattern and mb_type_I
		priorDecBinVal[binIdx & 3] := binVal;
	end

	cabacInitForSuffix: action ==>
	guard
		binarizationDone(binIdx, binString, syntaxElement, cabacFLRemainBits,
			binVal, not prefixDecoded, cMax, byPass, sliceType, signedFlagDecoded)
	do
		prefixDecoded   := true;
		binStringPrefix := binString + (binVal << binIdx);
		binString := 0;
		CodeNum   := 0;
		if(syntaxElement = CABAC_COEFF_ABS_LEVEL_MINUS1) then
			byPass      := true;
			first0Found := false;
			binIdx := 0;
		else if (syntaxElement = CABAC_MVD_LX_0 or syntaxElement =
			CABAC_MVD_LX_1) then
			byPass      := true;
			if(binVal = 1) then
				first0Found := false;
				binIdx := 3;
			else
				//We will decode the sign flag and stop decoding after that
				askSignedFlag := true;
				hasSuffix := false;
			end
		else
			binIdx := -1;
		end end
	end

/*
*
* Scheduler
*
*/

	schedule fsm CheckVLDType:
		CheckVLDType    (IsEndOfNal         )--> WaitForNewNal;
		CheckVLDType    (IsByteAligned      )--> CheckVLDType;
		CheckVLDType    (IsVldUe            )--> VLDecoding_Ue;
		CheckVLDType    (IsVldSe            )--> VLDecoding_Se;
		CheckVLDType    (IsVldTe.ue         )--> VLDecoding_Ue;
		CheckVLDType    (IsVldTe.u          )--> Decode_u;
		CheckVLDType    (IsReadBits         )--> Decode_u;
		CheckVLDType    (IsMoreRbspData     )--> FillBuffer;
		CheckVLDType    (IsCavlcLevelPrefix )--> DecCavlcLevPre;
		CheckVLDType    (IsCavlcCoeffToken  )--> DecCavlcCofTkn;
		CheckVLDType    (IsCavlcTotalZeros  )--> DecCavlcTotZer;
		CheckVLDType    (IsCavlcRunBefore   )--> DecCavlcRunBef;
		CheckVLDType    (isCabacInit        )--> CabacInitContextVar;
		CheckVLDType    (isCabacInitMbVal   )--> CheckVLDType;
		CheckVLDType    (isCabacGetMbType   )--> CheckVLDType;
		CheckVLDType    (isCabacDecod       )--> CabacGetCtxIdx;

		/* more_rbsp_data() */
		FillBuffer      (FillBufferRbsp                      )--> FillBuffer;
		FillBuffer      (FutureNewNalDetected                )--> CheckBuffer;
		FillBuffer      (CheckSizeBufferRbsp.IsEqualOrSupTo2 )--> CheckVLDType;
		FillBuffer      (NewNalHasBeenSent                   )--> CheckBuffer;
		CheckBuffer     (CheckSizeBufferRbsp                 )--> CheckVLDType;

		/* Skip the others values of the Rbsp */
		WaitForNewNal   (NewNalHasBeenSent          )--> ResetNewNal;
		WaitForNewNal   (WaitForNewNal              )--> WaitForNewNal2;
		WaitForNewNal   (NewNalDetected             )--> CheckVLDType;
		ResetNewNal     (ResetNewNalAlreadyDetected )--> CheckVLDType;
		WaitForNewNal2  (WaitForNewNal              )--> WaitForNewNal2;
		WaitForNewNal2  (NewNalDetected             )--> CheckVLDType;

		/* ue(v) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		VLDecoding_Ue   (Buffer                 )--> GetCodeNum4ue;
		VLDecoding_Ue   (FillBufferRbsp         )--> VLDecoding_Ue;
		GetCodeNum4ue   (GetCodeNum.Launch      )--> CheckCodeNum4ue;
		CheckCodeNum4ue (GetCodeNum.NotFinished )--> VLDecoding_Ue;
		CheckCodeNum4ue (GetCodeNum.Finished    )--> SendUeValue;
		SendUeValue     (SendUeValue            )--> CheckVLDType;

		/* se(v) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		VLDecoding_Se   (Buffer                 )--> GetCodeNum4se;
		VLDecoding_Se   (FillBufferRbsp         )--> VLDecoding_Se;
		GetCodeNum4se   (GetCodeNum.Launch      )--> CheckCodeNum4se;
		CheckCodeNum4se (GetCodeNum.NotFinished )--> VLDecoding_Se;
		CheckCodeNum4se (GetCodeNum.Finished    )--> SendUeValse;
		SendUeValse     (SendSeValue            )--> CheckVLDType;

		/* u(n) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		Decode_u        (Buffer            )--> Get_u;
		Decode_u        (FillBufferRbsp    )--> Decode_u;
		Get_u           (Get_u.Launch      )--> Check_u_Val;
		Check_u_Val     (Get_u.NotFinished )--> Decode_u;
		Check_u_Val     (Get_u.Finished    )--> SendUValue;
		SendUValue      (SendUValue        )--> CheckVLDType;

		/* Cavlc coeff_token() decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcCofTkn  (Buffer                         )--> GetCavlcCofTkn;
		DecCavlcCofTkn  (FillBufferRbsp                 )--> DecCavlcCofTkn;
		GetCavlcCofTkn  (GetCavlcCoeffToken.Launch      )--> ChckCavlcCofTkn;
		ChckCavlcCofTkn (GetCavlcCoeffToken.NotFinished )--> DecCavlcCofTkn;
		ChckCavlcCofTkn (GetCavlcCoeffToken.Finished    )--> CheckVLDType;

		/* Cavlc level_prefix decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcLevPre  (Buffer                          )--> GetCavlcLevPre;
		DecCavlcLevPre  (FillBufferRbsp                  )--> DecCavlcLevPre;
		GetCavlcLevPre  (GetCavlcLevelPrefix.Launch      )--> ChckCavlcLevPre;
		ChckCavlcLevPre (GetCavlcLevelPrefix.NotFinished )--> DecCavlcLevPre;
		ChckCavlcLevPre (GetCavlcLevelPrefix.Finished    )--> CheckVLDType;

		/* Cavlc total_zeros decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcTotZer  (Buffer                         )--> GetCavlcTotZer;
		DecCavlcTotZer  (FillBufferRbsp                 )--> DecCavlcTotZer;
		GetCavlcTotZer  (GetCavlcTotalZeros.Launch      )--> ChckCavlcTotZer;
		ChckCavlcTotZer (GetCavlcTotalZeros.NotFinished )--> DecCavlcTotZer;
		ChckCavlcTotZer (GetCavlcTotalZeros.Finished    )--> CheckVLDType;

		/* Cavlc run_before decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcRunBef  (Buffer                        )--> GetCavlcRunBef;
		DecCavlcRunBef  (FillBufferRbsp                )--> DecCavlcRunBef;
		GetCavlcRunBef  (GetCavlcRunBefore.Launch      )--> ChckCavlcRunBef;
		ChckCavlcRunBef (GetCavlcRunBefore.NotFinished )--> DecCavlcRunBef;
		ChckCavlcRunBef (GetCavlcRunBefore.Finished    )--> CheckVLDType;

		/* Cabac Init */
		CabacInitContextVar (cabacInitContextVar )--> CabacInitArithDecod;
		CabacInitArithDecod (Buffer.IsEmpty      )--> CabacInitArithDecod;
		CabacInitArithDecod (cabacInitArithDecod )--> CheckVLDType;
		CabacInitArithDecod (FillBufferRbsp      )--> CabacInitArithDecod;

		/* Cabac Decode Syntax Element */
		CabacGetCtxIdx     (cabacGetCtxIdx      )--> CabacFillOutBuffer;
		CabacFillOutBuffer (Buffer              )--> EndDecodeBin;
		CabacFillOutBuffer (FillBufferRbsp      )--> CabacFillOutBuffer;
		CabacFillOutBuffer (cabacEndDecode.skip )--> CabacCheckBinarization;
		EndDecodeBin (cabacRenormD.launch  )--> CabacFillOutBuffer;
		EndDecodeBin (cabacEndDecode.skip  )--> CabacCheckBinarization;
		EndDecodeBin (decodeByPass         )--> CabacCheckBinarization;

		CabacCheckBinarization (cabacCheckBinarization.done        )--> CheckVLDType;
		CabacCheckBinarization (cabacInitForSuffix                 )--> CabacGetCtxIdx;
		CabacCheckBinarization (cabacCheckBinarization.notFinished )--> CabacGetCtxIdx;
	end
	
	priority
		NewNalHasBeenSent               > WaitForNewNal                        > NewNalDetected;
		IsVldUe                         > IsReadBits                           > IsCavlcLevelPrefix;  
		IsCavlcLevelPrefix              > IsCavlcRunBefore > IsCavlcCoeffToken > IsVldSe;
		IsVldSe                         > IsVldTe.ue       > IsVldTe.u         > FillBufferRbsp;
		FillBufferRbsp                  > IsMoreRbspData   > IsByteAligned     > IsEndOfNal;
		CheckSizeBufferRbsp             > FillBufferRbsp                       > FutureNewNalDetected > NewNalHasBeenSent;
		GetCodeNum.NotFinished          > GetCodeNum.Finished;
		Buffer.IsNotEmpty               > Buffer.IsEmpty                       > FillBufferRbsp;
		GetCavlcCoeffToken.NotFinished  > GetCavlcCoeffToken.Finished;
		GetCavlcLevelPrefix.NotFinished > GetCavlcLevelPrefix.Finished;
		GetCavlcTotalZeros.NotFinished  > GetCavlcTotalZeros.Finished;
		GetCavlcRunBefore.NotFinished   > GetCavlcRunBefore.Finished;
		Buffer > cabacInitArithDecod    > FillBufferRbsp;
		cabacCheckBinarization.done     > cabacInitForSuffix > cabacCheckBinarization.notFinished;
	end
end