/***********************************************************************

This software module was originally developed by Damien de Saint Jorre IETR/INSA of Rennes (France), in the course of development of the <standard> for reference purposes and its performance may not have been optimized. This software module is an implementation of one or more tools as specified by the MPEG-C part 4.
ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works of this software module or modifications thereof for use in implementations of the <standard>  in products that satisfy conformance criteria (if any).
Those intending to use this software module in products are advised that its use may infringe existing patents. ISO/IEC have no liability for use of this software module or modifications thereof.
Copyright is not released for products that do not conform to audiovisual and image-coding related ITU Recommendations and/or ISO/IEC International Standards.
Assurance that the originally developed software module can be used (1) in the <standard> once the <standard> has been adopted; and (2) to develop the MPEG-C part 4:

IETR/INSA of Rennes (France) grants ISO/IEC all rights necessary to include the originally developed software module or modifications thereof in the <standard> and to permit ISO/IEC to offer You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works for use in implementations of the MPEG-C part 4 in products that satisfy conformance criteria (if any), and to the extent that such originally developed software module or portions of it are included in the MPEG-C part 4.  To the extent that IETR/INSA of Rennes (France) owns patent rights that would be required to make, use, or sell the originally developed software module or portions thereof included in the MPEG-C part 4 in a conforming product,  IETR/INSA of Rennes (France) will assure the ISO/IEC that it is willing to negotiate licenses under reasonable and non-discriminatory terms and conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications thereof for the sole  purpose of developing the MPEG-C part 4

IETR/INSA of Rennes (France) retains full right to modify and use the code for its own purpose, assign or donate the code to a third party and to inhibit third parties from using the code for products that do not conform to MPEG-related ITU Recommendations and/or ISO/IEC International Standards.
This copyright notice must be included in all copies or derivative works. Copyright (c) ISO/IEC 2008.
***********************************************************************/
package org.mpeg4.part10.svc.synParser.modular.decodePic;

import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;

actor MgntLayer()
	bool          IsSvcSlice,
	int (size=32) ValVLD,

	int (size=6)  Header1TypeOfVLD,
	int (size=32) Header1VLDParam,

	int (size=6)  Header2TypeOfVLD,
	int (size=32) Header2VLDParam,

	uint(size=5)  DecRefPicMarkingTypeOfVLD,
	int (size=32) DecRefPicMarkingNbBitsToRead,

	uint(size=5)  RefPicListTypeOfVLD,
	int (size=32) RefPicListNbBitsToRead,

	int (size=6)  DataTypeOfVLD,
	int (size=32) DataVLDParam,

	int (size=6)  MbLayerTypeOfVLD,
	int (size=32) MbLayerVLDParam,

	uint(size=5)  MbPredTypeOfVLD,
	int (size=32) MbPredNbBitsToRead,

	uint(size=5)  SubMbPredTypeOfVLD,
	int (size=32) SubMbPredNbBitsToRead,

	int (size=6)  ResTypeOfVLD,
	int (size=32) ResActorLaunched,

	uint(size=5)  ResBlkCavlcTypeOfVLD,
	int (size=32) ResBlkCavlcVLD_Param
		==>
	uint(size=5)  TypeOfVLD,
	int (size=32) VLD_Param,
	int (size=32) Header1ValVLD,
	int (size=32) Header2ValVLD,
	int (size=32) DecRefPicMarkingValVLD,
	int (size=32) RefPicListValVLD,
	int (size=32) DataValVLD,
	int (size=32) MbLayerValVLD,
	int (size=32) MbPredValVLD,
	int (size=32) SubMbPredValVLD,
	int (size=32) ResBlkCavlcValVLD
		:

	uint(size=32) nbParamToDecod := 0;

	getNewSlice: action IsSvcSlice :[isSvc] ==>
	do
	end

	SliceHeader1.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		Header1ValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceHeader1.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	SliceHeader1.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	SliceHeader1.FunctionWithoutParam: action Header1TypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceHeader1.FunctionWith1Param: action
		Header1TypeOfVLD :[ValTypeOfVLD],
		Header1VLDParam  :[Param]
			==>
		TypeOfVLD       :[ValTypeOfVLD],
		VLD_Param       :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceHeader1.Done: action Header1TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	SliceHeader2.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		Header2ValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceHeader2.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	SliceHeader2.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	SliceHeader2.FunctionWithoutParam: action Header2TypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceHeader2.FunctionWith1Param: action
		Header2TypeOfVLD :[ValTypeOfVLD],
		Header2VLDParam  :[Param]
			==>
		TypeOfVLD       :[ValTypeOfVLD],
		VLD_Param       :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceHeader2.LaunchRefPicMark: action
		Header2TypeOfVLD :[Function],
		Header2VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = DEC_REF_PIC_MARKING
	end

	SliceHeader2.LaunchRefPicList: action
		Header2TypeOfVLD :[Function],
		Header2VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = REF_PIC_LIST
	end

	SliceHeader2.Done: action Header2TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	RefPicMark.GetVLDVal.Launch: action
		ValVLD                 :[VLDecoding]
			==>
		DecRefPicMarkingValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	RefPicMark.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	RefPicMark.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	RefPicMark.FunctionWithoutParam: action
		DecRefPicMarkingTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD                 :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	RefPicMark.FunctionWith1Param: action
		DecRefPicMarkingTypeOfVLD    :[ValTypeOfVLD],
		DecRefPicMarkingNbBitsToRead :[Param]
			==>
		TypeOfVLD :[ValTypeOfVLD],
		VLD_Param :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	RefPicMark.Done: action DecRefPicMarkingTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	RefPicList.GetVLDVal.Launch: action
		ValVLD           :[VLDecoding]
			==>
		RefPicListValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	RefPicList.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	RefPicList.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	RefPicList.FunctionWithoutParam: action
		RefPicListTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD           :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	RefPicList.FunctionWith1Param: action
		RefPicListTypeOfVLD    :[ValTypeOfVLD],
		RefPicListNbBitsToRead :[Param]
			==>
		TypeOfVLD              :[ValTypeOfVLD],
		VLD_Param              :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	RefPicList.Done: action RefPicListTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	SliceData.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		DataValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SliceData.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	SliceData.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	SliceData.FunctionWithoutParam: action
		DataTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD     :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SliceData.FunctionWith1Param: action
		DataTypeOfVLD :[ValTypeOfVLD],
		DataVLDParam  :[Param]
			==>
		TypeOfVLD     :[ValTypeOfVLD],
		VLD_Param     :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SliceData.LaunchMbLayer: action
		DataTypeOfVLD :[Function],
		DataVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = MB_LAYER
	end

	SliceData.Done: action DataTypeOfVLD :[Function] ==> TypeOfVLD :[SKIP_NAL]
	guard
		Function = SKIP_NAL
	end

	MbLayer.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==> MbLayerValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	MbLayer.GetVLDVal.Done: action ==> 
	guard
		nbParamToDecod = 0
	end

	MbLayer.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	MbLayer.FunctionWithoutParam: action
		MbLayerTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD        :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	MbLayer.FunctionWith1Param: action
		MbLayerTypeOfVLD :[ValTypeOfVLD],
		MbLayerVLDParam  :[Param]
			==>
		TypeOfVLD        :[ValTypeOfVLD],
		VLD_Param        :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbLayer.LaunchSubMbPred: action
		MbLayerTypeOfVLD :[Function],
		MbLayerVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = SUB_MB_PRED
	end

	MbLayer.LaunchMbPred: action
		MbLayerTypeOfVLD :[Function],
		MbLayerVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = MB_PRED
	end

	MbLayer.LaunchResidual: action
		MbLayerTypeOfVLD :[Function],
		MbLayerVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = RESIDUAL
	end

	MbLayer.Done: action
		MbLayerTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	end

	MbPred.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
		MbPredValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	MbPred.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	MbPred.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	MbPred.FunctionWithoutParam: action MbPredTypeOfVLD :[ValTypeOfVLD] ==>
		TypeOfVLD       :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	MbPred.FunctionWith1Param: action
		MbPredTypeOfVLD    :[ValTypeOfVLD],
		MbPredNbBitsToRead :[VldParam]
			==>
		TypeOfVLD          :[ValTypeOfVLD],
		VLD_Param          :[VldParam]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	MbPred.Done: action MbPredTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	SubMbPred.GetVLDVal.Launch: action ValVLD :[VLDecoding] ==>
			SubMbPredValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	SubMbPred.GetVLDVal.Done: action ==>
	guard
		nbParamToDecod = 0
	end

	SubMbPred.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	SubMbPred.FunctionWithoutParam: action SubMbPredTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD          :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	SubMbPred.FunctionWith1Param: action
		SubMbPredTypeOfVLD    :[ValTypeOfVLD],
		SubMbPredNbBitsToRead :[VldParam]
			==>
		TypeOfVLD             :[ValTypeOfVLD],
		VLD_Param             :[VldParam]
	guard
			NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	SubMbPred.Done: action
		SubMbPredTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	end


	Residual.LaunchResBlkCavlc: action
		ResTypeOfVLD     :[Function],
		ResActorLaunched :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR,
		Actor    = RESIDUAL_BLOCK_CAVLC
	end

	Residual.Done: action
		ResTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	end

	ResBlkCavlc.GetVLDVal.Launch: action
		ValVLD            :[VLDecoding]
			==>
		ResBlkCavlcValVLD :[VLDecoding]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	ResBlkCavlc.GetVLDVal.Done: action
			==>
	guard
		nbParamToDecod = 0
	end

	ResBlkCavlc.FunctionWithoutParam: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD]
			==>
		TypeOfVLD            :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD != SKIP_NAL
	do
		if(ValTypeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	ResBlkCavlc.FunctionWith1Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCavlc.FunctionWith2Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param1 , Param2]
			==>
		TypeOfVLD            :[ValTypeOfVLD],
		VLD_Param            :[Param1 , Param2]
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	ResBlkCavlc.WaitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	ResBlkCavlc.Done: action
		ResBlkCavlcTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	end


	schedule fsm GetNewSlice:
		GetNewSlice       (getNewSlice                       )--> LaunchSliceHeader;
		LaunchSliceHeader (SliceHeader1.FunctionWithoutParam )--> LaunchSliceHeader;
		LaunchSliceHeader (SliceHeader1.FunctionWith1Param   )--> LaunchSliceHeader;
		LaunchSliceHeader (SliceHeader1.WaitVLDVal           )--> LaunchSliceHeaderGetVLDVal;
		LaunchSliceHeader (SliceHeader1.Done                 )--> LaunchSliceHeader2;
			LaunchSliceHeaderGetVLDVal (SliceHeader1.GetVLDVal.Launch )--> LaunchSliceHeaderGetVLDVal;
			LaunchSliceHeaderGetVLDVal (SliceHeader1.GetVLDVal.Done   )--> LaunchSliceHeader;

		LaunchSliceHeader2 (SliceHeader2.FunctionWithoutParam )--> LaunchSliceHeader2;
		LaunchSliceHeader2 (SliceHeader2.FunctionWith1Param   )--> LaunchSliceHeader2;
		LaunchSliceHeader2 (SliceHeader2.WaitVLDVal           )--> LaunchSliceHeader2GetVLDVal;
		LaunchSliceHeader2 (SliceHeader2.LaunchRefPicMark     )--> LaunchRefPicMark;
		LaunchSliceHeader2 (SliceHeader2.LaunchRefPicList     )--> LaunchRefPicList;
		LaunchSliceHeader2 (SliceHeader2.Done                 )--> LaunchSliceData;
			LaunchSliceHeader2GetVLDVal (SliceHeader2.GetVLDVal.Launch )--> LaunchSliceHeader2GetVLDVal;
			LaunchSliceHeader2GetVLDVal (SliceHeader2.GetVLDVal.Done   )--> LaunchSliceHeader2;

			LaunchRefPicMark (RefPicMark.FunctionWithoutParam )--> LaunchRefPicMark;
			LaunchRefPicMark (RefPicMark.FunctionWith1Param   )--> LaunchRefPicMark;
			LaunchRefPicMark (RefPicMark.WaitVLDVal           )--> LaunchRefPicMarkGetVLDVal;
			LaunchRefPicMark (RefPicMark.Done                 )--> LaunchSliceHeader2;
				LaunchRefPicMarkGetVLDVal (RefPicMark.GetVLDVal.Launch )--> LaunchRefPicMarkGetVLDVal;
				LaunchRefPicMarkGetVLDVal (RefPicMark.GetVLDVal.Done   )--> LaunchRefPicMark;
	
			LaunchRefPicList (RefPicList.FunctionWithoutParam )--> LaunchRefPicList;
			LaunchRefPicList (RefPicList.FunctionWith1Param   )--> LaunchRefPicList;
			LaunchRefPicList (RefPicList.WaitVLDVal           )--> LaunchRefPicListGetVLDVal;
			LaunchRefPicList (RefPicList.Done                 )--> LaunchSliceHeader2;
				LaunchRefPicListGetVLDVal (RefPicList.GetVLDVal.Launch )--> LaunchRefPicListGetVLDVal;
				LaunchRefPicListGetVLDVal (RefPicList.GetVLDVal.Done   )--> LaunchRefPicList;

		LaunchSliceData   (SliceData.FunctionWithoutParam   )--> LaunchSliceData;
		LaunchSliceData   (SliceData.FunctionWith1Param     )--> LaunchSliceData;
		LaunchSliceData   (SliceData.WaitVLDVal             )--> LaunchSliceDataGetVLDVal;
		LaunchSliceData   (SliceData.LaunchMbLayer          )--> LaunchMbLayer;
		LaunchSliceData   (SliceData.Done                   )--> GetNewSlice;
			LaunchSliceDataGetVLDVal   (SliceData.GetVLDVal.Launch )--> LaunchSliceDataGetVLDVal;
			LaunchSliceDataGetVLDVal   (SliceData.GetVLDVal.Done   )--> LaunchSliceData;
	
			LaunchMbLayer    (MbLayer.FunctionWithoutParam    )--> LaunchMbLayer;
			LaunchMbLayer    (MbLayer.FunctionWith1Param      )--> LaunchMbLayer;
			LaunchMbLayer    (MbLayer.WaitVLDVal              )--> LaunchMbLayerGetVLDVal;
			LaunchMbLayer    (MbLayer.Done                    )--> LaunchSliceData;
			LaunchMbLayer    (MbLayer.LaunchSubMbPred         )--> LaunchSubMbPred;
			LaunchMbLayer    (MbLayer.LaunchMbPred            )--> LaunchMbPred;
			LaunchMbLayer    (MbLayer.LaunchResidual          )--> LaunchResidual;
				LaunchMbLayerGetVLDVal    (MbLayer.GetVLDVal.Launch    )--> LaunchMbLayerGetVLDVal;
				LaunchMbLayerGetVLDVal    (MbLayer.GetVLDVal.Done      )--> LaunchMbLayer;
	
				LaunchMbPred    (MbPred.FunctionWithoutParam    )--> LaunchMbPred;
				LaunchMbPred    (MbPred.FunctionWith1Param      )--> LaunchMbPred;
				LaunchMbPred    (MbPred.WaitVLDVal              )--> LaunchMbPredGetVLDVal;
				LaunchMbPred    (MbPred.Done                    )--> LaunchMbLayer;
					LaunchMbPredGetVLDVal    (MbPred.GetVLDVal.Launch )--> LaunchMbPredGetVLDVal;
					LaunchMbPredGetVLDVal    (MbPred.GetVLDVal.Done   )--> LaunchMbPred;
	
				LaunchSubMbPred (SubMbPred.FunctionWithoutParam )--> LaunchSubMbPred;
				LaunchSubMbPred (SubMbPred.FunctionWith1Param   )--> LaunchSubMbPred;
				LaunchSubMbPred (SubMbPred.WaitVLDVal           )--> LaunchSubMbPredGetVLDVal;
				LaunchSubMbPred (SubMbPred.Done                 )--> LaunchMbLayer;
					LaunchSubMbPredGetVLDVal (SubMbPred.GetVLDVal.Launch )--> LaunchSubMbPredGetVLDVal;
					LaunchSubMbPredGetVLDVal (SubMbPred.GetVLDVal.Done   )--> LaunchSubMbPred;

				LaunchResidual  (Residual.Done                  )--> LaunchMbLayer;
				LaunchResidual  (Residual.LaunchResBlkCavlc     )--> LaunchResBlkCavlc;

	
					LaunchResBlkCavlc (ResBlkCavlc.FunctionWithoutParam )--> LaunchResBlkCavlc;
					LaunchResBlkCavlc (ResBlkCavlc.FunctionWith1Param   )--> LaunchResBlkCavlc;
					LaunchResBlkCavlc (ResBlkCavlc.FunctionWith2Param   )--> LaunchResBlkCavlc;
					LaunchResBlkCavlc (ResBlkCavlc.WaitVLDVal        )--> LaunchResBlkCavlcGetVLDVal;
					LaunchResBlkCavlc (ResBlkCavlc.Done                 )--> LaunchResidual;
						LaunchResBlkCavlcGetVLDVal (ResBlkCavlc.GetVLDVal.Launch      )--> LaunchResBlkCavlcGetVLDVal;
						LaunchResBlkCavlcGetVLDVal (ResBlkCavlc.GetVLDVal.Done        )--> LaunchResBlkCavlc;
	end

	priority
		SliceHeader1.FunctionWithoutParam > SliceHeader1.FunctionWith1Param > SliceHeader1.WaitVLDVal > SliceHeader1.Done;
		SliceHeader1.GetVLDVal.Launch > SliceHeader1.GetVLDVal.Done;

		SliceHeader2.FunctionWithoutParam > SliceHeader2.FunctionWith1Param > SliceHeader2.WaitVLDVal > SliceHeader2.LaunchRefPicMark;
		SliceHeader2.LaunchRefPicMark > SliceHeader2.LaunchRefPicList     > SliceHeader2.Done;
		SliceHeader2.GetVLDVal.Launch > SliceHeader2.GetVLDVal.Done;

		RefPicMark.FunctionWithoutParam  > RefPicMark.FunctionWith1Param  > RefPicMark.WaitVLDVal > RefPicMark.Done;
		RefPicMark.GetVLDVal.Launch > RefPicMark.GetVLDVal.Done;
		RefPicList.FunctionWithoutParam  > RefPicList.FunctionWith1Param  > RefPicList.WaitVLDVal > RefPicList.Done;
		RefPicList.GetVLDVal.Launch > RefPicList.GetVLDVal.Done;

		SliceData.FunctionWithoutParam   > SliceData.FunctionWith1Param   > SliceData.WaitVLDVal          > SliceData.LaunchMbLayer        > SliceData.Done;
		SliceData.GetVLDVal.Launch       > SliceData.GetVLDVal.Done;
		MbLayer.FunctionWithoutParam     > MbLayer.FunctionWith1Param     > MbLayer.WaitVLDVal             > MbLayer.LaunchMbPred;
		MbLayer.LaunchMbPred             > MbLayer.LaunchSubMbPred        > MbLayer.LaunchResidual         > MbLayer.Done;
		MbLayer.GetVLDVal.Launch         > MbLayer.GetVLDVal.Done;
		MbPred.FunctionWithoutParam      > MbPred.FunctionWith1Param      > MbPred.WaitVLDVal              > MbPred.Done;
		MbPred.GetVLDVal.Launch          > MbPred.GetVLDVal.Done;
		SubMbPred.FunctionWithoutParam   > SubMbPred.FunctionWith1Param   > SubMbPred.WaitVLDVal           > SubMbPred.Done;
		SubMbPred.GetVLDVal.Launch       > SubMbPred.GetVLDVal.Done;
		ResBlkCavlc.FunctionWithoutParam > ResBlkCavlc.FunctionWith1Param > ResBlkCavlc.FunctionWith2Param > ResBlkCavlc.WaitVLDVal > ResBlkCavlc.Done;
		ResBlkCavlc.GetVLDVal.Launch     > ResBlkCavlc.GetVLDVal.Done;
	end
end
