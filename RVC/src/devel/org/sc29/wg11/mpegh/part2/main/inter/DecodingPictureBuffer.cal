/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * This actor is a picture buffer containing all reference pictures. It sends
 *		samples needed for the inter prediction
 * 
 * 
 * @input IsBiPred : Indicates when the current CU is using Bi-prediction.
 * 
 * @input IsReadOrNewSlc : Indicates if samples are asked from the current RPS
 *		or if there is a new picture.
 * 
 * @input Mv : movement vectors of the current CU.
 * 
 * @input PicSize : size of the current picture.
 * 
 * @input Poc : Picture Order Count of the current slice.
 * 
 * @output PocRef : POC used for the prediction of the current CU.
 * 
 * @input PredCuSize : Size of the largest side of the current CU.
 * 
 * @input Sample : Samples from the Inloop filter. It represents the pixels
 * 		which will be displayed.
 * 
 * 
 * @output Sample : Samples needed for the inter prediction.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
package devel.org.sc29.wg11.mpegh.part2.main.inter;

import std.util.Math.*;
import devel.org.sc29.wg11.mpegh.part2.common.CommonConstantHEVC.*;

actor DecodingPictureBuffer()
	uint(size=2)  IsBiPredOrLx,
	uint(size=1)  IsReadOrNewSlc,
	int (size=32) Mv,
	uint(size=16) PicSize,
	int (size=16) Poc,
	int (size=16) PocRef,
	uint(size=7)  PredCuSize,
	int (size=16) RpsPoc,
	uint(size=8)  Sample
		==>
	uint(size=8)  RefSample
		:


	/* /!\ : Check the size of the border. */
	uint(size = 16) BORDER_SIZE = 128;
	uint(size = 8) DPB_SIZE = 17;

	uint(size=8)  pictureBuffer[DPB_SIZE][3][PICT_WIDTH+2*BORDER_SIZE][PICT_HEIGHT+2*BORDER_SIZE];
	int (size=16) pocRef[DPB_SIZE];
	bool          isRef[DPB_SIZE];
	bool          isStillRef[DPB_SIZE];

	int (size=9)  currIdx := -1;
	uint(size=6)  picSizeInCu[2];
	uint(size=16) picSize[2];
	uint(size=16) picSizeChr[2];
	uint(size=6)  xIdx;
	uint(size=6)  yIdx;
	uint(size=1)  numPocRef;
	uint(size=3)  nbList;
	uint(size=3)  sizeOfList;
	bool          isNewPicture;
	int (size=32) xMvOffset;
	int (size=32) yMvOffset;
	int (size=32) xMvOffChr;
	int (size=32) yMvOffChr;
	uint(size=7)  iLoop;
	uint(size=7)  jLoop;
	uint(size=2)  compIdx;
	uint(size=7)  puSize[2];
	uint(size=7)  puSizeChr[2];
	int (size=9)  refPicIdx;
	bool          isBiPred;
	int (size=16) lastPocRef;
	int (size=32) lastMvRef[2];

	procedure fillBorder()
	var
		uint(size=16) xPixIdx,
		uint(size=16) yPixIdx,
		uint(size=16) xPixIdxChr,
		uint(size=16) yPixIdxChr,
		uint(size=16) xIdxDownRightBlk,
		uint(size=16) yIdxDownRightBlk,
		bool leftEdge,  bool upEdge,
		bool rightEdge, bool downEdge
	begin
		xIdxDownRightBlk := xIdx*16 + 16;
		yIdxDownRightBlk := yIdx*16 + 16;
		xPixIdx := BORDER_SIZE + xIdx*16;
		yPixIdx := BORDER_SIZE + yIdx*16;
		xPixIdxChr := BORDER_SIZE/2 + xIdx*8;
		yPixIdxChr := BORDER_SIZE/2 + yIdx*8;

		leftEdge := xIdx = 0;
		upEdge := yIdx = 0;
		rightEdge := xIdxDownRightBlk >= picSize[0];
		downEdge := yIdxDownRightBlk >= picSize[1];

		if(leftEdge) then
			foreach uint y in yPixIdx .. yPixIdx + 15
			do
				foreach uint x in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x][y] :=
						pictureBuffer[currIdx][0][BORDER_SIZE][y];
				end
			end
			foreach uint comp in 1 .. 2
			do
				foreach uint y in yPixIdxChr .. yPixIdxChr + 7
				do
					foreach uint x in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x][y] :=
							pictureBuffer[currIdx][comp][BORDER_SIZE/2][y];
					end
				end
			end
		end

		if(rightEdge) then
			foreach uint y in yPixIdx .. yPixIdx + 15
			do
				foreach uint x in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x + picSize[0] + BORDER_SIZE][y] :=
						pictureBuffer[currIdx][0][picSize[0] + BORDER_SIZE - 1][y];
				end
			end
			foreach uint comp in 1 .. 2
			do
				foreach uint y in yPixIdxChr .. yPixIdxChr + 7
				do
					foreach uint x in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x + picSizeChr[0] + BORDER_SIZE/2][y] :=
							pictureBuffer[currIdx][comp][picSizeChr[0] + BORDER_SIZE/2 - 1][y];
					end
				end
			end
		end

		if(upEdge) then
			foreach uint x in xPixIdx .. xPixIdx + 15
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x][y] :=
						pictureBuffer[currIdx][0][x][BORDER_SIZE];
				end
			end
			foreach uint comp in 1 .. 2
			do
				foreach uint x in xPixIdxChr .. xPixIdxChr + 7
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x][y] :=
							pictureBuffer[currIdx][comp][x][BORDER_SIZE/2];
					end
				end
			end
		end

		if(downEdge) then
			foreach uint x in xPixIdx .. xPixIdx + 15
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x][y + picSize[1] + BORDER_SIZE] :=
						pictureBuffer[currIdx][0][x][picSize[1] + BORDER_SIZE - 1];
				end
			end
			foreach int comp in 1 .. 2
			do
				foreach uint x in xPixIdxChr .. xPixIdxChr + 7
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x][y + picSizeChr[1] + BORDER_SIZE/2] :=
							pictureBuffer[currIdx][comp][x][picSizeChr[1] + BORDER_SIZE/2 - 1];
					end
				end
			end
		end

		if(leftEdge && upEdge) then
			foreach uint x in 0 .. BORDER_SIZE - 1
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x][y] :=
						pictureBuffer[currIdx][0][BORDER_SIZE][BORDER_SIZE];
				end
			end
			foreach int comp in 1 .. 2
			do
				foreach uint x in 0 .. BORDER_SIZE/2 - 1
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x][y] :=
							pictureBuffer[currIdx][comp][BORDER_SIZE/2][BORDER_SIZE/2];
					end
				end
			end
		end

		if(rightEdge && upEdge) then
			foreach uint x in 0 .. BORDER_SIZE - 1
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x + picSize[0] + BORDER_SIZE][y] :=
						pictureBuffer[currIdx][0][picSize[0] + BORDER_SIZE - 1][BORDER_SIZE];
				end
			end
			foreach int comp in 1 .. 2
			do
				foreach uint x in 0 .. BORDER_SIZE/2 - 1
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x + picSizeChr[0] + BORDER_SIZE/2][y] :=
							pictureBuffer[currIdx][comp][picSizeChr[0] + BORDER_SIZE/2 - 1][BORDER_SIZE/2];
					end
				end
			end
		end

		if(leftEdge && downEdge) then
			foreach uint x in 0 .. BORDER_SIZE - 1
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x][y + picSize[1] + BORDER_SIZE] :=
						pictureBuffer[currIdx][0][BORDER_SIZE][picSize[1] + BORDER_SIZE - 1];
				end
			end
			foreach int comp in 1 .. 2
			do
				foreach uint x in 0 .. BORDER_SIZE/2 - 1
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x][y + picSizeChr[1] + BORDER_SIZE/2] :=
							pictureBuffer[currIdx][comp][BORDER_SIZE/2][picSizeChr[1] + BORDER_SIZE/2 - 1];
					end
				end
			end
		end

		if(rightEdge && downEdge) then
			foreach uint x in 0 .. BORDER_SIZE - 1
			do
				foreach uint y in 0 .. BORDER_SIZE - 1
				do
					pictureBuffer[currIdx][0][x + picSize[0] + BORDER_SIZE][y + picSize[1] + BORDER_SIZE] :=
						pictureBuffer[currIdx][0][picSize[0] + BORDER_SIZE - 1][picSize[1] + BORDER_SIZE - 1];
				end
			end
			foreach int comp in 1 .. 2
			do
				foreach uint x in 0 .. BORDER_SIZE/2 - 1
				do
					foreach uint y in 0 .. BORDER_SIZE/2 - 1
					do
						pictureBuffer[currIdx][comp][x + picSizeChr[0] + BORDER_SIZE/2][y + picSizeChr[1] + BORDER_SIZE/2] :=
							pictureBuffer[currIdx][comp][picSizeChr[0] + BORDER_SIZE/2 - 1][picSizeChr[1] + BORDER_SIZE/2 - 1];
					end
				end
			end
		end
	end

	/* We get the whole Cu decoded (luma and chroma components) and we save
	 * it in the Dpb (pictureBuffer).
	 */
	getPix: action Sample:[samp]  repeat 16*16 + 8*8*2 ==>
	guard
		yIdx < picSizeInCu[1]
	var
		uint(size=16) xPixIdx,
		uint(size=16) yPixIdx,
		uint tokenOffset
	do
		xPixIdx := BORDER_SIZE + xIdx*16;
		yPixIdx := BORDER_SIZE + yIdx*16;
		foreach uint y in 0 .. 15
		do
			foreach uint x in 0 .. 15
			do
				pictureBuffer[currIdx][0][xPixIdx+x][yPixIdx+y] := samp[x+y*16];
			end
		end

		xPixIdx := (BORDER_SIZE + xIdx*16)>>1;
		yPixIdx := (BORDER_SIZE + yIdx*16)>>1;
		foreach uint component in 1 .. 2
		do
			tokenOffset:= 16*16 + 8*8*(component-1);
			foreach uint y in 0 .. 7
			do
				foreach uint x in 0 .. 7
				do
					pictureBuffer[currIdx][component][xPixIdx+x][yPixIdx+y] :=
						samp[x+y*8 + tokenOffset];
				end
			end
		end
		fillBorder();

		xIdx := xIdx + 1;
		if(xIdx = picSizeInCu[0]) then
			xIdx := 0;
			yIdx := yIdx + 1;
		end
	end

	/** Getting informations from new slice/picture. Updating these informations **/
	isNewSlice: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		/* Fix me : We should know when there is a new picture */
		isRead = 0 and if currIdx = -1 then false else poc = pocRef[currIdx] end
	do
		isNewPicture := false;
		nbList := 0;
		isStillRef := [false: for int i in 0 .. DPB_SIZE - 1];
	end
	isNewPict: action IsReadOrNewSlc:[isRead], PicSize:[picWidthInPix,
		picHeightInPix], Poc:[poc] ==>
	guard
		isRead = 0 and not(if currIdx = -1 then false else poc = pocRef[currIdx] end)
	do
		picSizeInCu[0] := (picWidthInPix+15)/16;
		picSizeInCu[1] := (picHeightInPix+15)/16;
		picSize[0] := picWidthInPix;
		picSize[1] := picHeightInPix;
		picSizeChr[0] := picWidthInPix/2;
		picSizeChr[1] := picHeightInPix/2;
		xIdx := 0;
		yIdx := 0;
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(not isRef[i]) then
				currIdx := i;
			end
		end
		isRef[currIdx]  := true;
		pocRef[currIdx] := poc;
		isNewPicture    := true;
		nbList          := 0;
		isStillRef := [false: for int i in 0 .. DPB_SIZE - 1];
	end

	/** We get all the references from the 5 lists of the Rps
	 * (StCurrentBefore, StCurrentAfter, StFollowing, LtCurrent, LtFollowing)
	 **/
	getRps.launch: action RpsPoc:[numPic]==>
	guard
		nbList < 5
	do
		sizeOfList := numPic;
	end

	getRps.done: action ==>
	guard
		nbList = 5
	do
		/* We remove from the Dpb all the pictures who are not in the Rps of
		 * the current picture
		 */
		foreach int i in 0 .. DPB_SIZE - 1
		do
			isRef[i] := isRef[i] and isStillRef[i];
		end
		isRef[currIdx] := true;
	end

	/** For each we check the index of the reference poc
	 * (in order to not remove them from the Dpb).
	 **/
	getRpsPoc.launch: action RpsPoc:[pocVal] ==>
	guard
		sizeOfList > 0
	do
		sizeOfList := sizeOfList - 1;
		foreach int i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = pocVal)then
				isStillRef[i] := true;
			end
		end
	end

	getRpsPoc.done: action ==>
	guard
		sizeOfList = 0
	do
		nbList := nbList + 1;
	end

	/* When we get the picture we add the border (in order to be able to give a
	 * reference pixels which can be outside of the picture).
	 */
	getCuPix.done: action ==>
	guard
		yIdx = picSizeInCu[1]
	end

	getReadAddr: action IsReadOrNewSlc:[isRead], IsBiPredOrLx:[isBiPredOrLx] ==>
	guard
		isRead = 1
	do
		numPocRef :=
			if(isBiPredOrLx = BI_PRED) then
				2
			else
				1
			end;
		isBiPred := isBiPredOrLx = BI_PRED;
	end

	/* We get all the informations needed for sending the reference Cu. We save
	 * these samples in RefCu, it will be sent with the actions sendCu.isYxY
	 */
	getMvInfo.launch: action PocRef:[poc], PredCuSize:[predUnitSize] repeat 2,
		Mv:[mvX, mvY] ==>
	guard
		numPocRef != 0,
		not(isBiPred && numPocRef = 1 && lastPocRef = poc && lastMvRef[0] = mvX && lastMvRef[1] = mvY)
	var
		int(size=7)  sideMax,
		uint(size=7)  sideMaxChr
	do
		lastPocRef := poc;
		lastMvRef  := [mvX, mvY];
		sideMax := max(predUnitSize[0], predUnitSize[1]);
		puSize := [predUnitSize[i] + 7: for int i in 0 .. 1];
		puSizeChr := [predUnitSize[i]/2 + 3: for int i in 0 .. 1];
		sideMaxChr := sideMax >> 1;
		refPicIdx := 0;
		foreach uint i in 0 .. DPB_SIZE - 1
		do
			if(pocRef[i] = poc and isRef[i]) then
				refPicIdx := i;
			end
		end

		/* The mv components correspond to the absolute movement vector, that's
		 * why we don't need to add the address of the current Cu
		 */

		/* We remove 3 for each coordinate component, because for the luma
		 * prediction we need the 3 previous pixels
		 */
		xMvOffset := (mvX >> 2);
		xMvOffset := min(xMvOffset, picSize[0]+3);
		xMvOffset := max(xMvOffset, -(8 +3 -1) -(sideMax-1));
		xMvOffset := xMvOffset +BORDER_SIZE-3;
		yMvOffset := (mvY >> 2);
		yMvOffset := min(yMvOffset, picSize[1]+3);
		yMvOffset := max(yMvOffset, -(8 +3 -1) -(sideMax-1));
		yMvOffset := yMvOffset +BORDER_SIZE-3;

		/* We remove 1 for each coordinate component, because for the chroma
		 * prediction we need the previous pixel
		 */
		xMvOffChr := (mvX >> 3);
		xMvOffChr := min(xMvOffChr, picSize[0]/2+1);
		xMvOffChr := max(xMvOffChr, -(4 +1 -1) -(sideMaxChr-1));
		xMvOffChr := xMvOffChr +BORDER_SIZE/2-1;
		yMvOffChr := (mvY >> 3);
		yMvOffChr := min(yMvOffChr, picSize[1]/2+1);
		yMvOffChr := max(yMvOffChr, -(4 +1 -1) -(sideMaxChr-1));
		yMvOffChr := yMvOffChr +BORDER_SIZE/2-1;

		numPocRef := numPocRef - 1;
		iLoop := 0;
		jLoop := 0;
	end

	getMvInfo.skip: action PocRef:[poc], PredCuSize:[predUnitSize] repeat 2,
		Mv:[mvX, mvY] ==>
	guard
		isBiPred && numPocRef = 1 && lastPocRef = poc && lastMvRef[0] = mvX && lastMvRef[1] = mvY
	end

	getMvInfo.done: action ==>
	guard
		numPocRef = 0
	end

	checkIsNewPict.isTrue: action ==>
	guard
		isNewPicture
	end

	checkIsNewPict.isFalse: action ==>
	guard
		not isNewPicture
	end

	/** We send the reference samples according to the size of the predCu. **/
	sendCu.luma.launch: action ==> RefSample:[sample]
	guard
		jLoop < puSize[1]
	var
		uint(size=8) sample
	do
		sample := pictureBuffer[refPicIdx][0][iLoop+xMvOffset][jLoop+yMvOffset];

		iLoop := iLoop + 1;
		if(iLoop = puSize[0]) then
			iLoop := 0;
			jLoop := jLoop + 1;
		end
	end

	sendCu.luma.done: action ==>
	guard
		jLoop = puSize[1]
	do
		iLoop   := 0;
		jLoop   := 0;
		compIdx := 1;
	end

	sendCu.chroma.launch: action ==> RefSample:[sample]
	guard
		compIdx < 3
	var
		uint(size=8) sample
	do
		sample := pictureBuffer[refPicIdx][compIdx][iLoop+xMvOffChr][jLoop+yMvOffChr];

		iLoop := iLoop + 1;
		if(iLoop = puSizeChr[0]) then
			iLoop := 0;
			jLoop := jLoop + 1;
			if(jLoop = puSizeChr[1]) then
				jLoop   := 0;
				compIdx := compIdx + 1;
			end
		end
	end

	sendCu.chroma.done: action ==>
	guard
		compIdx = 3
	end

	schedule fsm IsNewSlice:
		IsNewSlice (isNewPict   )--> GetRps;
		IsNewSlice (isNewSlice  )--> GetRps;

		GetRps (getRps.launch )--> GetRpsPoc;
		GetRps (getRps.done   )--> CheckIsNewPict;

		GetRpsPoc (getRpsPoc.launch )--> GetRpsPoc;
		GetRpsPoc (getRpsPoc.done   )--> GetRps;

		CheckIsNewPict (checkIsNewPict.isTrue  )--> GetCuPix;
		CheckIsNewPict (checkIsNewPict.isFalse )--> IsNewSlice;

		GetCuPix (getPix        )--> GetCuPix;
		GetCuPix (getReadAddr   )--> GetMvInfo;
		GetCuPix (getCuPix.done )--> IsNewSlice;

		GetMvInfo (getPix           )--> GetMvInfo;
		GetMvInfo (getMvInfo.launch )--> SendCuLuma;
		GetMvInfo (getMvInfo.skip   )--> GetCuPix;
		GetMvInfo (getMvInfo.done   )--> GetCuPix;
	
		SendCuLuma (getPix               )--> SendCuLuma;
		SendCuLuma (sendCu.luma.launch   )--> SendCuLuma;
		SendCuLuma (sendCu.luma.done     )--> SendCuChroma;

		SendCuChroma (getPix               )--> SendCuChroma;
		SendCuChroma (sendCu.chroma.launch )--> SendCuChroma;
		SendCuChroma (sendCu.chroma.done   )--> GetMvInfo;
	end
	priority
		getPix > sendCu;
		getPix > getMvInfo;
		getPix > getCuPix;
		getPix > getReadAddr;
		sendCu.luma.launch > sendCu.luma.done;
		sendCu.chroma.launch > sendCu.chroma.done;
	end
end
