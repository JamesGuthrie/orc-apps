package devel.org.sc29.wg11.mpegh.part2.main.inter;
import std.util.Math.*;
unit InterFunctions : 
	
	uint(size=4) bitDepth = 8;
	uint(size=10) maxPixVal = (1<<bitDepth)-1;
	int shift1 = 6;
	int(size=8) ff_hevc_epel_filters[7][4] = 
  [ [-2, 58, 10, -2],
	[-4, 54, 16, -2],
	[-6, 46, 28, -4],
	[-4, 36, 36, -4],
	[-4, 28, 46, -6],
	[-2, 16, 54, -4],
	[-2, 10, 58, -2]];
	
	function QPEL_FILTER_1_H(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(-src[0][x - 3 + y * srcStride] + 4 * src[0][x - 2 + y * srcStride] - 10 * src[0][x - 1 + y * srcStride] + 58 *
		src[0][x + y * srcStride] + 17 * src[0][x + 1 + y * srcStride] - 5 * src[0][x + 2 + y * srcStride] + 1 * src[0][x +
		3 + y * srcStride])
	end

	function QPEL_FILTER_2_H(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(-src[0][x - 3 + y * srcStride] + 4 * src[0][x - 2 + y * srcStride] - 11 * src[0][x - 1 + y * srcStride] + 40 *
		src[0][x + y * srcStride] + 40 * src[0][x + 1 + y * srcStride] - 11 * src[0][x + 2 + y * srcStride] + 4 * src[0][x
		+ 3 + y * srcStride] - src[0][x + 4 + y * srcStride])
	end

	function QPEL_FILTER_3_H(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(src[0][x - 2 + y * srcStride] - 5 * src[0][x - 1 + y * srcStride] + 17 * src[0][x + y * srcStride] + 58 * src[0][x
		+ 1 + y * srcStride] - 10 * src[0][x + 2 + y * srcStride] + 4 * src[0][x + 3 + y * srcStride] - src[0][x + 4 + y * srcStride])
	end
	
	function QPEL_FILTER_1_V(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(-src[0][x + (y - 3) * srcStride] + 4 * src[0][x + (y - 2) * srcStride] - 10 * src[0][x + (y - 1) * srcStride] + 58 *
		src[0][x + (y) * srcStride] + 17 * src[0][x + (y + 1) * srcStride] - 5 * src[0][x + (y + 2) * srcStride] + 1 * src[0][x + (y + 3) * srcStride])
	end

	function QPEL_FILTER_2_V(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(-src[0][x + (y - 3) * srcStride] + 4 * src[0][x + (y - 2) * srcStride] - 11 * src[0][x + (y - 1) * srcStride] + 40 *
		src[0][x + (y) * srcStride] + 40 * src[0][x + (y + 1) * srcStride] - 11 * src[0][x + (y + 2) * srcStride] + 4 * src[0][x + (y + 3) * srcStride] - src[0][x + (y + 4) * srcStride])
	end

	function QPEL_FILTER_3_V(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=16) :
		(src[0][x + (y - 2) * srcStride] - 5 * src[0][x + (y - 1) * srcStride] + 17 * src[0][x + (y) * srcStride] + 58 * src[0][x
		+ (y + 1) * srcStride] - 10 * src[0][x + (y + 2) * srcStride] + 4 * src[0][x + (y + 3) * srcStride] - src[0][x + (y + 4) * srcStride])
	end
	
	function QPEL_FILTER_1_V_16(int(size=16) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=32) :
		(-src[0][x + (y - 3) * srcStride] + 4 * src[0][x + (y - 2) * srcStride] - 10 * src[0][x + (y - 1) * srcStride] + 58 *
		src[0][x + (y) * srcStride] + 17 * src[0][x + (y + 1) * srcStride] - 5 * src[0][x + (y + 2) * srcStride] + 1 * src[0][x + (y + 3) * srcStride])
	end

	function QPEL_FILTER_2_V_16(int(size=16) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=32) :
		(-src[0][x + (y - 3) * srcStride] + 4 * src[0][x + (y - 2) * srcStride] - 11 * src[0][x + (y - 1) * srcStride] + 40 *
		src[0][x + (y) * srcStride] + 40 * src[0][x + (y + 1) * srcStride] - 11 * src[0][x + (y + 2) * srcStride] + 4 * src[0][x + (y + 3) * srcStride] - src[0][x + (y + 4) * srcStride])
	end

	function QPEL_FILTER_3_V_16(int(size=16) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y) --> int(size=32) :
		(src[0][x + (y - 2) * srcStride] - 5 * src[0][x + (y - 1) * srcStride] + 17 * src[0][x + (y) * srcStride] + 58 * src[0][x
		+ (y + 1) * srcStride] - 10 * src[0][x + (y + 2) * srcStride] + 4 * src[0][x + (y + 3) * srcStride] - src[0][x + (y + 4) * srcStride])
	end
	
	function EPEL_FILTER_H(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		uint(size=7) x, uint(size=7) y, int(size=8) filter_0,
		int(size=8) filter_1, int(size=8) filter_2, int(size=8) filter_3, int component) --> int(size=16) 
		:
		(filter_0*src[component][x - 1 + y * srcStride] + filter_1*src[component][x + y * srcStride] + filter_2*src[component][x + 1 + y * srcStride] + filter_3*src[component][x + 2 + y * srcStride])
	end
	
	function EPEL_FILTER_V(uint(size=8) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		int(size=16) x, int(size=16) y, int(size=8) filter_0,
		int(size=8) filter_1, int(size=8) filter_2, int(size=8) filter_3, int component) --> int(size=16) 
		:
		(filter_0*src[component][x + (y - 1) * srcStride] + filter_1*src[component][x + (y) * srcStride] + filter_2*src[component][x + (y + 1) * srcStride] + filter_3*src[component][x + (y + 2) * srcStride])
	end
	
	function EPEL_FILTER_V_16(int(size=16) src [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) srcStride,
		int(size=16) x, int(size=16) y, int(size=8) filter_0,
		int(size=8) filter_1, int(size=8) filter_2, int(size=8) filter_3, int component) --> int(size=32) 
		:
		(filter_0*src[component][x + (y - 1) * srcStride] + filter_1*src[component][x + (y) * srcStride] + filter_2*src[component][x + (y + 1) * srcStride] + filter_3*src[component][x + (y + 2) * srcStride])
	end
	
	procedure put_hevc_qpel_pixel_cal(int(size=16) predSamples [ 2 ] [ 3 ] [ 64 * 64 ], uint(size=2) listIdx,
		uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize, uint(size=7) width, uint(size=7) height )
	var
		uint(size=32) srcStride := 3*predCuSize,
		uint(size=32) dstStride := 0
	begin
		foreach int y in 0 .. height do
			foreach int x in 0 .. width do
				predSamples[listIdx][0][x + dstStride] := refSamples[0][(x+3) + srcStride] << shift1;
			end
			dstStride := dstStride + width + 1;
			srcStride := srcStride + predCuSize;
		end
	end
	
	procedure put_hevc_qpel_h_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
		uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize, int filterIdx, uint(size=7)
		width, uint(size=7) height)
	begin
		if filterIdx = 1 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do					
						predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_1_H(refSamples, predCuSize, x+3, y+3);	
				end
			end
		elsif filterIdx = 2 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do					
						predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_2_H(refSamples, predCuSize, x+3, y+3);
				end
			end
		elsif filterIdx = 3 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do					
						predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_3_H(refSamples, predCuSize, x+3, y+3);
				end
			end
		end
	end

	procedure put_hevc_qpel_v_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
		uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize, int filterIdx, uint(size=7)
		width, uint(size=7) height)
	begin
		if filterIdx = 1 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_1_V(refSamples, predCuSize, x+3, y+3);
				end
			end
		elsif filterIdx = 2 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_2_V(refSamples, predCuSize, x+3, y+3);
				end
			end
		elsif filterIdx = 3 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := QPEL_FILTER_3_V(refSamples, predCuSize, x+3, y+3);
				end
			end
		end
	end
	
	procedure put_hevc_qpel_hv_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
		uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize, int filterIdx[2], uint(size=7)
		width, uint(size=7) height)
	var
		int(size=16) intermSamples[3][ (64 + 7) *(64 + 7)]
	begin
		if filterIdx[0] = 1 then
			foreach int y in 0 .. height+7 do
				foreach int x in 0 .. width do
					intermSamples[0][x + y * predCuSize] := QPEL_FILTER_1_H(refSamples, predCuSize, x+3, y) - 8192;
				end
			end
		elsif filterIdx[0] = 2 then
			foreach int y in 0 .. height+7 do
				foreach int x in 0 .. width do
					intermSamples[0][x + y * predCuSize] := QPEL_FILTER_2_H(refSamples, predCuSize, x+3, y) - 8192;
				end
			end
		elsif filterIdx[0] = 3 then
			foreach int y in 0 .. height+7 do
				foreach int x in 0 .. width do
					intermSamples[0][x + y * predCuSize] := QPEL_FILTER_3_H(refSamples, predCuSize, x+3, y) - 8192;
				end
			end
		end
		
		if filterIdx[1] = 1 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := (QPEL_FILTER_1_V_16(intermSamples, predCuSize, x, y+3) + 524288)>>6;
				end
			end		
		elsif filterIdx[1] = 2 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := (QPEL_FILTER_2_V_16(intermSamples, predCuSize, x, y+3) + 524288)>>6;
				end
			end
		elsif filterIdx[1] = 3 then
			foreach int y in 0 .. height do
				foreach int x in 0 .. width do
					predSamples[listIdx][0][x + y * (width + 1)] := (QPEL_FILTER_3_V_16(intermSamples, predCuSize, x, y+3) + 524288)>>6;
				end
			end
		end
	end
 

	procedure put_hevc_epel_pixel_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
								uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize,
								uint(size=7) width, uint(size=7) height, int component)
	begin
	    foreach int y in 0 .. height do
			foreach int x in 0 .. width do
	            predSamples[listIdx][component][x + y * (width + 1)] := refSamples[component][(x+1) + (y+1)*predCuSize] << shift1;
	        end
	    end
	end
	
	procedure put_hevc_epel_h_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
								uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize,
								int filterIdx, uint(size=7) width, uint(size=7) height, int component)
	var
		int(size=8) filter_0 := ff_hevc_epel_filters[filterIdx - 1][0],
	    int(size=8) filter_1 := ff_hevc_epel_filters[filterIdx - 1][1],
	    int(size=8) filter_2 := ff_hevc_epel_filters[filterIdx - 1][2],
	    int(size=8) filter_3 := ff_hevc_epel_filters[filterIdx - 1][3]
	begin
	    foreach int y in 0 .. height do
			foreach int x in 0 .. width do
	            predSamples[listIdx][component][x + y * (width + 1)] := EPEL_FILTER_H(refSamples, predCuSize, x+1, y+1, filter_0, filter_1, filter_2, filter_3, component);
	        end
	    end
	end
	
	procedure put_hevc_epel_v_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
								uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize,
								int filterIdx, uint(size=7) width, uint(size=7) height, int component)
	var
		int(size=8) filter_0 := ff_hevc_epel_filters[filterIdx - 1][0],
	    int(size=8) filter_1 := ff_hevc_epel_filters[filterIdx - 1][1],
	    int(size=8) filter_2 := ff_hevc_epel_filters[filterIdx - 1][2],
	    int(size=8) filter_3 := ff_hevc_epel_filters[filterIdx - 1][3]
	begin
	    foreach int y in 0 .. height do
			foreach int x in 0 .. width do
	            predSamples[listIdx][component][x + y * (width + 1)] := EPEL_FILTER_V(refSamples, predCuSize, x+1, y+1, filter_0, filter_1, filter_2, filter_3, component);
	        end
	    end
	end
 	
 	procedure put_hevc_epel_hv_cal (int(size=16) predSamples [ 2 ] [ 3 ] [64*64], uint(size=2) listIdx,
								uint(size=8) refSamples [ 3 ] [ (64 + 7)*( 64 + 7) ], uint (size=7) predCuSize,
								int filterIdx[2], uint(size=7) width, uint(size=7) height, int component)
	var
		int(size=16) intermSamples[3][ (64 + 7)*( 64 + 7) ],
		int(size=8) filter_0 := ff_hevc_epel_filters[filterIdx[0] - 1][0],
	    int(size=8) filter_1 := ff_hevc_epel_filters[filterIdx[0] - 1][1],
	    int(size=8) filter_2 := ff_hevc_epel_filters[filterIdx[0] - 1][2],
	    int(size=8) filter_3 := ff_hevc_epel_filters[filterIdx[0] - 1][3]
	begin
		foreach int y in 0 .. height+3 do
				foreach int x in 0 .. width do
		            intermSamples[component][x + y * predCuSize] := EPEL_FILTER_H(refSamples, predCuSize, x+1, y, filter_0, filter_1, filter_2, filter_3, component) - 8192;
		        end
		end
	    filter_0 := ff_hevc_epel_filters[filterIdx[1] - 1][0];
	    filter_1 := ff_hevc_epel_filters[filterIdx[1] - 1][1];
	    filter_2 := ff_hevc_epel_filters[filterIdx[1] - 1][2];
	    filter_3 := ff_hevc_epel_filters[filterIdx[1] - 1][3];
	    foreach int y in 0 .. height do
			foreach int x in 0 .. width do
	            predSamples[listIdx][component][x + y * (width + 1)] := (EPEL_FILTER_V_16(intermSamples, predCuSize, x, y+1, filter_0, filter_1, filter_2, filter_3, component) + 524288) >> 6;
	        end
	    end
	end
	
	procedure put_unweighted_pred (int (size=16) src[2][3][64*64], int width, int height, uint(size=1) rdList, int component)
	begin
	    foreach int y in 0 .. height do
	        foreach int x in 0 .. width do
	            src[0][component][x + y * (width + 1)] := (src[rdList][component][x + y * (width + 1)] + (1 <<(shift1-1)))>>shift1;
	            src[0][component][x + y * (width + 1)] := clip_i32(src[0][component][x + y * (width + 1)], 0 , maxPixVal);
	        end
	    end
	end
 
	procedure put_weighted_pred_avg (int (size=16) src[2][3][64*64], int width, int height, int component)
	begin

	    foreach int y in 0 .. height do
	        foreach int x in 0 .. width do
	            src[0][component][x + y * (width + 1)] := (src[0][component][x + y * (width + 1)] + src[1][component][x + y * (width + 1)] + 64) >> 7;
				src[0][component][x + y * (width + 1)] := clip_i32(src[0][component][x + y * (width + 1)], 0 , maxPixVal);
	        end
	    end
	end

	procedure weighted_pred (int logWD [3], int weightCu [3][2], int offsetCu [3][2] ,
		int (size=16) src[2][3][64*64], int width, int height, uint(size=1) rdList, int component)
	var
	    int locLogWD,
	    int wX,
	    int oX	
	begin
	    
	    wX := weightCu[component][rdList];
		oX := offsetCu[component][rdList];
		locLogWD := logWD[component];
	
	    foreach int y in 0 .. height do
	        foreach int x in 0 .. width do
	            src[0][component][x + y * (width + 1)] :=
				if(locLogWD >= 1) then
					((src[rdList][component][x + y * (width + 1)]*wX + (1<<(locLogWD-1))) >> locLogWD)+oX
				else
					src[rdList][component][x + y * (width + 1)]*wX +oX
				end;
				src[0][component][x + y * (width + 1)] := clip_i32(src[0][component][x + y * (width + 1)], 0 , maxPixVal);
	        end
	    end
	end

	procedure weighted_pred_avg (int logWD [3], int weightCu [3][2], int offsetCu [3][2] ,
		int (size=16) src[2][3][64*64], int width, int height, int component)
	var
	    int locLogWD,
	    int w0,
	    int w1,
	    int o0,
	    int o1	
	begin
		w0 := weightCu[component][0];
		w1 := weightCu[component][1];
		o0 := offsetCu[component][0];
		o1 := offsetCu[component][1];
		locLogWD := logWD[component];
		foreach int y in 0 .. height do
	        foreach int x in 0 .. width do
				src[0][component][x + y * (width + 1)] := (src[0][component][x + y * (width + 1)]*w0 + src[1][component][x + y * (width + 1)]*w1 +
					((o0+o1+1)<<locLogWD))>>(locLogWD+1);
				
				src[0][component][x + y * (width + 1)] := clip_i32(src[0][component][x + y * (width + 1)], 0 ,
					maxPixVal);
			end
		end
	end
	
	procedure weighted_pred_mono (int logWD [3], int weightCu [3][2], int offsetCu [3][2] ,
		int (size=16) src[2][3][64*64], int width, int height, int component)
	var
	    int locLogWD,
	    int wX,
	    int oX	
	begin
	    
	   wX := weightCu[component][0] + weightCu[component][1];
	   oX := offsetCu[component][0] + offsetCu[component][1] +1;
	   locLogWD := logWD[component] +1;
	   foreach int y in 0 .. height do
	       foreach int x in 0 .. width do
				src[0][component][x + y * (width + 1)] := ((src[0][component][x + y * (width + 1)]*wX + (oX<<(locLogWD-1))) >>locLogWD);
				src[0][component][x + y * (width + 1)] := clip_i32(src[0][component][x + y * (width + 1)], 0 , maxPixVal);
	        end
	    end
	end

end
