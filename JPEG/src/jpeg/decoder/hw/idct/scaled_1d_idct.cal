/*
 * Copyright (c) 2010, Regents of the Akatech SA, EPFL
 * 
 * All rights reserved.
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 *     * Neither the name of the Akatech SA, EPFL nor the
 *       names of its contributors may be used to endorse or promote products
 *       derived from this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS'' AND ANY
 * EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE REGENTS AND CONTRIBUTORS BE LIABLE FOR ANY
 * DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */
 
package jpeg.decoder.hw.idct;
/**
 * This actor is doing 1d idct for eight elements
 * 
 * @author Endri Bezati
 */
actor scaled_1d_idct () int(size=32) In ==> int(size=32) Out :

	function pmul_1_1(int X) --> int
	var
		int Temp1 = (X >> 3) - (X >> 7) :
		X - Temp1
	end

	function pmul_1_2(int X) --> int
	var
		int Temp1 = (X >> 3) - (X >> 7),
		int Temp2 = Temp1 - (X >> 11) :
		Temp1 + (Temp2 >> 1)
	end

	function pmul_2_1(int X) --> int
	var
		int Temp1 = (X >> 9) - X :
		(Temp1 >> 2) - Temp1
	end

	function pmul_2_2(int X) --> int :
		(X >> 1)
	end

	function pmul_3_1(int X) --> int
	var
		int Temp1 = X + (X >> 5),
		int Temp2 = (Temp1 >> 2) :
		Temp2 + (X >> 4)
	end

	function pmul_3_2(int X) --> int
	var
		int Temp1 = X + (X >> 5),
		int Temp2 = (Temp1 >> 2) :
		Temp1 - Temp2
	end
	
	List(type:int,size=8) x;
	List(type:int,size=8) y;
	
	int Counter := 0;
	
	read: action In:[token] ==>
	do
		x[Counter] := token;
		Counter := Counter + 1;	
	end
	
	finish: action ==>
	guard
		Counter = 8
	do	
		Counter := 0;
	end
	
	process: action ==> 
	var
		int tmp_1,
		int tmp_2,
		int tmp_3,
		int tmp_4,
		int xa,
		int xb
	do
		xa := x[1] + x[7];
		xb := x[1] - x[7];
		x[1] := xa + x[3];
		x[3] := xa - x[3];
		x[7] := xb + x[5];
		x[5] := xb - x[5];
		tmp_1 := pmul_1_1(x[3]);
		tmp_4 := pmul_1_2(x[3]);
		tmp_3 := pmul_1_1(x[5]);
		tmp_2 := pmul_1_2(x[5]);
		x[3] := tmp_1 - tmp_2;
		x[5] := tmp_3 + tmp_4;
		tmp_1 := pmul_2_1(x[1]);
		tmp_4 := pmul_2_2(x[1]);
		tmp_3 := pmul_2_1(x[7]);
		tmp_2 := pmul_2_2(x[7]);
		x[1] := tmp_1 + tmp_2;
		x[7] := tmp_3 - tmp_4;
		tmp_1 := pmul_3_1(x[2]);
		tmp_4 := pmul_3_2(x[2]);
		tmp_3 := pmul_3_1(x[6]);
		tmp_2 := pmul_3_2(x[6]);
		x[2] := tmp_1 - tmp_2;
		x[6] := tmp_3 + tmp_4;
		xa := x[0] + x[4];
		xb := x[0] - x[4];
		x[0] := xa + x[6];
		x[6] := xa - x[6];
		x[4] := xb + x[2];
		x[2] := xb - x[2];
		
		y[0] := x[0] + x[1];
		y[1] := x[4] + x[5]; 
		y[2] := x[2] + x[3]; 
		y[3] := x[6] + x[7];
		y[4] := x[6] - x[7]; 
		y[5] := x[2] - x[3]; 
		y[6] := x[4] - x[5];
		y[7] := x[0] - x[1];
	end

	send: action ==> Out:[token]
	var
		int token
	do
		token := y[Counter];
		Counter := Counter + 1;
	end
	
	schedule fsm s0:
		s0		(read)		--> s0;
		s0		(finish)	--> s1;
		
		s1		(process)	--> s2;
		
		s2		(send)		--> s2;
		s2		(finish)	--> s0;
	end
	
	priority
		finish > read;
		finish > send;
	end
end


      
     
