package jpeg.encoder.converter;

actor BmpReader() uint(size=8) In ==> int(size=9) R, int(size=9) G, int(size=9) B, int SOI :
	
	int count := 0;

	int w;
	int h;
	
	
	header: action In:[ bytes ] repeat 54 ==> SOI:[ w, h ] 
	do
      w := ((((bytes[21] << 24) | (bytes[20] << 16))| (bytes[19] << 8)) | bytes[18]);
      h := ((((bytes[25] << 24) | (bytes[24] << 16))| (bytes[23] << 8)) | bytes[22]);
    end
    
	data: action In:[ b, g, r ]	==> R:[ r ], G:[ g ], B:[ b ]
	do
		count := count + 1;
    end
	
	done: action ==> 
	guard count = w * h
	do
		count := 0;
	end
	
	schedule fsm s0 :
	s0 ( header ) --> s1;
	s1 ( data ) --> s1;
	s1 ( done ) --> s0;
	end
	
	priority
	   done > data;
	end
end