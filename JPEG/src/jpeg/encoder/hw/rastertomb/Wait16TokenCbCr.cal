package jpeg.encoder.hw.rastertomb;


actor Wait16TokenCbCr()
	int(size=10) DataIn
		==>
			int(size=10) DataOut:
	
	int Counter := 0;
	int CbCounter := 0;
	int CrCounter := 0;

	List(type:int(size=10),size=16) Reg16 := [0:for int i in 0 .. 15];
	
	Save: action DataIn:[token] ==>
	do
		if (Counter & 1)  = 1 then
			Reg16[CbCounter] := token-128;
			CbCounter := CbCounter + 1;
		else
			Reg16[CrCounter + 8] := token-128;
			CrCounter := CrCounter + 1;
		end
		
		Counter := Counter + 1;
	end
	
	Sent: action ==> DataOut:[token]
	var
		int token
	do
		token := Reg16[Counter];
		Counter := Counter + 1;
	end
	
	Stop: action ==>
	guard
		Counter = 16
	do	
		Counter := 0;
		CbCounter := 0;
		CrCounter := 0;
	end
	
	schedule fsm s0: 
		s0		(Save)		--> s0;
		s0		(Stop)		--> s1;
		
		s1		(Sent)		--> s1;
		s1		(Stop)		--> s0;
	end 
	
	priority
		Stop > Save;
		Stop > Sent;
	end
				
end