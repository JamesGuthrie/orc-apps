/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Endri Bezati <endri.bezati@epfl.ch>

package jpeg.encoder.hw.quanta;

import jpeg.encoder.common.Tables.QT;
import jpeg.encoder.common.Tables.zigzag;

actor Quant()
	int(size=32) In
		==>
			int(size=32) QT_Out,
			int(size=32) ZZ_Out:
	
	int Block_Type := 0; // Block_Type = 0,1,2,3 (Luma), Block_Type = 4,5 (Chroma)	
	
	int Counter := 0;
	
	List(type:int(size=32), size=64) data := [0: for int i in 0 .. 63];
	
	Input.start: action ==>
	do	
		Counter := 0;
	end
	
	Input.save: action In:[token]==> 
	do
		data[Counter] := token;
		Counter := Counter + 1;
	end	
	
	Input.finish: action ==>
	guard
		Counter = 64
	end
	
	// Quant.star: Adding this action because the if The Counter := 0 is put in the
	// Input.finish the actor will send only one token (Really Strange !!!) 
	
	Quant.start: action ==>
	do	
		Counter := 0;
	end
	
	Quant.send: action ==> QT_Out:[qt], ZZ_Out:[zz]
	var
		int(size=32) qt := 0,
		int(size=32) zz	:= 0
	do
		qt := QT[Block_Type >> 2][Counter];
		zz := data[zigzag[Counter]];
		Counter := Counter + 1;
	end	
	
	Quant.finish: action ==>
	guard
		Counter = 64
	do
		Block_Type := (Block_Type + 1);
		if Block_Type = 6 then
			Block_Type := 0;
		end
	end
	
	schedule fsm s0:
		s0	(Input.start)	--> s01;
		s01	(Input.save)	--> s01;
		s01	(Input.finish)	--> s1;
		
		s1	(Quant.start)	--> s11;
		s11	(Quant.send)	--> s11;
		s11	(Quant.finish)	--> s0;
	end
	
	priority
		Input.finish > Input.save;
		Quant.finish > Quant.send;
	end
end