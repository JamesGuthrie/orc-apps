package synParser;

import  std.util.Math.*;

import  org.sc29.wg11.common.BitstreamReadUtils.*;
import  synParser.Algo_ParserUtils.*;
import  common.CommonConstantHEVC.*;
import  common.CabacDecodeBinFunction.*;
import  common.CabacContextConstantHEVC.*;
import  common.CabacContextFunctionHEVC.*;
import  common.CabacParserFunctionHEVC.*;

actor Algo_Parser () 
				uint(size= 8) byte
				==>
				uint(size=16) CUInfo,
				uint(size=16) Test,
				uint(size=8)  NaluType,
				uint(size=16) PictSize,
				uint(size=2)  SliceType
:
	bool DEBUG_MODE = false;
	bool DEBUG_BITSTREAM = false;
	uint(size=FIFO_DEPTH) fifo[FIFO_SIZE_ALL];
	uint(size= 2) zeroByte := 0;
	uint(size= 8) se_idx;
	uint(size=32) cnt_i;
	uint(size=10) sps_id;
	uint(size=10) pps_id;
	uint(size=32) wp[2][MAX_NUM_REF][3][WP_MAX_STRUCT];

	/**************************************************************************
	 **************************************************************************
	 ********        Bitstream Reader                     *********************
	 **************************************************************************
	 *************************************************************************/
	
	/**************************************************************************
	 * search_start_code or getByte
	 *************************************************************************/
 	action byte:[ b ] ==>
	guard 
		not isFifoFull(fifo)
	var
		uint(size=4) fifo_idx  =  fifo[FIFO_IDX]
	do
		if b != EPR_VALUE or zeroByte != 3 then
			if b = START_CODE_VALUE and zeroByte = 3 then
				fifo[fifo_idx]      := b + START_CODE_FLAG;
			else
				fifo[fifo_idx]      := b;
			end
			fifo[FIFO_CPT_BITS] := fifo[FIFO_CPT_BITS] + 8;
			if DEBUG_BITSTREAM then
				println("fifo["+fifo_idx+"] := "+b+" \t "+fifo[FIFO_CPT_BITS]);
			end
			fifo[FIFO_IDX]      := (fifo_idx+1) & (FIFO_SIZE-1);
		end
		if b = 0 then
			zeroByte := ((zeroByte << 1) + 1) & 0x3;
		else
			zeroByte := 0;
		end
	end
	/**************************************************************************
	 **************************************************************************
	 ********        NAL UNIT                             *********************
	 **************************************************************************
	 *************************************************************************/
	/**************************************************************************
	 * byte_align_a
	 *************************************************************************/	
	byte_align_a : action ==>
	guard
		isFifoFull(fifo)
	do
		byte_align(fifo);
	end 	
	/**************************************************************************
	 * search_start_code
	 *************************************************************************/
	start_code.search: action ==>
	guard
		not IsStartCode(fifo) and isFifoFull(fifo)
   	do
		flushBits( 8, fifo );
		if DEBUG_BITSTREAM then		
  			println("start_code.search");
   		end
	end
	start_code.done: action ==>
	guard
		IsStartCode(fifo) and isFifoFull(fifo)
	do
		flushBits( 8, fifo);
		if DEBUG_BITSTREAM then
			println("start_code.done");
		end
	end
	/**************************************************************************
	 * read_nal_unit_header
	 *************************************************************************/		
	uint(size= 6) nal_unit_type;
	
	read_nal_unit_header : action ==> NaluType:[nal_unit_type]
	var
		uint(size=32) res[1]
	do
		if DEBUG_BITSTREAM then
			println("=========== NAL_UNIT ===========");
			flushBits_name( 1, fifo,   "reserved_zero_1bits                     ");
//			vld_u_name(  1, fifo, res, "nal_ref_flag                            "); 
			vld_u_name(  6, fifo, res, "nal_unit_type                           "); 
			nal_unit_type := res[0];
			flushBits_name( 6, fifo,   "reserved_zero_5bits                     ");
			vld_u_name(  3, fifo, res, "temporal_id                             "); 
		else
			flushBits( 1, fifo );
//			vld_u(  1, fifo, res);
			vld_u(  6, fifo, res); nal_unit_type := res[0];
			flushBits( 6, fifo  );
			vld_u(  3, fifo, res); 
		end
		se_idx := 1;
	end
	/**************************************************************************
	 * look_for.VPS_header
	 *************************************************************************/
	look_for.VPS_header: action ==>
	guard
		nal_unit_type = NAL_UNIT_VPS
	do
		if DEBUG_PARSER then
			println("=========== Video Parameter Set ID:   ===========");
		end
	end
	/**************************************************************************
	 * look_for.SPS_header
	 *************************************************************************/
	look_for.SPS_header: action ==>
	guard
		nal_unit_type = NAL_UNIT_SPS
	do
		if DEBUG_PARSER then
			println("=========== Sequence Parameter Set ID:   ===========");
		end
	end
	/**************************************************************************
	 * look_for.PPS_header
	 *************************************************************************/
	look_for.PPS_header: action ==>
	guard
		nal_unit_type = NAL_UNIT_PPS
	do
		if DEBUG_PARSER then
			println("=========== Picture Parameter Set ID:   ===========");
		end
	end
	/**************************************************************************
	 * look_for.Slice_header
	 *************************************************************************/
	look_for.Slice_header: action ==>
	guard
		(nal_unit_type = NAL_UNIT_NON_IDR_AND_NON_CRA or
		 nal_unit_type = NAL_UNIT_CRA                 or
		 nal_unit_type = NAL_UNIT_IDR)
	do
		if DEBUG_PARSER then
			println("=========== Slice ===========");
		end
	end
	/**************************************************************************
	 * look_for_other_header
	 *************************************************************************/
	look_for_other_header: action ==>
	end
	/**************************************************************************
	 **************************************************************************
	 ********        Video Parameter Set HEADER           *********************
	 **************************************************************************
	 *************************************************************************/

	/**************************************************************************
	 * read_VPS_Header.se_idx_1
	 *************************************************************************/
	uint(size= 3) vps_max_temporal_layers_minus1;
	 
	read_VPS_Header.se_idx_1 : action ==>
	guard
		se_idx = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_u_name(  3, fifo, res, "vps_max_temporal_layers_minus1          ");
		vps_max_temporal_layers_minus1 := res[0];
		vld_u_name(  5, fifo, res, "vps_max_layers_minus1                   ");
		vld_ue_name(    fifo, res, "video_parameter_set_id                  ");
		vld_u_name(  1, fifo, res, "vps_temporal_id_nesting_flag            ");
		se_idx := se_idx + 1;
		cnt_i  := 0;
	end
	/**************************************************************************
	 * read_VPS_Header.se_idx_2
	 *************************************************************************/
	read_VPS_Header.se_idx_2.loop : action ==>
	guard
		se_idx = 2 and isFifoFull(fifo),
		cnt_i <= vps_max_temporal_layers_minus1		
	var
		uint(size=32) res[1]
	do
		vld_ue_name(    fifo, res, "vps_max_dec_pic_buffering[i]            ");
		vld_ue_name(    fifo, res, "vps_num_reorder_pics[i]                 ");
		vld_ue_name(    fifo, res, "vps_max_latency_increase[i]             ");
		cnt_i := cnt_i + 1;
	end
	read_VPS_Header.se_idx_2.loopEnd : action ==>
	guard
		se_idx = 2,
		cnt_i = vps_max_temporal_layers_minus1 + 1
	do
		se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_VPS_Header_done
	 *************************************************************************/	
	read_VPS_Header_done : action ==>
	guard
		se_idx = 3 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_u_name(  1, fifo, res, "vps_extension_flag                      ");
		if res[0] = 1 then
			println("moreRBSP data VPS");
		end
	end 	
	/**************************************************************************
	 **************************************************************************
	 ********        Sequence Parameter Set HEADER        *********************
	 **************************************************************************
	 *************************************************************************/

	/**************************************************************************
	 * read_SPS_Header.se_idx_1
	 *************************************************************************/
	uint(size= 4) sps_max_temporal_layers_minus1[NB_MAX_SPS];
	uint(size=16) sps_pic_width_in_luma_samples[NB_MAX_SPS];

	read_SPS_Header.se_idx_1 : action ==>
	guard
		se_idx = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_u_name(  3, fifo, res, "profile_space                           "); 
		vld_u_name(  5, fifo, res, "profile_idc                             "); 
		flushBits_name(16, fifo,   "reserved_indicator_flags                ");
		vld_u_name(  8, fifo, res, "level_idc                               ");
		se_idx := 10;
	end
	read_SPS_Header.se_idx_1_0 : action ==>
	guard
		se_idx = 10 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_u_name( 32, fifo, res, "profile_compatibility                   "); 
		se_idx := 11;
	end
	read_SPS_Header.se_idx_1_1 : action ==>
	guard
		se_idx = 11 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_ue_name(    fifo, res, "seq_parameter_set_id                    ");
		sps_id := res[0];
		vld_ue_name(    fifo, res, "video_parameter_set_id                  ");
		vld_ue_name(    fifo, res, "chroma_format_idc                       ");
		vld_u_name(  3, fifo, res, "max_temporal_layers_minus1              ");
		sps_max_temporal_layers_minus1[sps_id] := res[0];
		vld_ue_name(    fifo, res, "pic_width_in_luma_samples               ");
		sps_pic_width_in_luma_samples[sps_id] := res[0];
		se_idx := 2;
	end
	/**************************************************************************
	 * read_SPS_Header.se_idx_2
	 *************************************************************************/
	uint(size=16) sps_pic_height_in_luma_samples[NB_MAX_SPS];
	uint(size= 1) sps_pcm_enabled_flag[NB_MAX_SPS];
	uint(size= 8) sps_log2_max_pic_order_cnt_lsb_minus4[NB_MAX_SPS];
	
	read_SPS_Header.se_idx_2 : action ==>
	guard
		se_idx = 2 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size=1)  pic_cropping_flag
	do
		vld_ue_name(    fifo, res, "pic_height_in_luma_samples              "); 
		sps_pic_height_in_luma_samples[sps_id] := res[0];
		vld_u_name(  1, fifo, res, "pic_cropping_flag                       ");
		pic_cropping_flag := res[0];
		if pic_cropping_flag=1 then
			vld_ue_name(    fifo, res, "pic_crop_left_offset                    "); 
			vld_ue_name(    fifo, res, "pic_crop_right_offset                   "); 
			vld_ue_name(    fifo, res, "pic_crop_top_offset                     "); 
			vld_ue_name(    fifo, res, "pic_crop_bottom_offset                  "); 
		end
		vld_ue_name(    fifo, res, "bit_depth_luma_minus8                   ");
		vld_ue_name(    fifo, res, "bit_depth_chroma_minus8                 ");
		vld_u_name(  1, fifo, res, "pcm_enabled_flag                        ");
		sps_pcm_enabled_flag[sps_id] := res[0];
		if sps_pcm_enabled_flag[sps_id] = 1 then
			vld_u_name(  4, fifo, res, "pcm_bit_depth_luma_minus1                      ");
			vld_u_name(  4, fifo, res, "pcm_bit_depth_chroma_minus1                    ");
		end
		vld_ue_name(    fifo, res, "log2_max_pic_order_cnt_lsb_minus4       ");
		sps_log2_max_pic_order_cnt_lsb_minus4[sps_id]:=res[0];
		se_idx := se_idx + 1;
		cnt_i := 0;
	end

	/**************************************************************************
	 * read_SPS_Header.se_idx_3
	 *************************************************************************/
	read_SPS_Header.se_idx_3.loop : action ==>
	guard
		se_idx = 3 and isFifoFull(fifo),
		cnt_i <= sps_max_temporal_layers_minus1[sps_id]
	var
		uint(size=32) res[1]
	do
		vld_ue_name(    fifo, res, "max_dec_pic_buffering                   "); 
		vld_ue_name(    fifo, res, "num_reorder_pics                        "); 
		vld_ue_name(    fifo, res, "max_latency_increase                    "); 
		cnt_i := cnt_i + 1;
	end 

	read_SPS_Header.se_idx_3.loopEnd : action ==>
	guard
		se_idx = 3,
		cnt_i = sps_max_temporal_layers_minus1[sps_id] + 1
	do
		se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SPS_Header.se_idx_4
	 *************************************************************************/
	uint(size= 1) sps_lists_modification_present_flag[NB_MAX_SPS];
	uint(size= 8) sps_log2_min_coding_block_size_minus3[NB_MAX_SPS];
	uint(size= 8) sps_log2_diff_max_min_coding_block_size[NB_MAX_SPS];
	uint(size= 8) sps_log2_min_transform_block_size_minus2[NB_MAX_SPS];
	uint(size= 8) sps_log2_diff_max_min_transform_block_size[NB_MAX_SPS];
	uint(size= 8) sps_maxCUWidth[NB_MAX_SPS];
	uint(size= 8) sps_addCUDepth[NB_MAX_SPS];
	uint(size= 8) sps_maxCUDepth[NB_MAX_SPS];

	read_SPS_Header.se_idx_4 : action ==>
	guard
		se_idx = 4 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) restricted_ref_pic_lists_flag
	do
		vld_u_name(  1, fifo, res, "restricted_ref_pic_lists_flag           "); 
		restricted_ref_pic_lists_flag := res[0];
		if restricted_ref_pic_lists_flag = 1 then
			vld_u_name(  1, fifo, res, "lists_modification_present_flag         ");
			sps_lists_modification_present_flag[sps_id] := res[0];
		else
			sps_lists_modification_present_flag[sps_id] := 1;
		end
		vld_ue_name(    fifo, res, "log2_min_coding_block_size_minus3       ");
		sps_log2_min_coding_block_size_minus3[sps_id] := res[0];
		vld_ue_name(    fifo, res, "log2_diff_max_min_coding_block_size     ");
		sps_log2_diff_max_min_coding_block_size[sps_id] := res[0];
		vld_ue_name(    fifo, res, "log2_min_transform_block_size_minus2    ");
		sps_log2_min_transform_block_size_minus2[sps_id] := res[0];
		vld_ue_name(    fifo, res, "log2_diff_max_min_transform_block_size  ");
		sps_log2_diff_max_min_transform_block_size[sps_id] := res[0];
		sps_maxCUWidth[sps_id] := 1 << (Log2MinCbSize + sps_log2_diff_max_min_coding_block_size[sps_id]); 
    	sps_addCUDepth[sps_id] := 0;
    	while ( ( sps_maxCUWidth[sps_id] >> sps_log2_diff_max_min_coding_block_size[sps_id] ) > 
    		( 1 << ( sps_log2_min_transform_block_size_minus2[sps_id] + 2 + sps_log2_diff_max_min_coding_block_size[sps_id] )  )
    	)
    	do
        	sps_addCUDepth[sps_id] := sps_addCUDepth[sps_id] + 1;
    	end
    	sps_maxCUDepth[sps_id] := sps_log2_diff_max_min_coding_block_size[sps_id] + sps_addCUDepth[sps_id]; 
				
		se_idx := se_idx + 1;
	end 
	/**************************************************************************
	 * read_SPS_Header.se_idx_5
	 *************************************************************************/	 	
	uint(size= 8) sps_max_transform_hierarchy_depth_inter[NB_MAX_SPS];
	uint(size= 8) sps_max_transform_hierarchy_depth_intra[NB_MAX_SPS];
	
	read_SPS_Header.se_idx_5 : action ==>
	guard
		se_idx = 5 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		if sps_pcm_enabled_flag[sps_id] = 1 then
			vld_ue_name(    fifo, res, "log2_min_pcm_coding_block_size_minus3   ");
			vld_ue_name(    fifo, res, "log2_diff_max_min_pcm_coding_block_size ");
		end
		vld_ue_name(    fifo, res, "max_transform_hierarchy_depth_inter     ");
		sps_max_transform_hierarchy_depth_inter[sps_id] := res[0];
		vld_ue_name(    fifo, res, "max_transform_hierarchy_depth_intra     ");
		sps_max_transform_hierarchy_depth_intra[sps_id] := res[0];
		se_idx := se_idx + 1;
	end 
	/**************************************************************************
	 * read_SPS_Header.se_idx_6
	 *************************************************************************/
	uint(size= 1) sps_sample_adaptive_offset_enabled_flag[NB_MAX_SPS];
	uint(size= 8) sps_num_short_term_ref_pic_sets[NB_MAX_SPS];
	
	read_SPS_Header.se_idx_6 : action ==>
	guard
		se_idx = 6 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
   	do
		vld_u_name(  1, fifo, res, "scaling_list_enabled_flag               ");
		vld_u_name(  1, fifo, res, "asymmetric_motion_partitions_enabled_flag"); 
		vld_u_name(  1, fifo, res, "sample_adaptive_offset_enabled_flag     ");
		sps_sample_adaptive_offset_enabled_flag[sps_id] := res[0];
		if sps_pcm_enabled_flag[sps_id] = 1 then
			vld_u_name(  1, fifo, res, "pcm_loop_filter_disable_flag            ");
		end
		vld_u_name(  1, fifo, res, "temporal_id_nesting_flag                ");
		vld_ue_name(    fifo, res, "num_short_term_ref_pic_sets             "); 
		sps_num_short_term_ref_pic_sets[sps_id] := res[0];
		se_idx := se_idx + 1;
		cnt_i  := 0;
	end 
	/**************************************************************************
	 * read_SPS_Header.se_idx_7
	 *************************************************************************/
	uint(size=32) pcRPS[NB_MAX_PPS][PC_RPS_STRUCT_SIZE];	

	read_SPS_Header.se_idx_7.loop : action ==>
	guard
		se_idx = 7 and isFifoFull(fifo),
		cnt_i < sps_num_short_term_ref_pic_sets[sps_id]
   	do
   		parseShortTermRefPicSet(cnt_i, fifo, pcRPS);
		cnt_i := cnt_i + 1;
 	end
 	read_SPS_Header.se_idx_7.loopEnd : action ==>
	guard
		se_idx = 7,
		cnt_i = sps_num_short_term_ref_pic_sets[sps_id]
   	do
		se_idx := se_idx + 1;
		cnt_i  := 0;
	end 
	/**************************************************************************
	 * read_SPS_Header.se_idx_8
	 *************************************************************************/	
	uint(size= 1) sps_long_term_ref_pics_present_flag[NB_MAX_SPS];
	
	read_SPS_Header.se_idx_8 : action ==>
	guard
		se_idx = 8 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
	do
		vld_u_name(  1, fifo, res, "long_term_ref_pics_present_flag         "); 
		sps_long_term_ref_pics_present_flag[sps_id] := res[0];
		vld_u_name(  1, fifo, res, "sps_temporal_mvp_enable_flag            ");
		foreach int i in 0 .. sps_maxCUDepth[sps_id] do
			vld_u_name(  1, fifo, res, "AMVP_MODE                               "); 
		end
		se_idx := se_idx + 1;
	end 
	
	/**************************************************************************
	 * read_SPS_Header_done
	 *************************************************************************/	
	read_SPS_Header_done : action ==>
	guard
		se_idx = 9 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) sps_extension_flag
	do
		vld_u_name(  1, fifo, res, "sps_extension_flag                      ");
		sps_extension_flag := res[0];
		if sps_extension_flag = 1 then
			println("moreRBSP data SPS");
		end
	end 
	/**************************************************************************
	 **************************************************************************
	 ********        Picture Parameter Set HEADER         *********************
	 **************************************************************************
	 *************************************************************************/
	  
	/**************************************************************************
	 * read_PPS_Header.se_idx_1
	 *************************************************************************/	
	uint(size= 8) pps_sps_id[NB_MAX_PPS];
	uint(size= 2) pps_tiles_or_entropy_coding_sync_idc[NB_MAX_PPS];

	read_PPS_Header.se_idx_1 : action ==>
	guard
		se_idx = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
   	do
		vld_ue_name(    fifo, res, "pic_parameter_set_id                    "); 
		pps_id := res[0];
		vld_ue_name(    fifo, res, "seq_parameter_set_id                    "); 
		sps_id := res[0];
		pps_sps_id[pps_id] := res[0];
		se_idx := se_idx + 1;
		cnt_i  := 0;
 	end
	/**************************************************************************
	 * read_PPS_Header.se_idx_2
	 *************************************************************************/
	uint(size= 1) pps_sign_data_hiding_flag[NB_MAX_PPS];
	uint(size= 1) pps_cabac_init_present_flag[NB_MAX_PPS];
	 int(size=32) pps_pic_init_qp_minus26[NB_MAX_PPS];
	uint(size= 1) pps_transform_skip_enabled_flag[NB_MAX_PPS];
	uint(size= 2) pps_SliceGranularity[NB_MAX_PPS];
	uint(size=16) pps_diff_cu_qp_delta_depth[NB_MAX_PPS];
	uint(size= 1) pps_output_flag_present_flag[NB_MAX_PPS];
	uint(size= 1) pps_transquant_bypass_enable_flag[NB_MAX_PPS];
	uint(size= 1) pps_tile_control_present_flag[NB_MAX_PPS];
	uint(size= 8) pps_num_tile_columns_minus1[NB_MAX_PPS];
	uint(size= 8) pps_num_tile_rows_minus1[NB_MAX_PPS];
	uint(size= 1) pps_uniform_spacing_flag[NB_MAX_PPS];

	read_PPS_Header.se_idx_2 : action ==>
	guard
		se_idx = 2 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) tile_info_present_flag
   	do
		vld_u_name(  1, fifo, res, "sign_data_hiding_flag                   ");
		pps_sign_data_hiding_flag[pps_id] := res[0];
		if pps_sign_data_hiding_flag[pps_id] = 1 then
			//FIXME vld_u_name(  4, fifo, res, "sign_hiding_threshold                   ");
		end
		vld_u_name(  1, fifo, res, "cabac_init_present_flag                 ");
		pps_cabac_init_present_flag[pps_id] := res[0];
		vld_ue_name(    fifo, res, "num_ref_idx_l0_default_active_minus1    ");
		vld_ue_name(    fifo, res, "num_ref_idx_l1_default_active_minus1    ");
//		vld_ue_name(    fifo, res, "num_ref_idx_l0_default_active_minus1    ");
//		vld_ue_name(    fifo, res, "num_ref_idx_l1_default_active_minus1    ");
		vld_se_name(    fifo, res, "pic_init_qp_minus26                     ");
		pps_pic_init_qp_minus26[pps_id] := res[0]; 
		vld_u_name(  1, fifo, res, "constrained_intra_pred_flag             ");
		vld_u_name(  1, fifo, res, "transform_skip_enabled_flag             ");
		pps_transform_skip_enabled_flag[pps_id] := res[0];
//		vld_u_name(  2, fifo, res, "slice_granularity                       ");
//		pps_SliceGranularity[pps_id] := res[0] * 2;
		vld_u_name(  1, fifo, res, "cu_qp_delta_enabled_flag                ");
		if res[0] = 1 then
			vld_ue_name(    fifo, res, "diff_cu_qp_delta_depth                  ");
			pps_diff_cu_qp_delta_depth[pps_id] := res[0];
		else
			pps_diff_cu_qp_delta_depth[pps_id] := 0;
		end
		vld_se_name(    fifo, res, "cb_qp_offset                            ");
		vld_se_name(    fifo, res, "cr_qp_offset                            ");
		vld_u_name(  1, fifo, res, "slicelevel_chroma_qp_flag               ");
		vld_u_name(  1, fifo, res, "weighted_pred_flag                      ");
		vld_u_name(  1, fifo, res, "weighted_bipred_flag                    ");
		vld_u_name(  1, fifo, res, "output_flag_present_flag                ");
		pps_output_flag_present_flag[pps_id]:=res[0];
		vld_u_name(  1, fifo, res, "dependent_slices_enabled_flag           ");
		vld_u_name(  1, fifo, res, "transquant_bypass_enable_flag           ");
		pps_transquant_bypass_enable_flag[pps_id]:=res[0];
		vld_u_name(  2, fifo, res, "tiles_or_entropy_coding_sync_idc        ");
		pps_tiles_or_entropy_coding_sync_idc[pps_id]:=res[0];
		pps_num_tile_columns_minus1[pps_id] := 0;
		pps_num_tile_rows_minus1[pps_id]    := 0;
		pps_uniform_spacing_flag[pps_id]    := 1;
		if pps_tiles_or_entropy_coding_sync_idc[pps_id] = 1 then
			vld_u_name(  1, fifo, res, "tile_info_present_flag                  ");
			tile_info_present_flag := res[0];
			vld_u_name(  1, fifo, res, "tile_control_present_flag               ");
			pps_tile_control_present_flag[pps_id] := res[0];
			if tile_info_present_flag = 1 then
				vld_ue_name(    fifo, res, "num_tile_columns_minus1                 "); 
				pps_num_tile_columns_minus1[pps_id] := res[0];
				vld_ue_name(    fifo, res, "num_tile_rows_minus1                    ");
				pps_num_tile_rows_minus1[pps_id] := res[0];
				vld_u_name ( 1, fifo, res, "uniform_spacing_flag                    ");
				pps_uniform_spacing_flag[pps_id] :=res[0];
			end
		end
		se_idx := se_idx + 1;
		cnt_i  := 0;
 	end
	/**************************************************************************
	 * read_PPS_Header.se_idx_3
	 *************************************************************************/	
	uint(size=16) pps_column_width[NB_MAX_PPS][COLUMN_WIDTH_MAX];

  	read_PPS_Header.se_idx_3.loop : action ==>
	guard
		se_idx = 3  and isFifoFull(fifo),
		pps_uniform_spacing_flag[pps_id] = 0 and cnt_i <= pps_num_tile_columns_minus1[pps_id]
	var
		uint(size=32) res[1]
   	do
		vld_ue_name(    fifo, res, "column_width[ ]                         ");
		pps_column_width[pps_id][cnt_i] := res[0];
		if cnt_i >= COLUMN_WIDTH_MAX then
			println("Error read_PPS_Header.se_idx_3.loop : "+cnt_i+" >= "+COLUMN_WIDTH_MAX);
		end		
		cnt_i := cnt_i + 1;
	end 
	read_PPS_Header.se_idx_3.loopEnd : action ==>
	guard
		se_idx = 3,
		pps_uniform_spacing_flag[pps_id] = 0 and cnt_i > pps_num_tile_columns_minus1[pps_id]
   	do
		se_idx := se_idx + 1;
		cnt_i  := 0;
	end 
	read_PPS_Header.se_idx_3.noLoop : action ==>
	guard
		se_idx = 3,
		pps_uniform_spacing_flag[pps_id] = 1
   	do
		se_idx := se_idx + 2;
	end 
	/**************************************************************************
	 * read_PPS_Header.se_idx4
	 *************************************************************************/	
	uint(size=16) pps_row_height[NB_MAX_PPS][COLUMN_WIDTH_MAX];
	
	read_PPS_Header.se_idx_4.loop : action ==>
	guard
		se_idx = 4 and isFifoFull(fifo),
		pps_uniform_spacing_flag[pps_id] = 0 and cnt_i <= pps_num_tile_rows_minus1[pps_id]
	var
		uint(size=32) res[1]
   	do
		vld_ue_name(    fifo, res, "row_height[ ]                           ");
		pps_row_height[pps_id][cnt_i] := res[0];
		if cnt_i >= COLUMN_WIDTH_MAX then
			println("Error read_PPS_Header.se_idx_4.loop : "+cnt_i+" >= "+COLUMN_WIDTH_MAX);
		end
		cnt_i := cnt_i + 1;
	end 
	read_PPS_Header.se_idx_4.loopEnd : action ==>
	guard
		se_idx = 4,
		pps_uniform_spacing_flag[pps_id] = 0 and cnt_i > pps_num_tile_rows_minus1[pps_id]
   	do
		se_idx := se_idx + 1;
	end 
	
	/**************************************************************************
	 * read_PPS_Header.se_idx5
	 *************************************************************************/	
	uint(size= 1) pps_deblocking_filter_control_present_flag[NB_MAX_PPS];
	uint(size =1) deblocking_filter_override_enabled_flag[NB_MAX_PPS];
	uint(size= 1) pps_loop_filter_across_slice_flag[NB_MAX_PPS];

	read_PPS_Header.se_idx_5 : action ==>
	guard
		se_idx = 5 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) pps_extension_flag
   	do
   		if (pps_tiles_or_entropy_coding_sync_idc[pps_id] = 1) then
			if pps_tile_control_present_flag[pps_id] = 1 then
				vld_u_name(  1, fifo, res, "loop_filter_across_tiles_enabled_flag   ");
			end
		elsif (pps_tiles_or_entropy_coding_sync_idc[pps_id] = 2) then
			vld_ue_name(    fifo, res, "num_substreams_minus1                   ");
		end
		vld_u_name(  1, fifo, res, "loop_filter_across_slice_flag           ");
		pps_loop_filter_across_slice_flag[pps_id] := res[0];
		vld_u_name(  1, fifo, res, "deblocking_filter_control_present_flag  ");
		pps_deblocking_filter_control_present_flag[pps_id] := res[0];
		if pps_deblocking_filter_control_present_flag[pps_id] = 1 then
			vld_u_name(  1, fifo, res, "pps_deblocking_filter_flag              ");
			deblocking_filter_override_enabled_flag[pps_id] := res[0];
		else
			deblocking_filter_override_enabled_flag[pps_id] := 0;
		end
		vld_u_name(  1, fifo, res, "pps_scaling_list_data_present_flag      ");
		vld_ue_name(    fifo, res, "log2_parallel_merge_level_minus2        ");
		vld_u_name(  1, fifo, res, "slice_header_extension_present_flag     ");
		vld_u_name(  1, fifo, res, "pps_extension_flag                      ");
		pps_extension_flag := res[0];
		if pps_extension_flag = 1 then
			println("moreRBSP data PPS");
		end
		se_idx := se_idx + 1;
	end 

	/**************************************************************************
	 * read_PPS_Header_done
	 *************************************************************************/	
	read_PPS_Header_done : action ==>
	guard
		se_idx = 6
	end 	

	/**************************************************************************
	 **************************************************************************
	 ********        Slice HEADER                         *********************
	 **************************************************************************
	 *************************************************************************/

	/**************************************************************************
	 * read_SliceHeader.se_idx_1
	 *************************************************************************/
	uint(size=32) poc;
	uint(size= 3) slice_type;
	uint(size= 1) entropy_slice_flag;
	uint(size=32) slice_address;
	uint(size= 1) slice_sample_adaptive_offset_flag[3];
	
	procedure compute_POC(int pic_order_cnt_lsb)
	var
	     int iPOClsb = pic_order_cnt_lsb,
	     int iPrevPOC = poc,
	     int iMaxPOClsb := 1 << (sps_log2_max_pic_order_cnt_lsb_minus4[sps_id] + 4),
	     int iPrevPOClsb,
	     int iPrevPOCmsb,
	     int iPOCmsb
	begin
		iPrevPOClsb := iPrevPOC mod iMaxPOClsb;
		iPrevPOCmsb := iPrevPOC-iPrevPOClsb;
	    if( (iPOClsb < iPrevPOClsb) and ( (iPrevPOClsb - iPOClsb) >= (iMaxPOClsb / 2) ) ) then
	    	iPOCmsb := iPrevPOCmsb + iMaxPOClsb;
	    else 
	    	if( (iPOClsb > iPrevPOClsb)  and ( (iPOClsb - iPrevPOClsb) > (iMaxPOClsb / 2) ) ) then
	 			iPOCmsb := iPrevPOCmsb - iMaxPOClsb;
	   		else
				iPOCmsb := iPrevPOCmsb;
			end
		end
	    poc:= iPOCmsb+iPOClsb;
	end
	
	read_SliceHeader.se_idx_1.read : action ==> PictSize:[pictSize] repeat 2, SliceType:[slice_type]
	guard
		se_idx = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size=32) prev := 0,
		uint(size=32) j,
		uint(size= 1) first_slice_in_pic_flag,
		uint(size= 1) dependent_slice_flag,
		uint(size=16) pictSize[2]
   	do	
		vld_u_name(  1, fifo, res, "first_slice_in_pic_flag                 ");
		first_slice_in_pic_flag := res[0];
		if nal_unit_type >= NAL_UNIT_CRA and nal_unit_type <= NAL_UNIT_IDR then
			vld_u_name(  1, fifo, res, "no_output_of_prior_pics_flag            ");
		end
		vld_ue_name(    fifo, res, "pic_parameter_set_id                    "); 
		pps_id := res[0];
		if first_slice_in_pic_flag = 0 then
			vld_ue_name(    fifo, res, "slice_address                           ");
			slice_address := res[0];
		else
			slice_address := 0;
		end
		vld_ue_name(    fifo, res, "slice_type                              ");
		slice_type := res[0];
		vld_u_name(  1, fifo, res, "dependent_slice_flag                    ");
		dependent_slice_flag := res[0];
		if dependent_slice_flag = 0 then
			sps_id := pps_sps_id[pps_id];
			if(pps_output_flag_present_flag[pps_id] = 1) then	
				vld_u_name(  1, fifo, res, "pic_output_flag                         ");
			end
//			RapPicFlag := if ( nal_unit_type  >=  4  &&  nal_unit_type  <=  8  )  then  1  else  0 end;
//			if RapPicFlag = 1 then
//			    vld_ue_name(    fifo, res, "rap_pic_id                              ");
//	   			vld_u_name(  1, fifo, res, "no_output_of_prior_pics_flag            ");
//	   			poc:=0;
//			else
			if nal_unit_type = NAL_UNIT_IDR then
				poc:=0;
			else
				vld_u_name( sps_log2_max_pic_order_cnt_lsb_minus4[sps_id] + 4, fifo, res, "pic_order_cnt_lsb                       "); 

				//compute the number of POC
				compute_POC(res[0]);
				
				vld_u_name(  1, fifo, res, "short_term_ref_pic_set_sps_flag         ");
				if res[0] = 0 then
			   		parseShortTermRefPicSet(pps_id, fifo, pcRPS);
		   		else
				    vld_ue_name(    fifo, res, "short_term_ref_pic_set_idx              ");
				end
				if sps_long_term_ref_pics_present_flag[sps_id] = 1 then
					vld_ue_name(    fifo, res, "num_long_term_pics                      "); pcRPS[pps_id][NUM_LONG_TERM_PICS]	:= res[0];
				   	j := res[0] + pcRPS[pps_id][NUM_PICS]-1;
			   		foreach uint(size=8) i in 0 .. res[0]-2 do
			    		vld_ue_name(    fifo, res, "delta_poc_lsb_lt                        "); prev := prev + res[0] + 1;
						pcRPS[j][POC] := pcRPS[pps_id][POC] - prev;
						vld_u_name(  1, fifo, res, "delta_poc_msb_present_flag              ");
						if res[0] = 1 then
			    			vld_ue_name(    fifo, res, "delta_poc_msb_cycle_lt_minus1                "); 
						end
						vld_u_name(  1, fifo, res, "used_by_curr_pic_lt_flag                "); pcRPS[pps_id][USED+i]		:= res[0];
					end
					pcRPS[pps_id][NUM_PICS] := pcRPS[pps_id][NUM_PICS] + pcRPS[pps_id][NUM_LONG_TERM_PICS];
				end
			end
			if( sps_sample_adaptive_offset_enabled_flag[sps_id] = 1) then
				//FIXME vld_u_name(  1, fifo, res, "slice_sao_interleaving_flag             ");
				//FIXME slice_sao_interleaving_flag := res[0];
				vld_u_name(  1, fifo, res, "slice_sample_adaptive_offset_flag       "); 
				slice_sample_adaptive_offset_flag[0] := res[0];
				if( /*FIXME: slice_sao_interleaving_flag = 1 && */
					slice_sample_adaptive_offset_flag[0] = 1) then
					vld_u_name(  1, fifo, res, "sao_chroma_enable_flag                  ");
					slice_sample_adaptive_offset_flag[1] := res[0];
//					vld_u_name(  1, fifo, res, "sao_cr_enable_flag                      "); 
					slice_sample_adaptive_offset_flag[2] := res[0];
				end
			end
//			vld_ue_name(    fifo, res, "aps_id                                  "); 
		end
		pictSize[0] := sps_pic_width_in_luma_samples[sps_id];
		pictSize[1] := sps_pic_height_in_luma_samples[sps_id];
	    se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader.se_idx_2
	 *************************************************************************/
	uint(size= 8) num_ref_idx_l0_active_minus1;
	uint(size= 8) num_ref_idx_l1_active_minus1;
	uint(size= 8) num_ref_idx_lc_active_minus1;
	
	read_SliceHeader.se_idx_2.read : action ==>
	guard
		se_idx = 2 and isFifoFull(fifo)
	var
		uint(size=32) res[1]
   	do
	   	num_ref_idx_l0_active_minus1 := 0;
	   	num_ref_idx_l1_active_minus1 := 0;
	   	num_ref_idx_lc_active_minus1 := 0;
		if entropy_slice_flag = 0 then
			if slice_type = P_SLICE or slice_type = B_SLICE then
				vld_u_name(  1, fifo, res, "num_ref_idx_active_override_flag        ");		
	      		if res[0] = 1 then
					vld_ue_name(    fifo, res, "num_ref_idx_l0_active_minus1            ");
					num_ref_idx_l0_active_minus1 := res[0];
					if slice_type = B_SLICE then
						vld_ue_name(    fifo, res, "num_ref_idx_l1_active_minus1            ");
						num_ref_idx_l1_active_minus1 := res[0];
					end
	      		end
	        end
		    if (sps_lists_modification_present_flag[sps_id] = 1) then
		    	println("TODO ref_pic_list_modification( )");
		    	println("TODO ref_pic_list_combination( )");
		    end
		    if slice_type = B_SLICE then
				vld_u_name(  1, fifo, res, "mvd_l1_zero_flag                        ");		
			end	    
	    end
		se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader.se_idx_3
	 *************************************************************************/
	 int(size=32) slice_qp;
	uint(size= 8) MaxNumMergeCand;
	 	
	read_SliceHeader.se_idx_3 : action ==>
	guard
		se_idx = 3 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) deblocking_filter_override_flag := 0,
		uint(size= 1) slice_header_disable_deblocking_filter_flag := 0
   	do	
	if( pps_cabac_init_present_flag[pps_id] = 1  and  slice_type  !=  I_SLICE )	then
		vld_u_name(  1, fifo, res, "cabac_init_flag                         ");		
	end
	if(entropy_slice_flag = 0) then
		vld_se_name(    fifo, res, "slice_qp_delta                          ");
		slice_qp := pps_pic_init_qp_minus26[pps_id] + 26 +res[0];
		if(pps_deblocking_filter_control_present_flag[pps_id] = 1) then	
 			if(deblocking_filter_override_enabled_flag[pps_id] = 1) then
				vld_u_name(  1, fifo, res, "deblocking_filter_override_flag        ");
				deblocking_filter_override_flag := res[0];	
			end
			if(deblocking_filter_override_flag = 0) then
				vld_u_name(  1, fifo, res, "slice_header_disable_deblocking_filter_flag          ");
				if( slice_header_disable_deblocking_filter_flag = 0) then
					vld_se_name(    fifo, res, "beta_offset_div2                        ");
					vld_se_name(    fifo, res, "tc_offset_div2                          ");
				end
			end
		end	
		if( slice_type = B_SLICE ) then	
			vld_u_name(  1, fifo, res, "collocated_from_l0_flag                 ");
		end
/* 		if( slice_type != I  &&  
			((collocated_from_l0_flag  &&	num_ref_idx_l0_active_minus1 > 0)  | |
			  (!collocated_from_l0_flag  &&  num_ref_idx_l1_active_minus1 > 0) )	
			collocated_ref_idx	ue(v)
		if( ( weighted_pred_flag  &&   slice_type = = P)  | |
			 ( weighted_bipred_idc  = =  1  &&  slice_type  = =  B ) )	
			pred_weight_table( )	
*/	
	end	
	vld_ue_name(    fifo, res, "5_minus_max_num_merge_cand              ");
	MaxNumMergeCand := 5 - res[0];
 	if( pps_loop_filter_across_slice_flag[sps_id] = 1  and  (
	 	slice_sample_adaptive_offset_flag[0] = 1 
	 	or slice_sample_adaptive_offset_flag[1] = 1 
//	 	or slice_sample_adaptive_offset_flag[2] = 1 
	 	or  slice_header_disable_deblocking_filter_flag = 0)) then
		vld_u_name( 1,  fifo, res, "slice_loop_filter_across_slices_enabled_flag");
 	end
 	
	   se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader.se_idx_4
	 *************************************************************************/	
	read_SliceHeader.se_idx_4 : action ==>
	guard
		se_idx = 4 and isFifoFull(fifo)
	do
	    se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader.se_idx_5
	 *************************************************************************/	
	 procedure initWpScaling()
	 begin
		foreach uint(size=8) e in 0 .. 1 do
			foreach uint(size=8) i in 0 .. MAX_NUM_REF-1 do
				foreach uint(size=8) yuv in 0 .. 2 do
					if wp[e][i][yuv][WP_PRESENT_FLAG] = 1 then
						wp[e][i][yuv][WP_I_WEIGHT] := (1 << wp[e][i][yuv][WP_LOG2_WEIGHT_DENOM]);
						wp[e][i][yuv][WP_I_OFFSET] := 0;
					end
					wp[e][i][yuv][WP_W]     := wp[e][i][yuv][WP_I_WEIGHT];
					wp[e][i][yuv][WP_O]     := wp[e][i][yuv][WP_I_OFFSET] * (1);
					wp[e][i][yuv][WP_SHIFT] := wp[e][i][yuv][WP_LOG2_WEIGHT_DENOM];
					if wp[e][i][yuv][WP_LOG2_WEIGHT_DENOM] >= 1 then
						wp[e][i][yuv][WP_ROUND] := (1 << wp[e][i][yuv][WP_LOG2_WEIGHT_DENOM]) - 1;
					else
						wp[e][i][yuv][WP_ROUND] := 0;
					end
				end
			end
		end
	end
   	read_SliceHeader.se_idx_5 : action ==>
	guard
		se_idx = 5 and isFifoFull(fifo)
	do
	    se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader.se_idx_6
	 *************************************************************************/	
	read_SliceHeader.se_idx_6 : action ==>
	guard
		se_idx = 6 and isFifoFull(fifo)
   	do	
	    se_idx := se_idx + 1;
	end
	/**************************************************************************
	 * read_SliceHeader_done
	 *************************************************************************/
	uint(size=16) sliceCbAddrZs;
	read_SliceHeader_done : action ==>
	guard
		se_idx = 7
	do
		if DEBUG_MODE then
			println("\tPOC: "+poc);
		end
		sliceData_idx := 1;
		sliceCbAddrZs := 0;
	end 	

	/**************************************************************************
	 **************************************************************************
	 ********        Slice Data                           *********************
	 **************************************************************************
	 *************************************************************************/
		
	uint(size= 9) codIRange[1];
	uint(size=16) codIOffset[1];
	uint(size= 9) ctxTable[NB_MAX_SE][NB_MAX_NUM_CTX];
	uint(size= 8) sliceData_idx;

	/**************************************************************************
	 * read_SliceData.init
	 *************************************************************************/	
	uint(size= 8) Log2CtbSize;
	uint(size= 8) Log2MinCbSize;
	uint(size=16) PicWidthInCtbsY;
	
	uint(size= 8) Log2MinTrafoSize;
	uint(size= 8) Log2MaxTrafoSize;
	uint(size=16) ctbAddrRStoTS[CTB_ADDR_TS_MAX];
	uint(size=16) ctbAddrTStoRS[CTB_ADDR_TS_MAX];
	uint(size=16) TileId[CTB_ADDR_TS_MAX];
	uint(size=16) MinCbAddrZS[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX];
	
	uint(size= 1) skip_flag[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX];
	uint(size= 1) inter_pred_flag[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX];
	uint(size= 1) merge_flag[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX];
	
	uint(size= 8) top_ipm[CTB_ADDR_TS_MAX];
	uint(size= 8) left_ipm[CTB_ADDR_TS_MAX];

	read_SliceData.init: action ==>
	guard
		sliceData_idx = 1
	var
		uint(size=16) PicHeightInCtbsY,
		uint(size=16) PicWidthInMinCbs,
		uint(size=16) PicHeightInMinCbs,
		uint(size=16) PicSizeInCtbsY,
		uint(size=16) column_width[COLUMN_WIDTH_MAX]= [pps_column_width[pps_id][ i ] : for int i in 0 .. COLUMN_WIDTH_MAX-1],
		uint(size=16) row_height[COLUMN_WIDTH_MAX]	= [pps_row_height[pps_id][ i ] : for int i in 0 .. COLUMN_WIDTH_MAX-1],
		uint(size= 8) num_tile_columns_minus1		= pps_num_tile_columns_minus1[pps_id],
		uint(size= 8) num_tile_rows_minus1			= pps_num_tile_rows_minus1[pps_id],
		uint(size= 1) uniform_spacing_flag			= pps_uniform_spacing_flag[pps_id],
		uint(size=16) colWidth[COLUMN_WIDTH_MAX],
		uint(size=16) rowHeight[COLUMN_WIDTH_MAX],
		uint(size=16) ColBd[COLUMN_WIDTH_MAX],
		uint(size=16) RowBd[COLUMN_WIDTH_MAX],
		uint(size=16) tileX,
		uint(size=16) tileY,
		uint(size=16) val,
		uint(size=16) tbX,
		uint(size=16) tbY,
		uint(size=16) tIdx,
		uint(size=16) ctbAddrRS,
		uint(size=16) m,
		uint(size=16) p
	do
		Log2MinCbSize     := sps_log2_min_coding_block_size_minus3[sps_id] + 3;
		Log2CtbSize       := Log2MinCbSize + sps_log2_diff_max_min_coding_block_size[sps_id];
		PicWidthInCtbsY   := ( sps_pic_width_in_luma_samples[sps_id]  + ( 1 << Log2CtbSize )-1 ) >> Log2CtbSize;
		PicHeightInCtbsY  := ( sps_pic_height_in_luma_samples[sps_id] + ( 1 << Log2CtbSize )-1 ) >> Log2CtbSize;
		PicWidthInMinCbs  := sps_pic_width_in_luma_samples[sps_id] >> Log2MinCbSize;
		PicHeightInMinCbs := sps_pic_height_in_luma_samples[sps_id] >> Log2MinCbSize;
		PicSizeInCtbsY    := PicWidthInCtbsY * PicHeightInCtbsY;
		
		Log2MinTrafoSize  := sps_log2_min_transform_block_size_minus2[sps_id] + 2;
		Log2MaxTrafoSize  := Log2MinTrafoSize + sps_log2_diff_max_min_transform_block_size[sps_id];
		
		skip_flag		  := [[0	: for int j in 0 .. CTB_ADDR_TS_MAX-1]	: for int i in 0 .. CTB_ADDR_TS_MAX-1];	
		inter_pred_flag   := [[0	: for int j in 0 .. CTB_ADDR_TS_MAX-1]	: for int i in 0 .. CTB_ADDR_TS_MAX-1];
		merge_flag		  := [[0	: for int j in 0 .. CTB_ADDR_TS_MAX-1]	: for int i in 0 .. CTB_ADDR_TS_MAX-1];
		
		
		if num_tile_columns_minus1 >= COLUMN_WIDTH_MAX then
			println("Error read_SliceData.init : ColumnWidth : "+num_tile_columns_minus1+" >= "+COLUMN_WIDTH_MAX);
		end
		if uniform_spacing_flag = 1 then
			foreach int i in 0 .. num_tile_columns_minus1 do
				colWidth[ i ] := ((i + 1)*PicWidthInCtbsY) / (num_tile_columns_minus1+1) - (i*PicWidthInCtbsY) / (num_tile_columns_minus1+1);
			end
		else
			colWidth[ num_tile_columns_minus1 ] := PicWidthInCtbsY;
			foreach int i in 0 .. num_tile_columns_minus1 do
				colWidth[ i ] := column_width[ i ];
				colWidth[ num_tile_columns_minus1 ] := colWidth[ num_tile_columns_minus1 ] - colWidth[ i ];
			end
		end
		if num_tile_rows_minus1 >= COLUMN_WIDTH_MAX then
			println("Error read_SliceData.init : RowHeight : "+num_tile_rows_minus1+" >= "+COLUMN_WIDTH_MAX);
		end
		if uniform_spacing_flag = 1 then
			foreach int i in 0 .. num_tile_rows_minus1 do
				rowHeight[ i ] := ((i+1)*PicHeightInCtbsY) / (num_tile_rows_minus1+1) - (i*PicHeightInCtbsY) / (num_tile_rows_minus1+1);
			end
		else
			rowHeight[ num_tile_rows_minus1 ] := PicHeightInCtbsY;
			foreach int i in 0 .. num_tile_rows_minus1 do
				rowHeight[ i ] := row_height[ i ];
				rowHeight[ num_tile_rows_minus1 ] := rowHeight[ num_tile_columns_minus1 ] - rowHeight[ i ];
			end
		end
		ColBd[ 0 ] := 0;
		foreach int i in 0 .. num_tile_columns_minus1 do
			ColBd[ i + 1 ] := ColBd[ i ] + colWidth[ i ];
		end
		RowBd[ 0 ] := 0;
		foreach int i in 0 .. num_tile_rows_minus1 do
			RowBd[ i + 1 ] := RowBd[ i ] + rowHeight[ i ];
		end
		/*********************************************************************
		 * 6.5 Conversion process for coding tree block address 
		 * in coding tree block raster order to tile scan order
		 ********************************************************************/
		if PicHeightInCtbsY * PicWidthInCtbsY >= CTB_ADDR_TS_MAX then
			println("Error read_SliceData.init : CtbAddrTS : "+(PicHeightInCtbsY * PicWidthInCtbsY)+" >= "+CTB_ADDR_TS_MAX);
		end
		foreach int ctbAddrRS_v in 0 .. PicSizeInCtbsY - 1 do
			tbX   := ctbAddrRS_v mod PicWidthInCtbsY;
			tbY   := ctbAddrRS_v / PicWidthInCtbsY;
			tileX := 0;
			tileY := 0;
			foreach int i in 0 .. num_tile_columns_minus1 do
				if tbX < ColBd[i] then
					tileX := i;
				end
			end
			foreach int j in 0 .. num_tile_rows_minus1 do
				if tbY < RowBd[j] then
					tileY := j;
				end
			end
			val := 0;
			foreach int i in 0 .. tileX-1 do
				val := val + rowHeight[ tileY ] * colWidth[ i ];
			end
			foreach int j in 0 .. tileY-1 do
				val := val + PicWidthInCtbsY * rowHeight[ j ];
			end
			val 						 := val + ( tbY - RowBd[tileY] ) * colWidth[tileY] + tbX - ColBd[tileX];
			ctbAddrRStoTS[ ctbAddrRS_v ] := val;
			ctbAddrTStoRS[ val ] 		 := ctbAddrRS_v;			
		end
		
		tIdx := 0;
		foreach int j in 0 .. num_tile_rows_minus1 do
			foreach int i in 0 .. num_tile_columns_minus1 do
				foreach int y in RowBd[ j ] .. RowBd[ j + 1 ] - 1 do
					foreach int x in ColBd[ i ] .. ColBd[ i + 1 ] - 1 do
						TileId[ ctbAddrRStoTS[ y*PicWidthInCtbsY + x ] ] := tIdx;						
					end
				end
				tIdx := tIdx + 1;
			end
		end
		
		if PicHeightInMinCbs >= CTB_ADDR_TS_MAX then
			println("Error read_SliceData.init : MinCbAddrZS y : "+PicHeightInMinCbs+" >= "+CTB_ADDR_TS_MAX);
		end
		if PicWidthInMinCbs >= CTB_ADDR_TS_MAX then
			println("Error read_SliceData.init : MinCbAddrZS x : "+PicWidthInMinCbs+" >= "+CTB_ADDR_TS_MAX);
		end
		foreach int y in 0 .. PicHeightInMinCbs-1 do
			foreach int x in 0 .. PicWidthInMinCbs-1 do
				tbX := ( x << Log2MinCbSize ) >> Log2CtbSize;
				tbY := ( y << Log2MinCbSize ) >> Log2CtbSize;
				ctbAddrRS := PicWidthInCtbsY * tbY + tbX;
				p := ctbAddrRStoTS[ctbAddrRS] << ( (Log2CtbSize - Log2MinCbSize) * 2 );
				foreach int i in 0 .. Log2CtbSize - Log2MinCbSize - 1 do
					m := 1 << i;
					p := p + if (m & x) != 0 then m*m else 0 end + if (m & y) != 0 then 2 * m * m else 0 end;
				end
				MinCbAddrZS[x][y] := p;	
			end
		end
		
		top_ipm		:= [INTRA_DC	: for int i in 0 .. CTB_ADDR_TS_MAX-1];	
		left_ipm	:= [INTRA_DC	: for int i in 0 .. CTB_ADDR_TS_MAX-1];
		//
		sliceData_idx := 2;
	end
	/**************************************************************************
	 * read_SliceData.start
	 *************************************************************************/	
	uint(size=32) CtbAddrRS;
	uint(size=32) CtbAddrTS;
	uint(size= 1) end_of_slice_flag;
	 
	read_SliceData.start : action ==>
	guard
		sliceData_idx = 2
	var
		uint(size=32) SliceCtbAddrRS = ( slice_address >> pps_SliceGranularity[pps_id] )
	do
		decodeStart(codIRange, codIOffset, fifo);
		contextInit(slice_qp, slice_type, ctxTable);
		//
		CtbAddrRS     := SliceCtbAddrRS;
		CtbAddrTS     := ctbAddrRStoTS[CtbAddrRS];
		end_of_slice_flag  := 0;
		sliceData_idx := 3;
	end
	/**************************************************************************
	 * read_SliceData_gotoCodingTree
	 *************************************************************************/	
	read_SliceData_gotoCodingTree : action ==>
	guard
		sliceData_idx = 3
	do
		sliceData_idx  := 4;
		codingTree_idx := 1;
	end
	/**************************************************************************
	 * read_SliceData.retCodingTree
	 *************************************************************************/	
	read_SliceData.retCodingTree : action ==>
	guard
		sliceData_idx = 4 and isFifoFull(fifo)
	var
		uint(size=32) res[1] := [0]
	do
		get_END_OF_SLICE_FLAG(codIRange, codIOffset, fifo, res);
		end_of_slice_flag := res[0];
		CtbAddrTS := CtbAddrTS + 1;
		CtbAddrRS := ctbAddrTStoRS[CtbAddrTS];
		if( end_of_slice_flag = 0 and ( 
			( pps_tiles_or_entropy_coding_sync_idc[pps_id] = 1 and TileId[ CtbAddrTS ] != TileId[ CtbAddrTS - 1 ] ) or
			( pps_tiles_or_entropy_coding_sync_idc[pps_id] = 2 and (CtbAddrTS mod PicWidthInCtbsY) = 0 ) 
			) ) then
//			rbsp_trailing_bits( )
			showBits (24, fifo, res);
			if( res[0] = 0x000002 ) then
				flushBits(24, fifo);
			end
	 	end
	 	if end_of_slice_flag = 0 then
			sliceData_idx := 3;
		else
			sliceData_idx := 5;
		end
	end
	/**************************************************************************
	 * read_SliceData_end
	 *************************************************************************/	
	read_SliceData_end: action ==>
	guard
		sliceData_idx = 5	
	end

	/**************************************************************************
	 **************************************************************************
	 ********        Coding Tree                          *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size=16) xCtb;
	uint(size=16) yCtb;
	uint(size= 8) NumPCMBlock;
	uint(size=32) CtbAddrInSlice;
	uint(size= 8) codingTree_idx;
	uint(size= 8) ctStack_idx;
	uint(size=16) ctStack[5][CT_STRUCT_SIZE];
	
	/**************************************************************************
	 * read_CodingTree.start
	 *************************************************************************/	
	read_CodingTree.start : action ==>
	guard
		codingTree_idx = 1
	var
		uint(size=32) CtbSize        = 1 << Log2CtbSize
	do
//		println("read_CodingTree.start");
		NumPCMBlock    := 0;
		xCtb           := InverseRasterScan( CtbAddrRS, CtbSize, CtbSize, sps_pic_width_in_luma_samples[sps_id], 0 );
		yCtb           := InverseRasterScan( CtbAddrRS, CtbSize, CtbSize, sps_pic_width_in_luma_samples[sps_id], 1 );
//		println("CtbAddrRS = "+CtbAddrRS+", CtbSize = "+CtbSize+", pic_width_in_luma_samples = "+sps_pic_width_in_luma_samples[sps_id]+", x_ctb = "+xCtb+", y_ctb = "+yCtb+", log2_ctb_size = "+Log2CtbSize);
		
		CtbAddrInSlice := CtbAddrRS - ( slice_address >> pps_SliceGranularity[pps_id] );
		codingTree_idx := 2;
	end
	/**************************************************************************
	 * read_CodingTree_gotoSaoParam
	 *************************************************************************/	
	read_CodingTree_gotoSaoParam : action ==>
	guard
		codingTree_idx = 2,
		slice_sample_adaptive_offset_flag[0] = 1 or slice_sample_adaptive_offset_flag[1] = 1
	do
		codingTree_idx	:= 3;
		sao_idx			:= 1;
		sao_rx			:= xCtb >> Log2CtbSize;
		sao_ry			:= yCtb >> Log2CtbSize;
		sao_cIdx		:= 0;
	end
	read_CodingTree.noGotoSaoParam : action ==>
	guard
		codingTree_idx = 2,
		slice_sample_adaptive_offset_flag[0] = 0 and slice_sample_adaptive_offset_flag[1] = 0
	do
		codingTree_idx := 3;
	end
	/**************************************************************************
	 * read_CodingTree_gotoCodingQuadTree
	 *************************************************************************/	
	read_CodingTree_gotoCodingQuadTree : action ==>
	guard
		codingTree_idx = 3
	do
		codingTree_idx				:= 4;
		ctStack_idx					:= 0;
		ctStack[0][CT_idx]			:= 1;
		ctStack[0][CT_x0]			:= xCtb;
		ctStack[0][CT_y0]			:= yCtb;
		ctStack[0][CT_log2CbSize]	:= Log2CtbSize;
		ctStack[0][CT_ctDepth]		:= 0;
	end
	/**************************************************************************
	 * read_CodingTree_end
	 *************************************************************************/	
	read_CodingTree_end : action ==>
	guard
		codingTree_idx = 4
	end
	/**************************************************************************
	 **************************************************************************
	 ********        sao_param                            *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 3) sao_cIdx;
	uint(size=16) sao_rx;
	uint(size=16) sao_ry;
	uint(size= 8) sao_idx;
	/**************************************************************************
	 * read_SaoParam.start
	 *************************************************************************/	
	read_SaoParam.start : action ==>
	guard
		sao_idx = 1 and isFifoFull(fifo)
	var
		 int(size=32) res[1],
		bool		  leftCtbInSlice,
		bool		  leftCtbInTile,
		bool		  upCtbInSlice,
		bool		  upCtbInTile,
		uint(size= 1) sao_merge_left_flag	:= 0,
		uint(size= 1) sao_merge_up_flag		:= 0
	do
		if sao_rx > 0 then
			leftCtbInSlice := CtbAddrInSlice > 0;
			leftCtbInTile  := true; //(TileId[ CtbAddrTS ] = TileId[ ctbAddrRStoTS[ CtbAddrRS - 1 ] ]);
			if leftCtbInSlice and leftCtbInTile then
				get_SAO_MERGE_LEFT_FLAG(codIRange, codIOffset, ctxTable, fifo, res);
				sao_merge_left_flag := res[0];
			end
		end
		if sao_ry > 0 and sao_merge_left_flag = 0 then
			upCtbInSlice   := true; //(( CtbAddrTS - ctbAddrRStoTS[CtbAddrRS - PicWidthInCtbsY] ) <= CtbAddrInSlice);
			upCtbInTile    := true; //(TileId[ CtbAddrTS ] = TileId[ ctbAddrRStoTS[ CtbAddrRS - PicWidthInCtbsY ] ]);
			if upCtbInSlice and upCtbInTile then
				get_SAO_MERGE_UP_FLAG(codIRange, codIOffset, ctxTable, fifo, res);
				sao_merge_up_flag := res[0];
			end
		end
		sao_idx  := 2;
		if sao_merge_left_flag = 0 and sao_merge_up_flag = 0 then
			sao_cIdx := 0;
		else 
			sao_cIdx := 3;
		end		
	end		
	/**************************************************************************
	 * read_SaoParam.loop
	 *************************************************************************/	
	uint(size= 8) sao_typeIdx;
	read_SaoParam.loop : action ==>
	guard
		sao_idx  = 2 and isFifoFull(fifo),
		sao_cIdx < 3 and slice_sample_adaptive_offset_flag[sao_cIdx] = 1
	var
		uint(size= 8) offsetTh = (1<<3)-1,
		 int(size=16) offset[4],
		 int(size=32) res[1] := [0]
	do
		if sao_cIdx != 2 then
			get_SAO_TYPE_IDX(codIRange, codIOffset, ctxTable, fifo, res);
			sao_typeIdx := res[0];
        end
		if sao_typeIdx != SAO_NOT_APPLIED then
			foreach int i in 0 .. 3 do
				get_SAO_OFFSET_ABS(offsetTh, codIRange, codIOffset, fifo, res);
				offset[i] := res[0]; 
			end
			if sao_typeIdx = SAO_BAND then
				foreach int i in 0 .. 3 do
					if offset[i] != 0 then
						get_SAO_OFFSET_SIGN(codIRange, codIOffset, fifo, res);
						if res[0] != 0 then
							offset[i] := -offset[i];
						end 
					end
				end
				get_SAO_BAND_POSITION(codIRange, codIOffset, fifo, res);
			else
				if sao_cIdx != 2 then
					get_SAO_EO(codIRange, codIOffset, fifo, res);
				end
			end
		end 
		sao_cIdx := sao_cIdx + 1;
	end
	read_SaoParam.nextLoop : action ==>
	guard
		sao_idx  = 2 ,
		sao_cIdx < 3 and slice_sample_adaptive_offset_flag[sao_cIdx] = 0
	do
		sao_cIdx := sao_cIdx + 1;
	end
	read_SaoParam_endLoop : action ==>
	guard
		sao_idx  = 2 ,
		sao_cIdx = 3
	end
	/**************************************************************************
	 **************************************************************************
	 ********        Coding quadTree                      *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 1) IsCuQpDeltaCoded;
	
	/**************************************************************************
	 * read_CodingQuadTree.start
	 *************************************************************************/	
	read_CodingQuadTree.start : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size=16) ct_x0			= ctStack[ctStack_idx][CT_x0],
		uint(size=16) ct_y0			= ctStack[ctStack_idx][CT_y0],
		uint(size= 8) ct_ctDepth	= ctStack[ctStack_idx][CT_ctDepth],
		uint(size= 8) ct_log2CbSize = ctStack[ctStack_idx][CT_log2CbSize],
		uint(size= 8) top_ctDepth	= cu_top_ctDepth[ct_x0 >> Log2MinCbSize],
		uint(size= 8) left_ctDepth	= cu_left_ctDepth[ct_y0 >> Log2MinCbSize],
		uint(size= 1) split_coding_unit_flag,
		uint(size= 8) Log2MinCuQpDeltaSize = Log2CtbSize - pps_diff_cu_qp_delta_depth[pps_id] - pps_SliceGranularity[pps_id]
	do
			if DEBUG_TRACE1 then
				println("read_CodingTree.start ("+ct_x0+", "+ct_y0+", "+(1<<ct_log2CbSize)+", "+ctStack[ctStack_idx][CT_ctDepth]+")");
			else if DEBUG_MODE then
				println("read_CodingTree.start");
			end end
			
		if	ct_x0 + ( 1 << ct_log2CbSize ) <= sps_pic_width_in_luma_samples[sps_id] and
			ct_y0 + ( 1 << ct_log2CbSize ) <= sps_pic_height_in_luma_samples[sps_id] and
			MinCbAddrZS[ct_x0>>Log2MinCbSize][ct_y0>>Log2MinCbSize] >= sliceCbAddrZs and
			ct_log2CbSize > Log2MinCbSize and NumPCMBlock = 0 then
			get_SPLIT_CODING_UNIT_FLAG(codIRange, codIOffset, ctxTable, fifo, res, ct_ctDepth, ct_x0 > 0, left_ctDepth, ct_y0 > 0, top_ctDepth);
			split_coding_unit_flag := res[0];
		else
			split_coding_unit_flag := if ct_log2CbSize > Log2MinCbSize then 1 else 0 end;
		end
		IsCuQpDeltaCoded := 1;

		if( (pps_diff_cu_qp_delta_depth[pps_id] > 0 ) and ct_log2CbSize >= Log2MinCuQpDeltaSize ) then
			IsCuQpDeltaCoded := 0;
//			CuQpDelta        := 0;
		end

		if split_coding_unit_flag = 1 then
			ctStack[ctStack_idx][CT_x1]  := ct_x0 + ( ( 1 << ct_log2CbSize ) >> 1 );
			ctStack[ctStack_idx][CT_y1]  := ct_y0 + ( ( 1 << ct_log2CbSize ) >> 1 );
			ctStack[ctStack_idx][CT_idx] := 2;
		else
			ctStack[ctStack_idx][CT_idx] := 6;
		end
	end

	/**************************************************************************
	 * read_CodingQuadTree.case1
	 *************************************************************************/	
	read_CodingQuadTree.case1 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 2
	var
		uint(size= 8) idx =  ctStack_idx
	do
		ctStack[ctStack_idx][CT_idx]        := 3;
		ctStack_idx							:= ctStack_idx + 1;
		ctStack[ctStack_idx][CT_idx]        := 1;
		ctStack[ctStack_idx][CT_x0]         := ctStack[idx][CT_x0];
		ctStack[ctStack_idx][CT_y0]         := ctStack[idx][CT_y0];
		ctStack[ctStack_idx][CT_log2CbSize] := ctStack[idx][CT_log2CbSize] - 1;
		ctStack[ctStack_idx][CT_ctDepth]    := ctStack[idx][CT_ctDepth] + 1;
	end

	/**************************************************************************
	 * read_CodingQuadTree.case2
	 *************************************************************************/		
	read_CodingQuadTree.case2 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 3,
		ctStack[ctStack_idx][CT_x1] < sps_pic_width_in_luma_samples[sps_id]
	var
		uint(size= 8) idx =  ctStack_idx
	do
		ctStack[ctStack_idx][CT_idx]        := 4;
		ctStack_idx							:= ctStack_idx + 1;
		ctStack[ctStack_idx][CT_idx]        := 1;
		ctStack[ctStack_idx][CT_x0]         := ctStack[idx][CT_x1];
		ctStack[ctStack_idx][CT_y0]         := ctStack[idx][CT_y0];
		ctStack[ctStack_idx][CT_log2CbSize] := ctStack[idx][CT_log2CbSize] - 1;
		ctStack[ctStack_idx][CT_ctDepth]    := ctStack[idx][CT_ctDepth] + 1;
	end
	read_CodingQuadTree.noCase2 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 3,
		ctStack[ctStack_idx][CT_x1] >= sps_pic_width_in_luma_samples[sps_id]
	do
		ctStack[ctStack_idx][CT_idx]        := 4;
	end
	/**************************************************************************
	 * read_CodingQuadTree.case3
	 *************************************************************************/	
	read_CodingQuadTree.case3 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 4,
		ctStack[ctStack_idx][CT_y1] < sps_pic_height_in_luma_samples[sps_id]
	var
		uint(size= 8) idx =  ctStack_idx
	do
		ctStack[ctStack_idx][CT_idx]        := 5;
		ctStack_idx							:= ctStack_idx + 1;
		ctStack[ctStack_idx][CT_idx]        := 1;
		ctStack[ctStack_idx][CT_x0]         := ctStack[idx][CT_x0];
		ctStack[ctStack_idx][CT_y0]         := ctStack[idx][CT_y1];
		ctStack[ctStack_idx][CT_log2CbSize] := ctStack[idx][CT_log2CbSize] - 1;
		ctStack[ctStack_idx][CT_ctDepth]    := ctStack[idx][CT_ctDepth] + 1;
	end
	read_CodingQuadTree.noCase3 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 4,
		ctStack[ctStack_idx][CT_y1] >= sps_pic_height_in_luma_samples[sps_id]
	do
		ctStack[ctStack_idx][CT_idx]        := 5;
	end
	/**************************************************************************
	 * read_CodingQuadTree.case4
	 *************************************************************************/	
	read_CodingQuadTree.case4 : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 5,
		ctStack[ctStack_idx][CT_x1] < sps_pic_width_in_luma_samples[sps_id],
		ctStack[ctStack_idx][CT_y1] < sps_pic_height_in_luma_samples[sps_id]		
	var
		uint(size= 8) idx =  ctStack_idx
	do
		ctStack[ctStack_idx][CT_idx]        := 7;
		ctStack_idx							:= ctStack_idx + 1;
		ctStack[ctStack_idx][CT_idx]        := 1;
		ctStack[ctStack_idx][CT_x0]         := ctStack[idx][CT_x1];
		ctStack[ctStack_idx][CT_y0]         := ctStack[idx][CT_y1];
		ctStack[ctStack_idx][CT_log2CbSize] := ctStack[idx][CT_log2CbSize] - 1;
		ctStack[ctStack_idx][CT_ctDepth]    := ctStack[idx][CT_ctDepth] + 1;
	end
	read_CodingQuadTree.noCase4 : action ==> CUInfo:[MODE_NONE], Test:[ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0]]
	guard
		ctStack[ctStack_idx][CT_idx] = 5,
		ctStack[ctStack_idx][CT_x1] >= sps_pic_width_in_luma_samples[sps_id] or
		ctStack[ctStack_idx][CT_y1] >= sps_pic_height_in_luma_samples[sps_id]
	do
		ctStack[ctStack_idx][CT_idx]        := 7;
	end
	/**************************************************************************
	 * read_CodingQuadTree_gotoCodingUnit
	 *************************************************************************/	
	read_CodingQuadTree_gotoCodingUnit : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 6,
		NumPCMBlock = 0
	do
		ctStack[ctStack_idx][CT_idx]        := 7;
		cu_idx			:= 1;
		cu_x0			:= ctStack[ctStack_idx][CT_x0];
		cu_y0			:= ctStack[ctStack_idx][CT_y0];
		cu_log2CbSize	:= ctStack[ctStack_idx][CT_log2CbSize];
		cu_ctDepth		:= ctStack[ctStack_idx][CT_ctDepth];
	end
	/**************************************************************************
	 * read_CodingQuadTree_gotoPCMSample
	 *************************************************************************/	
	read_CodingQuadTree_gotoPCMSample : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 6,
		NumPCMBlock   != 0
	do
		PCMSample_log2CbSize 		 := ctStack[ctStack_idx][CT_log2CbSize];
		ctStack[ctStack_idx][CT_idx] := 7;
	end
	/**************************************************************************
	 * read_CodingQuadTree_end
	 *************************************************************************/
	read_CodingQuadTree.noEnd : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 7,
		ctStack_idx != 0
	do
		ctStack_idx := ctStack_idx - 1;
//		println("read_CodingTree_end");
	end
	read_CodingQuadTree_end : action ==>
	guard
		ctStack[ctStack_idx][CT_idx] = 7,
		ctStack_idx = 0
	do
//		println("read_CodingTree_end");
	end

	/**************************************************************************
	 **************************************************************************
	 ********        Coding Unit                          *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 8) cu_idx;
	uint(size=16) cu_x0;
	uint(size=16) cu_y0;
	uint(size= 8) cu_log2CbSize;
	uint(size= 8) cu_ctDepth;
	uint(size= 8) predMode;
	uint(size= 8) partMode;
	uint(size= 1) IntraSplitFlag;
	uint(size= 8) MaxTrafoDepth;
	uint(size= 1) cu_transquant_bypass_flag;
	uint(size= 8) cu_top_ctDepth[CTB_ADDR_TS_MAX];
	uint(size= 8) cu_left_ctDepth[CTB_ADDR_TS_MAX];
	
	/**************************************************************************
	 * read_CodingUnit.start
	 *************************************************************************/
	read_CodingUnit.start : action ==>
	guard 
		cu_idx = 1 and isFifoFull(fifo)
	var
		uint(size=32) res[1]//,
//		uint(size=16) CurrCbAddrTS = MinCbAddrZS[ cu_x0 >> Log2MinCbSize ][ cu_y0 >> Log2MinCbSize ]
	do
		if DEBUG_TRACE1 then
			println("read_CodingUnit.start ("+cu_x0+", "+cu_y0+", "+cu_log2CbSize+")");
		else if DEBUG_MODE then
			println("read_CodingUnit.start");
		end end
		predMode := MODE_INTRA;
		if pps_transquant_bypass_enable_flag[pps_id] != 0 then
			//cu_transquant_bypass_flag
		end
		if slice_type != I_SLICE then
			// get skip_flag
			skip_flag[cu_x0][cu_y0] := res[0];
		end
		cu_idx := 2; 
	end
	/**************************************************************************
	 * read_CodingUnit_gotoPredictionUnit.goto1
	 *************************************************************************/
	read_CodingUnit_gotoPredictionUnit.goto1 : action ==> CUInfo:[MODE_SKIP], Test:[ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0]]
	guard 
		cu_idx = 2,
		skip_flag[cu_x0][cu_y0] != 0
	do
		cu_idx		:= 8; 
		pu_idx		:= 1;
		pu_x0		:= cu_x0;
		pu_y0		:= cu_y0;
		pu_PbW		:= cu_nCbS;
		pu_PbH		:= cu_nCbS;
		predMode	:= MODE_SKIP;
	end
	/**************************************************************************
	 * read_CodingUnit.noGoto1
	 *************************************************************************/
	uint(size= 8) intra_pred_mode[4];
	uint(size= 8) intra_pred_mode_c;
	uint(size= 8) cu_nCbS;
	read_CodingUnit_noGoto1 : action ==>
	guard 
		cu_idx = 2 and isFifoFull(fifo),
		skip_flag[cu_x0][cu_y0] = 0
	var
		uint(size= 1) prev_intra_luma_pred_flag[4],
		uint(size= 8) intra_chroma_table[4] = [0, 26, 10, 1],
		uint(size=32) res[1],
		uint(size= 8) Log2MinIPCMCUSize = Log2MinCbSize,
		uint(size= 8) Log2MaxIPCMCUSize = Log2MinCbSize,
		uint(size= 8) pbOffset,
		uint(size= 8) pbEnd
	do
		partMode		:= PART_2Nx2N;
		IntraSplitFlag	:= 0;
		cu_nCbS			:= ( 1 << cu_log2CbSize );
		if slice_type != I_SLICE then
			get_PRED_MODE_FLAG(codIRange, codIOffset, ctxTable, fifo, res);
			predMode := res[0];
		end
		if predMode != MODE_INTRA or cu_log2CbSize = Log2MinCbSize then
			get_PART_SIZE(codIRange, codIOffset, ctxTable, fifo, res);
			partMode := res[0];
		end
		cu_idx := 4;
		if( predMode = MODE_INTRA ) then
 			pcm_flag := 0;
			if  partMode = PART_2Nx2N and sps_pcm_enabled_flag[sps_id] = 1 and
				cu_log2CbSize >= Log2MinIPCMCUSize and
				cu_log2CbSize <= Log2MaxIPCMCUSize then
				//pcm_flag := res[0]
			end
			if( pcm_flag = 1) then
				//num_subsequent_pcm
				// NumPCMBlock = res[0] + 1
				byte_align(fifo);
				cu_idx := 3;
			else 
			
				pbOffset := if partMode = PART_NxN then cu_nCbS>>1 else cu_nCbS end;
				pbEnd    := if partMode = PART_NxN then 1 else 0 end;
				foreach int i in 0 .. pbEnd do
					foreach int j in 0 .. pbEnd do 
						get_PREV_INTRA_LUMA_PRED_FLAG(codIRange, codIOffset, ctxTable, fifo, res);
						prev_intra_luma_pred_flag[ (i<<1) + j ] := res[0];
					end
				end
				foreach int i in 0 .. pbEnd do
					foreach int j in 0 .. pbEnd do
						if prev_intra_luma_pred_flag[ (i<<1) + j ] = 1 then
							get_MPM_IDX(codIRange, codIOffset, fifo, res);
//							mpm_idx[ cu_x0 + i*pbOffset ][ cu_y0 + j*pbOffset ] := res[0];
						else
							get_REM_INTRA_LUMA_PRED_MODE(codIRange, codIOffset, fifo, res);
						end
						luma_intra_pred_mode(cu_x0 + j*pbOffset, cu_y0 + i*pbOffset, pbOffset, prev_intra_luma_pred_flag[ (i<<1) + j ], res[0], res, Log2MinIPCMCUSize-1 ,Log2CtbSize, top_ipm, left_ipm);
						intra_pred_mode[ (i<<1) + j ] := res[0];
					end
				end
				get_INTRA_CHROMA_PRED_MODE(codIRange, codIOffset, ctxTable, fifo, res);
				if res[0] != 4 then
        			if intra_pred_mode[0] = intra_chroma_table[res[0]] then
            			intra_pred_mode_c := 34;
        			else
	            		intra_pred_mode_c := intra_chroma_table[res[0]];
        			end
    			else
	        		intra_pred_mode_c := intra_pred_mode[0];
    			end    			
				cu_idx := 8;
			end
		end
	end

	sendCUInfo.has1Part: action ==> CUInfo:[predMode], Test:[addrTab] repeat 2
	guard
		partModeToNumPart[partMode] = 1
	var
		uint(size=16) addrTab[2] = [ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0]]
	end

	sendCUInfo.has2Part: action ==> CUInfo:[predModeTab] repeat 2, Test:[addrTab] repeat 4
	guard
		partModeToNumPart[partMode] = 2
	var
		uint(size= 8) predModeTab[2] = [predMode, predMode],
		uint(size=16) addrTab[4] = [ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0],4242,4242]
	end

	sendCUInfo.has4Part: action ==> CUInfo:[predModeTab] repeat 4, Test:[addrTab] repeat 8
	guard
		partModeToNumPart[partMode] = 4
	var
		uint(size=8) predModeTab[4] = [predMode: for uint i in 0 .. 3],
		uint(size=7) CUSize = (1<<(cu_log2CbSize-1)),
		uint(size=16) addrTab[8] = [ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0],
									ctStack[ctStack_idx][CT_x0]+CUSize, ctStack[ctStack_idx][CT_y0],
									ctStack[ctStack_idx][CT_x0], ctStack[ctStack_idx][CT_y0]+CUSize,
									ctStack[ctStack_idx][CT_x0]+CUSize, ctStack[ctStack_idx][CT_y0]+CUSize]
	end
	/**************************************************************************
	 * read_CodingUnit_gotoPCMSample
	 *************************************************************************/
	read_CodingUnit_gotoPCMSample : action ==>
	guard 
		cu_idx = 3
	do
		PCMSample_log2CbSize := cu_log2CbSize;
		cu_idx				 := 8;
	end
	/**************************************************************************
	 * read_CodingUnit_gotoPredictionUnit.goto2
	 *************************************************************************/
	read_CodingUnit_gotoPredictionUnit.goto2 : action ==>
	guard 
		cu_idx = 4 or cu_idx = 5 or
		cu_idx = 6 or cu_idx = 7
	var
		uint(size= 8) nCbS_2   = cu_nCbS >> 1,
		uint(size= 8) nCbS_4   = cu_nCbS >> 2,
		uint(size= 8) nCbS_3_4 = cu_nCbS - nCbS_4
	do
	
		pu_idx := 1;
		pu_x0  := cu_x0;
		pu_y0  := cu_y0;
		pu_PbW := cu_nCbS;
		pu_PbH := cu_nCbS;
//	FIXME :		if partMode = PART_2Nx2N then
		if partMode = PART_NxN then
			//prediction_unit( x0, y0, nCbS, nCbS )
			cu_idx    := 8;
		elsif partMode = PART_2NxN then
			pu_PbH := nCbS_2;
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS, nCbS / 2 )
				cu_idx := 5;
			else
				//prediction_unit( x0, y0 + ( nCbS / 2 ), nCbS, nCbS / 2 )
				pu_y0  := cu_y0 + nCbS_2;
				cu_idx := 8;
			end
		elsif partMode = PART_Nx2N then
			pu_PbW := nCbS_2;
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS / 2, nCbS )
				cu_idx := 5;
			else
				//prediction_unit( x0 + ( nCbS / 2 ), y0, nCbS / 2, nCbS )
				pu_x0  := cu_x0 + nCbS_2;
				cu_idx := 8;
			end
		elsif partMode = PART_2NxnU then
			pu_PbH := nCbS_4;
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS, nCbS / 4 )
				cu_idx := 5;
			else
				//prediction_unit( x0, y0 + ( nCbS / 4 ), nCbS, nCbS *3 / 4 )
				pu_y0  := cu_y0 + nCbS_4;
				pu_PbH := nCbS_3_4;
				cu_idx := 8;
			end
		elsif partMode = PART_2NxnD then
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS, nCbS *3 / 4 )
				pu_PbH := nCbS_3_4;
				cu_idx := 5;
			else
				//prediction_unit( x0, y0 + ( nCbS * 3 / 4 ), nCbS, nCbS / 4 )
				pu_y0  := cu_y0 + nCbS_3_4;
				pu_PbH := nCbS_4;
				cu_idx := 8;
			end
		elsif partMode = PART_nLx2N then
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS /4, nCbS )
				pu_PbW := nCbS_4;
				cu_idx := 5;
			else
				//prediction_unit( x0 + ( nCbS / 4 ), y0, nCbS *3 / 4, nCbS)
				pu_x0  := cu_x0 + nCbS_4;
				pu_PbW := nCbS_3_4;
				cu_idx := 8;
			end
		elsif partMode = PART_nRx2N then
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS *3 / 4, nCbS )
				pu_PbW := nCbS_3_4;
				cu_idx := 5;
			else
				//prediction_unit( x0 + ( nCbS * 3 / 4 ), y0, nCbS / 4, nCbS )
				pu_x0  := cu_x0 + nCbS_3_4;
				pu_PbW := nCbS_4;
				cu_idx := 8;
			end
		else /* PART_NxN */
			pu_PbW := nCbS_2;
			pu_PbH := nCbS_2;
			if cu_idx = 4 then
				//prediction_unit( x0, y0, nCbS / 2, nCbS / 2)
				cu_idx := 5;
			elsif cu_idx = 5 then
				//prediction_unit( x0 + ( nCbS / 2 ), y0, nCbS / 2, nCbS / 2 )
				pu_x0  := cu_x0 + nCbS_2;
				cu_idx := 6;
			elsif cu_idx = 6 then
				//prediction_unit( x0, y0 + ( nCbS / 2 ), nCbS / 2, nCbS / 2 )
				pu_y0  := cu_y0 + nCbS_2;
				cu_idx := 5;
			else
				//prediction_unit( x0 + ( nCbS / 2 ), y0 + ( nCbS / 2 ), nCbS / 2, nCbS / 2 )
				pu_x0  := cu_x0 + nCbS_2;
				pu_y0  := cu_y0 + nCbS_2;
				cu_idx := 8;
			end
		end
	end
	/**************************************************************************
	 * read_CodingUnit.endFunction
	 *************************************************************************/
	read_CodingUnit.endFunction : action ==>
	guard 
		cu_idx = 8
	var
		uint(size=32) res[1] := [0]
	do
		cu_idx := 10;
		if pcm_flag = 0 then
			if( predMode != MODE_INTRA and not(partMode = PART_2Nx2N and merge_flag[cu_x0][cu_y0] = 1) ) then
				//no_residual_data_flag
			end
			if res[0] = 0 then
				MaxTrafoDepth := if predMode = MODE_INTRA then
									sps_max_transform_hierarchy_depth_intra[sps_id] + IntraSplitFlag
								else
									sps_max_transform_hierarchy_depth_inter[sps_id]
								end;
				cu_idx	:= 9;				
				//transform_tree( x0, y0, x0, y0, x0, y0, log2CbSize, log2CbSize, log2CbSize, 0, 0 )
			end
		end
	end
	/**************************************************************************
	 * read_CodingUnit_gotoTransformTree
	 *************************************************************************/
	read_CodingUnit_gotoTransformTree : action ==>
	guard 
		cu_idx = 9
	do
		cu_idx							:= 10;
		ttStack_idx						:=  0;
		ttStack[0][TT_idx]				:=  1;
		ttStack[0][TT_x0L]				:=  cu_x0;
		ttStack[0][TT_y0L]				:=  cu_y0;
		ttStack[0][TT_x0C]				:=  cu_x0;
		ttStack[0][TT_y0C]				:=  cu_y0;
		ttStack[0][TT_xBase]			:=  cu_x0;
		ttStack[0][TT_yBase]			:=  cu_y0;
		ttStack[0][TT_log2TrafoSize]	:=  cu_log2CbSize;
		ttStack[0][TT_trafoDepth]		:=  0;
		ttStack[0][TT_blkIdx]			:=  0;
	end
	/**************************************************************************
	 * read_CodingUnit_end
	 *************************************************************************/
	read_CodingUnit_end : action ==>
	guard 
		 cu_idx = 10
	var
		uint(size= 8) length = (1 << cu_log2CbSize) >> Log2MinCbSize,
		uint(size=16) x_cb = cu_x0 >> Log2MinCbSize,
    	uint(size=16) y_cb = cu_y0 >> Log2MinCbSize
	do
//		println("read_CodingUnit_end ");
    	foreach uint i  in 0 .. length-1 do
    		cu_top_ctDepth[x_cb + i]  := cu_ctDepth;
    		cu_left_ctDepth[y_cb + i] := cu_ctDepth;
		end
	end
	
	/**************************************************************************
	 **************************************************************************
	 ********        Prediction Unit                      *********************
	 **************************************************************************
	 *************************************************************************/

	uint(size= 8) pu_idx;
	uint(size=16) pu_x0;
	uint(size=16) pu_y0;
	uint(size= 8) pu_PbW;
	uint(size= 8) pu_PbH;
	uint(size= 1) pcm_flag;
	
	/**************************************************************************
	 * read_PredictionUnit.start
	 *************************************************************************/
	read_PredictionUnit.start : action ==>
	guard 
		isFifoFull(fifo),
		pu_idx = 1
	var
		uint(size=32) res[1]
	do
		if DEBUG_MODE then
			println("read_PredictionUnit.start ("+pu_x0+", "+pu_y0+", "+pu_PbW+", "+pu_PbH+")");
		end
		pu_idx := 7;
		if ( skip_flag[pu_x0][pu_y0] = 1) then
			if( MaxNumMergeCand > 1 ) then
				//merge_idx[ x0 ][ y0 ]
			end
			pu_idx := 7;
		else /* MODE_INTER */
			//merge_flag[pu_x0][pu_y0] := res[0]
			if res[0] = 1 then
				if MaxNumMergeCand > 1 then
					//merge_idx[pu_x0][pu_y0] := res[0]
				end
				pu_idx := 7;
			else
				inter_pred_flag[pu_x0][pu_y0] := Pred_L0;
				if( slice_type = B_SLICE ) then
					//inter_pred_flag[pu_x0][pu_y0] := res[0];
				end
				if( inter_pred_flag[pu_x0][pu_y0] = Pred_LC ) then
					if( num_ref_idx_lc_active_minus1 > 0 ) then
						//ref_idx_lc[ x0 ][ y0 ]
					end
				else // Pred_L0 or Pred_BI
					if( num_ref_idx_l0_active_minus1 > 0 ) then
						//ref_idx_l0[ x0 ][ y0 ]
					end
				end	
				pu_idx := 3;
			end
		end
	end
	/**************************************************************************
	 * read_PredictionUnit.retMVDcoding_goto1
	 *************************************************************************/
	read_PredictionUnit.retMVDcoding_goto1 : action ==>
	guard 
		pu_idx = 4
	do
		if( inter_pred_flag[pu_x0][pu_y0] = Pred_LC ) then
			//mvp_lc_flag[ x0 ][ y0 ];
		else // Pred_L0 or Pred_BI
			//mvp_l0_flag[ x0 ][ y0 ]
		end	
		pu_idx := 7;
		if( inter_pred_flag[pu_x0][pu_y0] = Pred_BI ) then
			if( num_ref_idx_l1_active_minus1 > 0 ) then
				//ref_idx_l1[ x0 ][ y0 ]
			end
			if( /*mvd_l1_zero_flag*/ true ) then
				//mvd_l1[ x0 ][ y0 ][ 0 ] = 0
				//mvd_l1[ x0 ][ y0 ][ 1 ] = 0
				pu_idx := 6;
			else
				pu_idx := 5;
			end
		end
	end
	/**************************************************************************
	 * read_PredictionUnit_gotoMVDCoding
	 *************************************************************************/
	read_PredictionUnit_gotoMVDCoding : action ==>
	guard 
		pu_idx = 3 or pu_idx = 5
	do
		pu_idx := pu_idx + 1;
	end
	/**************************************************************************
	 * read_PredictionUnit.retMVDcoding_goto2
	 *************************************************************************/
	read_PredictionUnit.retMVDcoding_goto2 : action ==>
	guard 
		pu_idx = 6
	do
		//mvp_l1_flag[ x0 ][ y0 ]
		pu_idx := 7;
	end
	/**************************************************************************
	 * read_PredictionUnit_end
	 *************************************************************************/
	read_PredictionUnit_end : action ==>
	guard 
		pu_idx = 7
	do
		if DEBUG_MODE then
			println("read_PredictionUnit_end");
		end
	end
	
	/**************************************************************************
	 **************************************************************************
	 ********        MVD Coding                           *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 8) mvd_x;
	uint(size= 8) mvd_y;
	read_MVDCoding.start : action ==>
	guard
		isFifoFull(fifo)
	var
		uint(size=32) res[1],
		uint(size= 1) abs_mvd_greater0_flag_0,
		uint(size= 1) abs_mvd_greater0_flag_1,
		uint(size= 1) abs_mvd_greater1_flag_0,
		uint(size= 1) abs_mvd_greater1_flag_1,
		 int(size=32) abs_mvd_minus2_0 := 0,
		uint(size= 1) mvd_sign_flag_0  := 0,
		 int(size=32) abs_mvd_minus2_1 := 0,
		uint(size= 1) mvd_sign_flag_1  := 0
	do
		if DEBUG_MODE then
			println("read_MVDCoding");
		end
		abs_mvd_greater0_flag_0 := res[0];
		abs_mvd_greater0_flag_1 := res[0];
		if abs_mvd_greater0_flag_0 = 1 then
			abs_mvd_greater1_flag_0 := res[0];
		end
		if abs_mvd_greater0_flag_1 = 1 then
			abs_mvd_greater1_flag_1 := res[0];
		end
		if abs_mvd_greater0_flag_0 = 1 then
			abs_mvd_minus2_0 := -1;
			if abs_mvd_greater1_flag_0 = 1 then
				abs_mvd_minus2_0 := res[0];
			end
			mvd_sign_flag_0 := res[0];
		end
		if abs_mvd_greater0_flag_1 = 1 then
			if abs_mvd_greater1_flag_1 = 1 then
				abs_mvd_minus2_1 := res[0];
			end
			mvd_sign_flag_1 := res[0];
		end
		mvd_x := abs_mvd_greater0_flag_0 * ( abs_mvd_minus2_0 + 2 ) * ( 1 - (mvd_sign_flag_0 << 1) );
		mvd_y := abs_mvd_greater0_flag_1 * ( abs_mvd_minus2_1 + 2 ) * ( 1 - (mvd_sign_flag_1 << 1) );
	end

	/**************************************************************************
	 **************************************************************************
	 ********        PCM Sample                           *********************
	 **************************************************************************
	 *************************************************************************/

	uint(size= 8) PCMSample_log2CbSize;
	read_PCMSample.start : action ==>
	guard
		isFifoFull(fifo)
	var
		uint(size=32) res[1]		
	do
		if DEBUG_MODE then
			println("read_PCMSample");
		end
		foreach int i  in 0 .. 1 << ( PCMSample_log2CbSize << 1 ) do
			vld_u_name(  1, fifo, res, "pcm_sample_luma[i]                      ");
			//pcm_sample_luma[ i ];
		end
		foreach int i  in 0 .. 1 << ( PCMSample_log2CbSize << 1 ) do
			vld_u_name(  1, fifo, res, "pcm_sample_chroma[i]                    ");
			//pcm_sample_chroma[ i ]
		end
		NumPCMBlock := NumPCMBlock - 1;
	end
	read_PCMSample2.start : action ==>		
	var
		uint(size=32) res[1]		
	do
		if DEBUG_MODE then
			println("read_PCMSample2");
		end
		foreach int i  in 0 .. 1 << ( PCMSample_log2CbSize << 1 ) do
			vld_u_name(  1, fifo, res, "pcm_sample_luma[i]                      ");
			//pcm_sample_luma[ i ];
		end
		foreach int i  in 0 .. 1 << ( PCMSample_log2CbSize << 1 ) do
			vld_u_name(  1, fifo, res, "pcm_sample_chroma[i]                    ");
			//pcm_sample_chroma[ i ]
		end
		NumPCMBlock := NumPCMBlock - 1;
	end

	/**************************************************************************
	 **************************************************************************
	 ********        Transform Tree                       *********************
	 **************************************************************************
	 *************************************************************************/

	uint(size= 8) ttStack_idx;
	uint(size=16) ttStack[10][TT_STRUCT_SIZE];
	uint(size= 1) split_transform_flag[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX][256];
	uint(size= 1) cbf_cb[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX][256];
	uint(size= 1) cbf_cr[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX][256];
	uint(size= 1) cbf_luma[CTB_ADDR_TS_MAX][CTB_ADDR_TS_MAX][256];
	uint(size= 8) cur_intra_pred_mode;
	/**************************************************************************
	 * read_TransformTree.start
	 *************************************************************************/
	read_TransformTree.start : action ==>
	guard 
		isFifoFull(fifo),
		ttStack[ttStack_idx][TT_idx] = 1
	var
		uint(size=16) x0L				= ttStack[ttStack_idx][TT_x0L],
		uint(size=16) y0L				= ttStack[ttStack_idx][TT_y0L],
		uint(size=16) x0C				= ttStack[ttStack_idx][TT_x0C],
		uint(size=16) y0C				= ttStack[ttStack_idx][TT_y0C],
		uint(size=16) xBase				= ttStack[ttStack_idx][TT_xBase],
		uint(size=16) yBase				= ttStack[ttStack_idx][TT_yBase],
		uint(size= 8) log2TrafoSize	    = ttStack[ttStack_idx][TT_log2TrafoSize],
		uint(size= 8) trafoDepth		= ttStack[ttStack_idx][TT_trafoDepth],
		uint(size=32) res[1] := [0],
		uint(size= 1) IntraSplitFlag   = if predMode = MODE_INTRA and partMode = PART_NxN then 1 else 0 end,
		uint(size= 1) InterSplitFlag   = if sps_max_transform_hierarchy_depth_inter[sps_id] = 0 and predMode = MODE_INTER and partMode != PART_2Nx2N and trafoDepth = 0 then 1 else 0 end
	do	
		if DEBUG_TRACE1 then
			println("read_TransformTree.start("+x0L+", "+y0L+", "+x0C+", "+y0C+", "+xBase+", "+yBase+", "+log2TrafoSize+", "+trafoDepth+", "+ttStack[ttStack_idx][TT_blkIdx]+")");
		else if DEBUG_MODE then
			println("read_TransformTree.start");
		end end
		if IntraSplitFlag = 1 then
        	if trafoDepth = 1 then
            	cur_intra_pred_mode := intra_pred_mode[ttStack[ttStack_idx][TT_blkIdx]];
            end
    	else
        	cur_intra_pred_mode := intra_pred_mode[0];
    	end
		split_transform_flag[ x0L ][ y0L ][ trafoDepth ] := 0;
		if( log2TrafoSize <= Log2MaxTrafoSize and log2TrafoSize > Log2MinTrafoSize and
			trafoDepth < MaxTrafoDepth and not(IntraSplitFlag = 1 and trafoDepth = 0) ) then
			get_SPLIT_TRANSFORM_FLAG(codIRange, codIOffset, ctxTable, fifo, res, log2TrafoSize);
			split_transform_flag[ x0L ][ y0L ][ trafoDepth ] := res[0];
		elsif log2TrafoSize > Log2MaxTrafoSize or (IntraSplitFlag = 1 and trafoDepth = 0) or InterSplitFlag = 1 then
			split_transform_flag[ x0L ][ y0L ][ trafoDepth ] := 1;							
		end
		
		cbf_cb[ x0C ][ y0C ][ trafoDepth ] := 0;
		cbf_cr[ x0C ][ y0C ][ trafoDepth ] := 0;
		if trafoDepth = 0 or log2TrafoSize > 2  then
			if trafoDepth = 0 then
				get_CBF_CB_CR(codIRange, codIOffset, ctxTable, fifo, res, trafoDepth);
				cbf_cb[ x0C ][ y0C ][ trafoDepth ] := res[0];
				get_CBF_CB_CR(codIRange, codIOffset, ctxTable, fifo, res, trafoDepth);
				cbf_cr[ x0C ][ y0C ][ trafoDepth ] := res[0];
			else
				if cbf_cb[ xBase ][ yBase ][ trafoDepth-1 ] = 1 then
					get_CBF_CB_CR(codIRange, codIOffset, ctxTable, fifo, res, trafoDepth);
					cbf_cb[ x0C ][ y0C ][ trafoDepth ] := res[0];
				end
				if cbf_cr[ xBase ][ yBase ][ trafoDepth-1 ] = 1 then
					get_CBF_CB_CR(codIRange, codIOffset, ctxTable, fifo, res, trafoDepth);
					cbf_cr[ x0C ][ y0C ][ trafoDepth ] := res[0];
				end
			end
		end
		if trafoDepth > 0 and log2TrafoSize = 2  then
			cbf_cb[ x0C ][ y0C ][ trafoDepth ] := cbf_cb[ xBase ][ yBase ][ trafoDepth-1 ];
			cbf_cr[ x0C ][ y0C ][ trafoDepth ] := cbf_cr[ xBase ][ yBase ][ trafoDepth-1 ];
		end
				
		if split_transform_flag[ x0L ][ y0L ][ trafoDepth ] = 1 then
			ttStack[ttStack_idx][TT_x1L]  := x0L + ( ( 1 << log2TrafoSize ) >> 1 );
			ttStack[ttStack_idx][TT_y1L]  := y0L + ( ( 1 << log2TrafoSize ) >> 1 );
			if log2TrafoSize > 3 then
				ttStack[ttStack_idx][TT_x1C]  := x0C + ( ( 1 << log2TrafoSize ) >> 1 );
				ttStack[ttStack_idx][TT_y1C]  := y0C + ( ( 1 << log2TrafoSize ) >> 1 );
			else
				ttStack[ttStack_idx][TT_x1C]  := x0C;
				ttStack[ttStack_idx][TT_y1C]  := y0C;
			end
			ttStack[ttStack_idx][TT_idx] := 3;
		else
			cbf_luma[ x0L ][ y0L ][ trafoDepth ] := 1;
			if predMode = MODE_INTRA or trafoDepth != 0 or
				cbf_cb[ x0C ][ y0C ][ trafoDepth ] = 1 or cbf_cr[ x0C ][ y0C ][ trafoDepth ] = 1 then
				get_CBF_LUMA(codIRange, codIOffset, ctxTable, fifo, res, trafoDepth);
				cbf_luma[ x0L ][ y0L ][ trafoDepth ] := res[0];
			end
			ttStack[ttStack_idx][TT_idx] := 2;
		end
	end
	/**************************************************************************
	 * read_TransformTree_gotoTransformUnit
	 *************************************************************************/
	read_TransformTree_gotoTransformUnit : action ==>
	guard
		ttStack[ttStack_idx][TT_idx] = 2
	do
		ttStack[ttStack_idx][TT_idx]	:= 7;
		tu_idx							:= 1;
		tu_x0L							:= ttStack[ttStack_idx][TT_x0L];
		tu_y0L							:= ttStack[ttStack_idx][TT_y0L];
		tu_x0C							:= ttStack[ttStack_idx][TT_x0C];
		tu_y0C							:= ttStack[ttStack_idx][TT_y0C];
		tu_log2TrafoSize				:= ttStack[ttStack_idx][TT_log2TrafoSize];
		tu_trafoDepth					:= ttStack[ttStack_idx][TT_trafoDepth];
		tu_blkIdx						:= ttStack[ttStack_idx][TT_blkIdx];
	end
	/**************************************************************************
	 * read_TransformTree.gotoTransformTree
	 *************************************************************************/
	read_TransformTree.gotoTransformTree : action ==>
	guard
		ttStack[ttStack_idx][TT_idx] = 3 or ttStack[ttStack_idx][TT_idx] = 4 or
		ttStack[ttStack_idx][TT_idx] = 5 or ttStack[ttStack_idx][TT_idx] = 6
	var
		uint(size= 8) idx = ttStack_idx
	do
		ttStack[ttStack_idx][TT_idx]				:= ttStack[ttStack_idx][TT_idx] + 1;
		ttStack_idx 								:= ttStack_idx + 1;
		ttStack[ttStack_idx][TT_idx]				:= 1;
		ttStack[ttStack_idx][TT_x0L]				:= ttStack[idx][TT_x0L];
		ttStack[ttStack_idx][TT_y0L]				:= ttStack[idx][TT_y0L];
		ttStack[ttStack_idx][TT_x0C]				:= ttStack[idx][TT_x0C];
		ttStack[ttStack_idx][TT_y0C]				:= ttStack[idx][TT_y0C];
		ttStack[ttStack_idx][TT_xBase]				:= ttStack[idx][TT_x0C];
		ttStack[ttStack_idx][TT_yBase]				:= ttStack[idx][TT_y0C];
		ttStack[ttStack_idx][TT_log2TrafoSize]		:= ttStack[idx][TT_log2TrafoSize] - 1;
		ttStack[ttStack_idx][TT_trafoDepth]			:= ttStack[idx][TT_trafoDepth] + 1;
		if ttStack[idx][TT_idx] = 4 then
			ttStack[ttStack_idx][TT_blkIdx]			:= 0;
		elsif ttStack[idx][TT_idx] = 5 then
			ttStack[ttStack_idx][TT_x0L]		    := ttStack[idx][TT_x1L];
			ttStack[ttStack_idx][TT_x0C]		    := ttStack[idx][TT_x1C];
			ttStack[ttStack_idx][TT_blkIdx]			:= 1;
		elsif ttStack[idx][TT_idx] = 6 then
			ttStack[ttStack_idx][TT_y0L]			:= ttStack[idx][TT_y1L];
			ttStack[ttStack_idx][TT_y0C]			:= ttStack[idx][TT_y1C];
			ttStack[ttStack_idx][TT_blkIdx]			:= 2;
		else
			ttStack[ttStack_idx][TT_x0L]			:= ttStack[idx][TT_x1L];
			ttStack[ttStack_idx][TT_y0L]			:= ttStack[idx][TT_y1L];
			ttStack[ttStack_idx][TT_x0C]			:= ttStack[idx][TT_x1C];
			ttStack[ttStack_idx][TT_y0C]			:= ttStack[idx][TT_y1C];
			ttStack[ttStack_idx][TT_blkIdx]			:= 3;
		end
	end
	/**************************************************************************
	 * read_TransformTree_end
	 *************************************************************************/
	read_TransformTree_end : action ==>
	guard
		ttStack[ttStack_idx][TT_idx] = 7 and ttStack_idx = 0 
	end
	read_TransformTree.endCall : action ==>
	guard
		ttStack[ttStack_idx][TT_idx] = 7 and ttStack_idx != 0 
	do
		ttStack_idx := ttStack_idx - 1;
	end

	/**************************************************************************
	 **************************************************************************
	 ********        Transform Unit                       *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 8) tu_idx;
	uint(size=16) tu_x0L;
	uint(size=16) tu_y0L;
	uint(size=16) tu_x0C;
	uint(size=16) tu_y0C;
	uint(size= 8) tu_trafoDepth;
	uint(size= 8) tu_blkIdx;
	uint(size= 8) tu_log2TrafoSize;
	
	/**************************************************************************
	 * read_TransformUnit.start
	 *************************************************************************/
	read_TransformUnit.start : action ==>
	guard 
		isFifoFull(fifo),
		tu_idx = 1
	do
		if DEBUG_TRACE1 then
			println("read_TransformUnit.start("+tu_x0L+", "+tu_y0L+", "+tu_x0C+", "+tu_y0C+", "+
					tu_log2TrafoSize+", "+tu_log2TrafoSize+", "+tu_trafoDepth+", "+tu_blkIdx+")");
		else if DEBUG_MODE then
			println("read_TransformUnit.start");
		end end
		tu_idx := 7;
		if  cbf_luma[ tu_x0L ][ tu_y0L ][ tu_trafoDepth ] = 1 or
			cbf_cb[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 or
			cbf_cr[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 then
			if pps_diff_cu_qp_delta_depth[pps_id] > 0  and IsCuQpDeltaCoded = 0 then
				//cu_qp_delta
				IsCuQpDeltaCoded := 1;
			end
			tu_idx := 3;			
			if cbf_luma[ tu_x0L ][ tu_y0L ][ tu_trafoDepth ] = 1 then
				tu_idx 				:= 2;
				rc_idx 				:= 1;
				rc_x0 				:= tu_x0L;
				rc_y0 				:= tu_y0L;
				rc_log2TrafoSize	:= tu_log2TrafoSize;
				rc_scanIdx			:= getScanIdx(predMode, tu_log2TrafoSize, cur_intra_pred_mode);
				rc_cIdx				:= 0;
				rc_TType			:= TEXT_LUMA;
			end
		end
	end
	/**************************************************************************
	 * read_TransformUnit.retLuma
	 *************************************************************************/
	read_TransformUnit.retLuma : action ==>
	guard 
		tu_idx = 3
	do
		tu_idx 				:= 7;
		rc_idx 				:= 1;
		rc_x0 				:= tu_x0C;
		rc_y0 				:= tu_y0C;
		rc_log2TrafoSize	:= tu_log2TrafoSize;
		rc_scanIdx			:= getScanIdx(predMode, rc_log2TrafoSize, intra_pred_mode_c);
		rc_cIdx				:= 1;
		rc_TType			:= TEXT_CHROMA_U;						
		if tu_log2TrafoSize > 2 then
			tu_idx := 5;
			if cbf_cb[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 then
				rc_log2TrafoSize	:= tu_log2TrafoSize - 1;
				tu_idx				:= 4;				
			end
		elsif tu_blkIdx = 3 then
			tu_idx := 5;
			if cbf_cb[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 then
				tu_idx	:= 4;
			end
		end
	end
	/**************************************************************************
	 * read_TransformUnit.retCb
	 *************************************************************************/
	read_TransformUnit.retCb : action ==>
	guard 
		tu_idx = 5
	do	
		tu_idx	 := 7;
		rc_idx 	 := 1;
		rc_cIdx	 := 2;
		rc_TType := TEXT_CHROMA_V;						
		if tu_log2TrafoSize > 2 then
			if cbf_cr[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 then
				rc_log2TrafoSize	:= tu_log2TrafoSize - 1;
				tu_idx 				:= 6;
			end
		elsif tu_blkIdx = 3 then
			if cbf_cr[ tu_x0C ][ tu_y0C ][ tu_trafoDepth ] = 1 then
				tu_idx := 6;
			end
		end
	end
	/**************************************************************************
	 * read_TransformUnit_gotoResidualCoding
	 *************************************************************************/
	read_TransformUnit_gotoResidualCoding : action ==>
	guard 
		tu_idx = 2 or tu_idx = 4 or tu_idx = 6
	do	
		tu_idx := tu_idx + 1;
	end
	/**************************************************************************
	 * read_TransformUnit_end
	 *************************************************************************/
	read_TransformUnit_end : action ==>
	guard 
		tu_idx = 7
	do
		if DEBUG_MODE then
			println("read_TransformUnit_end");
		end
	end

	/**************************************************************************
	 **************************************************************************
	 ********        Residual Coding                      *********************
	 **************************************************************************
	 *************************************************************************/
	uint(size= 8) rc_idx;
	uint(size=16) rc_x0;
	uint(size=16) rc_y0;
	uint(size= 8) rc_log2TrafoSize;
	uint(size= 8) rc_scanIdx;
	uint(size= 8) rc_cIdx;
	uint(size= 4) rc_TType;
	//
	uint(size= 8) ScanOrder[3][BLW_WIDTH_MAX*BLW_HEIGHT_MAX][2];
	uint(size= 8) ScanOrder2[3][BLW_WIDTH_MAX*BLW_HEIGHT_MAX][2];
	uint(size= 1) significant_coeff_flag[16];
	uint(size= 1) coded_sub_block_flag[8][8];
	uint(size= 8) LastSignificantCoeffX;
	uint(size= 8) LastSignificantCoeffY;
		
	/**************************************************************************
	 * read_ResidualCoding.start
	 *************************************************************************/
	uint(size= 8) rc_lastScanPos;
	 int(size= 8) rc_lastSubBlock;
	 int(size= 8) rc_i;
//	uint(size= 1) transform_skip_flag;
	read_ResidualCoding.start : action ==>
	guard 
		rc_idx = 1 and isFifoFull(fifo)
	var
		 int(size=32) res[1],
		uint(size=16) xS := 0,
		uint(size=16) yS := 0,
		 int(size=16) xC := -1,
		 int(size=16) yC := -1
	do	
		if DEBUG_TRACE1 then
			if rc_TType = TEXT_LUMA then
				println("read_ResidualCoding.start cbY("+rc_x0+", "+rc_y0+", "+rc_log2TrafoSize+", "+rc_scanIdx+", "+rc_cIdx+")");
			elsif rc_TType = TEXT_CHROMA_U then
				println("read_ResidualCoding.start cbU("+rc_x0+", "+rc_y0+", "+rc_log2TrafoSize+", "+rc_scanIdx+", "+rc_cIdx+")");
			else
				println("read_ResidualCoding.start cbV("+rc_x0+", "+rc_y0+", "+rc_log2TrafoSize+", "+rc_scanIdx+", "+rc_cIdx+")");
			end
		else if DEBUG_MODE then
			if rc_TType = TEXT_LUMA then
				println("read_ResidualCoding.start cbY");
			elsif rc_TType = TEXT_CHROMA_U then
				println("read_ResidualCoding.start cbU");
			else
				println("read_ResidualCoding.start cbV");
			end
		end end
		coded_sub_block_flag := [[0	: for int j in 0 .. 7]	: for int i in 0 .. 7];
		if pps_transform_skip_enabled_flag[pps_id] = 1 and cu_transquant_bypass_flag = 0 and
			rc_log2TrafoSize = 2 then
			get_TRANSFORM_SKIP_FLAG(codIRange, codIOffset, ctxTable, fifo, res, rc_TType);
//			transform_skip_flag := res[0];
		end
		get_LAST_SIGNIFICANT_COEFF_X_PREFIX(codIRange, codIOffset, ctxTable, fifo, res, rc_log2TrafoSize, rc_cIdx);
		LastSignificantCoeffX := res[0];
		get_LAST_SIGNIFICANT_COEFF_Y_PREFIX(codIRange, codIOffset, ctxTable, fifo, res, rc_log2TrafoSize, rc_cIdx);
		LastSignificantCoeffY := res[0];
		if LastSignificantCoeffX > 3 then
			get_LAST_SIGNIFICANT_COEFF_XY_SUFFIX(codIRange, codIOffset, ctxTable, fifo, res, LastSignificantCoeffX);
			LastSignificantCoeffX := (1 << ((LastSignificantCoeffX >> 1) - 1)) *
									 (2  +  (LastSignificantCoeffX & 1 )) + res[0];			
		end
		if LastSignificantCoeffY > 3 then 
			get_LAST_SIGNIFICANT_COEFF_XY_SUFFIX(codIRange, codIOffset, ctxTable, fifo, res, LastSignificantCoeffY);
			LastSignificantCoeffY := (1 << ((LastSignificantCoeffY >> 1) - 1)) *
									 (2  +  (LastSignificantCoeffY & 1 )) + res[0];
		end
		
		InitScanningArray(rc_log2TrafoSize-2, ScanOrder);
		InitScanningArray(2, ScanOrder2);
		
		if rc_scanIdx = 2 then
			res[0] := LastSignificantCoeffX;
			LastSignificantCoeffX := LastSignificantCoeffY;
			LastSignificantCoeffY := res[0];
		end
		
		rc_lastScanPos  := 16;
		rc_lastSubBlock := ( 1 << ( rc_log2TrafoSize - 2 ) ) * ( 1 << ( rc_log2TrafoSize - 2 ) ) - 1;
		while  not( xC = LastSignificantCoeffX and yC = LastSignificantCoeffY) do
			if rc_lastScanPos = 0 then
				rc_lastScanPos  := 16;
				rc_lastSubBlock := rc_lastSubBlock - 1;
			end
			rc_lastScanPos := rc_lastScanPos - 1;
			xS := ScanOrder[ rc_scanIdx ][ rc_lastSubBlock ][ 0 ]; 
			yS := ScanOrder[ rc_scanIdx ][ rc_lastSubBlock ][ 1 ]; 
			xC := ( xS << 2 ) + ScanOrder2[ rc_scanIdx ][ rc_lastScanPos ][ 0 ];
			yC := ( yS << 2 ) + ScanOrder2[ rc_scanIdx ][ rc_lastScanPos ][ 1 ];
//			println(xC+", "+LastSignificantCoeffX+" : "+yC+", "+LastSignificantCoeffY+", rc_lastSubBlock = "+rc_lastSubBlock+", rc_lastScanPos = "+rc_lastScanPos);
		end
//		println("LastSignificantCoeffX = "+LastSignificantCoeffX+", LastSignificantCoeffY = "+LastSignificantCoeffY+", rc_lastSubBlock = "+(rc_lastSubBlock)+" rc_scanIdx = "+rc_scanIdx);
		rc_i			 := rc_lastSubBlock;
		rc_idx           := 2;
 	end
	/**************************************************************************
	 * read_ResidualCoding.for_numLastSubset.start
	 *************************************************************************/
	uint(size=16) rc_xS;
	uint(size=16) rc_yS;
	 int(size= 8) m_end;
	read_ResidualCoding.for_numLastSubset.start : action ==>
	guard 
		rc_idx = 2 and isFifoFull(fifo),
		rc_i >= 0
	var
		 int(size=32) res[1],
		uint(size= 8) n,
		uint(size=16) xC,
		uint(size=16) yC,
		uint(size= 1) inferSigCoeffFlag
	do	   
		rc_xS := ScanOrder[ rc_scanIdx ][ rc_i ][ 0 ];
   		rc_yS := ScanOrder[ rc_scanIdx ][ rc_i ][ 1 ]; 
	    inferSigCoeffFlag := 0; 
        if rc_i < rc_lastSubBlock and rc_i > 0 then 
	        get_CODED_SUB_BLOCK_FLAG( codIRange, codIOffset, ctxTable, fifo, res, coded_sub_block_flag, rc_xS, rc_yS, rc_cIdx, rc_log2TrafoSize);
	        coded_sub_block_flag[ rc_xS ][ rc_yS ] := res[0];
           	inferSigCoeffFlag := 1;
        else
        	if (rc_xS = LastSignificantCoeffX>>2 and rc_yS = LastSignificantCoeffY>>2) or (rc_xS = 0 and rc_yS = 0) then
        		coded_sub_block_flag[ rc_xS ][ rc_yS ] := 1;
        	end
        end
		significant_coeff_flag := [0	: for int j in 0 .. 15];        
        m_end := if rc_i = rc_lastSubBlock then rc_lastScanPos - 1 else 15 end;
        significant_coeff_flag[ rc_lastScanPos ] := 1;
        foreach int m in 0 .. m_end do
  			n  := m_end - m;  
			xC := ( rc_xS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 0 ]; 
			yC := ( rc_yS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 1 ];			
			if coded_sub_block_flag[ rc_xS ][ rc_yS ] = 1 and ( n > 0 or inferSigCoeffFlag = 0 ) then
				get_SIGNIFICANT_COEFF_FLAG( codIRange, codIOffset, ctxTable, fifo, res, coded_sub_block_flag, xC, yC, rc_cIdx, rc_log2TrafoSize, rc_scanIdx);
                significant_coeff_flag[ n ] := res[0];
				if significant_coeff_flag[ n ] = 1 then 
                   	inferSigCoeffFlag := 0;
               	end
            else
            	significant_coeff_flag[ n ] := 0;
            	if (xC = LastSignificantCoeffX and yC = LastSignificantCoeffY) or
            	   (( (xC&3) = 0 and (yC&3) = 0) and inferSigCoeffFlag = 1 and coded_sub_block_flag[ rc_xS ][ rc_yS ] = 1 ) then
					significant_coeff_flag[ n ] := 1;
            	end
			end
		end
		m_end := if rc_i = rc_lastSubBlock then rc_lastScanPos else 15 end;
		rc_idx := 3;
	end
	/**************************************************************************
	 * read_ResidualCoding_end
	 *************************************************************************/
	read_ResidualCoding_end : action ==>
	guard 
		rc_idx = 2,
		rc_i < 0
	do
		if DEBUG_MODE then
			println("read_ResidualCoding_end");
		end
	end
	/**************************************************************************
	 * read_ResidualCoding.for_numLastSubset.get_COEFF_ABS_LEVEL_GREATER1_FLAG
	 *************************************************************************/
	uint(size= 8) rc_ctxSet[1];
	uint(size= 8) rc_greater1Ctx[1];
	uint(size= 8) rc_firstSigScanPos;
	 int(size= 8) rc_firstGreater1ScanPos;
	uint(size= 8) rc_m;
	uint(size= 8) coeff_abs_level_greater1_flag[16];
	uint(size= 8) coeff_abs_level_greater2_flag[16];
	uint(size= 1) rc_signHidden;
	read_ResidualCoding.for_numLastSubset.get_COEFF_ABS_LEVEL_GREATER1_FLAG : action ==>
	guard 
		rc_idx = 3 and isFifoFull(fifo)
	var
		 int(size=32) res[1] := [0],
		 int          n,
//		uint(size=16) xC,
//		uint(size=16) yC,
		 int(size= 8) lastSigScanPos  := -1,
		uint(size= 8) numGreater1Flag :=  0,
		uint(size= 9) ctxIdxInc[1]
	do	
		rc_firstSigScanPos		:= 16;
		rc_firstGreater1ScanPos	:= -1;
		coeff_abs_level_greater1_flag := [ 0 : for int i in 0 .. 15 ];
		coeff_abs_level_greater2_flag := [ 0 : for int i in 0 .. 15 ];

		foreach int m in 0 .. m_end do
  			n  := m_end - m;  
//			xC := ( rc_xS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 0 ]; 
//			yC := ( rc_yS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 1 ];			
			if significant_coeff_flag[ n ] = 1 then
				if numGreater1Flag < 8 then
					context_93315(rc_cIdx, rc_i, numGreater1Flag = 0, rc_i = rc_lastSubBlock, rc_ctxSet, rc_greater1Ctx, ctxIdxInc);
					get_COEFF_ABS_LEVEL_GREATER1_FLAG( codIRange, codIOffset, ctxTable, fifo, res, ctxIdxInc[0]);
					if res[0] = 1 then
	    				rc_greater1Ctx[0] := 0;
    				elsif rc_greater1Ctx[0] > 0 and rc_greater1Ctx[0] < 3 then
	        			rc_greater1Ctx[0] := rc_greater1Ctx[0] + 1;
    				end
    				coeff_abs_level_greater1_flag[ n ] := res[0];
					numGreater1Flag := numGreater1Flag + 1; 
					if (coeff_abs_level_greater1_flag[ n ] = 1 and rc_firstGreater1ScanPos = -1) then
						rc_firstGreater1ScanPos := n;
					end	
				end
				if lastSigScanPos = -1 then
					lastSigScanPos := n;
				end
				rc_firstSigScanPos := n;
			end
		end
				
		rc_signHidden := if lastSigScanPos - rc_firstSigScanPos > 3 and cu_transquant_bypass_flag = 0 then 1 else 0 end;
		if rc_firstGreater1ScanPos != -1 then 
			get_COEFF_ABS_LEVEL_GREATER2_FLAG(codIRange, codIOffset, ctxTable, fifo, res, rc_cIdx, rc_ctxSet[0]);
			coeff_abs_level_greater2_flag[ rc_firstGreater1ScanPos] := res[0];
		end
		
		foreach int m in 0 .. m_end do
  			n  := m_end - m;  
//			xC := ( rc_xS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 0 ]; 
//			yC := ( rc_yS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 1 ];			
			if significant_coeff_flag[ n ] = 1 and
				(pps_sign_data_hiding_flag[pps_id] = 0 or rc_signHidden = 0 or n != rc_firstSigScanPos) then
				get_COEFF_SIGN_FLAG(codIRange, codIOffset, fifo, res);
//				coeff_sign_flag[ n ] := res[0];
			end
		end
		rc_numSigCoeff		:= 0;
		rc_sumAbsLevel		:= 0;
		rc_cLastRiceParam	:= 0;
		rc_m   := 0;
		rc_idx := 4;
	end
	/**************************************************************************
	 * read_ResidualCoding.for_numLastSubset.get_COEFF_ABS_LEVEL
	 *************************************************************************/
	uint(size= 8) rc_numSigCoeff;
	uint(size= 8) rc_sumAbsLevel;
	uint(size= 8) rc_cLastRiceParam;
	read_ResidualCoding.for_numLastSubset.get_COEFF_ABS_LEVEL : action ==>
	guard 
		rc_idx = 4 and isFifoFull(fifo),
		rc_m <= m_end
	var
		 int(size=32) res[1] := [0],
		 int          n,
//		uint(size=16) xC,
//		uint(size=16) yC,
//		uint(size= 1) coeff_sign_flag[16] := [ 0 : for int i in 0 .. 15 ],
		uint(size= 8) coeff_abs_level_remaining[16] := [ 0 : for int i in 0 .. 15 ],
		uint(size= 8) baseLevel,
		uint(size= 8) cLastAbsLevel
	do					
		n  := m_end - rc_m;  
//		xC := ( rc_xS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 0 ]; 
//		yC := ( rc_yS << 2 ) + ScanOrder2[ rc_scanIdx ][ n ][ 1 ];
		if significant_coeff_flag[ n ] = 1 then
			baseLevel := 1 + coeff_abs_level_greater1_flag[ n ] + coeff_abs_level_greater2_flag[ n ];
			if baseLevel = ( if rc_numSigCoeff < 8 then if n = rc_firstGreater1ScanPos then 3 else 2 end else 1 end) then
				get_COEFF_ABS_LEVEL(codIRange, codIOffset, fifo, res, rc_cLastRiceParam);				
				coeff_abs_level_remaining[ n ] := res[0];
				cLastAbsLevel  := baseLevel + coeff_abs_level_remaining[ n ];
				rc_cLastRiceParam := min(if cLastAbsLevel> 3 * ( 1 << rc_cLastRiceParam ) then rc_cLastRiceParam + 1 else rc_cLastRiceParam end, 4);			
			end
			//TransCoeffLevel[ rc_x0 ][ rc_y0 ][ rc_cIdx ][ xC ][ yC ] := ( coeff_abs_level_remaining[ n ] + baseLevel ) * ( 1 - 2 * coeff_sign_flag[ n ] );
			if pps_sign_data_hiding_flag[pps_id] = 1 and rc_signHidden = 1 then
				rc_sumAbsLevel := rc_sumAbsLevel + coeff_abs_level_remaining[ n ] + baseLevel;
				if n = rc_firstSigScanPos and ( rc_sumAbsLevel & 1 ) = 1 then
					//TransCoeffLevel[ rc_x0 ][ rc_y0 ][ rc_cIdx ][ xC ][ yC ] = − TransCoeffLevel[ rc_x0 ][ rc_y0 ][ rc_cIdx ][ xC ][ yC ]
				end
			end
			rc_numSigCoeff := rc_numSigCoeff + 1;
		end
		rc_m := rc_m + 1;		
	end
	read_ResidualCoding.for_numLastSubset.get_COEFF_ABS_LEVEL_end : action ==>
	guard 
		rc_idx = 4 and isFifoFull(fifo),
		rc_m > m_end
	do
		rc_i	:= rc_i - 1;
		rc_idx  := 2;
	end
	
	
	/**************************************************************************
	 **************************************************************************
	 ********        undefined                            *********************
	 **************************************************************************
	 *************************************************************************/
  	
  	bool actorNotFinished := true;
	
	undefined: action ==>
	guard
		actorNotFinished
	do
		println("Algo_Parser doesn't manage this case yet");
		actorNotFinished := false;
	end

	/**************************************************************************
	 **************************************************************************
	 ********        schedule                             *********************
	 **************************************************************************
	 *************************************************************************/
  	schedule fsm start_code:
		// byte align
		byte_align          			( byte_align_a						) --> start_code;
		// search start code
		start_code          			( start_code.search					) --> start_code;
		start_code			            ( start_code.done					) --> read_Nal_unit_header;
		// read Nal Unit header
		read_Nal_unit_header			( read_nal_unit_header				) --> find_header;
		find_header	  					( look_for.VPS_header	  			) --> read_VPS_Header;
		find_header	  					( look_for.SPS_header	  			) --> read_SPS_Header;
		find_header	  					( look_for.PPS_header	  			) --> read_PPS_Header;
		find_header	  					( look_for.Slice_header	  			) --> read_SliceHeader;
		// read VPS header
		read_VPS_Header	  				( read_VPS_Header	    			) --> read_VPS_Header;
		read_VPS_Header	  				( read_VPS_Header_done    			) --> byte_align;
		// read SPS header
		read_SPS_Header	  				( read_SPS_Header	    			) --> read_SPS_Header;
		read_SPS_Header	  				( read_SPS_Header_done    			) --> byte_align;
		// read PPS header
		read_PPS_Header 				( read_PPS_Header   				) --> read_PPS_Header;
		read_PPS_Header	  				( read_PPS_Header_done    			) --> byte_align;
		// read_SliceHeader
		read_SliceHeader				( read_SliceHeader					) --> read_SliceHeader;
		read_SliceHeader				( read_SliceHeader_done				) --> read_SliceData;
		// read_SliceData
		read_SliceData				    ( read_SliceData	  				) --> read_SliceData;
		read_SliceData				    ( read_SliceData_gotoCodingTree		) --> read_CodingTree;
		read_SliceData				    ( read_SliceData_end	  			) --> byte_align;
		// read_CodingTree
		read_CodingTree				    ( read_CodingTree	  				) --> read_CodingTree;
		read_CodingTree				    ( read_CodingTree_gotoSaoParam		) --> read_SaoParam;
		read_CodingTree				    ( read_CodingTree_gotoCodingQuadTree) --> read_CodingQuadTree;
		read_CodingTree				    ( read_CodingTree_end	 			) --> read_SliceData;
		// read_SaoParam
		read_SaoParam					( read_SaoParam						) --> read_SaoParam;
		read_SaoParam					( read_SaoParam_endLoop				) --> read_CodingTree;
		// read_CodingQuadTree
		read_CodingQuadTree			    ( read_CodingQuadTree	  				) --> read_CodingQuadTree;
		read_CodingQuadTree			    ( read_CodingQuadTree_gotoCodingUnit	) --> read_CodingUnit;
		read_CodingQuadTree			    ( read_CodingQuadTree_gotoPCMSample		) --> read_PCMSample;
		read_CodingQuadTree				( read_CodingQuadTree_end	 			) --> read_CodingTree;
		// read_CodingUint
		read_CodingUnit				    ( read_CodingUnit                   ) --> read_CodingUnit;
		read_CodingUnit				    ( read_CodingUnit_noGoto1           ) --> send_CUInfo;
		read_CodingUnit				    ( read_CodingUnit_gotoPCMSample		) --> read_PCMSample2;
		read_CodingUnit				    ( read_CodingUnit_gotoPredictionUnit) --> read_PredictionUnit;
		read_CodingUnit				    ( read_CodingUnit_gotoTransformTree ) --> read_TransformTree;
		read_CodingUnit				    ( read_CodingUnit_end				) --> read_CodingQuadTree;

		send_CUInfo                     (sendCUInfo                         ) --> read_CodingUnit;
		// read_PredictionUnit
		read_PredictionUnit			    ( read_PredictionUnit	  			) --> read_PredictionUnit;
		read_PredictionUnit			    ( read_PredictionUnit_gotoMVDCoding ) --> read_MVDCoding;
		read_PredictionUnit			    ( read_PredictionUnit_end			) --> read_CodingUnit;
		// read_MVDCoding
		read_MVDCoding				    ( read_MVDCoding	  				) --> undefined;//read_PredictionUnit;
		// read_PCMSample
		read_PCMSample				    ( read_PCMSample	  				) --> undefined;//read_CodingTree;
		read_PCMSample2				    ( read_PCMSample2	  				) --> undefined;//read_PredictionUnit;
		// read_TransformTree
		read_TransformTree			    ( read_TransformTree	  			) --> read_TransformTree;
		read_TransformTree			    ( read_TransformTree_gotoTransformUnit) --> read_TransformUnit;
		read_TransformTree			    ( read_TransformTree_end	  		) --> read_CodingUnit;
		// read_TransformUnit
		read_TransformUnit			    ( read_TransformUnit	  			) --> read_TransformUnit;
		read_TransformUnit			    ( read_TransformUnit_gotoResidualCoding) --> read_ResidualCoding;
		read_TransformUnit			    ( read_TransformUnit_end			) --> read_TransformTree;
		// read_ResidualCoding
		read_ResidualCoding				( read_ResidualCoding				) --> read_ResidualCoding;
		read_ResidualCoding				( read_ResidualCoding_end			) --> read_TransformUnit;
		
		find_header                     ( look_for_other_header             ) --> byte_align;		
		
		// undefined
		undefined	  					( undefined		    				) --> undefined;
	end
	
	/**************************************************************************
	 * priority
	 *************************************************************************/
	priority
		look_for        > look_for_other_header;
	end
end