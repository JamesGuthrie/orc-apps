/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */

/**
 * Decoding process for intra blocks.
 * 
 * 
 * @input CUInfo : Informations related to the current CU
 *        (size, address and mode : INTRA,INTER,...).
 * 
 * @input IntraPredMode : Luma and chroma intra prediction mode for the
 *        current CU.
 * 
 * @input PartMode : Giving the partitioning mode of the current CU.
 * 
 * @input PicSize : Size of the current picture.
 * 
 * @input Sample : Value of the YUV components of every pixel from the pictures.
 *        The tokens correspond to the pixels of the unfiltered picture.
 * 
 * @input SplitTransf : split_transform_flag syntax element.
 * 
 * 
 * @output PredSample : Pixel's values predicted from the intra prediction
 *         provess.
 * 
 * 
 * @author Damien de Saint Jorre (EPFL) <damien.desaintjorre@epfl.ch>
 */
 
package intraPrediction;

import std.util.Math.*;
import common.CommonConstantHEVC.*;
import interPrediction.NaluType.*;

actor IntraPrediction()
	uint(size=16) CUInfo,
	uint(size=6)  IntraPredMode,
	uint(size=4)  PartMode,
	uint(size=16) PictSize,
	uint(size=8)  Sample,
	uint(size=16) SliceAddr,
	bool          SplitTransform
		==>
	uint(size=8) PredSample
		:

	uint(size=6) INTRA_PLANAR = 0;
	uint(size=6) INTRA_DC = 1;

	uint(size=1) ABOVE = 0;
	uint(size=1) LEFT  = 1;

	uint(size=16) TABLE_HEIGHT = 128;
	uint(size=16) TABLE_HEIGHT_MASK = 128-1;

	uint(size=8) leftBlkAvail[256] =
	[0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 8, 1, 7, 1, 2, 1, 1, 1, 6, 1, 5, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 0, 1, 0, 1, 2, 1, 1, 1, 0, 1, 0, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,

	 8, 1, 7, 1, 2, 1, 1, 1, 6, 1, 5, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1,
	 4, 1, 3, 1, 2, 1, 1, 1, 2, 1, 1, 1, 2, 1, 1, 1];

	uint(size=8) aboveBlkAvail[256] =
	[0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 0, 0, 2, 1, 0, 0, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 16, 15, 2, 1, 14, 13, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 12, 11, 2, 1, 10,  9, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	  8,  7, 2, 1,  6,  5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	  4,  3, 2, 1,  2,  1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,

	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 8, 7, 2, 1, 6, 5, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1,
	 4, 3, 2, 1, 2, 1, 2, 1, 4, 3, 2, 1, 2, 1, 2, 1];

	int(size=7) intraPredAngle[64] = [0, 0, 32, 26, 21, 17, 13, 9, 5, 2, 0, -2,
	-5, -9, -13, -17, -21, -26, -32, -26, -21, -17, -13, -9, -5, -2, 0, 2, 5,
	9, 13, 17, 21, 26, 32];

	int(size=9) invAngle[64] =
	[  0,     0,  -256, -315,  -390,  -482, -630,  -910, -1638, -4096,
	   0, -4096, -1638, -910,  -630,  -482, -390,  -315,  -256,  -315,
	-390,  -482,  -630, -910, -1638, -4096,    0, -4096, -1638,  -910,
	-630,  -482,  -390, -315,  -256];

	uint(size=7)  nS;
	uint(size=3)  log2NS;
	uint(size=16) cuAddr[2];
	uint(size=2)  cuMode;
	uint(size=16) treeAddr[2];
	uint(size=16) pictSize[3][2];
	uint(size=16) sliceAddr[3][2];
	uint(size=3)  numPart;
	uint(size=3)  numPartDecoded;

	uint(size=6) predMode;
	uint(size=6) predModeTab[3];
	int (size=4) trafoTreeDepth;
	uint(size=3) trafoTreeIdx[6];

	uint(size=8) pNeighb[2][129];
	uint(size=8) currPict[3][PICT_WIDTH][TABLE_HEIGHT];

	uint(size=8) result[3][64][64];
	uint(size=9) numBlkToSend;
	uint(size=9) idxBlk;

	function absCoordToRasterScan(uint(size=16) coord[2]) --> uint(size=8)
	var
		uint(size=4) xCoordInBlk = coord[0]/4,
		uint(size=4) yCoordInBlk = coord[1]/4
	:
		if((xCoordInBlk & 1)!=0) then 1   else 0 end +
		if((xCoordInBlk & 2)!=0) then 4   else 0 end +
		if((xCoordInBlk & 4)!=0) then 16  else 0 end +
		if((xCoordInBlk & 8)!=0) then 64  else 0 end +
		if((yCoordInBlk & 1)!=0) then 2   else 0 end +
		if((yCoordInBlk & 2)!=0) then 8   else 0 end +
		if((yCoordInBlk & 4)!=0) then 32  else 0 end +
		if((yCoordInBlk & 8)!=0) then 128 else 0 end
	end

	procedure getNeighbSamples(uint(size=16) cuAddr[2], uint(size=2) cIdx,
		uint(size=7) nS)
	var
		int minVal[2], int maxVal[2],
		int xMin, int yMin, int xMax, int yMax
	begin
		neighbExist(cuAddr, minVal, maxVal, cIdx, nS);
		xMin := minVal[0];
		yMin := minVal[1];
		xMax := maxVal[0];
		yMax := maxVal[1];

		foreach int y in yMin .. yMax-1
		do
			pNeighb[LEFT][y+1] :=
				currPict[cIdx][cuAddr[0]-1][(cuAddr[1]+y)&TABLE_HEIGHT_MASK];
		end
		if(yMax != yMin) then
			foreach int y in 0 .. yMin-1
			do
				pNeighb[LEFT][y+1] := pNeighb[LEFT][yMin+1];
			end
			foreach int y in yMax .. 2*nS-1
			do
				pNeighb[LEFT][y+1] := pNeighb[LEFT][yMax-1+1];
			end
		end
	
		foreach int x in xMin .. xMax-1
		do
			pNeighb[ABOVE][x] :=
				currPict[cIdx][cuAddr[0]+x-1][(cuAddr[1]-1)&TABLE_HEIGHT_MASK];
		end
		if(xMax != xMin) then
			foreach int x in 0 .. xMin-1
			do
				pNeighb[ABOVE][x] := pNeighb[LEFT][0+1];
			end
			foreach int x in xMax .. 2*nS
			do
				pNeighb[ABOVE][x] := pNeighb[ABOVE][xMax-1];
			end
		end

		/* When the neighbours are not available, the nearest valid value is
		 * propagated to the other neighbours
		 */
		if(xMax=xMin && yMax=yMin) then
			foreach int idx in 0 .. 2*nS
			do
				pNeighb[ABOVE][idx] := 128;
				pNeighb[LEFT][idx]  := 128;
			end
		else if(xMax=xMin && yMax!=yMin) then
			foreach int x in 0 .. 2*nS
			do
				pNeighb[ABOVE][x] := pNeighb[LEFT][0+1];
			end
		else if(xMax!=xMin && yMax=yMin) then
			foreach int y in 0 .. 2*nS-1
			do
				pNeighb[LEFT][y+1] := pNeighb[ABOVE][0+1];
			end
			pNeighb[ABOVE][0] := pNeighb[ABOVE][0+1];
		else if(xMin!=0 || yMin != 0) then
			pNeighb[ABOVE][0] := pNeighb[LEFT][0+1];
		else
			pNeighb[ABOVE][0] :=
				currPict[cIdx][cuAddr[0]-1][(cuAddr[1]-1)&TABLE_HEIGHT_MASK];
		end end end end
	end

	/* Check if the neighbours are available. maxVal contained either 2*nS or
	 * the index of the first pixel unavailable.
	 */
	procedure neighbExist(uint(size=16) cuAddr[2], int minVal[2], int maxVal[2], int cIdx,
		uint(size=7) nS)
	var
		uint(size=7) maxCuSize = if(cIdx=0) then 64 else 32 end,
		int xMin := 0, int yMin := 0, int xMax := 0, int yMax := 0,
		int numPixRemain
	begin

		if(cuAddr[0]!=0) then
			numPixRemain :=
				if((cuAddr[0]&~(maxCuSize-1)) =
					((cuAddr[0]-1)&~(maxCuSize-1))) then
					leftBlkAvail[absCoordToRasterScan(cuAddr)]<<2
				else
					maxCuSize-(cuAddr[1]&(maxCuSize-1))
				end;
			yMax := min(min(2*nS, numPixRemain), pictSize[cIdx][1]-cuAddr[1]);
			if(cuAddr[0] <= sliceAddr[cIdx][0]) then
				if(sliceAddr[cIdx][1]+maxCuSize>cuAddr[1]+2*nS)then
					yMin := yMax;
				else if(sliceAddr[cIdx][1]+maxCuSize>cuAddr[1]) then
					yMin := sliceAddr[cIdx][1] + maxCuSize - cuAddr[1];
				end end
			else
				if(sliceAddr[cIdx][1]>cuAddr[1]+2*nS) then
					yMin := yMax;
				else if(sliceAddr[cIdx][1]>cuAddr[1]) then
					yMin := sliceAddr[cIdx][1] - cuAddr[1];
				end end
			end
		end

		if(cuAddr[1]!=0) then
			numPixRemain :=
				if((cuAddr[1]&~(maxCuSize-1)) =
					((cuAddr[1]-1)&~(maxCuSize-1))) then
					min( aboveBlkAvail[absCoordToRasterScan(cuAddr)]<<2,
						pictSize[cIdx][0]-cuAddr[0])
				else
					pictSize[cIdx][0]-cuAddr[0]
				end;
			xMax := min(2*nS, numPixRemain)+1;
			if(cuAddr[1] = sliceAddr[cIdx][1]) then
				xMin := xMax;
			else if((cuAddr[1] = sliceAddr[cIdx][1]+maxCuSize) && (cuAddr[0]-1<sliceAddr[cIdx][0])) then
				if(sliceAddr[cIdx][0]>cuAddr[0]+2*nS) then
					xMin := xMax;
				else if(sliceAddr[cIdx][0]>cuAddr[0]-1) then
					xMin := sliceAddr[cIdx][0]-cuAddr[0]+1;
				end end
			end end
		end

		maxVal[0] := xMax;
		maxVal[1] := yMax;
		minVal[0] := xMin;
		minVal[1] := yMin;
	end

	/* Filtering process of neighbouring sample. */
	procedure filterNeighbSamp(uint(size=7) nS)
	var
		uint(size=5) minDistVertHor,
		uint pFNeighb[2][64]
	begin
		if(predMode != INTRA_DC) then
			minDistVertHor := min(abs(predMode-26), abs(predMode-10));
			if(nS=4  && minDistVertHor>10 ||
			   nS=8  && minDistVertHor>7 ||
			   nS=16 && minDistVertHor>1 ||
			   nS=32 && minDistVertHor>0 ||
			   nS=64 && minDistVertHor>10) then
				foreach uint neighb in 0 .. 1
				do
					pFNeighb[neighb][0] := (pNeighb[ABOVE][0] + 2*pNeighb[neighb][0+1] +
						pNeighb[neighb][1+1] + 2) >> 2;
					foreach uint i in 1 .. nS*2-2
					do
						pFNeighb[neighb][i] := (pNeighb[neighb][i-1+1] +
							2*pNeighb[neighb][i+1] + pNeighb[neighb][i+1+1] + 2)
							>> 2;
					end
				end
				pNeighb[ABOVE][0] := (pNeighb[LEFT][0+1] + 2*(pNeighb[ABOVE][0]) +
					pNeighb[ABOVE][0+1] + 2) >> 2;
				foreach uint neighb in 0 .. 1
				do
					foreach uint i in 0 .. nS*2-2
					do
						pNeighb[neighb][i+1] := pFNeighb[neighb][i];
					end
				end
			end
		end
	end

	procedure intraPlanarPred(uint(size=2) cIdx, uint(size=7) nS,
		uint(size=3) log2NS)
	var
		uint(size=8) pUpRight,
		uint(size=8) pBottomLeft
	begin
		pUpRight    := pNeighb[ABOVE][nS+1];
		pBottomLeft := pNeighb[LEFT][nS+1];
		foreach uint y in 0 .. nS-1
		do
			foreach uint x in 0 .. nS-1
			do
				result[cIdx][x][y] := ((nS-1-x)*pNeighb[LEFT][y+1] +
					(x+1)*pUpRight + (nS-1-y)*pNeighb[ABOVE][x+1] +
					(y+1)*pBottomLeft + nS) >> (log2NS+1);
			end
		end
	end

	/* Implementation of Intra_DC prediction mode. */
	procedure intraDcPred(uint(size=2) cIdx, uint(size=7) nS, uint(size=3) log2NS)
	var
		uint(size=13) dcVal
	begin
		dcVal := nS;
		foreach uint x in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb[ABOVE][x+1];
		end
		foreach uint y in 0 .. nS-1
		do
			dcVal := dcVal + pNeighb[LEFT][y+1];
		end
		dcVal := dcVal >> (log2NS+1);
		if(cIdx=0) then // Luma component
			result[cIdx][0][0] := (pNeighb[LEFT][0+1] + 2*dcVal +
				pNeighb[ABOVE][0+1] + 2)>>2;
			foreach uint x in 1 .. nS-1
			do
				result[cIdx][x][0] := (pNeighb[ABOVE][x+1] + 3*dcVal + 2)>>2;
			end
			foreach uint y in 1 .. nS-1
			do
				result[cIdx][0][y] := (pNeighb[LEFT][y+1] + 3*dcVal + 2)>>2;
				foreach uint x in 1 .. nS-1
				do
					result[cIdx][x][y] := dcVal;
				end
			end
		else // Chroma component
			foreach uint y in 0 .. nS-1
			do
				foreach uint x in 0 .. nS-1
				do
					result[cIdx][x][y] := dcVal;
				end
			end
		end
	end

	/* Implementation of Intra_Angular prediction mode. */
	procedure intraAngularPred(uint(size=2) cIdx, uint(size=7) nS)
	var
		uint(size=6) OFFSET = 33,
		uint(size=6) angle := predMode & 63,
		uint(size=1) neighbIdx,
		uint refMain[128+33],
		int(size=9) iIdx,
		uint(size=5) iFact
	begin
		if(angle=26 || angle = 10) then
			intraAngularVertOrHor( cIdx, angle=26, nS);
		else
			/* Implementation of Intra_Angular(2..9,11..25,27..34) prediction
			 * mode.
			 */
			neighbIdx := if(angle>=18) then ABOVE else LEFT end;
			refMain[OFFSET] := pNeighb[ABOVE][0];
			foreach uint x in 1 .. nS
			do
				refMain[x+OFFSET] := pNeighb[neighbIdx][x-1+1];
			end
			if(intraPredAngle[angle]<0) then
				neighbIdx := neighbIdx^1;
				foreach int k in (nS*intraPredAngle[angle])>>5 .. -1
				do
					refMain[k+OFFSET] :=
						pNeighb[neighbIdx][((k*invAngle[angle]+128)>>8)-1+1];
				end
			else
				foreach uint x in nS+1 .. 2*nS
				do
					refMain[x+OFFSET] := pNeighb[neighbIdx][x-1+1];
				end
			end
			if(angle>=18) then
				foreach uint y in 0 .. nS-1
				do
					iIdx  := ((y+1)*intraPredAngle[angle])>>5;
					iFact := ((y+1)*intraPredAngle[angle])&31;
					foreach uint x in 0 .. nS-1
					do
						if(iFact!=0) then
							result[cIdx][x][y] :=
								((32-iFact)*refMain[x+iIdx+1 +OFFSET] +
								iFact*refMain[x+iIdx+2 +OFFSET]+16) >> 5;
						else
							result[cIdx][x][y] := refMain[x+iIdx+1 +OFFSET];
						end
					end
				end
			else
				foreach uint y in 0 .. nS-1
				do
					iIdx  := ((y+1)*intraPredAngle[angle])>>5;
					iFact := ((y+1)*intraPredAngle[angle])&31;
					foreach uint x in 0 .. nS-1
					do
						if(iFact!=0) then
							result[cIdx][y][x] :=
								((32-iFact)*refMain[x+iIdx+1 +OFFSET] +
								iFact*refMain[x+iIdx+2 +OFFSET]+16) >> 5;
						else
							result[cIdx][y][x] := refMain[x+iIdx+1 +OFFSET];
						end
					end
				end
			end
		end
	end

	/* Implementation of Intra_angular(10,26) prediction mode. */
	procedure intraAngularVertOrHor(uint(size=2) cIdx, bool isVertical,
		uint(size=7) nS)
	var
		uint(size=1) neighbIdx = if(isVertical) then ABOVE else LEFT end
	begin
		if(cIdx=0) then
			if(isVertical) then
				foreach uint y in 0 .. nS-1
				do
					result[cIdx][0][y] := clip_i32(pNeighb[ABOVE][0+1]+
						((pNeighb[LEFT][y+1]-pNeighb[ABOVE][0])>>1), 0, 255);
				end				
				foreach uint x in 1 .. nS-1
				do
					foreach uint y in 0 .. nS-1
					do
						result[cIdx][x][y] := pNeighb[ABOVE][x+1];
					end
				end
			else
				foreach uint x in 0 .. nS-1
				do
					result[cIdx][x][0] := clip_i32(pNeighb[LEFT][0+1]+
						((pNeighb[ABOVE][x+1]-pNeighb[ABOVE][0])>>1), 0, 255);
				end
				foreach uint x in 0 .. nS-1
				do
					foreach uint y in 1 .. nS-1
					do
						result[cIdx][x][y] := pNeighb[LEFT][y+1];
					end
				end
			end
		else
			foreach uint x in 0 .. nS-1
			do
				foreach uint y in 0 .. nS-1
				do
					if(neighbIdx=0) then
						result[cIdx][x][y] := pNeighb[ABOVE][x+1];
					else
						result[cIdx][y][x] := pNeighb[LEFT][x+1];
					end
				end
			end
		end
	end

	/* Updating the picture size. */
	isNewSliceOrPict: action PartMode:[partMode],
		PictSize:[pictSizeVal] repeat 2, SliceAddr:[slcAddr] repeat 2 ==>
	guard
		partMode = PART_MODE_SLICE || partMode = PART_MODE_PICT
	do
		sliceAddr[0] := [slcAddr[i]:for int i in 0 .. 1];
		sliceAddr[1] := [slcAddr[i]>>1:for int i in 0 .. 1];
		sliceAddr[2] := [slcAddr[i]>>1:for int i in 0 .. 1];
		pictSize[0] := [pictSizeVal[i]:for int i in 0 .. 1];
		pictSize[1] := [pictSizeVal[i]>>1:for int i in 0 .. 1];
		pictSize[2] := [pictSizeVal[i]>>1:for int i in 0 .. 1];
	end

	/* Check when there are two parts in the CU.  */
	getPartMode.isNonQuadTree: action PartMode:[partMode] ==>
	guard
		partMode != PART_MODE_SLICE && partMode != PART_MODE_PICT,
		partMode != PART_2Nx2N && partMode != PART_NxN
	end

	/* Check if all parts of the CU are a square. */
	getPartMode.isQuadTree: action PartMode:[partMode] ==>
	guard
		partMode != PART_MODE_SLICE  && partMode != PART_MODE_PICT,
		partMode = PART_2Nx2N || partMode = PART_NxN
	do
		numPart := partModeToNumPart[partMode];
		numPartDecoded := 0;
	end

	/* Get the information concerning the CU to be able to save the values in
	 * currPict.
	 */
	getCuInfo.launch: action CUInfo:[cuInfo] repeat 5 ==>
	guard
		numPartDecoded < numPart
	do
		cuMode := cuInfo[0];
		nS := cuInfo[3];
		cuAddr := [cuInfo[1], cuInfo[2]];
		numPartDecoded := numPartDecoded + 1;
	end

	/* When there are two parts, we get both part in once in order to perform a
	 * raster scan on the square (the SelectCU actor reorder the differents
	 * blocks)
	 */
	getCuInfo.nonQT.isInter: action CUInfo:[cuInfo] repeat 10 ==>
	do
		cuMode := cuInfo[0];
		nS := max(cuInfo[3], cuInfo[4]);
		idxBlk := 0;
		numBlkToSend := nS*nS/16;
		treeAddr := [cuInfo[1], cuInfo[2]];
		numPart := 2;
		numPartDecoded := 2;
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	getCuInfo.done: action ==>
	guard
		numPartDecoded = numPart
	end

	/* When it's an intra CU, getting the intra prediction mode for chroma and luma component. */
	getCuMode.isIntra: action IntraPredMode:[predMode] repeat 2 ==>
	guard
		cuMode = INTRA
	do
		predModeTab[0] := predMode[0];
		predModeTab[1] := predMode[1];
		predModeTab[2] := predMode[1];

		if(predMode[1] = 35) then
			println("Warning : we don't manage Intra_FromLuma yet !!!");
		end
		log2NS := 2;
		while((1<<log2NS) < nS)
		do
			log2NS := log2NS+1;
		end
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	/* Check if it's an inter prediction, then the pixels sent will be saved in
	 * currPict.
	 */
	getCuMode.isInter: action ==>
	guard
		cuMode = INTER || cuMode = SKIP
	do
		idxBlk := 0;
		numBlkToSend := nS*nS/16;
		treeAddr := [cuAddr[i]: for int i in 0 .. 1];
		trafoTreeDepth  := 0;
		trafoTreeIdx[0] := 3;
	end

	/* When it's neither an intra nor inter prediction CU, do neither
	 * prediction nor saving pixels' value
	 */
	getCuMode.isOther: action ==>
	guard
		cuMode = OTHER
	end

	/* Check if the intra prediction CU has to be split in 4 "subCU" */
	getSplitTrafo.isTrue: action SplitTransform:[splitTrafo] ==>
	guard
		splitTrafo && trafoTreeDepth>-1
	do
		trafoTreeDepth := trafoTreeDepth + 1;
		trafoTreeIdx[trafoTreeDepth] := 0;
	end

	/* Check if the intra prediction will not be split again, then realize the
	 * prediction for this part of CU
	 */
	getSplitTrafo.isFalse: action SplitTransform:[splitTrafo] ==>
	guard
		not splitTrafo && trafoTreeDepth>-1
	var
		uint(size=2)  cIdxMax,
		uint(size=7)  treeNS,
		uint(size=3)  treeLog2NS,
		uint(size=16) locAddr[2],
		uint(size=3)  off[2]
	do
		/* Computing the size and the address of the current block. */
		treeLog2NS := log2NS - trafoTreeDepth;
		treeNS := nS;
		treeAddr := [cuAddr[i]: for int i in 0 .. 1];
		foreach uint idx in 1 .. trafoTreeDepth
		do
			treeNS := treeNS >> 1;
			if((trafoTreeIdx[idx]&1) != 0) then
				treeAddr[0] := treeAddr[0] + treeNS;
			end
			if((trafoTreeIdx[idx]&2) != 0) then
				treeAddr[1] := treeAddr[1] + treeNS;
			end
		end

		numBlkToSend := treeNS*treeNS/16;
		idxBlk := 0;

		/* Chroma component are not split if we get a 2x2 block (this problem
		 * appear with YUV420)
		 */
		if(treeLog2NS != 2) then
			cIdxMax := 3;
		else
			/* In this case, we interpolate chroma component only once*/
			if((treeAddr[0]&7) = 0 && (treeAddr[1]&7) = 0) then
				cIdxMax := 3;
			else
				cIdxMax := 1; // Computation will be only for luma component.
			end
		end
		locAddr := [treeAddr[i]:for int i in 0 .. 1];
		foreach uint cIdx in 0 .. cIdxMax-1
		do
			predMode := predModeTab[cIdx];
			if(cIdx = 1) then // chroma component.
				if(treeLog2NS != 2) then
					/* 2x2 blocks are forbidden */
					treeNS := treeNS >> 1;
					treeLog2NS := treeLog2NS - 1;
				end
				locAddr[0] := locAddr[0] >> 1;
				locAddr[1] := locAddr[1] >> 1;
			end

			getNeighbSamples(locAddr, cIdx, treeNS);

			if(cIdx=0) then
				filterNeighbSamp(treeNS);
			end
			if(predMode = INTRA_PLANAR) then
				intraPlanarPred(cIdx, treeNS, treeLog2NS);
			else if(predMode = INTRA_DC) then
				intraDcPred(cIdx, treeNS, treeLog2NS);
			else
				intraAngularPred(cIdx, treeNS);
			end end
		end

		
		if(cIdxMax=1) then
			/* When we have 4x4 luma blocks, the chroma block corresponding to
			 * the luma one is copied at the begining of result in order to be
			 * sent (this block might have been computed beforehand).
			 */
			off := [(treeAddr[i]>>1)&2: for int i in 0 .. 1];
			if(off[0]!=0 || off[1]!=0) then
				foreach uint comp in 1 .. 2
				do
					foreach int y in 0 .. 1
					do
						foreach int x in 0 .. 1
						do
							result[comp][x][y] := result[comp][x+off[0]][y+off[1]];
						end
					end
				end
			end
		end

		/* going to the next "branch of the tree" */
		trafoTreeIdx[trafoTreeDepth] := trafoTreeIdx[trafoTreeDepth] + 1;
		while(trafoTreeDepth > -1 && trafoTreeIdx[trafoTreeDepth] = 4)
		do
			trafoTreeDepth := trafoTreeDepth - 1;
			if(trafoTreeDepth>-1) then
				trafoTreeIdx[trafoTreeDepth] :=
					trafoTreeIdx[trafoTreeDepth] + 1;
			end
		end
	end

	getSplitTrafo.isInter: action SplitTransform:[splitTrafo] ==>
	guard
		trafoTreeDepth>-1
	do
		if(splitTrafo) then
			trafoTreeDepth := trafoTreeDepth + 1;
			trafoTreeIdx[trafoTreeDepth] := 0;
		else
			trafoTreeIdx[trafoTreeDepth] := trafoTreeIdx[trafoTreeDepth] + 1;
			while(trafoTreeDepth > -1 && trafoTreeIdx[trafoTreeDepth] = 4)
			do
				trafoTreeDepth := trafoTreeDepth - 1;
				if(trafoTreeDepth>-1) then
					trafoTreeIdx[trafoTreeDepth] :=
						trafoTreeIdx[trafoTreeDepth] + 1;
				end
			end
		end
	end

	getSplitTrafo.done: action ==>
	guard
		trafoTreeDepth = -1
	end

	/* Sending One 4x4 luma blocks and the corresponding chroma values. */
	sendSamples.launch: action ==> PredSample:[sampleVal] repeat 16 + 2*4
	guard
		idxBlk < numBlkToSend
	var
		uint(size=8) sampleVal[16 + 2*4],
		uint(size=7) idxSample := 0,
		uint(size=16) blkAddr[2]
	do
		rasterScanToAbsCoord(idxBlk, blkAddr);

		foreach uint y in blkAddr[1] .. blkAddr[1]+3
		do
			foreach uint x in blkAddr[0] .. blkAddr[0]+3
			do
				sampleVal[idxSample] := result[0][x][y];
				idxSample := idxSample + 1;
			end
		end
		blkAddr[0] := blkAddr[0]>>1;
		blkAddr[1] := blkAddr[1]>>1;
		foreach uint comp in 1 .. 2
		do
			foreach uint y in blkAddr[1] .. blkAddr[1]+1
			do
				foreach uint x in blkAddr[0] .. blkAddr[0]+1
				do
					sampleVal[idxSample] := result[comp][x][y];
					idxSample := idxSample + 1;
				end
			end
		end
		idxBlk := idxBlk + 1;
	end

	sendSamples.done: action ==>
	guard
		idxBlk = numBlkToSend
	do
		idxBlk := 0;
	end

	/* Getting One 4x4 luma blocks and the corresponding chroma values. */
	getSamples.launch: action Sample:[sample] repeat 16 + 2*4 ==>
	guard
		idxBlk < numBlkToSend
	var
		uint(size=7) idxSample := 0,
		uint(size=16) blkAddr[2]
	do
		rasterScanToAbsCoord(idxBlk, blkAddr);
		blkAddr[0] := blkAddr[0] + treeAddr[0];
		blkAddr[1] := blkAddr[1] + treeAddr[1];

		foreach uint y in blkAddr[1] .. blkAddr[1]+3
		do
			foreach uint x in blkAddr[0] .. blkAddr[0]+3
			do
				currPict[0][x][y&TABLE_HEIGHT_MASK] := sample[idxSample];
				idxSample := idxSample + 1;
			end
		end

		
		blkAddr[0] := blkAddr[0]>>1;
		blkAddr[1] := blkAddr[1]>>1;
		foreach uint comp in 1 .. 2
		do
			foreach uint y in blkAddr[1] .. blkAddr[1]+1
			do
				foreach uint x in blkAddr[0] .. blkAddr[0]+1
				do
					currPict[comp][x][y&TABLE_HEIGHT_MASK] :=
						sample[idxSample];
					idxSample := idxSample + 1;
				end
			end
		end
		idxBlk := idxBlk + 1;
	end

	getSamples.done: action ==>
	guard
		idxBlk = numBlkToSend
	end

	schedule fsm GetPartMode:
		GetPartMode(isNewSliceOrPict          )--> GetPartMode;
		GetPartMode(getPartMode.isQuadTree    )--> GetCuInfo;
		GetPartMode(getPartMode.isNonQuadTree )--> GetCuInfoNonQT;

		GetCuInfoNonQT (getCuInfo.nonQT )--> GetSplitTrafoInter;

		GetCuInfo (getCuInfo.launch )--> GetCuMode;
		GetCuInfo (getCuInfo.done   )--> GetPartMode;

		GetCuMode (getCuMode.isIntra )--> GetSplitTrafoIntra;
		GetCuMode (getCuMode.isInter )--> GetSplitTrafoInter;
		GetCuMode (getCuMode.isOther )--> GetCuInfo;

		GetInterSamples (getSamples.launch )--> GetInterSamples;
		GetInterSamples (getSamples.done   )--> GetCuInfo;

		GetSplitTrafoIntra (getSplitTrafo.isTrue  )--> GetSplitTrafoIntra;
		GetSplitTrafoIntra (getSplitTrafo.isFalse )--> SendSamples;
		GetSplitTrafoIntra (getSplitTrafo.done    )--> GetCuInfo;

		GetSplitTrafoInter (getSplitTrafo.isInter  )--> GetSplitTrafoInter;
		GetSplitTrafoInter (getSplitTrafo.done    )--> GetInterSamples;

		SendSamples (sendSamples.launch )--> SendSamples;
		SendSamples (sendSamples.done   )--> GetIntraSamples;

		GetIntraSamples (getSamples.launch )--> GetIntraSamples;
		GetIntraSamples (getSamples.done   )--> GetSplitTrafoIntra;
	end
end