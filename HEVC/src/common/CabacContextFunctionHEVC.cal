/******************************************************************************
 * Definition of CabacContextFunction
 *****************************************************************************/
package common;

import  std.util.Math.*;
import  common.CommonConstantHEVC.*;
import  common.CabacContextConstantHEVC.*;
import  common.CabacContextTableHEVC.*;

unit CabacContextFunctionHEVC :

	/*************************************************************************
	 * ContextInit
	 *************************************************************************/
	procedure contextInit(uint(size=8) qp, uint(size=8) sliceType, uint(size=9) ctxTable[NB_MAX_SE][NB_MAX_NUM_CTX])
	var
		uint(size= 8) qp_clip3 = clip_i32(qp, 0, 51),
		uint(size= 8) initValue,
		 int(size= 8) slope,
		 int(size= 8) offset,
		uint(size= 8) initState,
		uint(size= 8) mpState,
		uint(size= 8) pStateIdx
	begin
		foreach uint(size=8) i in 0 .. NB_MAX_SE-1 do
			foreach uint(size=8) j in 0 .. NUM_SE_CONTEXT_INDEX[i]-1 do
				initValue	:= InitContextIndex[i][sliceType][j];
				slope		:= (initValue>>4)*5 - 45;
				offset		:= ((initValue & 15) << 3) - 16;
				initState   := clip_i32( ((qp_clip3*slope)>>4)+offset, 1, 126);
				mpState		:= if initState >= 64 then 1 else 0 end;
				pStateIdx	:= if initState >= 64 then initState - 64 else 63 - initState end;
				ctxTable[i][j] := (pStateIdx<<1) + mpState;
//				if i = SE_LAST_SIGNIFICANT_COEFF_X_PREFIX then
//					println(" initValue = "+initValue+" slope = "+slope+" offset = "+offset+" state = "+initState+" mps = "+mpState+" ucState = "+ctxTable[i][j]);
//				end
			end	
		end
	end
	/*************************************************************************
	 * getCtx_SPLIT_CODING_UNIT_FLAG
	 *************************************************************************/
	function getCtx_SPLIT_CODING_UNIT_FLAG(
		uint(size= 8) depthSubPart,
		bool		  availableA,
		uint(size= 8) depthSubPartA,
		bool		   availableB,
		uint(size= 8) depthSubPartB
	) --> uint(size=2) :
		uint(size=1) condTermA = if depthSubPartA > depthSubPart and availableA = true then 1 else 0 end;
		uint(size=1) condTermB = if depthSubPartB > depthSubPart and availableB = true then 1 else 0 end;
		condTermA + condTermB
	end
	/*************************************************************************
	 * Context_92312
	 *************************************************************************/
	uint(size=8) LASTCTX[28] = [
	 	0,  1,  2,  3,  4,  5,  6,  7,  8,  2,  6,  7,  8,  8,
	 	9,  9,  2,  2, 10, 11, 12, 12, 13, 13, 14, 14,  2,  2
	 	];
	function context_92312(
		uint(size=8) binIdx,
		uint(size=8) log2TrafoSize,
		uint(size=8) cIdx
	) --> uint(size=8) 
	var
		uint(size=8) ctx_offset = if cIdx = 0 then 3 * (log2TrafoSize - 2)  + ((log2TrafoSize - 1) >> 2) else 15 end,
		uint(size=8) ctx_shift  = if cIdx = 0 then (log2TrafoSize + 1) >> 2 else log2TrafoSize - 2 end
	:
		(binIdx >> ctx_shift) + ctx_offset
	end
	/*************************************************************************
	 * Context_93313
	 *************************************************************************/
	procedure context_93313(
		uint(size=1) coded_sub_block_flag[8][8],
		uint(size=8) xS,
		uint(size=8) yS,
		uint(size=8) cIdx,
		uint(size=8) log2TrafoSize,
		uint(size=9) ctxIdx[1]
	)
	var
		uint(size=8) csbfCtx  := 0,
		uint(size=8) trafoSize = (1 << (log2TrafoSize - 2))-1
	begin	
//		println(" get93313_ctxInc("+xS+", "+yS+", "+cIdx+", "+(1 << log2TrafoSize)+", "+(1 << log2TrafoSize)+")");
		
		if xS < trafoSize then
			csbfCtx := coded_sub_block_flag[ xS + 1 ][ yS ];
		end
		if yS < trafoSize then
			csbfCtx := csbfCtx + coded_sub_block_flag[ xS ][ yS + 1 ];
		end
		ctxIdx[0] := min(csbfCtx,1) + if cIdx = 0 then 0 else 2 end;
	end
	/*************************************************************************
	 * Context_93314
	 *************************************************************************/
	uint(size=8) ctxInMap[16] = [
		0, 1, 4, 5, 2, 3, 4, 5, 6, 6, 8, 8, 7, 7, 8, 8
		];
	procedure context_93314(
		uint(size=1) coded_sub_block_flag[8][8],
		uint(size=8) xC,
		uint(size=8) yC,
		uint(size=8) cIdx,
		uint(size=8) log2TrafoSize,
		uint(size=8) scanIdx,		
		uint(size=9) ctxIdx[1]
	)
	var
		uint(size=8) xS = xC >> 2,
		uint(size=8) yS = yC >> 2,		
		uint(size=8) xP = xC & 3,
		uint(size=8) yP = yC & 3,		
		uint(size=8) sigCtx,
		uint(size=8) prevCsbf := 0,
		uint(size=8) trafoSize = ((1 << log2TrafoSize) - 1)>>2
	begin
//		println(" get93314_ctxInc( "+xC+", "+yC+", "+cIdx+", "+log2TrafoSize+", "+scanIdx+")");
		if log2TrafoSize = 2 then
			sigCtx := ctxInMap[(yC<<2) + xC];
		elsif xC + yC = 0 then
			sigCtx := 0;
		else
			if xS < trafoSize then
				prevCsbf := prevCsbf + coded_sub_block_flag[ xS + 1 ][ yS ];
			end
			if yS < trafoSize then
				prevCsbf := prevCsbf + (coded_sub_block_flag[ xS ][ yS + 1 ] << 1);
			end
			if prevCsbf = 0 then
				sigCtx := if xP + yP = 0 then 2 elsif xP + yP < 3 then 1 else 0 end;
			elsif prevCsbf = 1 then
				sigCtx := if yP = 0 then 2 elsif yP = 1 then 1 else 0 end;
			elsif prevCsbf = 2 then
				sigCtx := if xP = 0 then 2 elsif xP = 1 then 1 else 0 end;
			else
				sigCtx := 2;
			end
			if cIdx = 0 then
				if xS + yS > 0 then
					sigCtx := sigCtx + 3;
				end
				if log2TrafoSize = 3 then
					sigCtx := sigCtx + if scanIdx = 0 then 9 else 15 end;
				else
					sigCtx := sigCtx + 21;
				end
			else
				if log2TrafoSize = 3 then
					sigCtx := sigCtx + 9;
				else
					sigCtx := sigCtx + 12;
				end
			end
		end
		if cIdx = 0 then
			ctxIdx[0] := sigCtx;
		else
			ctxIdx[0] := 27 + sigCtx;
		end
	end
	/*************************************************************************
	 * Context_93315
	 *************************************************************************/
	procedure context_93315(
		uint(size= 8) cIdx,
		 int(size= 8) i,
		bool          first_elem,
		bool          first_subset,
		uint(size= 8) ctxSet[1],
		uint(size= 8) greater1Ctx[1],
		uint(size= 9) ctxIdxInc[1]
	)
	begin
//		println(" get93315_ctxInc("+cIdx+", "+i+", "+first_elem+", "+first_subset+", "+ctxSet[0]+")");	
	    if first_elem = true then
        	ctxSet[0] := if i > 0 and cIdx = 0 then 2 else 0 end;

        	if first_subset = false and greater1Ctx[0] = 0 then
            	ctxSet[0] := ctxSet[0] + 1;
            end
        	greater1Ctx[0] := 1;
    	end
//		println("ctxSet = "+ctxSet[0]+" : lastGreater1Ctx = "+greater1Ctx[0]);
    	ctxIdxInc[0] := (ctxSet[0] << 2) + greater1Ctx[0];
    	if cIdx > 0 then
        	ctxIdxInc[0] := ctxIdxInc[0] + 16;
		end
	end
end