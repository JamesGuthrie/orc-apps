/*
 * Copyright (c) 2012, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
package inLoopFilter;

import std.util.Math.*;
import interPrediction.NaluType.*;

actor SaoFilter(bool DEBUG)
	uint(size=16) PicSize,
	uint(size=8)  SampleIn,
	int (size=9)  SaoSe
		==>
	uint(size=8)  FiltSample
		:

	uint(size=16) PIC_WIDTH_MAX=2048;
	uint(size=16) PIC_HEIGHT_MAX=2048;
	uint(size=4)  BIT_DEPTH = 8;


	int(size=3) constHPos0[4][2] = [[-1,1],  [ 0,0], [-1,1],  [ 1,-1]];
	int(size=3) constVPos0[4][2] = [[ 0,0],  [-1,1], [-1,1],  [-1, 1]];
	int(size=3) edgeIdxConv[5] = [1, 2, 0, 3, 4];

	uint(size=32) numSlice := 0;
	bool          slcSaoCompEn[3];

	uint(size=8) OrigPict[3][PIC_WIDTH_MAX][4*64];
	uint(size=8) FiltPict[3][PIC_WIDTH_MAX][PIC_HEIGHT_MAX];

	uint(size=2) cIdx;
	uint(size=2) saoTypeIdx[PIC_WIDTH_MAX/64][2][3];
	int (size=9) saoOffset[PIC_WIDTH_MAX/64][2][3][5];
	uint(size=2) saoEoClass[PIC_WIDTH_MAX/64][2][3];
	uint(size=5) saoLeftClass[PIC_WIDTH_MAX/64][2][3];

	uint(size=6)  picSizeInCu[2];
	uint(size=16) picSize[2];
	uint(size=6)  xCu := 0;
	uint(size=6)  yCu := 0;
	uint(size=6)  xCuFilt := 0;
	uint(size=6)  yCuFilt := 0;

	procedure nextLcuAddress()
	begin
		xCu := xCu + 1;
		if(xCu = picSizeInCu[0]) then
			xCu := 0;
			yCu := yCu + 1;
		end
	end

	procedure nextLcuAddressFilt()
	begin
		xCuFilt := xCuFilt + 1;
		if(xCuFilt = picSizeInCu[0]) then
			xCuFilt := 0;
			yCuFilt := yCuFilt + 1;
		end
	end

	procedure copyCu(uint(size=16) addrBeg[2], uint(size=16) addrEnd[2], uint(size=2) cIdx)
	begin
		foreach uint x in addrBeg[0] .. addrEnd[0]
		do
			foreach uint y in addrBeg[1] .. addrEnd[1]
			do
				FiltPict[cIdx][x][y] := OrigPict[cIdx][x][y &(4*64-1)];
			end
		end
	end

	procedure codingTreeBlockModif(uint(size=16) rx, uint(size=16) ry, uint(size=2) cIdx)
	var
		uint(size=3) xMax = if(cIdx=0) then 3 else 1 end,
		uint(size=3) yMax = if(cIdx=0) then 3 else 1 end,
		uint(size=16) xIdx,
		uint(size=16) yIdx,
		uint(size=16) pixIdx[2][2],
		bool pixIsInPict,
		uint(size=3) edgeIdx,
		uint(size=3) bandShift = BIT_DEPTH-5,
		uint(size=3) bandTable[32],
		uint(size=3) bandIdx
	begin
		rx := if(cIdx=0) then rx<<2 else rx<<1 end;
		ry := if(cIdx=0) then ry<<2 else rx<<1 end;
		foreach uint(size=3) y in 0 .. yMax
		do
			yIdx := ry + y;
			foreach uint(size=3) x in 0 .. xMax
			do
				xIdx := rx + x;
				if(xIdx<picSize[0] && yIdx<picSize[1]) then
					if(saoTypeIdx[xCuFilt][yCuFilt][cIdx] = 2) then
						pixIsInPict := true;
						foreach uint k in 0 .. 1
						do
							pixIdx[k][0] := xIdx + constHPos0[saoEoClass[xCuFilt][yCuFilt][cIdx]][k];
							pixIdx[k][1] := yIdx + constVPos0[saoEoClass[xCuFilt][yCuFilt][cIdx]][k];
							if(pixIdx[k][0] > picSize[0] || pixIdx[k][1]>picSize[1]) then
								pixIsInPict := false;
							end
						end
						if(pixIsInPict) then
							edgeIdx := 2;
							foreach uint k in 0 .. 1
							do
								edgeIdx := edgeIdx + Sign3(OrigPict[cIdx][xIdx][yIdx &(4*64-1)] -
									OrigPict[cIdx][pixIdx[k][0]][pixIdx[k][1] &(4*64-1)]);
							end
							edgeIdx := edgeIdxConv[edgeIdx];
						else
							edgeIdx := 0;
						end
						FiltPict[cIdx][xIdx][yIdx] := clip_i32(OrigPict[cIdx][xIdx][yIdx &(4*64-1)] +
							saoOffset[xCuFilt][yCuFilt][cIdx][edgeIdx], 0, 255);
					else
						foreach uint i in 0 .. 31
						do
							bandTable[i] := 0;
						end
						foreach uint k in 0 .. 3
						do
							bandTable[(k+saoLeftClass[xCuFilt][yCuFilt][cIdx]) & 31] := k+1;
						end
						bandIdx := bandTable[OrigPict[cIdx][xIdx][yIdx &(4*64-1)]>>bandShift];
						FiltPict[cIdx][xIdx][yIdx] := clip_i32(OrigPict[cIdx][xIdx][yIdx &(4*64-1)] +
							saoOffset[xCuFilt][yCuFilt][cIdx][bandIdx], 0, 255);
					end
				end		
			end
		end
	end

	procedure sampAdapOffFilt()
	begin
		while( (yCuFilt<yCu) && (xCuFilt<=xCu))
		do
			foreach uint cIdx in 0 .. 2
			do
				if(saoTypeIdx[xCuFilt][yCuFilt&1][cIdx]!=0) then
					foreach uint yBlk in 0 .. 15
					do
						foreach uint xBlk in 0 .. 15
						do
							codingTreeBlockModif((xCuFilt<<4)+xBlk,
								(yCuFilt<<4)+yBlk, cIdx);
						end
					end
				end
			end
			nextLcuAddressFilt();
		end
	end

	isNewPict: action PicSize:[picWidthInPix, picHeightInPix], SaoSe:[saoSe] ==>
	do
		picSizeInCu := [picWidthInPix/64, picHeightInPix/64];
		picSize     := [picWidthInPix   , picHeightInPix];
		slcSaoCompEn[0] := (saoSe & 1) !=0;
		slcSaoCompEn[1] := (saoSe & 2) !=0;
		slcSaoCompEn[2] := slcSaoCompEn[1];
	end

	getCuPix.launch: action SampleIn:[samp] repeat 64*64 + 32*32*2 ==>
	guard
		yCu < picSizeInCu[1]
	var
		uint(size=16) offset,
		uint(size=16) xPic,
		uint(size=16) yPic
	do
		xPic := xCu<<6;
		yPic := (yCu<<6)&(4*64-1);

		foreach uint y in 0 .. 63
		do
			foreach uint x in 0 .. 63
			do
				OrigPict[0][x+xPic][(y+yPic) &(4*64-1)] := samp[x + y*64]; 
			end
		end

		xPic := xPic>>1;
		yPic := yPic>>1;
		offset := 64*64;
		foreach uint cIdx in 1 .. 2
		do
			foreach uint y in 0 .. 31
			do
				foreach uint x in 0 .. 31
				do
					OrigPict[cIdx][x+xPic][(y+yPic) &(4*64-1)] := samp[x + y*32 +offset]; 
				end
			end
			offset := offset + 32*32;
		end
	end

	getCuPix.done: action ==>
	guard
		yCu = picSizeInCu[1]
	do
		xCu := 0;
		yCu := 0;
	end

	getSaoMerge.noMerge: action SaoSe:[mergeInfo] ==>
	guard
		(slcSaoCompEn[0] || slcSaoCompEn[1]) and mergeInfo = SAO_NO_MERGE
	do
	end

	getSaoMerge.merge: action SaoSe:[mergeInfo] ==>
	guard
		(slcSaoCompEn[0] || slcSaoCompEn[1]) and mergeInfo != SAO_NO_MERGE
	var
		uint(size=16) addrBeg[2],
		uint(size=16) addrEnd[2],
		uint(size=16) xMerge =
			if(mergeInfo = SAO_MERGE_LEFT) then xCu-1 else xCu end,
		uint(size=16) yMerge =
			if(mergeInfo = SAO_MERGE_UP)  then (yCu-1)&1 else yCu&1 end
	do
		foreach uint cIdx in 0 .. 2
		do
			saoTypeIdx[xCu][yCu][cIdx] := saoTypeIdx[xMerge][yMerge&1][cIdx];
			if(saoTypeIdx[xCu][yCu][cIdx] = 0) then
				addrBeg :=
					if(cIdx=0) then
						[xCu<<6, yCu<<6]
					else
						[xCu<<5, yCu<<5]
					end;
				addrEnd :=
					if(cIdx=0) then
						[(xCu<<6)+63, (yCu<<6)+63]
					else
						[(xCu<<5)+31, (yCu<<5)+31]
					end;
		
				copyCu(addrBeg, addrEnd, cIdx);
			else
				foreach uint i in 1 .. 4
				do
					saoOffset[xCu][yCu][cIdx][i] := saoOffset[xMerge][yMerge&1][cIdx][i];
				end
				saoEoClass[xCu][yCu][cIdx] := saoEoClass[xMerge][yMerge][cIdx];
				saoLeftClass[xCu][yCu][cIdx] := saoLeftClass[xMerge][yMerge&1][cIdx];
			end
		end
		sampAdapOffFilt();
		nextLcuAddress();
	end

	getSaoMerge.skip: action ==>
	guard
		not slcSaoCompEn[0] && not slcSaoCompEn[1]
	do
		copyCu([xCu<<6, yCu<<6], [(xCu<<6)+63, (yCu<<6)+63], 0);
		copyCu([xCu<<5, yCu<<5], [(xCu<<5)+31, (yCu<<5)+31], 1);
		copyCu([xCu<<5, yCu<<5], [(xCu<<5)+31, (yCu<<5)+31], 2);
		nextLcuAddress();
	end

	getSaoTypeIdx.is0: action SaoSe:[typeIdx] ==>
	guard
		cIdx<3 && slcSaoCompEn[cIdx] && typeIdx = 0
	var
		uint(size=16) addrBeg[2],
		uint(size=16) addrEnd[2]
	do
		addrBeg :=
			if(cIdx=0) then
				[xCu<<6, yCu<<6]
			else
				[xCu<<5, yCu<<5]
			end;
		addrEnd :=
			if(cIdx=0) then
				[(xCu<<6)+63, (yCu<<6)+63]
			else
				[(xCu<<5)+31, (yCu<<5)+31]
			end;

		copyCu(addrBeg, addrEnd, cIdx);
		saoTypeIdx[xCu][yCu&1][cIdx] := typeIdx;
		cIdx := cIdx + 1;
	end

	getSaoTypeIdx.isNot0: action SaoSe:[saoSe] repeat 6 ==>
	guard
		cIdx<3 && slcSaoCompEn[cIdx] && saoSe[0] != 0
	do
		saoTypeIdx[xCu][yCu&1][cIdx] := saoSe[0];
		foreach uint i in 1 .. 4
		do
			saoOffset[xCu][yCu&1][cIdx][i] := saoSe[i];
		end
		if(saoTypeIdx[xCu][yCu&1][cIdx]=2) then
			saoEoClass[xCu][yCu&1][cIdx] := saoSe[5];
		else
			saoLeftClass[xCu][yCu&1][cIdx] := saoSe[5];
		end

		cIdx := cIdx + 1;
	end

	getSaoTypeIdx.skip: action ==>
	guard
		cIdx<3 && not slcSaoCompEn[cIdx]
	var
		uint(size=16) addrBeg[2],
		uint(size=16) addrEnd[2]
	do
		addrBeg :=
			if(cIdx=0) then
				[xCu<<6, yCu<<6]
			else
				[xCu<<5, yCu<<5]
			end;
		addrEnd :=
			if(cIdx=0) then
				[(xCu<<6)+63, (yCu<<6)+63]
			else
				[(xCu<<5)+31, (yCu<<5)+31]
			end;

		copyCu(addrBeg, addrEnd, cIdx);
		cIdx := cIdx + 1;
	end

	getSaoTypeIdxDone: action ==>
	guard
		cIdx = 3
	do
		cIdx := 0;
		sampAdapOffFilt();
		nextLcuAddress();
	end

	

	sendSamples.launch: action ==> FiltSample:[filtSamp] repeat 64*64 + 2* 32*32
	guard
		yCu < picSizeInCu[1]
	var
		uint tokenOffset,
		uint(size=8) filtSamp[64*64+2*32*32],
		uint(size=16) blkAddrX, uint(size=16) blkAddrY
	do
		blkAddrX := xCu*64;
		blkAddrY := yCu*64;
		foreach uint y in 0 .. 63
		do
			foreach uint x in 0 .. 63
			do
				if(not DEBUG) then
					filtSamp[x+y*64] := FiltPict[0][blkAddrX+x][blkAddrY+y];
				else
					if(blkAddrX+x<picSize[0] && blkAddrY+y<picSize[1]) then
						filtSamp[x+y*64] := FiltPict[0][blkAddrX+x][blkAddrY+y];
					else
						filtSamp[x+y*64] := 0;
					end
				end
			end
		end

		blkAddrX := blkAddrX>>1;
		blkAddrY := blkAddrY>>1;
		tokenOffset:= 64*64;
		foreach uint comp in 1 .. 2
		do
			foreach uint y in 0 .. 31
			do
				foreach uint x in 0 .. 31
				do
					if(not DEBUG) then
						filtSamp[x+y*32 + tokenOffset] := FiltPict[comp][blkAddrX+x][blkAddrY+y];
					else
						if(blkAddrX+x<picSize[0]/2 && blkAddrY+y<picSize[1]/2) then
							filtSamp[x+y*32 + tokenOffset] := FiltPict[comp][blkAddrX+x][blkAddrY+y];
						else
							filtSamp[x+y*32 + tokenOffset] := 0;
						end
					end
				end
			end
			tokenOffset:= tokenOffset + 32*32;
		end

		nextLcuAddress();
	end

	sendSamples.done: action ==>
	guard
		yCu = picSizeInCu[1]
	do
		if(DEBUG) then
			println("slice num "+numSlice);
			numSlice := numSlice + 1;
		end
		xCuFilt := 0;
		yCuFilt := 0;
	end

	schedule fsm IsNewPict:
		IsNewPict (isNewPict )--> GetCuPix;

		GetCuPix  (getCuPix.launch )--> GetSaoMerge;
		GetCuPix  (getCuPix.done   )--> SendSamples;

		GetSaoMerge (getSaoMerge.skip    )--> GetCuPix;
		GetSaoMerge (getSaoMerge.merge   )--> GetCuPix;
		GetSaoMerge (getSaoMerge.noMerge )--> GetSaoTypeIdx;

		GetSaoTypeIdx(getSaoTypeIdx     )--> GetSaoTypeIdx;
		GetSaoTypeIdx(getSaoTypeIdxDone )--> GetCuPix;

		SendSamples (sendSamples.launch )--> SendSamples;
		SendSamples (sendSamples.done   )--> IsNewPict;
	end
end