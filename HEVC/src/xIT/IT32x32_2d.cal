// Authors: Eduardo Juarez, Wei Jianguo and Ren Rong <eduardo.juarez@upm.es> of UPM (Spain) 
// modified by: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>

package xIT;

import devel.org.sc29.wg11.mpegh.part2.xIT.CommonConstant.*;

actor IT32x32_2d (int shift) int(size=16) Src // 16 bits
                    ==> 
                    int(size=16) Dst // 16 bits
                    :

	int(size=16) x[32];
	int(size=16) y[32];
	int(size=16) dst[32];

	procedure it32x32_1d(int(size=16) input[32], int(size=16) output[32])
	var
		int(size=25) evenEvenEvenEven[2],
		int(size=25) evenEvenEvenOdd[2],
		int(size=26) evenEvenEven[4],
		int(size=26) evenEvenOdd[4],
		int(size=27) evenEven[8],
		int(size=27) evenOdd[8],
		int(size=28) even[16],
		int(size=28) odd[16],
   		int rounding := 1 << (shift - 1)
	begin
		evenEvenEvenEven := [( input[0] * g_aiT32[0][0] ) + ( input[16] * g_aiT32[16][0] ),
                         ( input[0] * g_aiT32[0][1] ) + ( input[16] * g_aiT32[16][1] )
                        ];

		evenEvenEvenOdd  := [( input[8] * g_aiT32[8][0] ) + ( input[24] * g_aiT32[24][0] ),
                         ( input[8] * g_aiT32[8][1] ) + ( input[24] * g_aiT32[24][1] )
                        ];

		evenEvenEven := [ evenEvenEvenEven[0] + evenEvenEvenOdd[0],
                      evenEvenEvenEven[1] + evenEvenEvenOdd[1],                                  
                      evenEvenEvenEven[1] - evenEvenEvenOdd[1],
                      evenEvenEvenEven[0] - evenEvenEvenOdd[0]
                    ];

		evenEvenOdd  := [ ( input[4] * g_aiT32[4][i] ) + ( input[12] * g_aiT32[12][i] ) + ( input[20] * g_aiT32[20][i] ) + ( input[28] * g_aiT32[28][i] )
    	              : for int i in 0 .. 3
                    ];

		evenEven := [ evenEvenEven[0] + evenEvenOdd[0],
                  evenEvenEven[1] + evenEvenOdd[1],
                  evenEvenEven[2] + evenEvenOdd[2],
                  evenEvenEven[3] + evenEvenOdd[3],
                  evenEvenEven[3] - evenEvenOdd[3],
                  evenEvenEven[2] - evenEvenOdd[2],                                    
                  evenEvenEven[1] - evenEvenOdd[1],
                  evenEvenEven[0] - evenEvenOdd[0]
                ];

		evenOdd  := [( input[2]  * g_aiT32[2] [i] ) + ( input[6]  * g_aiT32[6] [i] ) + ( input[10] * g_aiT32[10][i] ) + ( input[14] * g_aiT32[14][i] ) +
    	         ( input[18] * g_aiT32[18][i] ) + ( input[22] * g_aiT32[22][i] ) + ( input[26] * g_aiT32[26][i] ) + ( input[30] * g_aiT32[30][i] )
    	         : for int i in 0 .. 7
    	        ];

		even := [ evenEven[0] + evenOdd[0],
    	      evenEven[1] + evenOdd[1],
    	      evenEven[2] + evenOdd[2],
    	      evenEven[3] + evenOdd[3],
    	      evenEven[4] + evenOdd[4],
    	      evenEven[5] + evenOdd[5],
    	      evenEven[6] + evenOdd[6],
    	      evenEven[7] + evenOdd[7],
    	      evenEven[7] - evenOdd[7],
    	      evenEven[6] - evenOdd[6],
    	      evenEven[5] - evenOdd[5],
    	      evenEven[4] - evenOdd[4],    	      
    	      evenEven[3] - evenOdd[3],
    	      evenEven[2] - evenOdd[2],
    	      evenEven[1] - evenOdd[1],
    	      evenEven[0] - evenOdd[0]
            ];

		odd  := [( input[1]  * g_aiT32[1] [i] ) + ( input[3]  * g_aiT32[3] [i] ) + ( input[5]  * g_aiT32[5] [i] ) + ( input[7]  * g_aiT32[7] [i] ) +
    	     ( input[9]  * g_aiT32[9] [i] ) + ( input[11] * g_aiT32[11][i] ) + ( input[13] * g_aiT32[13][i] ) + ( input[15] * g_aiT32[15][i] ) +
    	     ( input[17] * g_aiT32[17][i] ) + ( input[19] * g_aiT32[19][i] ) + ( input[21] * g_aiT32[21][i] ) + ( input[23] * g_aiT32[23][i] ) +
    	     ( input[25] * g_aiT32[25][i] ) + ( input[27] * g_aiT32[27][i] ) + ( input[29] * g_aiT32[29][i] ) + ( input[31] * g_aiT32[31][i] )
    	     : for int i in 0 .. 15
    	    ];

		output := [ (even[0]  + odd[0] + rounding) >> shift ,
             (even[1]  + odd[1] + rounding) >> shift ,
             (even[2]  + odd[2] + rounding) >> shift ,
             (even[3]  + odd[3] + rounding) >> shift ,
             (even[4]  + odd[4] + rounding) >> shift ,
             (even[5]  + odd[5] + rounding) >> shift ,
             (even[6]  + odd[6] + rounding) >> shift ,
             (even[7]  + odd[7] + rounding) >> shift ,
             (even[8]  + odd[8] + rounding) >> shift ,
             (even[9]  + odd[9] + rounding) >> shift ,
             (even[10] + odd[10] + rounding) >> shift,
             (even[11] + odd[11] + rounding) >> shift,
             (even[12] + odd[12] + rounding) >> shift,
             (even[13] + odd[13] + rounding) >> shift,
             (even[14] + odd[14] + rounding) >> shift,
             (even[15] + odd[15] + rounding) >> shift, 
             (even[15] - odd[15] + rounding) >> shift,
             (even[14] - odd[14] + rounding) >> shift,
             (even[13] - odd[13] + rounding) >> shift,
             (even[12] - odd[12] + rounding) >> shift,
             (even[11] - odd[11] + rounding) >> shift,
             (even[10] - odd[10] + rounding) >> shift,
             (even[9]  - odd[9] + rounding) >> shift,
             (even[8]  - odd[8] + rounding) >> shift,
             (even[7]  - odd[7] + rounding) >> shift,
             (even[6]  - odd[6] + rounding) >> shift,
             (even[5]  - odd[5] + rounding) >> shift,
             (even[4]  - odd[4] + rounding) >> shift,
             (even[3]  - odd[3] + rounding) >> shift,
             (even[2]  - odd[2] + rounding) >> shift,
             (even[1]  - odd[1] + rounding) >> shift,
             (even[0]  - odd[0] + rounding) >> shift
            ];
	end
              
 	action
 		Src:[ src ] repeat 32
 			==>
		Dst:[dst] repeat 32
	do
		x := [src[i]: for int i in 0 .. 31];
		it32x32_1d(x, y);
		dst := [y[i]: for int i in 0 .. 31];
	end

end
