// Authors: Eduardo Juarez, Wei Jianguo and Ren Rong <eduardo.juarez@upm.es> of UPM (Spain) 

package xIT;

actor Transpose32x32 () int(size=30) Src  // 30 bit
                        ==> 
                        int(size=16) Dst  // 16 bit 
                        :
                      
  bool                               DEBUG_L1              := false;
  bool                               DEBUG_L2              := false; 
  int(size=32)                       DEBUG_nbTransformUnit := 0;
  List(type:int(size=16), size=1024) DEBUG_dst;
                     
                      
  action Src:[ src ] repeat 1024  
         ==> 
         Dst: [ [ src[ 32 * column + row ] : for int row in 0 .. 31, for int column in 0 .. 31 ] ] repeat 1024
         // 16-bit tokens
  do  
	if (DEBUG_L1) then
      println("Transpose32x32 ACTION");
      println("---------------------");
      println("TU      = " + DEBUG_nbTransformUnit);
      println("---------------------");      
      if (DEBUG_L2) then
        foreach int i in 0 .. 1023 do
          println("src    = " + src[i]);
        end                  			
        println("---------------------"); 
        DEBUG_dst := [ src[ 32 * column + row ] : for int row in 0 .. 31, for int column in 0 .. 31 ];
        foreach int i in 0 .. 1023 do
          println("dst    = " + DEBUG_dst[i]);
        end                  			
        println("---------------------");                
      end   
      DEBUG_nbTransformUnit := DEBUG_nbTransformUnit + 1;      	  
	end             
  end
  
end