// Authors: Eduardo Juarez, Wei Jianguo and Ren Rong <eduardo.juarez@upm.es> of UPM (Spain) 

package xIT;  

actor Block_Merge() int(size=6)   Size,                                          
                   int(size=16)  Block_in
                   ==>                                    
                   int(size=16)  Block  
                   :
	 int(size=16) CoeffY[1024];
	 int(size=16) CoeffU[256];
	 int(size=16) CoeffV[256];
	uint(size= 8) block4x4Idx;
	uint(size= 8) nbBlock4x4;
	uint(size= 8) scan16x16_UV_offset[4] = [0,  8, 16<< 8, (16<< 8) +  8]; 
	uint(size= 8) scan8x8_UV_offset[4]   = [0,  4, 16<< 4, (16<< 4) +  4]; 
	uint(size= 8) scan4x4_UV_offset[4]   = [0,  2, 16<< 2, (16<< 2) +  2]; 
	uint(size= 8) scan16x16_Y_offset[4]  = [0, 16, 32<<16, (32<<16) + 16]; 
	uint(size= 8) scan8x8_Y_offset[4]    = [0,  8, 32<< 8, (32<< 8) +  8]; 
	uint(size= 8) scan4x4_Y_offset[4]    = [0,  4, 32<< 4, (32<< 4) +  4]; 
	/*************************************************************************
	 * Y block 4x4
	 ************************************************************************/
	merge_Y_loop.block4x4: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	         			 		Block_in: [ coeff ] repeat 4*4
	                 	 		==>
	guard
 		sizeOfTU = 4 and block4x4Idx != 3
 	var
 		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffY[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := (32 << i) + scan4x4_Y_offset[block4x4Idx];			
		end
		block4x4Idx := block4x4Idx + 1; 
	end
	merge_Y.block4x4: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	         			 		Block_in: [ coeff ] repeat 4*4
	                 	 		==>
	guard
		sizeOfTU = 4 and block4x4Idx = 3
 	var
 		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffY[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := (32 << i) + scan4x4_Y_offset[block4x4Idx];			
		end
		block4x4Idx := 0;
		nbBlock4x4  := 4;	
	end
	/*************************************************************************
	 * Y block 8x8
	 ************************************************************************/
	merge_Y.block8x8: action 	Size:     [ sizeOfTU, RCskip, isLuma ], 
	                   			Block_in: [ coeff ] repeat 8*8 
	                  			==>
	guard
		sizeOfTU = 8
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffY[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 32 << i;			
		end
		nbBlock4x4 := 4;
	end
	/*************************************************************************
	 * Y block 16x16
	 ************************************************************************/
	merge_Y.block16x16: action	Size: 	  [ sizeOfTU, RCskip,isLuma ], 
	                    		Block_in: [ coeff ] repeat 16*16 
	                    		==>
	guard
		sizeOfTU = 16 
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffY[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 32 << i;			
		end
		nbBlock4x4 := 16;
	end	
	/*************************************************************************
	 * Y block 32x32
	 ************************************************************************/	
	merge_Y.block32x32: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                    		Block_in: [ coeff ] repeat 32*32 
	                    		==>
	guard
		sizeOfTU = 32 
	do
		foreach int i in 0 .. 1023 do
			CoeffY[i] := coeff[i];
		end
		nbBlock4x4 := 64;
	end	
	/*************************************************************************
	 * U block 4x4
	 ************************************************************************/
	merge_U.block4x4: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 4*4
	                 	 		==>
	guard
		sizeOfTU = 4 
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffU[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 16 << i;			
		end
	end
	/*************************************************************************
	 * V block 4x4
	 ************************************************************************/	
	merge_V.block4x4: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 4*4
	                 	 		==>
	guard
		sizeOfTU = 4 
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffV[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 16 << i;			
		end
	end
	/*************************************************************************
	 * U block 8x8
	 ************************************************************************/
	merge_U.block8x8: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 8*8
	                 	 		==>
	guard
		sizeOfTU = 8 
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffU[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 16 << i;			
		end
	end
	/*************************************************************************
	 * V block 8x8
	 ************************************************************************/	
	merge_V.block8x8: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 8*8
	                 	 		==>
	guard
		sizeOfTU = 8
	var
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. sizeOfTU-1 do
			foreach int j in 0 .. sizeOfTU-1 do
				CoeffV[offset + j] := coeff[i*sizeOfTU + j];
			end
			offset := 16 << i;			
		end
	end	
	/*************************************************************************
	 * U block 16x16
	 ************************************************************************/
	merge_U.block16x16: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 16*16
	                 	 		==>
	guard
		sizeOfTU = 16 
	do
		foreach int i in 0 .. 255 do
			CoeffU[i] := coeff[i];
		end
	end
	/*************************************************************************
	 * V block 16x16
	 ************************************************************************/	
	merge_V.block16x16: action 	Size:	  [ sizeOfTU, RCskip, isLuma ], 
	                 	 		Block_in: [ coeff ] repeat 16*16
	                 	 		==>
	guard
		sizeOfTU = 16
	do
		foreach int i in 0 .. 255 do
			CoeffV[i] := coeff[i];
		end
	end	
	/*************************************************************************
	 * output_Y
	 ************************************************************************/	
	output_Y : action ==> Block:[ coeff ] repeat 16
	guard
		block4x4Idx != nbBlock4x4
    var
    	 int(size=16) coeff[16],
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. 3 do
			foreach int j in 0 .. 3 do
				coeff[(i<<2) + j] := CoeffY[offset + j];
			end
			offset := (32 << i) + scan4x4_Y_offset[block4x4Idx & 3] + scan8x8_Y_offset[(block4x4Idx>>2) & 3] + scan16x16_Y_offset[(block4x4Idx>>4) & 3];			
		end
	end	
	/*************************************************************************
	 * output_done
	 ************************************************************************/	
	output_done : action ==>
	guard
		block4x4Idx = nbBlock4x4 
	do
		block4x4Idx := 0;
	end
	/*************************************************************************
	 * output_U
	 ************************************************************************/	
	output_U : action ==> Block: [ coeff ] repeat 4
    var
    	 int(size=16) coeff[4],
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. 1 do
			foreach int j in 0 .. 1 do
				coeff[(i<<1) + j] := CoeffU[offset + j];
			end
			offset := (16 << i) + scan4x4_UV_offset[block4x4Idx & 3] + scan8x8_UV_offset[(block4x4Idx>>2) & 3] + scan16x16_UV_offset[(block4x4Idx>>4) & 3];			
		end
	end	
	/*************************************************************************
	 * output_V
	 ************************************************************************/	
	output_V : action ==> Block: [ coeff ] repeat 4
    var
    	 int(size=16) coeff[4],
		uint(size= 8) offset := 0
	do
		foreach int i in 0 .. 1 do
			foreach int j in 0 .. 1 do
				coeff[(i<<1) + j] := CoeffV[offset + j];
			end
			offset := (16 << i) + scan4x4_UV_offset[block4x4Idx & 3] + scan8x8_UV_offset[(block4x4Idx>>2) & 3] + scan16x16_UV_offset[(block4x4Idx>>4) & 3];			
		end
		block4x4Idx := block4x4Idx + 1; 		
	end	
	/*************************************************************************
	 * schedule
	 ************************************************************************/	
	schedule fsm merge_Y:
		merge_Y   ( merge_Y_loop  )--> merge_Y;
		merge_Y   ( merge_Y       )--> merge_U;
		merge_U   ( merge_U       )--> merge_V;
		merge_V   ( merge_V       )--> output_Y;
		output_Y  ( output_Y      )--> output_U;
		output_Y  ( output_done   )--> merge_Y;
		output_U  ( output_U      )--> output_U;
		output_V  ( output_V      )--> merge_Y;
	end	
end
