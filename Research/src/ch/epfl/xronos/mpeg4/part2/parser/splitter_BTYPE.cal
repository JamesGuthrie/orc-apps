// splitter_BTYPE
//
// Author: Endri Bezati
// 
// Description: the sequence of BTYPE token is not homogeneous, there is fist a NEW VOP command, then, the width and height of this VOP, then the  corresponding 8x8 blocks of texture, motion or mixed tex/mot. This actor will delete the tokens concerning the information of the VOP and redistribute the tokens to the Y, U and V components. So that the adder have directly the token intra, accoded or mixed.

package ch.epfl.xronos.mpeg4.part2.parser;

import org.sc29.wg11.mpeg4.part2.Constants.*;
import org.ietr.mpeg4.part2.sp.ConstantsBtype.*;

actor splitter_BTYPE () int(size=BTYPE_SZ) BTYPE ==> int(size=BTYPE_SZ) Y ,
int(size=BTYPE_SZ) U , int(size=BTYPE_SZ) V :

	cmd.newVop: action BTYPE:[ cmd ] ==> Y:[ cmd ], U:[ cmd ], V:[ cmd ]
	guard
		(cmd & NEWVOP) != 0
	end
	
	skip: action BTYPE:[ cmd ] ==> Y:[ cmd ], U:[ cmd ], V:[ cmd ]
	end
	
	cmd.splitY: action BTYPE:[ cmd ]  ==> Y:[ cmd ]
	end

	cmd.splitU: action BTYPE:[ cmd ] ==> U:[ cmd ]
	end
	
	cmd.splitV: action BTYPE:[ cmd ] ==> V:[ cmd ]
	end
	
	schedule fsm cmd :
		cmd ( cmd.newVop ) --> skip;
		cmd ( cmd.splitY ) --> cmdY1;
		cmdY1 ( cmd.splitY ) --> cmdY2;
		cmdY2 ( cmd.splitY ) --> cmdY3;
		cmdY3 ( cmd.splitY ) --> cmdC0;
		cmdC0 ( cmd.splitU ) --> cmdC1;
		cmdC1 ( cmd.splitV ) --> cmd;

		skip ( skip ) --> skip1;
		skip1 ( skip ) --> cmd;
	end
 
	priority
		cmd.newVop > cmd.splitY;
	end

end

