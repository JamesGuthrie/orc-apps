package ch.epfl.mpeg4.part2;

actor BlockExpand () int(size=8) RUN, int(size=13) VALUE,
bool LAST ==> int(size=13) QFS :

	int(size=8) count := 0;

	int(size=13) qfs[64];
	
	read_write: action RUN:[ run ], VALUE:[ value ], LAST:[ last ]  ==> QFS:[ qfs ] repeat 64
	guard last
	do
		qfs[count + run] := value;
	end

	read: action RUN:[ run ], VALUE:[ value ], LAST:[ last ] ==>
	do
		qfs[count + run] := value;
		count := count + run + 1;
	end
	
	reset: action ==>
	do
		qfs := [0 : for int i in 0 .. 63];
		count := 0;
	end
		
	schedule fsm s0 :
	s0 (read)       --> s0;
	s0 (read_write) --> s1;
	s1 (reset)      --> s0;
	end
	
	priority read_write > read; end

end