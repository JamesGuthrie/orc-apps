package ch.epfl.mpeg4.part2.texture;

import org.mpeg4.part2.Constants.*;

actor InverseACPred () 
	int(size=SAMPLE_SZ) PQF,
	int POS,
	int(size=2) AC_PRED_DIR,
	bool SKIPPED
	==>
	int(size=SAMPLE_SZ) QF:

int MAXW_IN_MB = 121;

int(size=SAMPLE_SZ) ac_buf[2][MAXW_IN_MB][6][14];

luma: action AC_PRED_DIR:[ s ], POS:[ mby, mbx, comp ], PQF:[ pqf ] repeat 64, SKIPPED:[skipped] ==> QF:[ pqf ] repeat 64
var
 	bool acpred_flag = s > 0,
 	bool top = s = 2,
 	int curr_line = mby & 1,
 	int prev_line = (mby - 1) & 1 
do
	if acpred_flag and not skipped then
		if comp = 0 then
			if top then 
				if mby != 0 then
					foreach int i in 1 .. 7 do
						pqf[i] := pqf[i] + ac_buf[prev_line][mbx][2][i - 1];
					end					
				end
			else
				if mbx != 0 then
					foreach int i in 1 .. 7 do
						pqf[8 * i] := pqf[8 * i] + ac_buf[curr_line][mbx-1][1][7 + i - 1];
					end
				end
			end
		elsif comp = 1 then
			if top then
				if mby != 0 then
					foreach int i in 1 .. 7 do
						pqf[i] := pqf[i] + ac_buf[prev_line][mbx][3][i - 1];
					end
				end
			else
				foreach int i in 1 .. 7 do
					pqf[8 * i] := pqf[8 * i] + ac_buf[curr_line][mbx][0][7 + i - 1];
				end
			end
		elsif comp = 2 then
			if top then
				foreach int i in 1 .. 7 do
					pqf[i] := pqf[i] + ac_buf[curr_line][mbx][0][i - 1];
				end
			else
				if mbx != 0 then
					foreach int i in 1 .. 7 do
						pqf[8 * i] := pqf[8 * i] + ac_buf[curr_line][mbx-1][3][7 + i - 1];
					end
				end
			end
		elsif comp = 3 then
			if top then
				foreach int i in 1 .. 7 do
					pqf[i] := pqf[i] + ac_buf[curr_line][mbx][1][i - 1];
				end
			else
				foreach int i in 1 .. 7 do
					pqf[8 * i] := pqf[8 * i] + ac_buf[curr_line][mbx][2][7 + i - 1];
				end
			end
		else // comp = 4, 5 
			if top and  mby != 0 then
				foreach int i in 1 .. 7 do
					pqf[i] := pqf[i] + ac_buf[prev_line][mbx][comp][i - 1];
				end
			elsif not top and mbx != 0 then
				foreach int i in 1 .. 7 do
					pqf[8 * i] := pqf[8 * i] + ac_buf[curr_line][mbx-1][comp][7 + i - 1];
				end
			end
		end
	end
	
	foreach int i in 1 .. 7 do
		ac_buf[curr_line][mbx][comp][i - 1] := pqf[i];
		ac_buf[curr_line][mbx][comp][7 + i - 1] := pqf[8*i];
	end
end

end
