/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
  
// Author: Ghislain Roquier <ghislain.roquier@epfl.ch>

package ch.epfl.mpeg4.part2.texture;

import std.util.Math.*;
import org.sc29.wg11.mpeg4.part2.Constants.*;
import org.ietr.mpeg4.part2.sp.ConstantsBtype.*;

actor InverseDCPred () 
	int(size=BTYPE_SZ) BTYPE, int(size=SAMPLE_SZ) QFS_in, int(size=6) QP 
	==>  
	int(size=SAMPLE_SZ) QFS, uint(size=2) AC_PRED_DIR, bool SKIPPED, int POS, int(size=6) QP_OUT, bool SIGNED :
	
	int DC_DEFAULT = 2 << 9;
	
	int(size=32) mbx := 0;
	int(size=32) mby := 0;

	bool top_edge := true;
	bool left_edge;

	int(size=4) comp := 0;

	int(size=8) mbwidth := 0;

	int(size=6) qp;

	int(size=13) dc;
	
	int(size=13) dc_pred;

	int(size=13) a;

	int(size=13) b;

	int(size=13) c;

	bool a_skipped;
	
	bool c_skipped;

	bool is_intra;
		
	bool ac_pred;

	bool coded[2][MAXW_IN_MB][6];

	int(size=13) dc_samples[2][MAXW_IN_MB][6];

	// New VOP
	start: action BTYPE:[ cmd ] ==>
	guard
		(cmd & NEWVOP) != 0
	do
		mbx := 0;
		mby := 0;
		top_edge := true;
		left_edge := true;
		comp := 0;
	end

	getwh: action BTYPE:[ w, h ] ==>
	do
		mbwidth := w;
	end
	
	read.intra: action BTYPE:[ cmd ], QP:[ qpval ] ==>
	do
		qp := qpval;
		is_intra := true;
		coded[mby & 1][mbx][comp] := true;
		ac_pred := (cmd & ACPRED) != 0;
	end

	read.inter_ac: action BTYPE:[ cmd ], QP:[ qpval ] ==>
	guard
		(cmd & INTER) != 0,
		(cmd & ACCODED) != 0
	do
		qp := qpval;
		is_intra := false;
		coded[mby & 1][mbx][comp] := false;
	end
	
	read.other: action BTYPE:[ cmd ], QP:[ qpval ] ==>
	guard
		(cmd & INTRA) = 0
	do
		qp := qpval;
		is_intra := false;
		coded[mby & 1][mbx][comp] := false;
	end
	
	predict.b0: action ==>
	guard
		comp = 0
	do
		a := DC_DEFAULT;
		b := DC_DEFAULT;
		c := DC_DEFAULT;
		if not left_edge then
			a := dc_samples[mby&1][mbx-1][1];
			a_skipped := not coded[mby & 1][mbx-1][1];
			if not coded[mby & 1][mbx-1][1] then
				a := DC_DEFAULT;
			end			
			if not top_edge then
				b := dc_samples[(mby-1)&1][mbx-1][3];
				if not coded[(mby-1)&1][mbx-1][3] then
					b := DC_DEFAULT;
				end
			end
		end

		if not top_edge then
			c := dc_samples[(mby-1)&1][mbx][2];
			c_skipped := not coded[(mby-1)&1][mbx][2];
			
			if not coded[(mby-1) & 1][mbx][2] then
				c := DC_DEFAULT;
			end
		end
	end

	predict.b1: action ==>
	guard
		comp = 1
	do
		a := dc_samples[mby&1][mbx][0];
		a_skipped := not coded[mby&1][mbx][0];
		b := DC_DEFAULT;
		c := DC_DEFAULT;
		if not coded[mby & 1][mbx][0] then
			a := DC_DEFAULT;
		end
		
		if not top_edge then
			b := dc_samples[(mby-1)&1][mbx][2];
			if not coded[(mby-1) & 1][mbx][2] then
				b := DC_DEFAULT;
			end
		end

		if not top_edge then
			c := dc_samples[(mby-1)&1][mbx][3];
			c_skipped := not coded[(mby-1) & 1][mbx][3];
			if not coded[(mby-1) & 1][mbx][3] then
				c := DC_DEFAULT;
			end
		end
	end

	predict.b2: action ==>
	guard
		comp = 2
	do
		a := DC_DEFAULT;
		b := DC_DEFAULT;
		if not left_edge then
			a := dc_samples[mby&1][mbx-1][3];
			a_skipped := not coded[mby&1][mbx-1][3];
			if not coded[mby&1][mbx-1][3] then
				a := DC_DEFAULT;
			end
			
			b := dc_samples[mby&1][mbx-1][1];
			if not coded[mby&1][mbx-1][1] then
				b := DC_DEFAULT;
			end
		end
		c := dc_samples[mby&1][mbx][0];
		c_skipped := not coded[mby&1][mbx][0];
		
		if not coded[mby&1][mbx][0] then
			c := DC_DEFAULT;
		end
	end

	predict.b3: action ==>
	guard
		comp = 3
	do
		a := dc_samples[mby&1][mbx][2];
		a_skipped := not coded[mby&1][mbx][2];
		if not coded[mby&1][mbx][2] then
			a := DC_DEFAULT;
		end
		
		b := dc_samples[mby&1][mbx][0];
		if not coded[mby&1][mbx][0] then
			b := DC_DEFAULT;
		end
		
		c := dc_samples[mby&1][mbx][1];
		c_skipped := not coded[mby&1][mbx][1];

		if not coded[mby&1][mbx][1] then
			c := DC_DEFAULT;
		end
	end

	// Locate prediction indices for blocks 4, 5
	predict.b45: action ==>
	do
		a := DC_DEFAULT;
		b := DC_DEFAULT;
		c := DC_DEFAULT;
		if not left_edge then
			a := dc_samples[mby&1][mbx-1][comp];
			a_skipped := not coded[mby&1][mbx-1][comp];
			if not coded[mby&1][mbx-1][comp] then
				a := DC_DEFAULT;
			end			

			if not top_edge then
				b := dc_samples[(mby-1)&1][mbx-1][comp];
				if not coded[(mby-1)&1][mbx-1][comp] then
					b := DC_DEFAULT;
				end
			end
		end

		if not top_edge then
			c := dc_samples[(mby-1)&1][mbx][comp];
			c_skipped := not coded[(mby-1)&1][mbx][comp];
			if not coded[(mby-1)&1][mbx][comp] then
				c := DC_DEFAULT;
			end	
		end
	end

	function dc_scaler() --> int(size=7) :
		if (comp & 4) = 0 then
		// Y component
			if qp > 0 and qp < 5 then
				8
			else
				if qp > 4 and qp < 9 then
					2 * qp
				else
					if qp > 8 and qp < 25 then
						qp + 8
					else
						(2 * qp) - 16
					end
				end
			end
		else
		// UV component
			if qp > 0 and qp < 5 then
				8
			else
				if qp > 4 and qp < 25 then
					(qp + 13) >> 1
				else
					qp - 6
				end
			end
		end
	end

	getdc.intra: action QFS_in:[qfs] repeat 64 ==> QFS:[qfs_out] repeat 64, AC_PRED_DIR:[ac_pred_dir], SKIPPED:[skipped], POS:[mby, mbx, comp], SIGNED:[ false ], QP_OUT:[ qp ]
	guard 
		is_intra
	var
		int(size=7) scaler,
		int(size=13) vert = abs(a - b),
		int(size=13) horiz = abs(c - b),
		bool c_dir = vert < horiz,
		bool skipped,
		int(size=13) ac_pred_dir,
		int(size=SAMPLE_SZ) qfs_out[64]
	do
		dc_pred := if c_dir then c else a end;
		ac_pred_dir := if not ac_pred then 0 else if c_dir then 2 else 1 end end;
		skipped := if c_dir then c_skipped else a_skipped end;
		
		scaler := dc_scaler();
		dc_pred := (dc_pred + (scaler >> 1)) / scaler;
		dc := (qfs[0] + dc_pred) * scaler;
		dc := clip_i32(dc, -2048, 2047);
		dc_samples[mby&1][mbx][comp] := dc;
		qfs_out[0] := dc;
		foreach int i in 1 .. 63 do
			qfs_out[i] := qfs[i];
		end
	end
	
	
	getdc.inter: action QFS_in:[qfs] repeat 64 ==> QFS:[qfs_out] repeat 64, AC_PRED_DIR:[0], SKIPPED:[true], POS:[mby, mbx, comp], SIGNED:[ true ], QP_OUT:[ qp ]
	var	
	int(size=13) dc, int(size=13) v,
	int(size=SAMPLE_SZ) qfs_out[64]
	do
		v := (qp * ((abs(qfs[0]) << 1) + 1)) - ((qp & 1) ^ 1);		
		dc := if qfs[0] = 0 then 0 else if qfs[0] < 0 then -v else v end end;
		dc := clip_i32(dc, -2048, 2047);
		dc_samples[mby&1][mbx][comp] := dc;
		qfs_out[0] := dc;
		foreach int i in 1 .. 63 do
			qfs_out[i] := qfs[i];
		end
		
	end
		
	advance: action ==>
	do
		comp := comp + 1;
		if comp = 6 then
			comp := 0;
			mbx := mbx + 1;
			left_edge := false;
			if mbx = mbwidth then
				mbx := 0;
				mby := mby + 1;
				top_edge := false;
				left_edge := true;
			end
		end
	end
	
	schedule fsm read :
		read ( start ) --> getwh;
		read ( read.intra ) --> predict;
		read ( read.inter_ac ) --> predict;
		read ( read.other ) --> advance;
		getwh(getwh) --> read;
		predict ( predict ) --> get_qfs;
		get_qfs ( getdc ) --> advance;
		advance ( advance ) --> read;
	end

	priority
		start > read;
		read.inter_ac > read.other > read.intra;
		predict.b0 > predict.b45;
		predict.b1 > predict.b45;
		predict.b2 > predict.b45;
		predict.b3 > predict.b45;
		getdc.intra > getdc.inter;
	end
end
