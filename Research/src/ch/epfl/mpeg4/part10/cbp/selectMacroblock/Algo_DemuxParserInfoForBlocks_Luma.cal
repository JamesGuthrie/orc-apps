/********************************************************************************

This software module was originally developed by Damien de Saint Jorre IETR/INSA of Rennes (France), in the course of development of the ISO/IEC 23002-4 for reference purposes and its performance may not have been optimized. This software module is an implementation of one or more tools as specified by the MPEG-C part 4.
ISO/IEC gives You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works of this software module or modifications thereof for use in implementations of the ISO/IEC 23002-4  in products that satisfy conformance criteria (if any).
Those intending to use this software module in products are advised that its use may infringe existing patents. ISO/IEC have no liability for use of this software module or modifications thereof.
Copyright is not released for products that do not conform to audiovisual and image-coding related ITU Recommendations and/or ISO/IEC International Standards.
Assurance that the originally developed software module can be used (1) in the ISO/IEC 23002-4 once the ISO/IEC 23002-4 has been adopted; and (2) to develop the MPEG-C part 4:

IETR/INSA of Rennes (France) grants ISO/IEC all rights necessary to include the originally developed software module or modifications thereof in the ISO/IEC 23002-4 and to permit ISO/IEC to offer You a royalty-free, worldwide, non-exclusive, copyright license to copy, distribute, and make derivative works for use in implementations of the MPEG-C part 4 in products that satisfy conformance criteria (if any), and to the extent that such originally developed software module or portions of it are included in the MPEG-C part 4.  To the extent that IETR/INSA of Rennes (France) owns patent rights that would be required to make, use, or sell the originally developed software module or portions thereof included in the MPEG-C part 4 in a conforming product,  IETR/INSA of Rennes (France) will assure the ISO/IEC that it is willing to negotiate licenses under reasonable and non-discriminatory terms and conditions with applicants throughout the world.

ISO/IEC gives You a free license to this software module or modifications thereof for the sole  purpose of developing the MPEG-C part 4

IETR/INSA of Rennes (France) retains full right to modify and use the code for its own purpose, assign or donate the code to a third party and to inhibit third parties from using the code for products that do not conform to MPEG-related ITU Recommendations and/or ISO/IEC International Standards.
This copyright notice must be included in all copies or derivative works. Copyright (c) ISO/IEC 2008.

*****************************************************************************/
package org.mpeg4.part10.cbp.selectMacroblock;

import org.sc29.wg11.mpeg4.part10.MacroBlockInfo.*;
import org.sc29.wg11.mpeg4.part10.Imp_PictureInfo.*;

actor Algo_DemuxParserInfoForBlocks_Luma()
	uint(size=6)  MBType,
//	bool          TransformSize8x8,
	int (size=9)  Coef_ACR,
	bool          ConstrainedIFlag,
	uint(size=4)  PredMode,
	uint(size=7)  MbLocation,
	bool          NeighbourAvail
		==>
	int (size=9)  Coef_ACR_Intra_4x4,
	uint(size=4)  PredMode_4x4,
	bool          NeighbourAvail4x4,

	int (size=9)  Coef_ACR_Intra_16x16,
	uint(size=4)  PredMode_16x16,
	bool          NeighbourAvail16x16,

	int (size=9)  Coef_ACR_Inter
		:


	List( type:bool, size = MAX_PICWIDTH_BLK) is_intra := [false : for int i in 0 .. MAX_PICWIDTH_BLK-1];
	bool          intra_top_left := false;	
	uint(size=13) curr_pos       := 0;
	bool          CurrValIsIntra := false;
/*
*
* Actions
*
*/

	MBTypeIsPCM: action
			MBType           :[ValMBType],
			ConstrainedIFlag :[c_intra_flag]
				==>
		guard
			ValMBType = BLOCK_TYPE_I_PCM
		do
			CurrValIsIntra := true;
		end


	MBTypeIsIntra4x4: action
			MBType           :[ValMBType]
				==>
		guard
			Is_Intra_NxN(ValMBType)
		do
			CurrValIsIntra := true;	
		end

	SendAcr4x4: action
			Coef_ACR           :[ValCoef] repeat 256
				==>
			Coef_ACR_Intra_4x4 :[ValCoef] repeat 256
		end

	SendPredMode4x4: action
			PredMode     :[pred_mode] repeat 16
				==>
			PredMode_4x4 :[pred_mode] repeat 16
		end

	SendNeighbourg4x4: action
			ConstrainedIFlag  :[c_intra_flag],
			MbLocation   :[MbLocxInPic , MbLocyInPic],
			NeighbourAvail :[NeighbourA_Avail , NeighbourB_Avail , NeighbourC_Avail , NeighbourD_Avail]
				==>
			NeighbourAvail4x4 :[NeighA, NeighB, NeighC, NeighD]
		var
			bool NeighA, 
			bool NeighB, 
			bool NeighC, 
			bool NeighD
		do
			curr_pos    := MbLocxInPic;
			NeighA := NeighbourA_Avail && (is_intra[(curr_pos-1) & 0x3F] or not (c_intra_flag));
			NeighB := NeighbourB_Avail && (is_intra[curr_pos & 0x3F] or not (c_intra_flag));
			NeighC := NeighbourC_Avail && (is_intra[(curr_pos+1) & 0x3F] or not (c_intra_flag)) && NeighB;
			NeighD := NeighbourD_Avail && (intra_top_left or not (c_intra_flag));
		end


	MBTypeIsIntra16x16: action
			MBType           :[ValMBType]
				==>
		guard
			ValMBType >= BLOCK_TYPE_INTRA_16x16_MIN,
			ValMBType <= BLOCK_TYPE_INTRA_16x16_MAX
		do
			CurrValIsIntra := true;
		end

	SendAcr16x16: action
			Coef_ACR             :[ValCoef] repeat 256
				==>
			Coef_ACR_Intra_16x16 :[ValCoef] repeat 256
		end

	SendPredMode16x16: action
			PredMode       :[pred_mode]
				==>
			PredMode_16x16 :[pred_mode]
		end

	SendNeighbourg16x16: action
			ConstrainedIFlag    :[c_intra_flag],
			MbLocation   :[MbLocxInPic , MbLocyInPic],
			NeighbourAvail :[NeighbourA_Avail , NeighbourB_Avail , NeighbourC_Avail , NeighbourD_Avail]
				==>
			NeighbourAvail16x16 :[NeighA, NeighB, NeighC, NeighD]
		var
			bool NeighA, 
			bool NeighB, 
			bool NeighC, 
			bool NeighD
		do
			curr_pos    := MbLocxInPic;
			NeighA := NeighbourA_Avail && (is_intra[(curr_pos-1) & 0x3F] or not (c_intra_flag));
			NeighB := NeighbourB_Avail && (is_intra[curr_pos & 0x3F] or not (c_intra_flag));
			NeighC := NeighbourC_Avail && (is_intra[(curr_pos+1) & 0x3F] or not (c_intra_flag)) && NeighB;
			NeighD := NeighbourD_Avail && (intra_top_left or not (c_intra_flag));
		end


	MBTypeIsInter: action
			MBType      :[ValMBType]
				==>
		guard
			ValMBType > BLOCK_TYPE_INTRA_MAX
		do
			CurrValIsIntra := false;
		end

	MBTypeIsInterPSkip: action
			MBType      :[ValMBType]/*,
			Coef_ACR       :[ValCoef] repeat 256*/
				==>
			Coef_ACR_Inter :[[0:  for int i in 0 .. 255]] repeat 256
		guard
			ValMBType = BLOCK_TYPE_P_SKIP or ValMBType = BLOCK_TYPE_B_SKIP
		do
			CurrValIsIntra := false;
		end

	SendAcrInter: action
			Coef_ACR       :[ValCoef] repeat 256
				==>
			Coef_ACR_Inter :[ValCoef] repeat 256
		end

	GetCnstrIAndFstMbInter: action
			ConstrainedIFlag :[c_intra_flag]
				==>
		end

	UpdateIsIntra: action
			MbLocation     :[MbLocxInPic , MbLocyInPic],
			NeighbourAvail :[NeighbourA_Avail , NeighbourB_Avail , NeighbourC_Avail , NeighbourD_Avail]
				==>
		do
			curr_pos        := MbLocxInPic;
		end	

	SendInfosDone: action ==>
		do
			intra_top_left     := is_intra[curr_pos];
			is_intra[curr_pos] := CurrValIsIntra;
		end

	schedule fsm CheckMbType:
		CheckMbType         (MBTypeIsPCM             )--> SendPCMInfos1;
		CheckMbType         (MBTypeIsIntra4x4        )--> SendIntra4x4Infos1;
		CheckMbType         (MBTypeIsIntra16x16      )--> SendIntra16x16Infos1;
		CheckMbType         (MBTypeIsInter           )--> SendInterInfos1;
		CheckMbType         (MBTypeIsInterPSkip      )--> SendInterInfos2;

		SendIntra4x4Infos1   (SendAcr4x4              )--> SendIntra4x4Infos2;
		SendIntra4x4Infos2   (SendPredMode4x4         )--> SendIntra4x4Infos3;
		SendIntra4x4Infos3   (SendNeighbourg4x4       )--> SendIntra4x4Infos4;
		SendIntra4x4Infos4   (SendInfosDone           )--> CheckMbType;

		SendIntra16x16Infos1 (SendAcr16x16            )--> SendIntra16x16Infos2;
		SendIntra16x16Infos2 (SendPredMode16x16       )--> SendIntra16x16Infos3;	
		SendIntra16x16Infos3 (SendNeighbourg16x16     )--> SendIntra16x16Infos4;
		SendIntra16x16Infos4 (SendInfosDone           )--> CheckMbType;

		SendInterInfos1      (SendAcrInter            )--> SendInterInfos2;
		SendInterInfos2      (GetCnstrIAndFstMbInter  )--> SendInterInfos3;
		SendInterInfos3      (UpdateIsIntra           )--> SendInterInfos4;
		SendInterInfos4      (SendInfosDone           )--> CheckMbType;

		SendPCMInfos1        (UpdateIsIntra           )--> SendPCMInfos2;
		SendPCMInfos2        (SendInfosDone           )--> CheckMbType;
	end

	priority
		MBTypeIsInterPSkip > MBTypeIsInter > MBTypeIsIntra16x16 > MBTypeIsIntra4x4 > MBTypeIsPCM;
	end

end