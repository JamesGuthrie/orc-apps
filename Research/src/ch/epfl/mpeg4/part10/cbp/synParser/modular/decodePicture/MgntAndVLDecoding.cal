/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>
package ch.epfl.mpeg4.part10.cbp.synParser.modular.decodePicture;

import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;
import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;
import org.mpeg4.part10.cbp.synParser.modular.cavlcLookUpTables.*;
import std.util.BitOps.*;

actor MgntAndVLDecoding()
	uint(size=8)  RbspByte,
	bool          NewNal,

	int (size=6)  Header1TypeOfVLD,
	int (size=32) Header1VLDParam,
	int (size=6)  Header2TypeOfVLD,
	int (size=32) Header2VLDParam,
	uint(size=5)  DecRefPicMarkingTypeOfVLD,
	int (size=32) DecRefPicMarkingNbBitsToRead,
	uint(size=5)  RefPicListTypeOfVLD,
	int (size=32) RefPicListNbBitsToRead,
	int (size=6)  DataTypeOfVLD,
	int (size=32) DataVLDParam,
	int (size=6)  MbLayerTypeOfVLD,
	int (size=32) MbLayerVLDParam,
	uint(size=5)  MbPredTypeOfVLD,
	int (size=32) MbPredNbBitsToRead,
	uint(size=5)  SubMbPredTypeOfVLD,
	int (size=32) SubMbPredNbBitsToRead,
	int (size=6)  ResTypeOfVLD,
	int (size=32) ResActorLaunched,
	uint(size=5)  ResBlkCavlcTypeOfVLD,
	int (size=32) ResBlkCavlcVLD_Param
		==>
	int (size=32) Header1ValVLD,
	int (size=32) Header2ValVLD,
	int (size=32) DecRefPicMarkingValVLD,
	int (size=32) RefPicListValVLD,
	int (size=32) DataValVLD,
	int (size=32) MbLayerValVLD,
	int (size=32) MbPredValVLD,
	int (size=32) SubMbPredValVLD,
	int (size=32) ResBlkCavlcValVLD
		:

	uint(size=8)  callStack[16];
	uint(size=3)  stackIdx;
	uint(size=5)  typeOfVld;
	int           valDecoded;
	bool          valDecodedFlag := false;
	int           paramSent[3];


	initialize ==>
	do
		stackIdx     := 0;
		callStack[0] := SLICE_HEADER1;
	end

	findOutNextActor.isSliceHeader1: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER1
	end

	findOutNextActor.isSliceHeader2Avc: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER2
	end

	findOutNextActor.isRefPicMark: action ==>
	guard
		callStack[stackIdx] = REF_PIC_MARKING
	end

	findOutNextActor.isRefPicList: action ==>
	guard
		callStack[stackIdx] = REF_PIC_LIST
	end

	findOutNextActor.isSliceData: action ==>
	guard
		callStack[stackIdx] = SLICE_DATA
	end

	findOutNextActor.isMbLayer: action ==>
	guard
		callStack[stackIdx] = MB_LAYER
	end

	findOutNextActor.isMbPred: action ==>
	guard
		callStack[stackIdx] = MB_PRED
	end

	findOutNextActor.isSubMbPred: action ==>
	guard
		callStack[stackIdx] = SUB_MB_PRED
	end

	findOutNextActor.isResidual: action ==>
	guard
		callStack[stackIdx] = RESIDUAL
	end

	findOutNextActor.isResidualCavlc: action ==>
	guard
		callStack[stackIdx] = RESIDUAL_BLOCK_CAVLC
	end

	SliceHeader1.GetVldVal.Launch: action ==>
		Header1ValVLD :[valDecoded]
	guard
		valDecodedFlag
	do
		valDecodedFlag := false;
	end

	SliceHeader1.FunctionWithoutParam: action Header1TypeOfVLD :[ValTypeOfVLD] ==>
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	do
		typeOfVld := ValTypeOfVLD;
	end

	SliceHeader1.FunctionWith1Param: action
		Header1TypeOfVLD :[ValTypeOfVLD],
		Header1VLDParam  :[Param]
			==>
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	do
		typeOfVld := ValTypeOfVLD;
		paramSent[0] := Param;
	end

	SliceHeader1.LaunchActor: action
		Header1TypeOfVLD :[Function],
		Header1VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx       := (stackIdx + 1) &0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceHeader1.Done: action Header1TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	end

	SliceHeader2.GetVLDVal.Launch: action /*ValVLD :[VLDecoding]*/ ==>
		Header2ValVLD :[42]
	end

	SliceHeader2.FunctionWithoutParam: action Header2TypeOfVLD :[ValTypeOfVLD] ==>
//		TypeOfVLD :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	SliceHeader2.FunctionWith1Param: action
		Header2TypeOfVLD :[ValTypeOfVLD],
		Header2VLDParam  :[Param]
			==>
//		TypeOfVLD       :[ValTypeOfVLD],
//		VLD_Param       :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	SliceHeader2.LaunchActor: action
		Header2TypeOfVLD :[Function],
		Header2VLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceHeader2.Done: action Header2TypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	RefPicMark.GetVLDVal.Launch: action
//		ValVLD                 :[VLDecoding]
			==>
		DecRefPicMarkingValVLD :[42]
	end

	RefPicMark.FunctionWithoutParam: action
		DecRefPicMarkingTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD                 :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	RefPicMark.FunctionWith1Param: action
		DecRefPicMarkingTypeOfVLD    :[ValTypeOfVLD],
		DecRefPicMarkingNbBitsToRead :[Param]
			==>
//		TypeOfVLD :[ValTypeOfVLD],
//		VLD_Param :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	RefPicMark.Done: action DecRefPicMarkingTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	RefPicList.GetVLDVal.Launch: action
//		ValVLD           :[VLDecoding]
			==>
		RefPicListValVLD :[42]
	end

	RefPicList.FunctionWithoutParam: action
		RefPicListTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD           :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	RefPicList.FunctionWith1Param: action
		RefPicListTypeOfVLD    :[ValTypeOfVLD],
		RefPicListNbBitsToRead :[Param]
			==>
//		TypeOfVLD              :[ValTypeOfVLD],
//		VLD_Param              :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	RefPicList.Done: action RefPicListTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	SliceData.GetVLDVal.Launch: action /*ValVLD :[VLDecoding]*/ ==>
		DataValVLD :[42]
	end

	SliceData.FunctionWithoutParam: action
		DataTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD     :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	SliceData.FunctionWith1Param: action
		DataTypeOfVLD :[ValTypeOfVLD],
		DataVLDParam  :[Param]
			==>
//		TypeOfVLD     :[ValTypeOfVLD],
//		VLD_Param     :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	SliceData.LaunchActor: action
		DataTypeOfVLD :[Function],
		DataVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	SliceData.Done: action DataTypeOfVLD :[Function] ==>
//		TypeOfVLD :[SKIP_NAL]
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	MbLayer.GetVLDVal.Launch: action /*ValVLD :[VLDecoding]*/ ==> MbLayerValVLD :[42]
	end

	MbLayer.FunctionWithoutParam: action
		MbLayerTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD        :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	MbLayer.FunctionWith1Param: action
		MbLayerTypeOfVLD :[ValTypeOfVLD],
		MbLayerVLDParam  :[Param]
			==>
//		TypeOfVLD        :[ValTypeOfVLD],
//		VLD_Param        :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	MbLayer.LaunchActor : action
		MbLayerTypeOfVLD :[Function],
		MbLayerVLDParam  :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	MbLayer.Done: action
		MbLayerTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	MbPred.GetVLDVal.Launch: action /*ValVLD :[VLDecoding]*/ ==>
		MbPredValVLD :[42]
	end

	MbPred.FunctionWithoutParam: action MbPredTypeOfVLD :[ValTypeOfVLD] ==>
//		TypeOfVLD       :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	MbPred.FunctionWith1Param: action
		MbPredTypeOfVLD    :[ValTypeOfVLD],
		MbPredNbBitsToRead :[VldParam]
			==>
//		TypeOfVLD          :[ValTypeOfVLD],
//		VLD_Param          :[VldParam]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	MbPred.Done: action MbPredTypeOfVLD :[Function] ==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	SubMbPred.GetVLDVal.Launch: action /*ValVLD :[VLDecoding]*/ ==>
			SubMbPredValVLD :[42]
	end

	SubMbPred.FunctionWithoutParam: action SubMbPredTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD          :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD  != SKIP_NAL
	end

	SubMbPred.FunctionWith1Param: action
		SubMbPredTypeOfVLD    :[ValTypeOfVLD],
		SubMbPredNbBitsToRead :[VldParam]
			==>
//		TypeOfVLD             :[ValTypeOfVLD],
//		VLD_Param             :[VldParam]
	guard
			NbParamForFunction(ValTypeOfVLD) = 1
	end

	SubMbPred.Done: action
		SubMbPredTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end


	Residual.LaunchActor: action
		ResTypeOfVLD     :[Function],
		ResActorLaunched :[Actor]
			==>
	guard
		Function = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := Actor;
	end

	Residual.Done: action
		ResTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end

	ResBlkCavlc.GetVLDVal.Launch: action
//		ValVLD            :[VLDecoding]
			==>
		ResBlkCavlcValVLD :[42]
	end

	ResBlkCavlc.FunctionWithoutParam: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD]
			==>
//		TypeOfVLD            :[ValTypeOfVLD]
	guard
		NbParamForFunction(ValTypeOfVLD) = 0,
		ValTypeOfVLD != SKIP_NAL
	end

	ResBlkCavlc.FunctionWith1Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param]
			==>
//		TypeOfVLD            :[ValTypeOfVLD],
//		VLD_Param            :[Param]
	guard
		NbParamForFunction(ValTypeOfVLD) = 1
	end

	ResBlkCavlc.FunctionWith2Param: action
		ResBlkCavlcTypeOfVLD :[ValTypeOfVLD],
		ResBlkCavlcVLD_Param :[Param1 , Param2]
			==>
//		TypeOfVLD            :[ValTypeOfVLD],
//		VLD_Param            :[Param1 , Param2]
	guard
		NbParamForFunction(ValTypeOfVLD) = 2
	end

	ResBlkCavlc.Done: action
		ResBlkCavlcTypeOfVLD :[Function]
			==>
	guard
		Function = SKIP_NAL
	do
		stackIdx := (stackIdx - 1) & 0x0F;
	end


/*****************************************************************************/
/*              VLDecoding                                                   */
/*****************************************************************************/
	//Contains the Byte which is currently read.
	uint(size=16) WorkingByte      := 0;
	//Contains the number of bit which are not read yet in WorkingByte
	uint(size=4)  CountBufferRbsp  := 0;
	//Contains up to 4 next byte in the flow. It helps for more_rbsp_data()
	uint(size=3)  SizeBufferRbsp   := 0;
	List(type: uint(size=8), size=4) BufferRbsp := [0: for int s in 0 .. 3];
	/* ue(v) and Cavlc coeff_token()*/
	bool          Bit1Found       := false;
	uint(size=5)  leadingZeroBits := 0;
	/* ue(v) only */
	int (size=31) CodeNum         := 0;
	/* u(n) */
	uint(size=5)  BitsCount       := 0;
	uint(size=31) ReadValue       := 0;
	uint(size=1)  TeVal           := 0;
	/* more_rbsp_data() */
	bool          NewNalAlreadyDetected  := false;
	/* Cavlc */
	uint(size=5) NbBitsRead;
	/* Cavlc coeff_token */
	uint(size=3) Idx_nC;
	uint(size=6) coeff_token;
	/* Cavlc total_zeros and run_before */
	uint(size=3) ValRead         := 0;
	/* Cavlc total_zeros */
	uint(size=2) IdxmaxNumCoeff  := 0;
	uint(size=4) TotalCoeffParam := 0;
	/* Cavlc run_before */
	uint(size=8) IdxzerosLeft    := 0;


	uint(size=32) Out_ReadNBits;
	procedure ReadNBitsInWorkingByte(uint(size=32) InitialValue, uint(size=4) NbBitsToRead)
	begin
			Out_ReadNBits    := InitialValue << NbBitsToRead;
			Out_ReadNBits    := Out_ReadNBits + selectBits(WorkingByte >> (8-NbBitsToRead), 0 , NbBitsToRead);
			WorkingByte     := WorkingByte << NbBitsToRead;
			CountBufferRbsp := CountBufferRbsp - NbBitsToRead;
	end


	//Syntaxe decoding have finished the decoding of this Nal
	IsEndOfNal: action /*TypeOfVLD :[Func]*/ ==>
//	guard
//		Func    = SKIP_NAL
	do
		CountBufferRbsp := 0;
		SizeBufferRbsp  := 0;
	end

	//We skip all bytes in the current nal
	WaitForNewNal: action NewNal :[IsNewNal], RbspByte :[ValRbsp] ==>
	guard
		IsNewNal = false
	end

	NewNalDetected: action NewNal :[IsNewNal] ==>
	guard
		IsNewNal = true
	do
		SizeBufferRbsp := 0;
	end

	NewNalHasBeenSent: action ==>
	guard
		NewNalAlreadyDetected = true
	end

	ResetNewNalAlreadyDetected: action ==>
	do
		NewNalAlreadyDetected := false;
	end

	IsMoreRbspData: action ==>
	guard
		typeOfVld = MORE_RBSP_DATA
	end

	CheckSizeBufferRbsp.IsEqualOrSupTo2: action ==>
	guard
		SizeBufferRbsp >= 2
	do
		valDecoded:=1;
		valDecodedFlag := true;
	end

	CheckSizeBufferRbsp.IsEqualTo1: action ==>
	guard
		SizeBufferRbsp = 1
	do
		valDecodedFlag := true;
		if(((WorkingByte = 0 and (BufferRbsp[0] & 0xFF) = 0x03) or 
		   ((BufferRbsp[0] & 0xFF) = 0x80)) and (CountBufferRbsp = 0) )then
			valDecoded := 0;
		else
			valDecoded := 1;
		end
	end

	CheckSizeBufferRbsp.IsEqualTo0: action ==>
	guard
		SizeBufferRbsp = 0
	do
		valDecodedFlag := true;
		valDecoded :=
			if( (WorkingByte & 0xFF) = 0x80 )then
				 0
			else
				1
			end;
	end

	FillBufferRbsp: action RbspByte :[ValRbspByte], NewNal :[ValNewNal] ==>
	guard
		SizeBufferRbsp        < 4,
		ValNewNal             = false,
		NewNalAlreadyDetected = false
	do
		BufferRbsp[3]  := BufferRbsp[2];
		BufferRbsp[2]  := BufferRbsp[1];
		BufferRbsp[1]  := BufferRbsp[0];
		BufferRbsp[0]  := ValRbspByte;
		SizeBufferRbsp := SizeBufferRbsp + 1;
	end

	FutureNewNalDetected: action NewNal :[IsNewNal] ==>
	guard
		IsNewNal              = true,
		NewNalAlreadyDetected = false
	do
		NewNalAlreadyDetected := true;
	end

	IsVldUe: action ==>
	guard
		typeOfVld = VLD_UE
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end


	IsVldSe: action ==>
	guard
		typeOfVld = VLD_SE
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end

	IsReadBits: action  ==>
	guard
		typeOfVld = READ_BITS
	do
		BitsCount := paramSent[0];
		ReadValue := 0;
		TeVal     := 0;
	end

	IsVldTe.u: action  ==>
	guard
		typeOfVld = VLD_TE,
		paramSent[0]    = 1
	do
		BitsCount := 1;
		ReadValue := 0;
		TeVal     := 1;
	end

	IsVldTe.ue: action ==>
	guard
		typeOfVld = VLD_TE,
		paramSent[0]    > 1
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
		CodeNum         := 0;
	end

	IsCavlcCoeffToken: action  ==>
	guard
		typeOfVld = CAVLC_COEFF_TOKEN
	do
		uint nC = paramSent[0];
		Idx_nC :=
			if(nC < 0)then
				if(nC = -1)then
					 4
				else
					5
				end
			else
				if(nC >=8)then
					3
				else
					if(nC >=4)then
						2
					else
						nC/2
					end
				end
			end;
		coeff_token := 0;
		NbBitsRead := 0;
	end

	IsCavlcTotalZeros: action ==>
	guard
		typeOfVld = CAVLC_TOTAL_ZEROS
	do
		int maxNumCoeff = paramSent[0];
		int TotalCoeff = paramSent[1];
		IdxmaxNumCoeff :=
			if(maxNumCoeff = 4) then
				0
			else
				if(maxNumCoeff=8) then
					1
				else
					2
				end
			end;
		NbBitsRead      := 0;
		ValRead         := 0;
		TotalCoeffParam := TotalCoeff & 0x0F;
	end

	IsCavlcRunBefore: action ==>
	guard
		typeOfVld = CAVLC_RUN_BEFORE
	do
		int zerosLeft = paramSent[0];
		IdxzerosLeft :=
			if(zerosLeft > 0 and zerosLeft <= 6) then
				zerosLeft - 1
			else
				6
			end;
		NbBitsRead      := 0;
		ValRead         := 0;
	end

	IsCavlcLevelPrefix: action  ==>
	guard
		typeOfVld = CAVLC_LEVEL_PREFIX
	do
		Bit1Found       := false;
		leadingZeroBits := 0;
	end

	IsByteAligned: action ==>
	guard
		typeOfVld = BYTE_ALIGNED_REQUIRED
	do
		if(CountBufferRbsp != 8)then
			CountBufferRbsp := 0;
		end
	end

	Buffer.IsEmpty: action ==>
	guard
		CountBufferRbsp = 0,
		SizeBufferRbsp  > 0
	do
		CountBufferRbsp := 8;
		SizeBufferRbsp  := SizeBufferRbsp - 1;
		WorkingByte     := BufferRbsp[SizeBufferRbsp];
	end

	Buffer.IsNotEmpty: action ==>
	guard
		CountBufferRbsp > 0
	end

	GetCodeNum.Launch: action ==>
	var
		uint(size=5) NbBitsToAdd
	do
		if(not Bit1Found)then
			while( ((WorkingByte & 0x80) = 0) and (CountBufferRbsp > 0))
			do
				CountBufferRbsp := CountBufferRbsp - 1;
				WorkingByte     := WorkingByte << 1;
				leadingZeroBits := leadingZeroBits + 1;
			end
			if(CountBufferRbsp > 0)then
				leadingZeroBits := leadingZeroBits + 1;
				Bit1Found       := true;
			end
		end
		if(Bit1Found)then
			NbBitsToAdd :=
				if(CountBufferRbsp < leadingZeroBits)then
					CountBufferRbsp
				else
					leadingZeroBits
				end;
			ReadNBitsInWorkingByte(CodeNum,NbBitsToAdd);
			CodeNum         := Out_ReadNBits;
			leadingZeroBits := leadingZeroBits - NbBitsToAdd;
		end
	end

	GetCodeNum.Finished: action ==>
	guard
		leadingZeroBits = 0
	end

	GetCodeNum.NotFinished: action ==>
	guard
		leadingZeroBits != 0
	end

	SendUeValue: action ==>
	do
		valDecoded := CodeNum - 1;
		valDecodedFlag := true;
	end

	SendSeValue: action ==>
	do
		//CodeNum computed is equal to CodeNum + 1
		valDecodedFlag := true;
		if( (CodeNum mod 2) = 0 )then
			valDecoded := CodeNum / 2;
		else
			valDecoded := -CodeNum / 2;
		end
	end

	Get_u.Launch: action ==>
	var
		uint(size=5) NbBitsToAdd
	do
		NbBitsToAdd :=
			if(CountBufferRbsp < BitsCount)then
				CountBufferRbsp
			else
				BitsCount
			end;
		ReadNBitsInWorkingByte(ReadValue,NbBitsToAdd);
		ReadValue       := Out_ReadNBits;
		BitsCount       := BitsCount - NbBitsToAdd;
	end

	Get_u.Finished: action ==>
	guard
		BitsCount = 0
	end

	Get_u.NotFinished: action ==>
	guard
		BitsCount != 0
	end

	SendUValue: action ==>
	do
		valDecoded := ReadValue ^ TeVal;
		valDecodedFlag := true;
	end

	GetCavlcCoeffToken.Launch: action ==>
	var
		//delete me! (Bug in the front-end)
		bool titi
	do
		ReadNBitsInWorkingByte(coeff_token,1);
		coeff_token := Out_ReadNBits;
		NbBitsRead  := NbBitsRead + 1;
		titi := TotalCoeffValid[Idx_nC][(NbBitsRead - 1) & 0x0F][coeff_token & 0x3F];
		while((not titi) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(coeff_token,1);
			coeff_token := Out_ReadNBits;
			NbBitsRead  := NbBitsRead + 1;
			titi := TotalCoeffValid[Idx_nC][(NbBitsRead - 1) & 0x0F][coeff_token & 0x3F];
		end
	end

	GetCavlcCoeffToken.NotFinished: action ==>
	guard
		TotalCoeffValid[Idx_nC][(NbBitsRead - 1) & 0x0F][coeff_token & 0x3F] = false
	end

	GetCavlcCoeffToken.Finished: action ==>
	do
		valDecoded:=TotalCoeffValue[Idx_nC][(NbBitsRead-1) & 0x0F][coeff_token & 0x3F];
		valDecodedFlag := true;
	end

	GetCavlcLevelPrefix.Launch: action ==>
	do
		while( ((WorkingByte & 0x80) = 0) and (CountBufferRbsp > 0))
		do
			CountBufferRbsp := CountBufferRbsp - 1;
			WorkingByte     := WorkingByte << 1;
			leadingZeroBits := leadingZeroBits + 1;
		end
		if(CountBufferRbsp > 0)then
			Bit1Found       := true;
			/* We read the one bit value */
			CountBufferRbsp := CountBufferRbsp - 1;
			WorkingByte     := WorkingByte << 1;
		end
	end

	GetCavlcLevelPrefix.NotFinished: action ==>
	guard
		not Bit1Found
	end

	GetCavlcLevelPrefix.Finished: action ==>
	guard
		Bit1Found
	do
		valDecoded:=leadingZeroBits;
		valDecodedFlag := true;
	end

	GetCavlcTotalZeros.Launch: action ==>
	var
		//delete me!
		bool titi
	do
		ReadNBitsInWorkingByte(ValRead,1);
		ValRead := Out_ReadNBits;
		NbBitsRead      := NbBitsRead + 1;
		titi := TotalZerosValid[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07];
		while((not titi) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(ValRead,1);
			ValRead    := Out_ReadNBits;
			NbBitsRead := NbBitsRead + 1;
			titi := TotalZerosValid[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07];
		end
	end

	GetCavlcTotalZeros.NotFinished: action ==>
	guard
		not TotalZerosValid[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07]
	end

	GetCavlcTotalZeros.Finished: action ==>
	do
		valDecoded:=TotalZerosValue[IdxmaxNumCoeff][TotalCoeffParam][NbBitsRead & 0x0F][ValRead & 0x07];
		valDecodedFlag := true;
	end

	GetCavlcRunBefore.Launch: action ==>
	var
		//delete me!
		bool titi
	do
		ReadNBitsInWorkingByte(ValRead,1);
		ValRead    := Out_ReadNBits;
		NbBitsRead := NbBitsRead + 1;
		titi := RunBeforeValid[IdxzerosLeft][NbBitsRead & 0x0F][ValRead & 0x07];
		while((not titi) and (CountBufferRbsp > 0))
		do
			ReadNBitsInWorkingByte(ValRead,1);
			ValRead    := Out_ReadNBits;
			NbBitsRead := NbBitsRead + 1;
			titi := RunBeforeValid[IdxzerosLeft][NbBitsRead & 0x0F][ValRead & 0x07];
		end
	end

	GetCavlcRunBefore.NotFinished: action ==>
	guard
		not RunBeforeValid[IdxzerosLeft][NbBitsRead & 0x0F][ValRead & 0x07]
	end

	GetCavlcRunBefore.Finished: action ==>
	do
		valDecoded:=RunBeforeValue[IdxzerosLeft][NbBitsRead & 0x0F][ValRead & 0x07];
		valDecodedFlag := true;
	end


	schedule fsm FindOutNextActor:
		FindOutNextActor (findOutNextActor.isSliceHeader1    )--> LaunchSlcHead;
		FindOutNextActor (findOutNextActor.isSliceHeader2Avc )--> LaunchSlcHead2Avc;
		FindOutNextActor (findOutNextActor.isRefPicMark      )--> LaunchRefPicMark;
		FindOutNextActor (findOutNextActor.isRefPicList      )--> LaunchRefPicList;
		FindOutNextActor (findOutNextActor.isSliceData       )--> LaunchSliceData;
		FindOutNextActor (findOutNextActor.isMbLayer         )--> LaunchMbLayer;
		FindOutNextActor (findOutNextActor.isMbPred          )--> LaunchMbPred;
		FindOutNextActor (findOutNextActor.isSubMbPred       )--> LaunchSubMbPred;
		FindOutNextActor (findOutNextActor.isResidual        )--> LaunchResidual;
		FindOutNextActor (findOutNextActor.isResidualCavlc   )--> LaunchResBlkCavlc;

		LaunchSlcHead (SliceHeader1.GetVldVal.Launch )--> LaunchSlcHead;
		LaunchSlcHead (SliceHeader1.FunctionWithoutParam )--> CheckVLDType;
		LaunchSlcHead (SliceHeader1.FunctionWith1Param   )--> CheckVLDType;
		LaunchSlcHead (SliceHeader1.LaunchActor          )--> FindOutNextActor;
		LaunchSlcHead (SliceHeader1.Done                 )--> WaitForNewNal;

		LaunchSlcHead2Avc (SliceHeader2.FunctionWithoutParam )--> LaunchSlcHead2Avc;
		LaunchSlcHead2Avc (SliceHeader2.FunctionWith1Param   )--> LaunchSlcHead2Avc;
		LaunchSlcHead2Avc (SliceHeader2.LaunchActor          )--> FindOutNextActor;
		LaunchSlcHead2Avc (SliceHeader2.Done                 )--> FindOutNextActor;
			LaunchSlcHead2AvcGetVLDVal (SliceHeader2.GetVLDVal.Launch )--> LaunchSlcHead2AvcGetVLDVal;
	
		LaunchRefPicMark (RefPicMark.FunctionWithoutParam )--> LaunchRefPicMark;
		LaunchRefPicMark (RefPicMark.FunctionWith1Param   )--> LaunchRefPicMark;
		LaunchRefPicMark (RefPicMark.Done                 )--> LaunchSlcHead2Avc;
			LaunchRefPicMarkGetVLDVal (RefPicMark.GetVLDVal.Launch )--> LaunchRefPicMarkGetVLDVal;
		
		LaunchRefPicList (RefPicList.FunctionWithoutParam )--> LaunchRefPicList;
		LaunchRefPicList (RefPicList.FunctionWith1Param   )--> LaunchRefPicList;
		LaunchRefPicList (RefPicList.Done                 )--> LaunchSlcHead2Avc;
			LaunchRefPicListGetVLDVal (RefPicList.GetVLDVal.Launch )--> LaunchRefPicListGetVLDVal;
	
		LaunchSliceData   (SliceData.FunctionWithoutParam   )--> LaunchSliceData;
		LaunchSliceData   (SliceData.FunctionWith1Param     )--> LaunchSliceData;
		LaunchSliceData   (SliceData.LaunchActor            )--> FindOutNextActor;
		LaunchSliceData   (SliceData.Done                   )--> FindOutNextActor;
			LaunchSliceDataGetVLDVal   (SliceData.GetVLDVal.Launch )--> LaunchSliceDataGetVLDVal;
		
		LaunchMbLayer    (MbLayer.FunctionWithoutParam    )--> LaunchMbLayer;
		LaunchMbLayer    (MbLayer.FunctionWith1Param      )--> LaunchMbLayer;
		LaunchMbLayer    (MbLayer.Done                    )--> FindOutNextActor;
		LaunchMbLayer    (MbLayer.LaunchActor             )--> FindOutNextActor;
			LaunchMbLayerGetVLDVal    (MbLayer.GetVLDVal.Launch    )--> LaunchMbLayerGetVLDVal;
		
		LaunchMbPred    (MbPred.FunctionWithoutParam    )--> LaunchMbPred;
		LaunchMbPred    (MbPred.FunctionWith1Param      )--> LaunchMbPred;
		LaunchMbPred    (MbPred.Done                    )--> FindOutNextActor;
			LaunchMbPredGetVLDVal    (MbPred.GetVLDVal.Launch )--> LaunchMbPredGetVLDVal;
		
		LaunchSubMbPred (SubMbPred.FunctionWithoutParam )--> LaunchSubMbPred;
		LaunchSubMbPred (SubMbPred.FunctionWith1Param   )--> LaunchSubMbPred;
		LaunchSubMbPred (SubMbPred.Done                 )--> FindOutNextActor;
			LaunchSubMbPredGetVLDVal (SubMbPred.GetVLDVal.Launch )--> LaunchSubMbPredGetVLDVal;
	
		LaunchResidual  (Residual.Done                  )--> FindOutNextActor;
		LaunchResidual  (Residual.LaunchActor           )--> FindOutNextActor;
	
		LaunchResBlkCavlc (ResBlkCavlc.FunctionWithoutParam )--> LaunchResBlkCavlc;
		LaunchResBlkCavlc (ResBlkCavlc.FunctionWith1Param   )--> LaunchResBlkCavlc;
		LaunchResBlkCavlc (ResBlkCavlc.FunctionWith2Param   )--> LaunchResBlkCavlc;
		LaunchResBlkCavlc (ResBlkCavlc.Done                 )--> FindOutNextActor;
			LaunchResBlkCavlcGetVLDVal (ResBlkCavlc.GetVLDVal.Launch      )--> LaunchResBlkCavlcGetVLDVal;




		CheckVLDType    (IsByteAligned                       )--> CheckVLDType;
		CheckVLDType    (IsVldUe                             )--> VLDecoding_Ue;
		CheckVLDType    (IsVldSe                             )--> VLDecoding_Se;
		CheckVLDType    (IsVldTe.ue                          )--> VLDecoding_Ue;
		CheckVLDType    (IsVldTe.u                           )--> Decode_u;
		CheckVLDType    (IsReadBits                          )--> Decode_u;
		CheckVLDType    (IsMoreRbspData                      )--> FillBuffer;
		CheckVLDType    (IsCavlcLevelPrefix                  )--> DecCavlcLevPre;
		CheckVLDType    (IsCavlcCoeffToken                   )--> DecCavlcCofTkn;
		CheckVLDType    (IsCavlcTotalZeros                   )--> DecCavlcTotZer;
		CheckVLDType    (IsCavlcRunBefore                    )--> DecCavlcRunBef;

		/* more_rbsp_data() */
		FillBuffer      (FillBufferRbsp                      )--> FillBuffer;
		FillBuffer      (FutureNewNalDetected                )--> CheckBuffer;
		FillBuffer      (CheckSizeBufferRbsp.IsEqualOrSupTo2 )--> FindOutNextActor;
		FillBuffer      (NewNalHasBeenSent                   )--> CheckBuffer;

		CheckBuffer     (CheckSizeBufferRbsp                 )--> FindOutNextActor;

		/* Skip the others values of the Rbsp */
		WaitForNewNal   (NewNalHasBeenSent                   )--> ResetNewNal;
		WaitForNewNal   (WaitForNewNal                       )--> WaitForNewNal2;
		WaitForNewNal   (NewNalDetected                      )--> LaunchSlcHead;

		ResetNewNal     (ResetNewNalAlreadyDetected          )--> LaunchSlcHead;

		WaitForNewNal2  (WaitForNewNal                       )--> WaitForNewNal2;
		WaitForNewNal2  (NewNalDetected                      )--> FindOutNextActor;

		/* ue(v) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		VLDecoding_Ue   (Buffer                              )--> GetCodeNum4ue;
		VLDecoding_Ue   (FillBufferRbsp                      )--> VLDecoding_Ue;

		GetCodeNum4ue   (GetCodeNum.Launch                   )--> CheckCodeNum4ue;

		CheckCodeNum4ue (GetCodeNum.NotFinished              )--> VLDecoding_Ue;
		CheckCodeNum4ue (GetCodeNum.Finished                 )--> SendUeValue;

		SendUeValue     (SendUeValue                         )--> FindOutNextActor;

		/* se(v) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		VLDecoding_Se   (Buffer                              )--> GetCodeNum4se;
		VLDecoding_Se   (FillBufferRbsp                      )--> VLDecoding_Se;

		GetCodeNum4se   (GetCodeNum.Launch                   )--> CheckCodeNum4se;

		CheckCodeNum4se (GetCodeNum.NotFinished              )--> VLDecoding_Se;
		CheckCodeNum4se (GetCodeNum.Finished                 )--> SendUeValse;

		SendUeValse     (SendSeValue                         )--> FindOutNextActor;

		/* u(n) decoding process */
			/*Fill in the WorkingByte if it's empty*/
		Decode_u        (Buffer                              )--> Get_u;
		Decode_u        (FillBufferRbsp                      )--> Decode_u;

		Get_u           (Get_u.Launch                        )--> Check_u_Val;

		Check_u_Val     (Get_u.NotFinished                   )--> Decode_u;
		Check_u_Val     (Get_u.Finished                      )--> SendUValue;

		SendUValue      (SendUValue                          )--> FindOutNextActor;

		/* Cavlc coeff_token() decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcCofTkn  (Buffer                              )--> GetCavlcCofTkn;
		DecCavlcCofTkn  (FillBufferRbsp                      )--> DecCavlcCofTkn;

		GetCavlcCofTkn   (GetCavlcCoeffToken.Launch          )--> ChckCavlcCofTkn;

		ChckCavlcCofTkn (GetCavlcCoeffToken.NotFinished      )--> DecCavlcCofTkn;
		ChckCavlcCofTkn (GetCavlcCoeffToken.Finished         )--> FindOutNextActor;

		/* Cavlc level_prefix decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcLevPre  (Buffer                              )--> GetCavlcLevPre;
		DecCavlcLevPre  (FillBufferRbsp                      )--> DecCavlcLevPre;

		GetCavlcLevPre  (GetCavlcLevelPrefix.Launch          )--> ChckCavlcLevPre;

		ChckCavlcLevPre (GetCavlcLevelPrefix.NotFinished     )--> DecCavlcLevPre;
		ChckCavlcLevPre (GetCavlcLevelPrefix.Finished        )--> FindOutNextActor;

		/* Cavlc total_zeros decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcTotZer  (Buffer                              )--> GetCavlcTotZer;
		DecCavlcTotZer  (FillBufferRbsp                      )--> DecCavlcTotZer;

		GetCavlcTotZer  (GetCavlcTotalZeros.Launch          )--> ChckCavlcTotZer;

		ChckCavlcTotZer (GetCavlcTotalZeros.NotFinished     )--> DecCavlcTotZer;
		ChckCavlcTotZer (GetCavlcTotalZeros.Finished        )--> FindOutNextActor;

		/* Cavlc run_before decoding process */
			/*Fill in the WorkingByte if it's empty*/
		DecCavlcRunBef  (Buffer                              )--> GetCavlcRunBef;
		DecCavlcRunBef  (FillBufferRbsp                      )--> DecCavlcRunBef;

		GetCavlcRunBef  (GetCavlcRunBefore.Launch            )--> ChckCavlcRunBef;

		ChckCavlcRunBef (GetCavlcRunBefore.NotFinished     )--> DecCavlcRunBef;
		ChckCavlcRunBef (GetCavlcRunBefore.Finished        )--> FindOutNextActor;
	end
	priority
		SliceHeader1.GetVldVal.Launch     > 
		SliceHeader1.FunctionWithoutParam > SliceHeader1.FunctionWith1Param  > SliceHeader1.LaunchActor;
		SliceHeader1.LaunchActor      > SliceHeader1.Done;

		SliceHeader2.FunctionWithoutParam > SliceHeader2.FunctionWith1Param  > SliceHeader2.LaunchActor;
		SliceHeader2.LaunchActor > SliceHeader2.Done;

		RefPicMark.FunctionWithoutParam  > RefPicMark.FunctionWith1Param   > RefPicMark.Done;
		RefPicList.FunctionWithoutParam  > RefPicList.FunctionWith1Param   > RefPicList.Done;


		SliceData.FunctionWithoutParam   > SliceData.FunctionWith1Param             > SliceData.LaunchActor        > SliceData.Done;
		MbLayer.FunctionWithoutParam     > MbLayer.FunctionWith1Param                  > MbLayer.LaunchActor         > MbLayer.Done;
		MbPred.FunctionWithoutParam      > MbPred.FunctionWith1Param                    > MbPred.Done;
		SubMbPred.FunctionWithoutParam   > SubMbPred.FunctionWith1Param              > SubMbPred.Done;
		ResBlkCavlc.FunctionWithoutParam > ResBlkCavlc.FunctionWith1Param > ResBlkCavlc.FunctionWith2Param  > ResBlkCavlc.Done;
		NewNalHasBeenSent               > WaitForNewNal                        > NewNalDetected;
		IsVldUe                         > IsReadBits                           > IsCavlcLevelPrefix;  
		IsCavlcLevelPrefix              > IsCavlcRunBefore > IsCavlcCoeffToken > IsVldSe;
		IsVldSe                         > IsVldTe.ue       > IsVldTe.u         > FillBufferRbsp;
		FillBufferRbsp                  > IsMoreRbspData   > IsByteAligned     > IsEndOfNal;
		CheckSizeBufferRbsp             > FillBufferRbsp                       > FutureNewNalDetected > NewNalHasBeenSent;
		GetCodeNum.NotFinished          > GetCodeNum.Finished;
		Buffer.IsNotEmpty               > Buffer.IsEmpty                       > FillBufferRbsp;
		GetCavlcCoeffToken.NotFinished  > GetCavlcCoeffToken.Finished;
		GetCavlcLevelPrefix.NotFinished > GetCavlcLevelPrefix.Finished;
		GetCavlcTotalZeros.NotFinished  > GetCavlcTotalZeros.Finished;
		GetCavlcRunBefore.NotFinished   > GetCavlcRunBefore.Finished;
	end
end
