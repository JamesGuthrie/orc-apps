/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>
package ch.epfl.mpeg4.part10.frext.synParser.modular.decodePicture;

import org.sc29.wg11.mpeg4.part10.MacroBlockInfo.*;

actor Algo_MergeDataFromSlcDataAndMbLayer()
	bool          SkipMbLayer,
	uint(size=6)  MbType,
	uint(size=2)  IsMbSliceOrPic,
	uint(size=4)  RemIntraYxYPredMode,
	int (size=8)  ScalingListIn
		==>
	uint(size=4)  IntraPredMode,
	int (size=8)  ScalingList_Y,
	int (size=8)  ScalingList_UV
	
		:
		
	uint(size=2) NEW_MB    = 2;
	

	uint(size=6) mb_type;
	uint(size=8) ScalingList4x4[6][16];
	uint(size=8) ScalingList8x8[2][64];
	/**************************************************************************
  	 * 
  	 *************************************************************************/
	isNotMbType.isNewSliceOrPic : action
		IsMbSliceOrPic :[isMbSliceOrPic_p],
		ScalingListIn  :[scalingList] repeat 6*16+2*64
			==>
	guard
		isMbSliceOrPic_p != NEW_MB
	do
		foreach int i in 0 .. 5 do
			foreach int j in 0 .. 15 do
				ScalingList4x4[i][j] := scalingList[16*i+j];
			end
		end
		foreach int i in 0 .. 1 do
			foreach int j in 0 .. 63 do
				ScalingList8x8[i][j] := scalingList[6*16+64*i+j];
			end
		end
	end
	/**************************************************************************
  	 * 
  	 *************************************************************************/
	isNotMbType.isSkip : action
		SkipMbLayer    :[skip],
		IsMbSliceOrPic :[isMbSliceOrPic_p]
			==>
	guard
		skip = true and (isMbSliceOrPic_p = NEW_MB)
	end
	/**************************************************************************
  	 * 
  	 *************************************************************************/
	getMbType : action
		SkipMbLayer    :[skip],
		MbType         :[mb_type_p],
		IsMbSliceOrPic :[isMbSliceOrPic_p]
			==>
	guard
		skip = false and (isMbSliceOrPic_p = NEW_MB)
	do
		mb_type := mb_type_p;
	end
	/**************************************************************************
  	 * 
  	 *************************************************************************/
	MbLayer.IsIntra4x4: action
		RemIntraYxYPredMode :[rem_intra_4x4_pred_mode] repeat 16
			==>
		ScalingList_Y :[[ScalingList4x4[0][i] : for int i in 0 .. 15]] repeat 16,
		ScalingList_UV :[[ScalingList4x4[j][i] : for int j in 1 .. 2, for int i in 0 .. 15]] repeat 32,
		IntraPredMode :[intra_pred_mode] repeat 17
	guard
		mb_type = BLOCK_TYPE_I_4x4
	var
		List(type: uint(size=4), size=17) intra_pred_mode
	do
		intra_pred_mode[0] := 1;
		foreach int i in 0 .. 15
		do
			intra_pred_mode[i+1] := rem_intra_4x4_pred_mode[i];
		end
	end
  	/**************************************************************************
  	 * 
  	 *************************************************************************/
	MbLayer.IsIntra8x8: action
		RemIntraYxYPredMode :[rem_intra_8x8_pred_mode] repeat 4
			==>
		ScalingList_Y :[[ScalingList8x8[0][i] : for int i in 0 .. 63]] repeat 64,
		ScalingList_UV :[[ScalingList4x4[j][i] : for int j in 1 .. 2, for int i in 0 .. 15]] repeat 32,
		IntraPredMode :[intra_pred_mode] repeat 5
	guard
		mb_type = BLOCK_TYPE_I_8x8
	var
		List(type: uint(size=4), size=5) intra_pred_mode
	do
		intra_pred_mode[0] := 2;
		foreach int i in 0 .. 3
		do
			intra_pred_mode[i+1] := rem_intra_8x8_pred_mode[i];
		end
	end
  	/**************************************************************************
  	 * 
  	 *************************************************************************/
	MbLayer.IsIntra16x16: action
			==>
		IntraPredMode :[ 3 , (mb_type - BLOCK_TYPE_INTRA_16x16_MIN) & 3],
		ScalingList_Y :[[ScalingList4x4[0][i] : for int i in 0 .. 15]] repeat 16,
		ScalingList_UV :[[ScalingList4x4[j][i] : for int j in 1 .. 2, for int i in 0 .. 15]] repeat 32
	guard
		mb_type >= BLOCK_TYPE_INTRA_16x16_MIN,
		mb_type <= BLOCK_TYPE_INTRA_16x16_MAX
	end
  	/**************************************************************************
  	 * 
  	 *************************************************************************/
	MbLayer.IsIntraPCM: action
			==>
		IntraPredMode  :[4]
	guard
		mb_type = BLOCK_TYPE_I_PCM
	end
  	/**************************************************************************
  	 * 
  	 *************************************************************************/
	MbLayer.IsNotIntra: action
			==>
		ScalingList_Y :[[ScalingList4x4[3][i] : for int i in 0 .. 15]] repeat 16,
		ScalingList_UV :[[ScalingList4x4[j][i] : for int j in 4 .. 5, for int i in 0 .. 15]] repeat 32
	guard
		mb_type > BLOCK_TYPE_INTRA_MAX
	end
  	/**************************************************************************
  	 * 
  	 *************************************************************************/
	schedule fsm getMbType:
		getMbType   (isNotMbType )--> getMbType;
		getMbType   (getMbType   )--> ReadMbLayer;
		ReadMbLayer (MbLayer     )--> getMbType;
	end
end
