/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
 
// Author: Damien de Saint Jorre <damien.desaintjorre@epfl.ch>
package ch.epfl.mpeg4.part10.svc.synParser.modular.decodePic;

import org.mpeg4.part10.cbp.synParser.modular.decodePicture.decodingFunctions.*;

actor MgntDecodePic()
	int (size=6)  DataTypeOfVLD,
	int (size=32) DataVLDParam,
	int (size=6)  Header1TypeOfVLD,
	int (size=32) Header1VLDParam,
	int (size=6)  Header2TypeOfVLD,
	int (size=32) Header2VLDParam,
	uint(size=5)  RefPicListTypeOfVLD,
	int (size=32) RefPicListVldParam,
	uint(size=5)  RefPicMarkTypeOfVLD,
	int (size=32) RefPicMarkVldParam,
	uint(size=5)  RefBaseMarkTypeOfVLD,
	int (size=32) RefBaseMarkVldParam,
	int (size=32) ValDecoded
		==>
	int (size=32) DataValDecod,
	int (size=32) Header1ValDecod,
	int (size=32) Header2ValDecod,
	int (size=32) RefPicListValVld,
	int (size=32) RefPicMarkValVLD,
	int (size=32) RefBaseMarkValVLD,
	uint(size=5)  TypeOfVLD,
	int (size=32) VldParam
		:

	uint(size=8)  callStack[16];
	uint(size=32) nbParamToDecod := 0;
	int (size=3)  stackIdx := -1;

	initialize ==>
	do
		stackIdx     := 0;
		callStack[0] := SLICE_HEADER1;
	end

	stackIsEmpty: action ==> TypeOfVLD:[SKIP_NAL]
	guard
		stackIdx < 0
	do
		stackIdx     := 0;
		callStack[0] := SLICE_HEADER1;
	end

	findOutNextActor.isSliceHeader1: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER1
	end

	findOutNextActor.isSliceHeader2: action ==>
	guard
		callStack[stackIdx] = SLICE_HEADER2
	end

	findOutNextActor.isRefPicList: action ==>
	guard
		callStack[stackIdx] = REF_PIC_LIST
	end

	findOutNextActor.isRefPicMark: action ==>
	guard
		callStack[stackIdx] = REF_PIC_MARKING
	end

	findOutNextActor.isRefBaseMark: action ==>
	guard
		callStack[stackIdx] = REF_BASE_PIC_MARKING
	end

	findOutNextActor.isSliceData: action ==>
	guard
		callStack[stackIdx] = SLICE_DATA_SVC
	end

	getVldVal.done: action ==>
	guard
		nbParamToDecod = 0
	end

	waitVLDVal: action ==>
	guard
		nbParamToDecod > 0
	end

	sliceHeader1.vld.withoutParam: action Header1TypeOfVLD:[typeOfVld] ==>
		TypeOfVLD:[typeOfVld]
	guard
		NbParamForFunction(typeOfVld) = 0,
		typeOfVld  != SKIP_NAL
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	sliceHeader1.vld.with1Param: action Header1TypeOfVLD:[typeOfVld],
		Header1VLDParam:[param] ==> TypeOfVLD:[typeOfVld], VldParam:[param]
	guard
		NbParamForFunction(typeOfVld) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	sliceHeader1.getVldVal: action ValDecoded:[valDecoded] ==>
		Header1ValDecod:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	sliceHeader1.launchActor: action Header1TypeOfVLD:[typeOfVld],
		Header1VLDParam:[actorLaunched] ==>
	guard
		typeOfVld = LAUNCH_ACTOR
	do
		stackIdx       := (stackIdx + 1) &0x0F;
		callStack[stackIdx] := actorLaunched;
	end

	sliceHeader1.done: action Header1TypeOfVLD:[typeOfVld] ==>
	guard
		typeOfVld = SKIP_NAL
	do
		stackIdx := stackIdx - 1;
	end

	sliceHeader2.vld.withoutParam: action Header2TypeOfVLD:[typeOfVld] ==>
		TypeOfVLD:[typeOfVld]
	guard
		NbParamForFunction(typeOfVld) = 0,
		typeOfVld  != SKIP_NAL
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	sliceHeader2.vld.with1Param: action Header2TypeOfVLD:[typeOfVld],
		Header2VLDParam:[param] ==> TypeOfVLD:[typeOfVld], VldParam:[param]
	guard
		NbParamForFunction(typeOfVld) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	sliceHeader2.getVldVal: action ValDecoded:[valDecoded] ==>
		Header2ValDecod:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	sliceHeader2.launchActor: action Header2TypeOfVLD:[typeOfVld],
		Header2VLDParam:[actorLaunched] ==>
	guard
		typeOfVld = LAUNCH_ACTOR
	do
		stackIdx       := (stackIdx + 1) &0x0F;
		callStack[stackIdx] := actorLaunched;
	end

	sliceHeader2.done: action Header2TypeOfVLD:[typeOfVld] ==>
	guard
		typeOfVld = SKIP_NAL
	do
		stackIdx := stackIdx - 1;
	end

	refPicList.vld.withoutParam: action RefPicListTypeOfVLD:[typeOfVLD] ==>
		TypeOfVLD:[typeOfVLD]
	guard
		NbParamForFunction(typeOfVLD) = 0,
		typeOfVLD  != SKIP_NAL
	do
		if(typeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	refPicList.vld.with1Param: action RefPicListTypeOfVLD:[typeOfVLD],
		RefPicListVldParam:[param] ==> TypeOfVLD:[typeOfVLD], VldParam:[param]
	guard
		NbParamForFunction(typeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	refPicList.getVLDVal: action ValDecoded:[valDecoded] ==> RefPicListValVld:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	refPicList.done: action RefPicListTypeOfVLD:[typeOfVLD] ==>
	guard
		typeOfVLD = SKIP_NAL
	do
		stackIdx := stackIdx - 1;
	end

	refPicMark.vld.withoutParam: action RefPicMarkTypeOfVLD:[typeOfVLD]
		==> TypeOfVLD:[typeOfVLD]
	guard
		NbParamForFunction(typeOfVLD) = 0,
		typeOfVLD  != SKIP_NAL
	do
		if(typeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	refPicMark.vld.with1Param: action RefPicMarkTypeOfVLD:[typeOfVLD],
		RefPicMarkVldParam:[param] ==> TypeOfVLD:[typeOfVLD], VldParam:[param]
	guard
		NbParamForFunction(typeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	refPicMark.getVldVal: action ValDecoded:[valDecoded] ==>
		RefPicMarkValVLD:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	refPicMark.done: action RefPicMarkTypeOfVLD:[typeOfVld] ==>
	guard
		typeOfVld = SKIP_NAL
	do
		stackIdx := stackIdx - 1;
	end

	refBaseMark.vld.withoutParam: action RefBaseMarkTypeOfVLD:[typeOfVLD]
		==> TypeOfVLD:[typeOfVLD]
	guard
		NbParamForFunction(typeOfVLD) = 0,
		typeOfVLD  != SKIP_NAL
	do
		if(typeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	refBaseMark.vld.with1Param: action RefBaseMarkTypeOfVLD:[typeOfVLD],
		RefBaseMarkVldParam:[param] ==> TypeOfVLD:[typeOfVLD], VldParam:[param]
	guard
		NbParamForFunction(typeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	refBaseMark.getVldVal: action ValDecoded:[valDecoded] ==>
		RefBaseMarkValVLD:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	refBaseMark.done: action RefBaseMarkTypeOfVLD:[typeOfVLD] ==>
	guard
		typeOfVLD = SKIP_NAL
	do
		stackIdx := stackIdx - 1;
	end

	slcData.vld.withoutParam: action DataTypeOfVLD:[typeOfVLD] ==>
		TypeOfVLD:[typeOfVLD]
	guard
		NbParamForFunction(typeOfVLD) = 0,
		typeOfVLD  != SKIP_NAL
	do
		if(typeOfVLD != BYTE_ALIGNED_REQUIRED)then
			nbParamToDecod := nbParamToDecod + 1;
		end
	end

	slcData.vld.with1Param: action DataTypeOfVLD:[typeOfVLD],
		DataVLDParam:[param] ==> TypeOfVLD:[typeOfVLD], VldParam:[param]
	guard
		NbParamForFunction(typeOfVLD) = 1
	do
		nbParamToDecod := nbParamToDecod + 1;
	end

	slcData.getVldVal: action ValDecoded:[valDecoded] ==>
		DataValDecod:[valDecoded]
	guard
		nbParamToDecod > 0
	do
		nbParamToDecod := nbParamToDecod - 1;
	end

	slcData.launchActor: action DataTypeOfVLD:[typeOfVLD],
		DataVLDParam:[actorLaunched] ==>
	guard
		typeOfVLD = LAUNCH_ACTOR
	do
		stackIdx := (stackIdx + 1) & 0x0F;
		callStack[stackIdx] := actorLaunched;
	end

	slcData.done: action DataTypeOfVLD:[typeOfVLD] ==> TypeOfVLD:[SKIP_NAL]
	guard
		typeOfVLD = SKIP_NAL
	do
		stackIdx := stackIdx - 1 ;
	end

	schedule fsm FindOutNextActor:
		FindOutNextActor (stackIsEmpty                    )--> FindOutNextActor;
		FindOutNextActor (findOutNextActor.isSliceHeader1 )--> LaunchSlcHead1;
		FindOutNextActor (findOutNextActor.isSliceHeader2 )--> LaunchSlcHead2;
		FindOutNextActor (findOutNextActor.isRefPicList   )--> LaunchRefPicList;
		FindOutNextActor (findOutNextActor.isRefPicMark   )--> LaunchRefPicMark;
		FindOutNextActor (findOutNextActor.isRefBaseMark  )--> LaunchRefBaseMark;
		FindOutNextActor (findOutNextActor.isSliceData    )--> LaunchSlcData;

		LaunchSlcHead1 (sliceHeader1.vld         )--> LaunchSlcHead1;
		LaunchSlcHead1 (waitVLDVal               )--> SlcHead1GetVLDVal;
		LaunchSlcHead1 (sliceHeader1.launchActor )--> FindOutNextActor;
		LaunchSlcHead1 (sliceHeader1.done        )--> FindOutNextActor;
			SlcHead1GetVLDVal (sliceHeader1.getVldVal )--> SlcHead1GetVLDVal;
			SlcHead1GetVLDVal (getVldVal.done         )--> LaunchSlcHead1;

		LaunchSlcHead2 (sliceHeader2.vld         )--> LaunchSlcHead2;
		LaunchSlcHead2 (waitVLDVal               )--> SlcHead2GetVLDVal;
		LaunchSlcHead2 (sliceHeader2.launchActor )--> FindOutNextActor;
		LaunchSlcHead2 (sliceHeader2.done        )--> FindOutNextActor;
			SlcHead2GetVLDVal (sliceHeader2.getVldVal )--> SlcHead2GetVLDVal;
			SlcHead2GetVLDVal (getVldVal.done         )--> LaunchSlcHead2;
		
		LaunchRefPicList (refPicList.vld  )--> LaunchRefPicList;
		LaunchRefPicList (waitVLDVal      )--> RefPicListGetVLDVal;
		LaunchRefPicList (refPicList.done )--> FindOutNextActor;
			RefPicListGetVLDVal (refPicList.getVLDVal )--> RefPicListGetVLDVal;
			RefPicListGetVLDVal (getVldVal.done       )--> LaunchRefPicList;
	
		LaunchRefPicMark (refPicMark.vld  )--> LaunchRefPicMark;
		LaunchRefPicMark (waitVLDVal      )--> RefPicMarkGetVLDVal;
		LaunchRefPicMark (refPicMark.done )--> FindOutNextActor;
			RefPicMarkGetVLDVal (refPicMark.getVldVal )--> RefPicMarkGetVLDVal;
			RefPicMarkGetVLDVal (getVldVal.done       )--> LaunchRefPicMark;
	
		LaunchRefBaseMark (refBaseMark.vld  )--> LaunchRefBaseMark;
		LaunchRefBaseMark (waitVLDVal       )--> RefBaseMarkGetVLDVal;
		LaunchRefBaseMark (refBaseMark.done )--> FindOutNextActor;
			RefBaseMarkGetVLDVal (refBaseMark.getVldVal )--> RefBaseMarkGetVLDVal;
			RefBaseMarkGetVLDVal (getVldVal.done        )--> LaunchRefBaseMark;

		LaunchSlcData (slcData.vld         )--> LaunchSlcData;
		LaunchSlcData (waitVLDVal            )--> SlcDataGetVLDVal;
		LaunchSlcData (slcData.launchActor )--> FindOutNextActor;
		LaunchSlcData (slcData.done        )--> FindOutNextActor;
			SlcDataGetVLDVal (slcData.getVldVal )--> SlcDataGetVLDVal;
			SlcDataGetVLDVal (getVldVal.done      )--> LaunchSlcData;
	end
	priority
		stackIsEmpty > findOutNextActor;
		sliceHeader1.vld > waitVLDVal > sliceHeader1.launchActor > sliceHeader1.done;
		refPicList.vld   > waitVLDVal > refPicList.done;
		refPicMark.vld   > waitVLDVal > refPicMark.done;
		refBaseMark.vld  > waitVLDVal > refBaseMark.done;
		slcData.vld      > waitVLDVal > slcData.launchActor > slcData.done;
	end
end