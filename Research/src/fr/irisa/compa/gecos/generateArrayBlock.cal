package fr.irisa.compa.gecos;
import  std.stdio.Source.*;
import fr.irisa.common.Simulation.*;

actor generateArrayBlock () ==> uint(size=32) token :

	int i := 0;
	int val := 0;
	int N = 8;
	int TS = 2;
	int NUM_BLOCKS = (N*N)/(TS*TS);
	int input[N*N];
	int state := 0;
	
 	a: action ==>
	guard
		state = 0 
	do
		println("Initializing data");
		val := 0;
		foreach int j in 0 .. N -1 do
			foreach int k in 0 .. N-1 do
				input[j*N+k] := val;
				if ( val < 9 ) then
					val := val + 1;
				else
					val := 0;
				end
			end
		end
		state := 1;
	end 
	
	b: action ==> token:[ t ] repeat TS * TS
	guard
		state = 1
	var
		uint(size=32) t[TS*TS]
	do
		println("Sending Block " + i);
		foreach int j in 0 .. TS*TS-1 do
			t[j] := input[i*(TS*TS)+j];
		end
		i := i + 1;
		if ( i = NUM_BLOCKS) then
			state := 2;
			i := 0;
		end
		//println("Pushing " + i);
	end

end