package org.ietr.mpegh.part2;
import std.util.Math.*;
unit SelectCuFunctions : 
	
  // DR 1402: use SSE optimized procedure if available
  @optimize_c(condition="OPEN_HEVC_ENABLE", name="getCuSample_isIntra_orcc")
  procedure getCuSample_isIntra_cal (
  	uint(size=8) intraSample[24],
    int(size=9) resSample[24],
    uint(size=8) sample[24],
    uint(size=8) scu_size)
  begin
    foreach int i in 0 .. scu_size-1
      do
	 	sample[i] := clip_i32(intraSample[i]+resSample[i], 0, 255);
      end
  end

  // DR 1402: use SSE optimized procedure if available
  @optimize_c(condition="OPEN_HEVC_ENABLE", name="getCuSample_isInter_orcc")
  procedure getCuSample_isInter_cal(
	  uint(size=8) interSamp[16][16][24],
      uint(size=8) interSample[24],
	  uint(size=5) xIdx,
	  uint(size=32) xOff,
	  uint(size=5) yIdx,
	  uint(size=32) yOff,
	  uint(size=8) scu_size)
  begin
	  foreach uint i in 0 .. scu_size-1
	    do
		  interSamp[xIdx+xOff][yIdx+yOff][i] := interSample[i];
	    end
  end

  // DR 1402: use SSE optimized procedure if available
  @optimize_c(condition="OPEN_HEVC_ENABLE", name="getCuSample_isInterRes_orcc")
  procedure getCuSample_isInterRes_cal(
	  uint(size=8) interSamp[16][16][24],
	  int(size=9) resSample[24],
      uint(size=8) sample[24],
	  uint(size=16) interIdx[2],
	  uint(size=8) scu_size)
  begin
	  foreach int i in 0 .. scu_size-1
		do
		  sample[i] := clip_i32(interSamp[interIdx[0]][interIdx[1]][i] +
			resSample[i], 0, 255);
		end
  end
end