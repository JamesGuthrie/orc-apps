// iir.cal -- part of 'DigitalFiltering' project
//
// Author: 
// Jani Boutellier (jani.boutellier@ee.oulu.fi)
// Department of Computer Science and Engineering
// University of Oulu, Finland
//
// Description:
// Monolithic implementation of IIR filter.
//
// Changelog:
// - initial version 08/03/2012
// - H0, H1 and SCALING moved as parameters 16/05/2012

package common;

import common.constants.*;

actor iir(int(size=SAMPLE_SZ) H0, int(size=SAMPLE_SZ) H1, int(size=SAMPLE_SZ) SCALING) int(size=SAMPLE_SZ) operand_1 ==> int(size=SAMPLE_SZ) result :

	int(size=SAMPLE_SZ) d1;
	
	init: action ==>
	do
		d1 := 0;
	end

	run: action operand_1:[ x ] ==> result:[ y ]
	var
		int(size=SAMPLE_SZ) y,
		int(size=32) accum
	do
		accum := x*H0 + d1*H1;
		d1 := accum >> SCALING;
		y := d1;
	end

	schedule fsm s_init :
		s_init	( init )	--> s_run;
		s_run	( run )		--> s_run;
	end

end
