/*******************************************************************************************************
* COPYRIGHT AND WARRANTY INFORMATION
*
* Copyright 2003, Advanced Audio Video Coding Standard, Part II
*
* DISCLAIMER OF WARRANTY
*
* The contents of this file are subject to the Mozilla Public License
* Version 1.1 (the "License"); you may not use this file except in
* compliance with the License. You may obtain a copy of the License at
* http://www.mozilla.org/MPL/
*
* Software distributed under the License is distributed on an "AS IS"
* basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the
* License for the specific language governing rights and limitations under
* the License.
*                     
* THIS IS NOT A GRANT OF PATENT RIGHTS - SEE THE AVS PATENT POLICY.
* The AVS Working Group doesn't represent or warrant that the programs
* furnished here under are free of infringement of any third-party patents.
* Commercial implementations of AVS, including shareware, may be
* subject to royalty fees to patent holders. Information regarding
* the AVS patent policy for standardization procedure is available at 
* AVS Web site http://www.avs.decode.intra.org.cn. Patent Licensing is outside
* of AVS Working Group.
*******************************************************************************************************/

// Author: Dandan Ding (dandan2036@163.com), Honggang Qi(hgqi@jdl.ac.cn), Lu Yu, and Wen Gao

// Mgnt_IntraPred_CHROMA_Addr.cal
// Function: This module is to calculate address in frame buffer for intra prediction for AVS Jizhun Profile.
// Note:

package avs.decode.intra;

import avs.Constants.*;

actor Mgnt_IntraPred_CHROMA_Addr()
  int(size=BTYPE_SZ) BTYPE
  ==>
  uint(size=1) AVAIL :
    
  int(size=MB_COORD_SZ) mbx;
  int(size=MB_COORD_SZ) mby;
  int(size=MB_COORD_SZ) mb_width;
 
  bool top_edge;
  bool left_edge;
  
  
  uint(size=4) left_addr_count;
  uint(size=4) top_addr_count;
  uint(size=1) top_left_addr_count;
  
  bool block_left_done;
  bool block_top_done;
  bool block_top_left_done;

  bool avail_left_done;
  bool avail_top_done;
  bool avail_top_left_done;

  uint(size=1) flag;
      
  get_new_vop: action BTYPE:[cmd] ==>
  guard
    (NEWVOP & cmd) != 0
  do
    mbx := 0;
    mby := 0;

    top_edge := true;
    left_edge := true;

    left_addr_count := 0;
    top_addr_count := 0;
    top_left_addr_count := 0;
    
    block_left_done := false;
    block_top_done := false;
    block_top_left_done := false;

    avail_left_done := false;
    avail_top_done := false;
    avail_top_left_done := false;

    flag := 1;
  end
  
  get_w: action BTYPE:[w] ==>
  do
    mb_width := w;
  end
  
  get_h: action BTYPE:[h] ==>
  end
  
  skip: action BTYPE:[btype] ==>
  do
    block_left_done := false;
    block_top_done := false;
    block_top_left_done := false;
    mbx := mbx + 1;
    if(mbx = mb_width) then
      mbx := 0;
      mby := mby + 1;
    end
  end

  //current block is intra
  get_intra: action BTYPE:[btype] ==>
  guard
    (btype & INTRA) != 0
  do
    block_left_done := false;
    block_top_done := false;
    block_top_left_done := false;
  end

  get_flag: action ==>
  guard
    flag = 1
  do
    flag := 0;
  end


  get_block_rec_done: action ==>
  end
  
  
  //output block_left available or not as well as the address of intra prediction.

  pred_left_navail: action ==> AVAIL:[0]
  guard
    not block_left_done,
    not block_top_done,
    not block_top_left_done,

    not avail_left_done,
    not avail_top_done,
    not avail_top_left_done,

    left_edge
  do
    left_addr_count := 0;
    block_left_done := true;
    avail_left_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_left_navail");
  end

  pred_left_avail: action ==> AVAIL:[1]
  guard
    not block_left_done,
    not block_top_done,
    not block_top_left_done,

    not avail_left_done,
    not avail_top_done,
    not avail_top_left_done,

    not left_edge
  do
    avail_left_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_left_avail");
  end

  pred_left_avail_addr: action ==>
  guard
    not block_left_done,
    not block_top_done,
    not block_top_left_done,

    avail_left_done,
    not avail_top_done,
    not avail_top_left_done,

    not left_edge
  do
    left_addr_count := left_addr_count + 1;
    if(left_addr_count = 9) then
      left_addr_count := 0;
      block_left_done := true;
    end
    //println("Mgnt_IntraPred_CHROMA_Addr:RA_INTRA left_addr : "+left_addr);
  end
  
  //output block_top available or not as well as the address of intra prediction.

  pred_top_navail: action ==> AVAIL:[0]
  guard
    block_left_done,
    not block_top_done,
    not block_top_left_done,
    
    avail_left_done,
    not avail_top_done,
    not avail_top_left_done,

    top_edge
  do
    top_addr_count := 0;    
    block_top_done := true;
    avail_top_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_top_navail");
  end

  pred_top_avail: action ==> AVAIL:[1]
  guard
    block_left_done,
    not block_top_done,
    not block_top_left_done,

    avail_left_done,
    not avail_top_done,
    not avail_top_left_done,

    not top_edge
  do
    avail_top_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_top_avail");
  end

  pred_top_avail_addr: action ==>
  guard
    block_left_done,
    not block_top_done,
    not block_top_left_done,
    
    avail_left_done,
    avail_top_done,
    not avail_top_left_done,
    
    not top_edge
  do
    top_addr_count := top_addr_count + 1;
    if (top_addr_count = 9) then
      top_addr_count := 0;
      block_top_done := true;    
    end
     
    //println("Mgnt_IntraPred_CHROMA_Addr:RA_INTRA top_addr : "+top_addr);
  end
  
  //output block_top_left available or not as well as the address of intra prediction.
 
  pred_top_left_navail: action ==> AVAIL:[0]
  guard
    block_left_done,
    block_top_done,
    not block_top_left_done,

    avail_left_done,
    avail_top_done,
    not avail_top_left_done,

    top_edge and left_edge
  do
    top_left_addr_count := 0;
    block_top_left_done := true;
    avail_top_left_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_top_left_navail");
  end

  pred_top_left_avail: action ==> AVAIL:[1]
  guard
    block_left_done,
    block_top_done,
    not block_top_left_done,

    avail_left_done,
    avail_top_done,
    not avail_top_left_done,

    not(top_edge and left_edge)
  do
    avail_top_left_done := true;
    //println("Mgnt_IntraPred_CHROMA_Addr:pred_top_left_avail");
  end

  pred_top_left_avail_addr: action ==>
  guard
    block_left_done,
    block_top_done,
    not block_top_left_done,
    
    avail_left_done,
    avail_top_done,
    avail_top_left_done,

    not (top_edge and left_edge)
  do
    top_left_addr_count := top_left_addr_count + 1;
    if (top_left_addr_count=1) then
      top_left_addr_count := 0;
      block_top_left_done := true;
    end
    //println("Mgnt_IntraPred_CHROMA_Addr:RA_INTRA top_left_addr : "+top_left_addr);
  end

  // top_left prediction finish
  pred_done: action ==>
  guard
    block_left_done,
    block_top_done,
    block_top_left_done,
    avail_left_done,
    avail_top_done,
    avail_top_left_done
  do
    block_left_done := false;
    block_top_done := false;
    block_top_left_done := false;

    avail_left_done := false;
    avail_top_done := false;
    avail_top_left_done := false;
    
    mbx := mbx + 1;
    left_edge := false;
    if(mbx = mb_width) then
      mbx := 0;
      mby := mby + 1;
      left_edge := true;
      top_edge := false;
    end
  end
  
  schedule fsm read:
   
    read                     ( get_new_vop          )  -->  get_w;
    get_w                    ( get_w                )  -->  get_h;
    get_h                    ( get_h                )  -->  read;
    read                     ( skip                 )  -->  read;
    read                     ( get_intra            )  -->  read_other;

    read_other               ( get_flag             )  -->  pred;
    read_other               ( get_block_rec_done   )  -->  pred;

    pred                     ( pred_left_navail     )  -->  pred;
    pred                     ( pred_left_avail      )  -->  pred_left_avail_addr_next;
    pred_left_avail_addr_next( pred_left_avail_addr )  -->  pred_left_avail_addr_next;
    pred_left_avail_addr_next( pred_top_navail      )  -->  pred;
    pred_left_avail_addr_next( pred_top_avail       )  -->  pred_top_avail_addr_next;
    /*
    pred_left_avail_addr     ( pred_left_avail_addr )  -->  pred_left_avail_addr;
    pred_left_avail_addr     ( pred_left_avail_addr )  -->  pred;
    */

    pred                     ( pred_top_navail      )  -->  pred;
    pred                     ( pred_top_avail       )  -->  pred_top_avail_addr_next;
    pred_top_avail_addr_next ( pred_top_avail_addr  )  -->  pred_top_avail_addr_next;
    pred_top_avail_addr_next ( pred_top_left_navail )  -->  pred_done;
    pred_top_avail_addr_next ( pred_top_left_avail  )  -->  pred_top_left_avail_addr;
    
    pred                     ( pred_top_left_navail     )  -->  pred_done;
    pred                     ( pred_top_left_avail      )  -->  pred_top_left_avail_addr;
    
    pred_top_left_avail_addr ( pred_top_left_avail_addr )  -->  pred_done;

    pred_done                 ( pred_done               )  -->  read;
  end
  
  priority
    get_new_vop > get_intra > skip;
    get_flag > get_block_rec_done;
    pred_left_navail > pred_left_avail > pred_top_navail > pred_top_avail > pred_top_left_navail > pred_top_left_avail;
    pred_left_avail_addr > pred_top_navail > pred_top_avail;
    pred_top_avail_addr > pred_top_left_navail > pred_top_left_avail;
  end

end