package avs.parser.motion;

import avs.Constants.*;

actor Algo_Get_Blockshape ()
	int(size=32) MbType
	==> 
		int(size=32) Part_SZ,
		int(size=32) IsIntraMb,
		int(size=32) Is_P_Skip
:

	int(size=32) Picture_type;

	int(size=32) blockshape_X;

	int(size=32) blockshape_Y;

	int(size=32) isIntraMb;

	int(size=32) is_P_Skip;

	get_new_vop: action MbType:[ new_vop ] ==>
	guard
		(new_vop & NEWVOP) !=0
	end

	get_info: action MbType:[picture_type, w, h] ==>
	do
		Picture_type := picture_type;
	end

	get_mb_type: action MbType:[ MB_Type ] ==> Part_SZ:[ blockshape_X, blockshape_Y ],
		IsIntraMb:[ isIntraMb ], Is_P_Skip:[ is_P_Skip ]
	do
		if Picture_type = 1 then
			if (MB_Type = 0) then
				blockshape_X := 16;
				blockshape_Y := 16;
				isIntraMb := 0;
				is_P_Skip := 1;
			end
			if (MB_Type = 1) then
				blockshape_X := 16;
				blockshape_Y := 16;
				isIntraMb := 0;
				is_P_Skip := 0;
			end
			if (MB_Type = 2) then
				blockshape_X := 16;
				blockshape_Y := 8;
				isIntraMb := 0;
				is_P_Skip := 0;
			end
			if (MB_Type = 3) then
				blockshape_X := 8;
				blockshape_Y := 16;
				isIntraMb := 0;
				is_P_Skip := 0;
			end
			if MB_Type = 4 then //MB_Type := P8MB; 
				blockshape_X := 8;
				blockshape_Y := 8;
				isIntraMb := 0;
				is_P_Skip := 0;
			else
				if MB_Type > 4 then //MB_Type := I8MB;
					blockshape_X := 8;
					blockshape_Y := 8;
					isIntraMb := 1;
					is_P_Skip := 0;
				end
			end //picture_type = 1
		else
			if Picture_type = 2 then
				if MB_Type = 0 then
					blockshape_X := 16;
					blockshape_Y := 16;
					isIntraMb := 0;
					is_P_Skip := 0;
				else
					if MB_Type > 22 then
						blockshape_X := 8;
						blockshape_Y := 8;
						isIntraMb := 1;
						is_P_Skip := 0;
					else
						if MB_Type = 22 then //MB_Type := P8MB;
							blockshape_X := 8;
							blockshape_Y := 8;
							isIntraMb := 0;
							is_P_Skip := 0;
						else
							if MB_Type < 4 then
								MB_Type := 1;
								blockshape_X := 16;
								blockshape_Y := 16;
								isIntraMb := 0;
								is_P_Skip := 0;
							else
								if (MB_Type & 1) = 0 then
									MB_Type := 2;
									blockshape_X := 16;
									blockshape_Y := 8;
									isIntraMb := 0;
									is_P_Skip := 0;
								else
									MB_Type := 3;
									blockshape_X := 8;
									blockshape_Y := 16;
									isIntraMb := 0;
									is_P_Skip := 0;
								end
							end
						end
					end
				end
			end
		end
	end
	
	schedule fsm start:
		start (get_new_vop) --> get_info;
		start (get_mb_type) --> start;
		get_info(get_info)  -->  start;
	end
	
	priority
		get_new_vop >  get_mb_type;
	end

end