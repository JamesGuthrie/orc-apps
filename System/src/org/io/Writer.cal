

/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
// Author: Richard Thavot <richard.thavot@epfl.ch>
package org.io;

import std.io.File.* ;

import std.lang.Integer.* ;

/**
 * The actor is a standard implmentation for writting byte in a File.
 */
actor Writer (String path) uint(size=32) Length,
	uint(size=8) Stream
	==> :

	uint(size=32) fsize;

	int(size=32) desc;

	initialize ==>
	do
		fsize := 0;
		desc := 0;
	end

	/*
	 * Open the File given by the path, then initalize the fsize varibale with the lenght of the file.
	 */
	openFile: action Length:[ lenght ] ==>
	do
		desc := accessFile(path);
		fsize := lenght;
	end

	/*
	 *  write an unsigned byte from the stream as while the variable fsize is greater than 0.
	 */
	writeByte: action Stream:[ b ] ==>
	do
		println(toHexString(b));
		if (desc != 0) then
			writeUnsignedByte(desc, b);
		end
		fsize := fsize - 1;
	end

	/*
	 * Close the file when the fsize variable reaches 0.
	 */
	closeFile: action ==>
	guard
		fsize = 0
	do
		desc := closeFile(desc);
	end

	/*The scheduler*/
	schedule fsm open :
		open ( openFile ) --> access;
		access ( writeByte ) --> access;
		access ( closeFile ) --> open;
	end

	priority
		closeFile > writeByte;
	end

end