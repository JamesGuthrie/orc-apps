/**
 * This FU is part of a RVC-CAL implementation of SKID3 user authentication protocol.
 * This FU (as part of the server's implementation) receives the client's authentication request and client's HMAC (H2), 
 * prepares data for HMAC operations (H1 and H2') and sends the needed data to its helper FU (Server_Sender).
 * 
 * FU ID: Server_Receiver
 * One FU parameter: 
 * 	 This FU gets its own UID as an FU parameter.
 *   1) int serverID: the UID of the server.
 * Three input port:
 * 	 1) uint(size=8) IP1_PRG: the nounce from pseudo random generator (PRG).
 *   2) int(size=8) IP2_Hash_Size: the hash size (in bytes) of the Hash operation (e.g. SHA-1, SHA-2 etc.) used in the HMAC.
 *   3) uint(size=8) IP3_Channel: the communication channel to receive client's data (the UID, the nounce rC and the HMAC H2). 
 * Four output port:
 * 	 1) int(size=64) OP1_Message_Size: the size (in bytes) of the message to be HMACed.
 *   2) uint(size=8) OP2_Message: the message to be HMACed.
 *   3) int(size=64) OP3_Hash_Size: the hash size (in bytes) of the Hash operation (e.g. SHA-1, SHA-2 etc.) used in the HMAC. 
 *   4) uint(size=8) OP4_Link: the link to exchange data between this FU and the Server_Sender FU.
 *   Note: OP1_Message_Size and OP3_Hash_Size ports have to be 64-bit in size to match the corresponding input ports in the HMAC. 
 *
 * Developer: Junaid Jameel Ahmad @ Uni-Konstanz 2012
 * 			  Thavot Richard @ EPFL
 * Last modified: 26.04.2012
 */

package Protocols.Authentication.SKID3.hw.impl;

actor ServerReceiver(int(size=8) serverID) uint(size=8) IP1_PRG, int(size=8) IP2_Hash_Size, uint(size=8) IP3_Channel 
	==> 
	int(size=64) OP1_Message_Size, uint(size=8) OP2_Message,  int(size=64) OP3_Hash_Size,  uint(size=8) OP4_Link:

	// used global var for working around a bug of HDL (casting pb??)
	uint(size=8) param_serverID := 0;

	int(size=8) clientID := 0;
	uint(size=8) rS := 0; // My nounce
	uint(size=8) rC := 0; // Client's nounce.
	
	int(size=8) hashSize := 0; // Number of bytes.	
	int counter := 0;
	
	initialize ==> 
	do
		param_serverID := serverID;
	end

	receiveConfigurations:
	action IP1_PRG:[r], IP2_Hash_Size:[hSize] ==>
	do
		rS := r;
		hashSize := hSize;
		counter := 0;
	end
	
	// Process clients's ID and nounce (rC)
	processClientsIDAndNounce.id:
	action IP3_Channel:[id] ==> 
	do
		clientID := id;
	end
	
	processClientsIDAndNounce.r:
	action IP3_Channel:[r] ==> 
	do
		rC := r;
	end

	// Prepare data to generate the HMAC on client's nounce (i.e., H1), which will be sent to the client for authentication.
	sendHMACData1.rS:
	action ==> OP1_Message_Size:[3], OP2_Message:[rS], OP3_Hash_Size:[hashSize]
	end
	
	sendHMACData1.rc:
	action ==> OP2_Message:[rC]
	end
	
	sendHMACData1.idS:
	action ==> OP2_Message:[param_serverID]
	end

	// Process (receive and forward) the client's HMAC (H2) on my nounce.
	processClientHMAC.recv:
	action IP3_Channel:[h] ==> OP4_Link:[h]
	do
		counter := counter + 1;	
	end
	
	processClientHMAC.done :
	action ==>
	guard counter >= hashSize
	end

	// Prepare data to generate my copy of the client's HMAC (i.e., H2'). Server_Sender actor will verify if H2' equals H2.
	sendHMACData2.rC:
	action ==> OP1_Message_Size:[3], OP2_Message:[rC], OP3_Hash_Size:[hashSize]
	end
	
	sendHMACData2.rS:
	action ==> OP2_Message:[rS]
	end
	
	sendHMACData2.idC:
	action ==> OP2_Message:[clientID]
	end

	schedule fsm start:
	
		start(receiveConfigurations) --> processClientsIDAndNounce_id;
		
		processClientsIDAndNounce_id(processClientsIDAndNounce.id) --> processClientsIDAndNounce_r;
		processClientsIDAndNounce_r(processClientsIDAndNounce.r) --> sendHMACData1_rS;

		sendHMACData1_rS(sendHMACData1.rS) --> sendHMACData1_rc;
		sendHMACData1_rc(sendHMACData1.rc) --> sendHMACData1_idS;
		sendHMACData1_idS(sendHMACData1.idS) --> listenChannel;
		
		listenChannel(processClientHMAC.recv) --> listenChannel;
		listenChannel(processClientHMAC.done) --> sendHMACData2_rC;
		
		sendHMACData2_rC (sendHMACData2.rC) --> sendHMACData2_rS;
		sendHMACData2_rS (sendHMACData2.rS) --> sendHMACData2_idC;
		sendHMACData2_idC(sendHMACData2.idC) --> start;
		
	end
	
	priority
		processClientHMAC.done > processClientHMAC.recv; 
	end

end
