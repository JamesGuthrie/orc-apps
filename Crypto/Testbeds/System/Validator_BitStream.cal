/**
 * This FU is part of the testbed of the Cryptographic Tool Library (CTL).
 * It validates if a bit stream matches the golden response.
 * 
 * FU ID: Validator_BitStream
 * Three input ports:
 *   1) uint(size=1) IP1_Data: the output of the PRBG
 *   2) uint IP2_StreamSize: the number of tokens in stream
 *   3) uint(size=1) IP3_GR: the golden response
 *
 * Developers: Junaid Jameel Ahmad, Shujun Li @ Uni-Konstanz 2010
 * Last modified: 04.04.2012
 */

package System;

actor Validator_BitStream () uint(size=1) IP1_Data, uint IP2_StreamSize, uint(size=1) IP3_GR ==> :

	int streamSize := 8;
	int counter := 0;
	int errorCounter := 0;
	bool streamSizeReceived := false;

	action IP2_StreamSize:[sSize] ==>
	guard not streamSizeReceived
	do
		streamSize := sSize;

		counter := 0;
		errorCounter := 0;
		streamSizeReceived := true;
	end
	
	action IP1_Data:[x], IP3_GR:[gr] ==>
	guard streamSizeReceived and counter < streamSize
	do
		counter := counter + 1;
		print("Actual Response vs. Golden Response = ");
		println(x+" vs. "+gr);
		if x!=gr then
			errorCounter := errorCounter + 1;
		end
		
		if counter = streamSize then
			if errorCounter = 0 then			
				println("Congratulations, the received stream matches the golden stream!");
			else
				println(errorCounter+" tokens of received stream do not match the golden stream!");
			end		
		end		
	end
	
	reset:
	action ==> 
	guard counter = streamSize
	do 
		streamSizeReceived := true;	
	end
	
end
