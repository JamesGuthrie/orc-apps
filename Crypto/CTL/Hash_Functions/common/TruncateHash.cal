/**
 * This FU is part of a CAL implementation of Secure Hash Algorithms specified in FIPS-180-3. This is common FU 
 * being used by SHA-224 and SHA-384 to generate hash output from SHA-256 and SHA-512 respectively. It truncates 
 * the hash output to the desired number of hash tokens.
 *
 * FU ID: TruncateHash
 * Two FU parameters:
 * 		1) int NUM_HASH_TOKENS: the number of tokens to be kept in the hash output.
 * 		2) int NUM_TRUNC_TOKENS: the number of tokens to be truncated (skipped).
 * One input port:
 * 		1) uint(size=32) IP: the hash message to be truncated
 * One output port:
 * 		1) uint(size=32) OP: the truncated hash
 *
 * Developer: Junaid Jameel Ahmad @ Uni-Konstanz 2010
 * Last modified: 04.04.2012 
 */

package Hash_Functions.common;

actor TruncateHash(int NUM_HASH_TOKENS = 7, int NUM_TRUNC_TOKENS = 1) uint(size=32) IP ==> uint(size=32) OP:
	
	int counter := 0;
	
	forwardTokens:
	action 	IP:[hash] ==> OP:[hash]
	guard counter < NUM_HASH_TOKENS
	do
		counter := counter + 1;
	end
	
	skipTokens:
	action 	IP:[hash] ==> 
	guard counter < (NUM_HASH_TOKENS+NUM_TRUNC_TOKENS)
	do
		counter := counter + 1;
	end	
	
	reset:
	action ==>
	guard counter = (NUM_HASH_TOKENS+NUM_TRUNC_TOKENS)
	do
		counter := 0;
	end
	
	priority
		forwardTokens > skipTokens > reset;
	end
	
end