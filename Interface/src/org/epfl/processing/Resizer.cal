/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
// Author: Richard Thavot <richard.thavot@epfl.ch>
package org.epfl.processing;

import org.epfl.model.pragma.GraphicsResolution.*;

actor Resizer ()
	uint(size=16) Width,
	uint(size=16) Height,
	uint(size=24) iPicture
	==> 
	uint(size=24) oPicture
	:
	
	uint(size=16) W = HorizontalDisplayPorch;
	uint(size=16) H = VerticalDisplayPorch;
	
	uint(size=16) iWidth := 0;
	uint(size=16) iHeight := 0;
	
	uint(size=16) w := 0;
	uint(size=16) h := 0;
	
	bool eof := false;
	
	newFrame: action  Width:[ t_width ], Height:[ t_height ] ==>
	do
		iWidth := t_width;
		iHeight := t_height;
	end
	
	endFrame: action ==>
	guard eof
	do
		w := 0;
		h := 0;
		eof := false;
	end
	
	newPixel.accurate: action iPicture:[ pix ] ==> oPicture:[ pix ]
	do
		w := w + 1;
		if(iWidth = W) and (w >= W) then
			w := 0;
			h := h + 1;
			if(iHeight = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.extend.width : action ==> oPicture:[0]
	guard (iWidth < W) and (w >= iWidth)
	do
		w := w + 1;
		if(w >= W) then
			w := 0;
			h := h + 1;
			if(iHeight = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.extend.height : action ==> oPicture:[0]
	guard (iHeight < H) and (h >= iHeight)
	do
		w := w + 1;
		if(w >= W) then
			w := 0;
			h := h + 1;
			if( h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.crop.width : action iPicture:[ pix ] ==>
	guard (iWidth > W) and (w >= W)
	do
		w := w + 1;
		if(w >= iWidth) then
			w := 0;
			h := h + 1;
			if(iHeight = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.crop.height : action iPicture:[ pix ] ==>
	guard (iHeight > H) and (h >= H)
	do
		w := w + 1;
		if(w >= iWidth) then
			w := 0;
			h := h + 1;
			if( h >= iHeight) then
				eof := true;
			end
		end
	end
	
	schedule fsm New:
		New (newFrame) --> Inside;
		Inside(newPixel) --> Inside;
		Inside(endFrame) --> New;
	end
	
	
	priority
		endFrame > newPixel;
		newPixel.crop > newPixel.extend > newPixel.accurate;
		newPixel.crop.height > newPixel.crop.width;
		newPixel.extend.height > newPixel.extend.width;
	end
	
end


