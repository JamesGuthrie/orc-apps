/* 
BEGINCOPYRIGHT X
	
	Copyright (c) 2004-2006, Xilinx Inc.
	All rights reserved.
	
	Redistribution and use in source and binary forms, 
	with or without modification, are permitted provided 
	that the following conditions are met:
	- Redistributions of source code must retain the above 
	  copyright notice, this list of conditions and the 
	  following disclaimer.
	- Redistributions in binary form must reproduce the 
	  above copyright notice, this list of conditions and 
	  the following disclaimer in the documentation and/or 
	  other materials provided with the distribution.
	- Neither the name of the copyright holder nor the names 
	  of its contributors may be used to endorse or promote 
	  products derived from this software without specific 
	  prior written permission.
	
	THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND 
	CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
	INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
	MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE 
	DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR 
	CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
	SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT 
	NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; 
	LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) 
	HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
	CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
	OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS 
	SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
	
ENDCOPYRIGHT
*/

// DCPred.cal
//
// Author: David B. Parlour (dave.parlour@xilinx.com)
//

package org.epfl.decoder.texture;

actor DCPred ()
 int(size=BTYPE_SZ) BTYPE, int(size=MB_COORD_SZ+3) A, int(size=MB_COORD_SZ+3) B, int(size=MB_COORD_SZ+3) C, int(size=SAMPLE_SZ) IN ==>
   int(size=SAMPLE_SZ) OUT, int(size=MB_COORD_SZ+3) PTR, int(size=3) START, int(size=QUANT_SZ) QUANT :

int DCVAL = 128*8;
	int MAXW_IN_MB = 121;
	int MB_COORD_SZ = 8;
	int BTYPE_SZ = 12;
	int SAMPLE_SZ = 13;
	int NEWVOP = 2048;
	int INTRA = 1024;
	int INTER = 512;
	int QUANT_MASK = 31;
	int ACCODED = 2;
	int ACPRED = 1;
	int QUANT_SZ = 6;

 // int SCALER_SZ = 7;

  int( size=QUANT_SZ ) QP;
  int( size=2 ) round;

  // Use 3 address lines for component index to save address calculation effort
  // Also, location 0 is reserved for all uncoded blocks. This protocol must
  // be identical to the buffer addressing in Reconstruct.cal, since pointer
  // values are passed on ports A, B, C.
  int BUF_SIZE = (MAXW_IN_MB + 2) * 8;
  int ptr := 8;  // Start at 8, since 0-7 are reserved

  // Initialize to the dummy values needed for uncoded blocks (always mapped to index 0)
 // List( type:int(size=SAMPLE_SZ), size=BUF_SIZE ) dc_buf := [ DCVAL : for int k in 1 .. BUF_SIZE ];
List(type: int(size=SAMPLE_SZ), size=BUF_SIZE) dc_buf := [ DCVAL : for int k in 1 .. BUF_SIZE ];

  int(size=4) comp := 0;

  // -ve means there is no data, so the inverse zigzag may flush its buffer
  start: action BTYPE:[ cmd ] ==> START:[ -2 ]
  guard
    (cmd & NEWVOP) != 0//bitand( cmd, NEWVOP ) != 0
  do
  	//println("start");
    comp := 0;
    ptr := 8;
    QP := (cmd & QUANT_MASK);//bitand( cmd, QUANT_MASK);
    round := (QP & 1) ^ 1;//bitxor( bitand( QP, 1 ), 1 );
  end

  skip: action BTYPE:[ x ] ==>
  //do
  	//println("skip");
  end

  read.inter_ac: action BTYPE:[ cmd ] ==> START:[ 0 ], PTR:[ 0 ], QUANT:[QP]
  guard
  	(cmd & INTER) != 0,
  	(cmd & ACCODED) != 0
    //bitand( cmd, INTER   ) != 0,
    //bitand( cmd, ACCODED ) != 0
  do
  	//println("read.inter_ac");
   // is_signed := true;
  end

  // Any other block except INTRA
  read.other: action BTYPE:[ cmd ] ==> START:[ -1 ]
  guard
    (cmd & INTRA) = 0//bitand( cmd, INTRA ) = 0
 // do
  	//println("read.other");
  end

  function abs(int x) --> int :
		if x < 0 then
			(-x)
		else
			x
		end
	end
  // Calculate DC scaler for INTRA blocks
  function dc_scaler( ) --> int(size=7):
    if (comp & 4) = 0/* bitand( comp, 4 ) = 0*/ then
      // Y component
      if QP > 0 and QP < 5 then 8 else
        if QP > 4 and QP < 9 then 2 * QP else
          if QP > 8 and QP < 25 then QP + 8 else (2 * QP) - 16 end
        end
      end
    else
      // UV component
      if QP > 0 and QP < 5 then 8 else
        if QP > 4 and QP < 25 then (QP + 13) >> 1/*rshift(QP + 13, 1)*/ else QP - 6 end
      end
    end
  end

	int diff := 0;
	int tmpDen := 0;
	int tmpNum := 0;
	int tmpRes := 0;
	int tmpResX := 0;
	//int res := 0;

  int(size=SAMPLE_SZ) dc_pred;
  int(size=SAMPLE_SZ) dc_pred1;
  int(size=7) scaler;
  bool is_signed := true;

  read.intra: action BTYPE:[ cmd ], A:[a], B:[b], C:[c] ==> PTR:[ if top then c else a end ], START:[ s ], QUANT:[QP]
  var
    int(size=SAMPLE_SZ) dca,// = dc_buf[ a ],
    int(size=SAMPLE_SZ) dcb,// = dc_buf[ b ],
    int(size=SAMPLE_SZ) dcc,// = dc_buf[ c ],
    int(size=SAMPLE_SZ) horiz,// := abs( dcb - dcc ),
    int(size=SAMPLE_SZ) vert,//  := abs( dca - dcb ),
    bool top,// := vert < horiz,
    int(size=3) s,
    bool ac = (cmd & ACPRED) != 0//bitand(cmd,ACPRED) != 0
  do
  	dca := dc_buf[a];
  	dcb := dc_buf[b];
  	dcc := dc_buf[c];
  	horiz := abs(dcb - dcc);
  	vert := abs(dca - dcb);
    scaler  := dc_scaler();
    top := vert < horiz;
    //dc_pred := ( (if top then dcc else dca end) + rshift(scaler,1) ) / scaler;
    dc_pred1 := (if top then dcc else dca end) + (scaler >> 1);//rshift(scaler,1);
    //divide(dc_pred1, scaler);
    //*****divide
    tmpResX := 0;
	
		tmpDen := scaler;
		tmpNum := dc_pred1;
	
		diff := dc_pred1 - scaler;
		while (diff > scaler) do
			tmpRes := 1;
			while (tmpDen <= tmpNum) do
				tmpDen := tmpDen << 1;
				tmpRes := tmpRes << 1;
			end
			
			tmpDen := tmpDen >> 1;
			tmpRes := tmpRes >> 1;
			diff := tmpNum - tmpDen;
			tmpNum := tmpNum - tmpDen;
			tmpDen := scaler;
			
			if (tmpNum = tmpDen) then
				tmpRes := tmpRes + 1;
			end
			
			tmpResX := tmpResX + tmpRes;
			
		end
		
		dc_pred := tmpResX;
    //******
    
    s := if not ac then 0 else if top then 2 else 1 end end;
    is_signed := scaler=0;
  end


  int(size=SAMPLE_SZ) dc_val;

  // Do dequant on INTER block DC
  getdc.inter: action IN:[ dc ] ==>
  var
   // int(size=SAMPLE_SZ) v = ( QP * (lshift( abs(dc), 1) + 1) ) - round
   int(size=SAMPLE_SZ) v = (QP * ((abs(dc) << 1) + 1)) - round
  do
    dc_val := if dc = 0 then 0 else
                if not is_signed then dc else
                  if dc < 0 then -v else v end end end;
  end

  // Do prediction on INTRA block DC
  getdc.intra: action IN:[ dc ] ==>
  do
    dc_val := (dc + dc_pred) * scaler;
  end

  function saturate( int x ) --> int
    var
      bool minus = x < -2048,
      bool plus  = x >  2047 :
    if minus then -2048 else if plus then 2047 else x end end
  end

  sat: action ==> OUT:[ dc ]
  var
    int(size=SAMPLE_SZ) dc = saturate( dc_val )
  do
    dc_buf [ (ptr | comp) ] :=  dc;
  end

  advance: action ==>
  do
    comp := comp + 1;
    if comp = 6 then
      comp := 0;
      ptr := if ptr = 8 then BUF_SIZE-8 else ptr-8 end;
    end
  end

  schedule fsm read :
    read          ( start          ) --> getw;
    read          ( read.inter_ac  ) --> inter;
    read          ( read.intra     ) --> intra;
    read          ( read.other     ) --> advance;

    getw          ( skip           ) --> geth;
    geth          ( skip           ) --> read;

    intra         ( getdc.intra    ) --> sat;

    inter         ( getdc.inter    ) --> sat;

    sat           ( sat            ) --> advance;

    advance       ( advance        ) --> read;
  end

  priority
    start         > read;
    read.inter_ac > read.other > read.intra;
  end
end