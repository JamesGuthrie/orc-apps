/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
// Author: Richard Thavot <richard.thavot@epfl.ch>
package org.epfl.system;

import org.epfl.processing.ColorSpace.*;
import std.util.BitOps.*;

actor Mire ()
	==> 
	uint(size=16) Width,
	uint(size=16) Height,
	uint(size=24) Picture
	:
	
	uint(size=16) width = 128;
	uint(size=16) height = 128;
	
	uint(size=16) w := width;
	uint(size=16) h := height;
	
	bool eof := true;

	
	procedure move() 
	begin
		w := w + 1;
		if(w >=  width) then 
			w := 0;
			h := h + 1;
		end
		if(h >= height) then
			eof := true;
		end
	end
	
	function percent(uint(size=16) s,uint rate) --> uint(size=16) :
		s * rate / 100
	end
	
	function perthousand(uint(size=16) s,uint rate) --> uint(size=16) :
		s * rate / 1000
	end
	
	/**
	 * Give Resolution
	 */
	Resolution: action ==>  Width:[ width ], Height:[ height ]
	guard eof
	do
		eof := false;
		w := 0;
		h := 0;
		println("New frame");
	end
	
	/**
	 * FIRST PORTION
	 */
	Picture.First.C1 : action ==> Picture:[ Anthracite ]
	guard (h < percent( height, 55)) and (w < perthousand(width,115))
	do
		println(Anthracite);
		move();
	end
	
	Picture.First.C2 : action ==> Picture:[ Grey ]
	guard (h < percent( height, 55)) and (w < perthousand(width,225))
	do
		move();
	end
	
	Picture.First.C3 : action ==> Picture:[ Yellow ]
	guard (h < percent( height, 55) ) and (w < perthousand(width,335))
	do
		move();
	end
	
	Picture.First.C4 : action ==> Picture:[ Cyan ]
	guard (h < percent( height, 55)) and (w < perthousand(width,445))
	do
		move();
	end
	
	Picture.First.C5 : action ==> Picture:[ Green ]
	guard (h < percent( height, 55)) and (w < perthousand(width,555))
	do
		move();
	end
	
	Picture.First.C6 : action ==> Picture:[ Magenta ]
	guard (h < percent( height, 55)) and (w < perthousand(width,665))
	do
		move();
	end
	
	Picture.First.C7 : action ==> Picture:[ Red ]
	guard (h < percent( height, 55) ) and (w < perthousand(width,775))
	do
		move();
	end
	
	Picture.First.C8 : action ==> Picture:[ Blue ]
	guard (h < percent( height, 55)) and (w < perthousand(width,885))
	do
		move();
	end
	
	Picture.First.C9 : action ==> Picture:[ Anthracite ]
	guard h < percent( height, 55) 
	do
		move();
	end
	
	/**
	 * SECOND PORTION
	 */
	Picture.Second.C1 : action ==> Picture:[ LightCyan ]
	guard (h < percent( height, 65)) and (w < perthousand(width,115))
	do
		move();
	end
	
	Picture.Second.C2 : action ==> Picture:[ DarkBlue ]
	guard (h < percent( height, 65)) and (w < perthousand(width,225))
	do
		move();
	end
	
	Picture.Second.C3 : action ==> Picture:[ Grey ]
	guard h < percent( height, 65) 
	do
		move();
	end
	
	/**
	 * THIRD PORTION
	 */
	Picture.Third.C1 : action ==> Picture:[ LightYellow ]
	guard (h < percent( height, 75)) and (w < perthousand(width,115))
	do
		move();
	end
	
	Picture.Third.C2 : action ==> Picture:[ DarkPurpule ]
	guard (h < percent( height, 75)) and (w < perthousand(width,225))
	do
		move();
	end
	
	//uint(size=16) pp = 255 * 16 / perthousand(width,775);
	uint(size=16) pp = 255 * 16 / (width * 775 / 1000);
	uint(size=16) px := 0;
	
	Picture.Third.C3 : action ==> Picture:[ rgb ]
	guard h < percent( height, 75)
	var uint(size=24) rgb = toRGB(px/16, px/16, px/16)
	do
		px := px + pp;
		move();
		if(w=0) then px := 0; end
	end

	/**
	 * FOURTH PORTION
	 */
	Picture.Fourth.C1 : action ==> Picture:[ Anthracite ]
	guard w < perthousand(width,115)
	do
		move();
	end
	
	Picture.Fourth.C2 : action ==> Picture:[ Black ]
	guard w < perthousand(width,285)
	do
		move();
	end
	
	Picture.Fourth.C3 : action ==> Picture:[ White ]
	guard w < perthousand(width,500)
	do
		move();
	end
	
	Picture.Fourth.C4 : action ==> Picture:[ Black ]
	guard w < perthousand(width,600)
	do
		move();
	end
	
	Picture.Fourth.C5 : action ==> Picture:[ DarkBlack ]
	guard w < perthousand(width,630)
	do
		move();
	end
	
	Picture.Fourth.C6 : action ==> Picture:[ Black ]
	guard w < perthousand(width,660)
	do
		move();
	end
	
	Picture.Fourth.C7 : action ==> Picture:[ LightBlack ]
	guard w < perthousand(width,690)
	do
		move();
	end
	
	Picture.Fourth.C8 : action ==> Picture:[ Black ]
	guard w < perthousand(width,720)
	do
		move();
	end
	
	Picture.Fourth.C9 : action ==> Picture:[ VividBlack ]
	guard w < perthousand(width,750)
	do
		move();
	end
	
	Picture.Fourth.C10 : action ==> Picture:[ Black ]
	guard w < perthousand(width,775)
	do
		move();
	end
	
	Picture.Fourth.C11 : action ==> Picture:[ DarkBlack ]
	guard w < perthousand(width,885)
	do
		move();
	end
	
	Picture.Fourth.C12 : action ==> Picture:[ Anthracite ]
	do
		move();
	end
	
	/**
	 * Priority
	 */
	priority 
		Resolution > Picture ;
		Picture.First > Picture.Second > Picture.Third > Picture.Fourth;
		Picture.First.C1 > Picture.First.C2 > Picture.First.C3 > Picture.First.C4 > 
			Picture.First.C5 > Picture.First.C6 > Picture.First.C7 > Picture.First.C8 > 
			Picture.First.C9;
		Picture.Second.C1 > Picture.Second.C2 > Picture.Second.C3;
		Picture.Third.C1 > Picture.Third.C2 > Picture.Third.C3;
		Picture.Fourth.C1 > Picture.Fourth.C2 > Picture.Fourth.C3 > Picture.Fourth.C4 > 
			Picture.Fourth.C5 > Picture.Fourth.C6 > Picture.Fourth.C7 > Picture.Fourth.C8 > 
			Picture.Fourth.C9 > Picture.Fourth.C10 > Picture.Fourth.C11 >Picture.Fourth.C12;
	end


end


