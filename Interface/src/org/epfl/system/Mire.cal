/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
// Author: Richard Thavot <richard.thavot@epfl.ch>
package org.epfl.system;

import org.epfl.processing.ColorSpace.*;
import std.util.BitOps.*;

actor Mire ()
	==> 
	uint(size=16) Width,
	uint(size=16) Height,
	uint(size=24) RGB
	:
	
	uint(size=16) width = 512+128;
	uint(size=16) height = 512;
	
	uint(size=16) w := width;
	uint(size=16) h := height;

	
	procedure advance() 
	begin
		w := w + 1;
		if(w >=  width) then 
			w := 0;
			h := h + 1;
		end
		if(h >= height) then
			h := 0;
		end
	end
	
	function percent(uint(size=16) s,uint rate) --> uint(size=16) :
		s * rate / 100
	end
	
	function perthousand(uint(size=16) s,uint rate) --> uint(size=16) :
		s * rate / 1000
	end
	
	/**
	 * Give Resolution
	 */
	Resolution: action ==>  Width:[ width ], Height:[ height ]
	guard
		(w >= width) and (h >= height)
	do
		w := 0;
		h := 0;
	end
	
	/**
	 * FIRST PORTION
	 */
	Picture.First.C1 : action ==> RGB:[ AsRGB(Anthracite) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,115))
	do
		advance();
	end
	
	Picture.First.C2 : action ==> RGB:[ AsRGB(Grey) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,225))
	do
		advance();
	end
	
	Picture.First.C3 : action ==> RGB:[ AsRGB(Yellow) ]
	guard (h < percent( height, 55) ) and (w < perthousand(width,335))
	do
		advance();
	end
	
	Picture.First.C4 : action ==> RGB:[ AsRGB(Cyan) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,445))
	do
		advance();
	end
	
	Picture.First.C5 : action ==> RGB:[ AsRGB(Green) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,555))
	do
		advance();
	end
	
	Picture.First.C6 : action ==> RGB:[ AsRGB(Magenta) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,665))
	do
		advance();
	end
	
	Picture.First.C7 : action ==> RGB:[ AsRGB(Red) ]
	guard (h < percent( height, 55) ) and (w < perthousand(width,775))
	do
		advance();
	end
	
	Picture.First.C8 : action ==> RGB:[ AsRGB(Blue) ]
	guard (h < percent( height, 55)) and (w < perthousand(width,885))
	do
		advance();
	end
	
	Picture.First.C9 : action ==> RGB:[ AsRGB(Anthracite) ]
	guard h < percent( height, 55) 
	do
		advance();
	end
	
	/**
	 * SECOND PORTION
	 */
	Picture.Second.C1 : action ==> RGB:[ AsRGB(LightCyan) ]
	guard (h < percent( height, 65)) and (w < perthousand(width,115))
	do
		advance();
	end
	
	Picture.Second.C2 : action ==> RGB:[ AsRGB(DarkBlue) ]
	guard (h < percent( height, 65)) and (w < perthousand(width,225))
	do
		advance();
	end
	
	Picture.Second.C3 : action ==> RGB:[ AsRGB(Grey) ]
	guard h < percent( height, 65) 
	do
		advance();
	end
	
	/**
	 * THIRD PORTION
	 */
	Picture.Third.C1 : action ==> RGB:[ AsRGB(LightYellow) ]
	guard (h < percent( height, 75)) and (w < perthousand(width,115))
	do
		advance();
	end
	
	Picture.Third.C2 : action ==> RGB:[ AsRGB(DarkPurpule) ]
	guard (h < percent( height, 75)) and (w < perthousand(width,225))
	do
		advance();
	end
	
	uint(size=16) pp = 255 * 16 / perthousand(width,775);
	uint(size=16) px := 0;
	
	Picture.Third.C3 : action ==> RGB:[ rgb ]
	guard h < percent( height, 75)
	var uint(size=24) rgb = ToRGB(px/16, px/16, px/16)
	do
		px := px + pp;
		advance();
		if(w=0) then px := 0; end
	end

	/**
	 * FOURTH PORTION
	 */
	Picture.Fourth.C1 : action ==> RGB:[ AsRGB(Anthracite) ]
	guard w < perthousand(width,115)
	do
		advance();
	end
	
	Picture.Fourth.C2 : action ==> RGB:[ AsRGB(Black) ]
	guard w < perthousand(width,285)
	do
		advance();
	end
	
	Picture.Fourth.C3 : action ==> RGB:[ AsRGB(White) ]
	guard w < perthousand(width,500)
	do
		advance();
	end
	
	Picture.Fourth.C4 : action ==> RGB:[ AsRGB(Black) ]
	guard w < perthousand(width,600)
	do
		advance();
	end
	
	Picture.Fourth.C5 : action ==> RGB:[ AsRGB(DarkBlack) ]
	guard w < perthousand(width,630)
	do
		advance();
	end
	
	Picture.Fourth.C6 : action ==> RGB:[ AsRGB(Black) ]
	guard w < perthousand(width,660)
	do
		advance();
	end
	
	Picture.Fourth.C7 : action ==> RGB:[ AsRGB(LightBlack) ]
	guard w < perthousand(width,690)
	do
		advance();
	end
	
	Picture.Fourth.C8 : action ==> RGB:[ AsRGB(Black) ]
	guard w < perthousand(width,720)
	do
		advance();
	end
	
	Picture.Fourth.C9 : action ==> RGB:[ AsRGB(VividBlack) ]
	guard w < perthousand(width,750)
	do
		advance();
	end
	
	Picture.Fourth.C10 : action ==> RGB:[ AsRGB(Black) ]
	guard w < perthousand(width,775)
	do
		advance();
	end
	
	Picture.Fourth.C11 : action ==> RGB:[ AsRGB(DarkBlack) ]
	guard w < perthousand(width,885)
	do
		advance();
	end
	
	Picture.Fourth.C12 : action ==> RGB:[ AsRGB(Anthracite) ]
	do
		advance();
	end
	
	/**
	 * Priority
	 */
	priority 
		Resolution > Picture ;
		Picture.First > Picture.Second > Picture.Third > Picture.Fourth;
		Picture.First.C1 > Picture.First.C2 > Picture.First.C3 > Picture.First.C4 > 
			Picture.First.C5 > Picture.First.C6 > Picture.First.C7 > Picture.First.C8 > 
			Picture.First.C9;
		Picture.Second.C1 > Picture.Second.C2 > Picture.Second.C3;
		Picture.Third.C1 > Picture.Third.C2 > Picture.Third.C3;
		Picture.Fourth.C1 > Picture.Fourth.C2 > Picture.Fourth.C3 > Picture.Fourth.C4 > 
			Picture.Fourth.C5 > Picture.Fourth.C6 > Picture.Fourth.C7 > Picture.Fourth.C8 > 
			Picture.Fourth.C9 > Picture.Fourth.C10 > Picture.Fourth.C11 >Picture.Fourth.C12;
	end


end


