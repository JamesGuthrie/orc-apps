/*
 * Copyright (c) 2011, EPFL
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 * 
 *   * Redistributions of source code must retain the above copyright notice,
 *     this list of conditions and the following disclaimer.
 *   * Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *   * Neither the name of the EPFL nor the names of its
 *     contributors may be used to endorse or promote products derived from this
 *     software without specific prior written permission.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
 * STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
 * WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 */
// Author: Richard Thavot <richard.thavot@epfl.ch>
package org.epfl.system;

import std.io.DVI.* ;
import std.trace.Debug.*;

actor AdaptativeDviDisplayer ()
	uint(size=16) Width,
	uint(size=16) Height,
	uint(size=24) Picture
	==> 
	:
	
	uint(size=16) W := 128;
	uint(size=16) H := 128;
	
	uint(size=16) width := 0;
	uint(size=16) height := 0;
	
	uint(size=16) w := width;
	uint(size=16) h := height;
	
	bool eof := false;
	
	initialize ==>
	do
		init(W,H);
	end 
	
	newFrame: action  Width:[ t_width ], Height:[ t_height ] ==>
	do
		width := t_width;
		height := t_height;
	end
	
	endFrame: action ==>
	guard eof
	do
		w := 0;
		h := 0;
		eof := false;
	end
	
	newPixel.accurate: action Picture:[ r ] ==>
	do
		setRGB(r);
		w := w + 1;
		if(width = W) and (w >= W) then
			w := 0;
			h := h + 1;
			if(height = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.extend.width : action ==>
	guard (width < W) and (w >= width)
	do
		setRGB(0);
		w := w + 1;
		if(w >= W) then
			w := 0;
			h := h + 1;
			if(height = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.extend.height : action ==>
	guard (height < H) and (h >= height)
	do
		setRGB(0);
		w := w + 1;
		if(w >= W) then
			w := 0;
			h := h + 1;
			if( h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.crop.width : action Picture:[ r ] ==>
	guard (width > W) and (w >= W)
	do
		w := w + 1;
		if(w >= width) then
			w := 0;
			h := h + 1;
			if(height = H) and (h >= H) then
				eof := true;
			end
		end
	end
	
	newPixel.crop.height : action Picture:[ r ] ==>
	guard (height > H) and (h >= H)
	do
		w := w + 1;
		if(w >= width) then
			w := 0;
			h := h + 1;
			if( h >= height) then
				eof := true;
			end
		end
	end
	
	schedule fsm New:
		New (newFrame) --> Inside;
		Inside(newPixel) --> Inside;
		Inside(endFrame) --> New;
	end
	
	
	priority
		endFrame > newPixel;
		newPixel.crop > newPixel.extend > newPixel.accurate;
		newPixel.crop.height > newPixel.crop.width;
		newPixel.extend.height > newPixel.extend.width;
	end

end


